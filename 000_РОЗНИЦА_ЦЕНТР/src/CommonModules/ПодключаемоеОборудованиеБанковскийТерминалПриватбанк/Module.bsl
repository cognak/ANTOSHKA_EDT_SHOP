&НаКлиенте
///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция осуществляет подключение устройства.
//
// Параметры:
//  ОбъектДрайвера   - <*>
//           - ОбъектДрайвера драйвера торгового оборудования.
//
// Возвращаемое значение:
//  <Булево> - Результат работы функции.
//
Функция ПодключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт

	Результат = Истина;
	ВыходныеПараметры = Новый Массив();
	ПараметрыПодключения.Вставить("ИДУстройства", Новый УникальныйИдентификатор);

	Попытка   
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ОписаниеОшибки());
	КонецПопытки;
	
	ПараметрыПодключения.Вставить("КодОригинальнойТранзакции", Неопределено);
	ПараметрыПодключения.Вставить("ТипТранзакции", "");

	Возврат Результат;

КонецФункции

// Функция осуществляет отключение устройства.
//
// Параметры:
//  ОбъектДрайвера - <*>
//         - ОбъектДрайвера драйвера торгового оборудования.
//
// Возвращаемое значение:
//  <Булево> - Результат работы функции.
//
Функция ОтключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт

	Результат = Истина;

	ВыходныеПараметры = Новый Массив();

	Попытка   
		ОбъектДрайвера = Неопределено;
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ОписаниеОшибки());
	КонецПопытки;
	
	
	
	Возврат Результат;

КонецФункции

// Функция получает, обрабатывает и перенаправляет на исполнение команду к драйверу
//
Функция ВыполнитьКоманду(Команда, ВходныеПараметры = Неопределено, ВыходныеПараметры = Неопределено,
                         ОбъектДрайвера, Параметры, ПараметрыПодключения) Экспорт
	
	Результат = Истина;
	
	ВыходныеПараметры = Новый Массив();
	
	// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩИЕ ДЛЯ ВСЕХ ТИПОВ ДРАЙВЕРОВ
	
	// Тестирование устройства
	Если Команда = "ТестУстройства" ИЛИ Команда = "CheckHealth" Тогда
		Результат = ТестУстройства(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
		
	// Получение версии драйвера
	ИначеЕсли Команда = "ПолучитьВерсиюДрайвера" ИЛИ Команда = "GetVersion" Тогда
		Результат = ПолучитьВерсиюДрайвера(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
		
	// Получение описание драйвера
	ИначеЕсли Команда = "ПолучитьОписаниеДрайвера" ИЛИ Команда = "GetDescription" Тогда
		Результат = ПолучитьОписаниеДрайвера(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
		
	// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩИЕ ДЛЯ РАБОТЫ С ЭКВАЙРИНГОВЫМИ ТЕРМИНАЛАМИ
	
	// Функция возвращает, будет ли печать слип-чеков на терминале
	ИначеЕсли Команда = "PrintSlipOnTerminal" ИЛИ Команда = "ПечатьКвитанцийНаТерминале" Тогда
		Результат = ПечатьКвитанцийНаТерминале(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
		
	// Оплата платежной картой
	ИначеЕсли Команда = "AuthorizeSales" ИЛИ Команда = "ОплатитьПлатежнойКартой" Тогда
		Сумма      = ВходныеПараметры[0];
		НомерКарты = ВходныеПараметры[1];
		Если ВходныеПараметры.Количество() > 2 Тогда
			НомерЧека  = ВходныеПараметры[2];
		Иначе
			НомерЧека  = "";
		КонецЕсли;
		Результат = ОплатитьПлатежнойКартой(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                                            НомерКарты, Сумма, НомерЧека, ВыходныеПараметры);
	// Возврат платежа
	ИначеЕсли Команда = "AuthorizeRefund" Тогда
		Сумма          = ВходныеПараметры[0];
		НомерКарты     = ВходныеПараметры[1];
		СсылочныйНомер = ВходныеПараметры[2];
		НомерЧека      = ВходныеПараметры[3];
		Результат = ВернутьПлатежПоПлатежнойКарте(ОбъектДрайвера, Параметры, ПараметрыПодключения,
		                                          Сумма, НомерКарты, СсылочныйНомер, НомерЧека, ВыходныеПараметры);
	// Отмена платежа
	ИначеЕсли Команда = "AuthorizeVoid" Тогда
		Сумма          = ВходныеПараметры[0];
		СсылочныйНомер = ВходныеПараметры[1];
		НомерЧека      = ВходныеПараметры[2];
		Результат = ОтменитьПлатежПоПлатежнойКарте(ОбъектДрайвера, Параметры, ПараметрыПодключения,
		                                           Сумма, СсылочныйНомер, НомерЧека, ВыходныеПараметры);
	Иначе
		
	//	Указанная команда не поддерживается данным драйвером
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(СтрЗаменить(
				"Команда «%Команда%» не підтримується інстальованим драйвером."
				, "%Команда%"
				, Команда
			)
		);

		Результат = Ложь;

	КонецЕсли;
	
	Возврат Результат;

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Процедуры и функции общие для работы с эквайринговыми терминалами

// Функция возвращает, будет ли печать слип-чеков на терминале
//
Функция ПечатьКвитанцийНаТерминале(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)
	
	Результат = Истина;
	
	Попытка
		Ответ = ОбъектДрайвера.ПечатьКвитанцийНаТерминале();
		ВыходныеПараметры.Очистить();  
		ВыходныеПараметры.Добавить(Ответ);
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.ПечатьКвитанцийНаТерминале>:'") + ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Функция осуществляет сверку итогов по картам.
//
Функция ИтогиДняПоКартам(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;
	Ответ     = Ложь;
	СлипЧек   = "";

	ПараметрыПодключения.ТипТранзакции = НСтр("ru='Сверка итогов'");

	Попытка
		Ответ = ОбъектДрайвера.ИтогиДняПоКартам(ПараметрыПодключения.ИДУстройства, СлипЧек);
		Если Ответ Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(Новый Массив());
			ВыходныеПараметры[0].Добавить("СлипЧек");
			ВыходныеПараметры[0].Добавить(СлипЧек);
		Иначе
			Результат = Ложь;
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить("");
			ОбъектДрайвера.ПолучитьОшибку(ВыходныеПараметры[1])
		КонецЕсли;
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.ИтогиДняПоКартам>:'") + ОписаниеОшибки());
	КонецПопытки;

	Возврат Результат;

КонецФункции

// Функция осуществляет авторизацию/оплату по карте.
//
Функция ОплатитьПлатежнойКартой(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                                НомерКарты, Сумма, НомерЧека, ВыходныеПараметры)

	Результат      = Истина;
	КодRRN         = Неопределено;
	КодАвторизации = Неопределено;
	СлипЧек        = "";
	ДействительнаДо = Неопределено;
	Клиент = Неопределено; 
	Эмитент = Неопределено;
	
	Если НЕ (Сумма > 0) Тогда
		Результат = Ложь;
		ПараметрыПодключения.Вставить("ТипТранзакции","Отказ");
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Не корректная сумма операции.'"));
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		//2024-10-18 sa Вызываем оплату;
		ПараметрыПлатежа = Новый Структура;
		ПараметрыПлатежа.Вставить("Сумма",Сумма);
		ПараметрыПлатежа.Вставить("ПараметрыДрайвера",Параметры);
		ОтключитьУстройство(ОбъектДрайвера,Параметры,ПараметрыПодключения,ВыходныеПараметры);
		
		Ответ = ПродажаДЖСОН(ПараметрыПлатежа);
		//ПодключитьУстройство(ОбъектДрайвера,Параметры,ПараметрыПодключения,ВыходныеПараметры);
		
		Если Ответ.Успешно Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(Ответ.НомерКарты);
			ВыходныеПараметры.Добавить(Ответ.КодRRN);
			ВыходныеПараметры.Добавить(Ответ.НомерЧека);
			ВыходныеПараметры.Добавить(Новый Массив());
			ВыходныеПараметры[3].Добавить("СлипЧек");
			ВыходныеПараметры[3].Добавить(Ответ.СлипЧек);
			ВыходныеПараметры.Добавить(Ответ.КодАвторизации);
			ВыходныеПараметры.Добавить(Ответ.ДействительнаДо);
			ВыходныеПараметры.Добавить(Ответ.Клиент);
			ВыходныеПараметры.Добавить(Ответ.Эмитент);
			ВыходныеПараметры.Добавить(Ответ.МерчантИД);
			ВыходныеПараметры.Добавить(Ответ.ТерминалИД);
		Иначе
			Результат = Ложь;
			ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отказ'");
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(Ответ.ОписаниеОшибки);
			//ОбъектДрайвера.ПолучитьОшибку(ВыходныеПараметры[1])
		КонецЕсли;
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.ОплатитьПлатежнойКартой>:'") + ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Функция осуществляет возврат платежа по карте.
//
Функция ВернутьПлатежПоПлатежнойКарте(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                                      Сумма, НомерКарты, СсылочныйНомер, НомерЧека, ВыходныеПараметры)
	  
	Результат      = Истина;
	КодRRN         = СсылочныйНомер;
	КодАвторизации = Неопределено;
	СлипЧек        = "";
	НомерКарты     = "";

	
	ПараметрыПодключения.ТипТранзакции = НСтр("ru='Вернуть платеж'");
	
	Если НЕ (Сумма > 0) Тогда
		Результат = Ложь;
		ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отказ'");
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Не корректная сумма операции.'"));
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		Ответ = ОбъектДрайвера.ВернутьПлатежПоПлатежнойКарте(ПараметрыПодключения.ИДУстройства, НомерКарты, Сумма, 
													НомерЧека, КодRRN, КодАвторизации, СлипЧек);
		Если Ответ Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(НомерКарты);
			ВыходныеПараметры.Добавить(КодRRN);
			ВыходныеПараметры.Добавить(НомерЧека);
			ВыходныеПараметры.Добавить(Новый Массив());
			ВыходныеПараметры[3].Добавить("СлипЧек");
			ВыходныеПараметры[3].Добавить(СлипЧек);
		Иначе
			Результат = Ложь;
			ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отказ'");
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить("");
			ОбъектДрайвера.ПолучитьОшибку(ВыходныеПараметры[1])
		КонецЕсли;
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.ВернутьПлатежПоПлатежнойКарте>:'") + ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Функция осуществляет отмену платежа по карте.
//
Функция ОтменитьПлатежПоПлатежнойКарте(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                                      Сумма, СсылочныйНомер, НомерЧека, ВыходныеПараметры)
	  
	Результат      = Истина;
	КодRRN         = СсылочныйНомер;
	КодАвторизации = Неопределено;
	СлипЧек        = "";
	НомерКарты     = "";

	
	ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отменить платеж'");
	
	Если НЕ (Сумма > 0) Тогда
		Результат = Ложь;
		ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отказ'");
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Не корректная сумма операции.'"));
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		//Из-за ограничений демо-версии библиотеки для обмена с терминалом 
		//инициализируем устройство каждый раз перед выполнением транзакции
		//ОтключитьУстройство(ОбъектДрайвера,Параметры,ПараметрыПодключения,ВыходныеПараметры);
		//2024-10-18 sa Вызываем возврат;
		ПараметрыПлатежа = Новый Структура;
		ПараметрыПлатежа.Вставить("Сумма",Сумма);
		ПараметрыПлатежа.Вставить("ПараметрыДрайвера",Параметры);
		ПараметрыПлатежа.Вставить("НомерТранзакции", СсылочныйНомер);
		ПараметрыПлатежа.Вставить("НомерЧека", НомерЧека);
		
		//Ответ = ОтменаОплатыДЖСОН(ПараметрыПлатежа);
		Ответ = ВозвратДЖСОН(ПараметрыПлатежа);
		
		Если Ответ.Успешно Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(Новый Массив());
			ВыходныеПараметры[0].Добавить("СлипЧек");
			ВыходныеПараметры[0].Добавить(Ответ.СлипЧек);
			ВыходныеПараметры.Добавить(Ответ);
		Иначе
			Результат = Ложь;
			ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отказ'");
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(Ответ.ОписаниеОшибки);
			//ОбъектДрайвера.ПолучитьОшибку(ВыходныеПараметры[1])
		КонецЕсли;
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.ОтменитьПлатежПоПлатежнойКарте>:'") + ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Результат;

КонецФункции



// Функция осуществляет аварийную отмену операции по карте.
//
Функция АварийнаяОтменаОперации(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                                Сумма, СсылочныйНомер, НомерЧека, ВыходныеПараметры)

	Ответ = Ложь;
	Результат = Истина;
	КомандаПрерватьОперацию  = "{""method"": ""ServiceMessage"",""step"": 0, ""params"": {""msgType"": ""interrupt""}}";

	Попытка
		//Отправляем сервисную команду на прерывание операции
		Ответ = ВыполнитьКомандуТерминала(ОбъектДрайвера, КомандаПрерватьОперацию,Параметры);
		Если Не Ответ.Успешно Тогда
			Результат = Ложь;
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.АварийнаяОтменаОперации>:'") + Ответ.ОписаниеОшибки);
		КонецЕсли;
		//ВыполнениеКоманды = ОжиданиеОтветаТерминала(ОбъектДрайвера,Истина,Параметры);
		//Если  ВыполнениеКоманды.method = "Ожидание" Тогда
		//	ВыполнениеКоманды = Новый Структура("error,errorDescription",true,"Не получен ответ терминала!");
		//	Ответ.Успешно = Ложь;
		//КонецЕсли;
		
		//Ответ = ОбъектДрайвера.АварийнаяОтменаОперации(ПараметрыПодключения.ИДУстройства);
		//Если НЕ Ответ.Успешно Тогда
		//	Результат = Ложь;
		//	ВыходныеПараметры.Очистить();
		//	ВыходныеПараметры.Добавить(999);
		//	ВыходныеПараметры.Добавить(ВыполнениеКоманды.errorDescription);
		//	//ОбъектДрайвера.ПолучитьОшибку(ВыходныеПараметры[1])
		//КонецЕсли;
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.АварийнаяОтменаОперации>:'") + ОписаниеОшибки());
	КонецПопытки;

	Возврат Результат;

КонецФункции

// Функция осуществляет преавторизацию по карте.
// 
Функция ПреавторизоватьПоПлатежнойКарте(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                                        Сумма, НомерКарты, ВыходныеПараметры)
	Результат      = Истина;
	КодRRN         = Неопределено;
	КодАвторизации = Неопределено;
	НомерЧека      = Неопределено;
	СлипЧек        = "";
	
	ПараметрыПодключения.ТипТранзакции = НСтр("ru='Преавторизовать платеж'");
	
	Если НЕ (Сумма > 0) Тогда
		Результат = Ложь;
		ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отказ'");
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Не корректная сумма операции.'"));
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		Ответ = ОбъектДрайвера.ПреавторизацияПоПлатежнойКарте(ПараметрыПодключения.ИДУстройства, НомерКарты, Сумма, 
													НомерЧека, КодRRN, КодАвторизации, СлипЧек);
		Если Ответ Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(НомерКарты);
			ВыходныеПараметры.Добавить(КодRRN);
			ВыходныеПараметры.Добавить(НомерЧека);
			ВыходныеПараметры.Добавить(Новый Массив());
			ВыходныеПараметры[3].Добавить("СлипЧек");
			ВыходныеПараметры[3].Добавить(СлипЧек);
		Иначе
			Результат = Ложь;
			ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отказ'");
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить("");
			ОбъектДрайвера.ПолучитьОшибку(ВыходныеПараметры[1])
		КонецЕсли;
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.ПреавторизацияПоПлатежнойКарте>:'") + ОписаниеОшибки());
	 КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Функция осуществляет отмену преавторизации по карте.
//
Функция ОтменитьПреавторизациюПоПлатежнойКарте(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                                               Сумма, НомерКарты, СсылочныйНомер, НомерЧека, ВыходныеПараметры)
	Результат      = Истина;
	КодАвторизации = Неопределено;
	СлипЧек        = "";
	
	ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отменить преавторизацию'");
	
	Попытка
		Ответ = ОбъектДрайвера.ОтменитьПреавторизациюПоПлатежнойКарте(ПараметрыПодключения.ИДУстройства, НомерКарты, Сумма, 
													НомерЧека, СсылочныйНомер, КодАвторизации, СлипЧек);
		Если Ответ Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(Новый Массив());
			ВыходныеПараметры[0].Добавить("СлипЧек");
			ВыходныеПараметры[0].Добавить(СлипЧек);
		Иначе
			Результат = Ложь; 
			ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отказ'");
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить("");
			ОбъектДрайвера.ПолучитьОшибку(ВыходныеПараметры[1])
		КонецЕсли;
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.ОтменитьПреавторизациюПоПлатежнойКарте>:'") + ОписаниеОшибки());
	 КонецПопытки;
	 
	 Возврат Результат;
	 
 КонецФункции

// Функция осуществляет завершение преавторизации по карте.
//
Функция ЗавершитьПреавторизациюПоПлатежнойКарте(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                                                Сумма, НомерКарты, СсылочныйНомер, НомерЧека, ВыходныеПараметры)
	Результат = Истина;
	
	Результат      = Истина;
	КодАвторизации = Неопределено;
	СлипЧек        = "";
	
	ПараметрыПодключения.ТипТранзакции = НСтр("ru='Завершить преавторизацию'");
	
	Попытка
		Ответ = ОбъектДрайвера.ЗавершитьПреавторизациюПоПлатежнойКарте(ПараметрыПодключения.ИДУстройства, НомерКарты, Сумма, 
													НомерЧека, СсылочныйНомер, КодАвторизации, СлипЧек);
		Если Ответ Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(Новый Массив());
			ВыходныеПараметры[0].Добавить("СлипЧек");
			ВыходныеПараметры[0].Добавить(СлипЧек);
		Иначе
			Результат = Ложь; 
			ПараметрыПодключения.ТипТранзакции = НСтр("ru='Отказ'");
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить("");
			ОбъектДрайвера.ПолучитьОшибку(ВыходныеПараметры[1])
		КонецЕсли;
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.ЗавершитьПреавторизациюПоПлатежнойКарте>:'") + ОписаниеОшибки());
	 КонецПопытки;
	 
	Возврат Результат;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////////
// Процедуры и функции общие для всех типов драйверов

// Функция осуществляет тестирование устройства.
//
Функция ТестУстройства(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат            = Истина;
	РезультатТеста       = "";
	АктивированДемоРежим = "";
	
	РезультатПроверки = Новый Структура;
	Попытка
		Ответ = ИнициализироватьУстройство(ОбъектДрайвера,Параметры);
		Если Ответ Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(0);
		Иначе
			Результат = Ложь;
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(Параметры.ОписаниеОшибки);
		КонецЕсли;
		ВыходныеПараметры.Добавить(РезультатТеста);
		ВыходныеПараметры.Добавить(АктивированДемоРежим);
		
		//ОбъектДрайвера.Disconnect();
	
	Исключение
		Результат = Ложь;
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Ошибка вызова метода <ОбъектДрайвера.ТестУстройства>:'") + ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Результат;

КонецФункции

// Функция возвращает версию установленного драйвера
//
Функция ПолучитьВерсиюДрайвера(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
	ВыходныеПараметры.Добавить(НСтр("ru='Не определена'"));

	Попытка
		ВыходныеПараметры[1] = "1.01";
	Исключение
	КонецПопытки;

	Возврат Результат;

КонецФункции

// Функция возвращает описание установленного драйвера
//
Функция ПолучитьОписаниеДрайвера(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;
	ВыходныеПараметры.Очистить();
	
	ОбъектДрайвера = ИнициализироватьКомпоненту();
	Если ОбъектДрайвера <> Неопределено Тогда
		ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
		ВыходныеПараметры.Добавить(ОбъектДрайвера.Version);
		ВыходныеПараметры.Добавить(НСтр("ru='ДрайверПриватбанкJSON'"));
		ВыходныеПараметры.Добавить(ОбъектДрайвера.GetInfo());
	Иначе
		ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
		ВыходныеПараметры.Добавить("");
		ВыходныеПараметры.Добавить(НСтр("ru='ДрайверПриватбанкJSON'"));
		ВыходныеПараметры.Добавить();
	КонецЕсли;
	
	ВыходныеПараметры.Добавить(НСтр("ru='ЭквайринговыйТерминал'"));
	ВыходныеПараметры.Добавить("1");
	ВыходныеПараметры.Добавить(Ложь);
	ВыходныеПараметры.Добавить(Истина);
	ВыходныеПараметры.Добавить("Redhead Family");
	ВыходныеПараметры.Добавить(Неопределено);
	ВыходныеПараметры.Добавить("");
	
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Функция ИнициализироватьУстройство(ОбъектДрайвера,ПараметрыДрайвера)
	
	Успешно = ОбъектДрайвера.Connect(ПараметрыДрайвера.АдресУстройства,ПараметрыДрайвера.ПортУстройства);
	Если Не Успешно Тогда
		ПараметрыДрайвера.Вставить("ОписаниеОшибки", ОбъектДрайвера.LastErrorMessage);
		Возврат Ложь;
	КонецЕсли;
	КомандаТерминала_ = "{""method"":""PingDevice"",""step"":0}";
	ОтветТерминала = ВыполнитьКомандуТерминала(ОбъектДрайвера, КомандаТерминала_,ПараметрыДрайвера);
	
	мДлительностьТаймаута = ПараметрыДрайвера.ДлительностьТаймаута;
	
	ПингУспешно = Ложь;
	Если ТипЗнч(ОтветТерминала) = Тип("Структура") И ОтветТерминала.Свойство("СтруктураОтвета") Тогда
		Если ТипЗнч(ОтветТерминала.СтруктураОтвета) = Тип("Структура") И ОтветТерминала.СтруктураОтвета.Свойство("method") Тогда
			Если ОтветТерминала.СтруктураОтвета.method = "PingDevice" Тогда
				ПингУспешно = Истина;
			Иначе
				Если ПараметрыДрайвера.Отладка = Истина Тогда
					Сообщить("Неправильный ответ на PingDevice: "+ОтветТерминала.Ответ);
				КонецЕсли;
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	КомандаТерминала_ = "{""method"":""ServiceMessage"",""step"":0,""params"":{""msgType"":""identify""}}";
	ОтветТерминала = ВыполнитьКомандуТерминала(ОбъектДрайвера, КомандаТерминала_,ПараметрыДрайвера);
	
	Если Не ОтветТерминала.Успешно Тогда
		ПараметрыДрайвера.Вставить("ОписаниеОшибки", ОтветТерминала.ОписаниеОшибки);
		Если ПараметрыДрайвера.Отладка = Истина Тогда
			Сообщить("ОШИБКА identify: "+ОтветТерминала.ОписаниеОшибки);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

	Возврат Истина;
КонецФункции

&НаКлиенте
Функция ВыполнитьКомандуТерминала(ОбъектДрайвера, КомандаТерминала_,ПараметрыДрайвера)
	ОтветТерминала = Новый Структура("Успешно,Запрос,Ответ,ОписаниеОшибки,СтруктураОтвета",Ложь,"","","","");
	
	ДанныеОтправки_ = СокрЛП(КомандаТерминала_)+Символ(00);
	Если Не ОбъектДрайвера.SendCommand(ДанныеОтправки_) Тогда
		ОтветТерминала.Запрос = КомандаТерминала_;
		ОтветТерминала.ОписаниеОшибки = ОбъектДрайвера.LastErrorMessage;
		Возврат ОтветТерминала;
	КонецЕсли;
	ОтветТерминала = ПрочитатьОтветТерминала(ОбъектДрайвера,ПараметрыДрайвера);
	
	////sa ##### В ответ всегда должна приходить структура.
	ОтветТерминала.Вставить("Запрос",КомандаТерминала_);
	
	Возврат ОтветТерминала;
КонецФункции

&НаКлиенте
Функция ПрочитатьОтветТерминала(ОбъектДрайвера,ПараметрыДрайвера)
	СтруктураОтвета = Новый Структура("Успешно,Запрос,Ответ,ОписаниеОшибки,СтруктураОтвета",Ложь,"","","","");
	
	//даем время на обработку команды
	ЧасНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Таймаут = ПараметрыДрайвера.ДлительностьТаймаута;
	ЧасОкончания = ЧасНачала + Таймаут * 1000;
	Ответ = "";
	КомандаПрерватьОперацию  = "{""method"": ""ServiceMessage"",""step"": 0, ""params"": {""msgType"": ""interrupt""}}";
	
	ПолныйОтвет = "";

	//ОбъектДрайвера.Timeout = Таймаут * 1000;
	Пока Истина Цикл
		Пауза(1000);
		Ответ = ОбъектДрайвера.ReadMessage();
		ПолныйОтвет = ПолныйОтвет + Ответ;  //если ответ большой, терминал отдает его по частям.
		Если (ОбъектДрайвера.LastErrorMessage <> "") ИЛИ (СтрДлина(Ответ)>1 И Прав(Ответ,1) = Символ(00)) Тогда
			Прервать;	
		КонецЕсли;  
		
		ТекВремя = ТекущаяУниверсальнаяДатаВМиллисекундах();
		Если ТекВремя > ЧасОкончания Тогда
			СообщитьОтладка("Таймаут 1С...",ПараметрыДрайвера.Отладка);
			Прервать;
		КонецЕсли;    
		СообщитьОтладка(""+ТекущаяДата()+" Ожидаю ответ терминала...",ПараметрыДрайвера.Отладка);
	КонецЦикла;	
	
	СообщитьОтладка(""+Ответ,ПараметрыДрайвера.Отладка);	
	
	//в конце ответа может приходить 00. От него нужно избавляться для преобразования JSON в структуру
	Ответ = ПолныйОтвет;
	
	Если Прав(Ответ,1) = Символ(00) Тогда
		Ответ = Лев(Ответ,СтрДлина(Ответ) - 1);
	Иначе                                        
		//значит не дочитали символ разделения (последний символ должен быть 00) 
		ПоследнийШанс = ОбъектДрайвера.ReadMessage(); 
		//2025-05-09 sa Если обработка прервалась по таймауту, а какой-то ответ от терминала пришел, то
		//1. Убираем разделитель 00
		//2. Пробуем обработать ответ как положено.
		Если Прав(ПоследнийШанс,1) = Символ(00) Тогда
			ПоследнийШанс = Лев(ПоследнийШанс,СтрДлина(ПоследнийШанс) - 1); 
		КонецЕсли; 
		Успешно = Ложь;
		Если ЗначениеЗаполнено(ПоследнийШанс) Тогда
			Попытка
				СтруктураДЖСОН = ТекстДЖСОНвСтруктуру(ПоследнийШанс);
				//Если вернулась структура и там нет Success То скорее всего ПоследнийШанс содержит нужные данные.
				Если Не СтруктураДЖСОН.Свойство("Success") Тогда
					Успешно = Истина;
					Ответ = ПоследнийШанс;
				КонецЕсли;
				
			Исключение
			КонецПопытки;
		КонецЕсли;
		
		Если Не Успешно Тогда 
			
			Если ЗначениеЗаполнено(ПоследнийШанс) Тогда
				Ошибка_ = "Несподівана відповідь термінала: "+ПоследнийШанс;
			Иначе
				Ошибка_ = "Не отримано відповідь від термінала!";
			КонецЕсли;
		
			ОбъектДрайвера.Timeout = 5000;
			СообщитьОтладка("Прерываем транзакцию...",ПараметрыДрайвера.Отладка);
			
			Успешно = ОбъектДрайвера.SendCommand(КомандаПрерватьОперацию+Символ(00));
			ОписаниеОшибкиКоманды = ?(ОбъектДрайвера.LastErrorMessage="","Транзакцію скасовано","Помилка скасування транзакції: "+ОбъектДрайвера.LastErrorMessage)+Ошибка_;
			Пауза(2000);
			ОтветОтмены = ОбъектДрайвера.ReadMessage();
			СообщитьОтладка("Отмена транзакции: "+ОтветОтмены,ПараметрыДрайвера.Отладка);
			Если Не Успешно Тогда
				СообщитьОтладка("КАРАУЛ! ОписаниеОшибки = "+ОбъектДрайвера.LastErrorMessage,ПараметрыДрайвера.Отладка);
				СтруктураОтвета.Вставить("Успешно",Ложь);
				СтруктураОтвета.Вставить("ОписаниеОшибки",ОписаниеОшибкиКоманды);
				СтруктураОтвета.Вставить("Ответ",ОтветОтмены);
			Иначе
				СтруктураОтвета.Вставить("Успешно",Ложь);
				СтруктураОтвета.Вставить("ОписаниеОшибки",ОписаниеОшибкиКоманды);
			КонецЕсли;
			
			Возврат СтруктураОтвета;
		КонецЕсли;
		
	КонецЕсли;
	//сюда попадаем если успешно получили ответ и можно ориентироваться на него.
	СтруктураДЖСОН = ТекстДЖСОНвСтруктуру(Ответ);
	Если ТипЗнч(СтруктураДЖСОН) = Тип("Структура") И СтруктураДЖСОН.Свойство("error") Тогда
		СтруктураОтвета.Вставить("Успешно", НЕ СтруктураДЖСОН.error И НЕ ЗначениеЗаполнено(СтруктураДЖСОН.errorDescription));
		СтруктураОтвета.Вставить("ОписаниеОшибки",СтруктураДЖСОН.errorDescription);
		СтруктураОтвета.Вставить("СтруктураОтвета",СтруктураДЖСОН);
	КонецЕсли;
	
	СтруктураОтвета.Вставить("Ответ",Ответ);
	
	
	Возврат СтруктураОтвета;
	
КонецФункции

//2023-01-05 sa преобразование текста JSON в структуру 
&НаКлиенте
Функция ТекстДЖСОНвСтруктуру(ТекстДЖСОН) Экспорт
	СтруктураОтвета = Новый Структура;
	Если ТекстДЖСОН = "" Тогда
		СтруктураОтвета.Вставить("Success",Ложь);
		СтруктураОтвета.Вставить("Description","Текст не в формате JSON (пустой)!");
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ФайлJSON = Новый ЧтениеJSON;
	ФайлJSON.УстановитьСтроку(ТекстДЖСОН);
	
	Попытка
		СтруктураОтвета = ПрочитатьJSON(ФайлJSON);
		ФайлJSON.Закрыть();
	Исключение
		СтруктураОтвета.Вставить("Success",Ложь);
		СтруктураОтвета.Вставить("Description","Текст не в формате JSON!"+Символы.ПС+ТекстДЖСОН);
	КонецПопытки;
	
	Возврат СтруктураОтвета;
КонецФункции

&НаКлиенте
Функция СтруктураВТекст(ВходнаяСтруктура)
	Рез = "";
	Если ТипЗнч(ВходнаяСтруктура) <> Тип("Структура")Тогда
		Возврат Строка(ВходнаяСтруктура);
	КонецЕсли;
	Для Каждого КлючИЗначение Из ВходнаяСтруктура Цикл
		Рез = Рез + КлючИЗначение.Ключ + ": "+КлючИЗначение.Значение + Символы.ПС;	
	КонецЦикла;
	
	Возврат Рез;
КонецФункции

&НаКлиенте
Процедура СообщитьОтладка(Стр, ЭтоОтладка=Ложь)
	Если ЭтоОтладка Тогда
		Сообщить(""+ТекущаяДата()+" "+Стр);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ОтменаОплатыДЖСОН(ПараметрыПлатежа)
	РезультатОплаты = Новый Структура("Успешно,Сумма,ОписаниеОшибки,Банк,НомерКарты,КодRRN,НомерЧека,КодАвторизации,ДействительнаДо,Клиент,Эмитент,ТерминалИД,МерчантИД,СлипЧек",Ложь,0);
	
	ПараметрыДрайвера = ПараметрыПлатежа.ПараметрыДрайвера;
	
	//Из-за того что библиотека работающая с TCP - является ДЕМО, у нее есть ограничение на количество обрабатываемых символов.
	//Около 30 кб. Поэтому будем переинициализировать компоненту перед каждой операцией заново.
	ОбъектДрайвера = ИнициализироватьКомпоненту();
	Если ОбъектДрайвера = Неопределено Тогда
		РезультатОплаты.ОписаниеОшибки = "Не вдалося ініціалізувати драйвер банківського терміналу";
		Возврат РезультатОплаты;
	КонецЕсли;
	
	Успешно = ИнициализироватьУстройство(ОбъектДрайвера,ПараметрыДрайвера);
	Если Не Успешно Тогда
		РезультатОплаты.ОписаниеОшибки = ПараметрыДрайвера.ОписаниеОшибки;
		Возврат РезультатОплаты;
	КонецЕсли;
	
	СуммаПлатежа = СтрЗаменить(ПараметрыПлатежа.Сумма,Символы.НПП,"");
	
	КомандаОплаты = "{""method"":""Withdrawal"",""step"": 0,""params"": {""invoiceNumber"": """+ПараметрыПлатежа.НомерЧека+"""}}";
	
	ОтветТерминала = ВыполнитьКомандуТерминала(ОбъектДрайвера,КомандаОплаты,ПараметрыДрайвера);
	ОбъектДрайвера.Disconnect();
	ОбъектДрайвера = Неопределено;
	Если Не ОтветТерминала.Успешно Тогда
		РезультатОплаты.Вставить("ОписаниеОшибки", ОтветТерминала.ОписаниеОшибки);
		СообщитьОтладка("ОШИБКА Withdrawal: "+ОтветТерминала.ОписаниеОшибки,ПараметрыДрайвера.Отладка);
		Возврат РезультатОплаты;
	КонецЕсли;
	
	СтруктураОтвета = ОтветТерминала.СтруктураОтвета;
	
	СтруктураОтвета = ОтветТерминала.СтруктураОтвета;
	РезультатыПлатежа = СтруктураОтвета.params;	
	РезультатОплаты.Успешно = Не СтруктураОтвета.error;
	РезультатОплаты.Сумма = СуммаПлатежа;
	РезультатОплаты.Банк = РезультатыПлатежа.bankAcquirer;
	РезультатОплаты.НомерКарты = РезультатыПлатежа.pan;
	РезультатОплаты.КодRRN = РезультатыПлатежа.rrn;
	РезультатОплаты.НомерЧека = РезультатыПлатежа.invoiceNumber;
	РезультатОплаты.КодАвторизации = ?(РезультатыПлатежа.approvalCode="","00000",РезультатыПлатежа.approvalCode);
	РезультатОплаты.ДействительнаДо = РезультатыПлатежа.cardExpiryDate;
	РезультатОплаты.Клиент = РезультатыПлатежа.cardHolderName;
	РезультатОплаты.Эмитент = РезультатыПлатежа.issuerName;
	РезультатОплаты.ТерминалИД = РезультатыПлатежа.terminalId;
	РезультатОплаты.МерчантИД = РезультатыПлатежа.merchant;
	 
	
	РезультатОплаты.СлипЧек = ""+РезультатОплаты.Банк + Символы.ПС + 
			  Строка(РезультатОплаты.Эмитент) + Символы.ПС +
			  //"ДЕЙСТВ. ДО: " + Прав(ДействительнаДо,2) + "/" + Лев(ДействительнаДо,2) + Символы.ПС +
			  Строка(РезультатОплаты.НомерКарты) + Символы.ПС +
			  "КОД АВТОРИЗАЦИИ: " + РезультатОплаты.КодАвторизации + Символы.ПС +
			  "RRN: " + РезультатОплаты.КодRRN + Символы.ПС +
			  "СУММА: " + СТРОКА(СуммаПлатежа) + " ГРН" + Символы.ПС + 
			  "КЛИЕНТ: " + РезультатОплаты.Клиент + Символы.ПС+
			  "МЕРЧАНТ ID: " + РезультатОплаты.МерчантИД + Символы.ПС+
			  "ТЕРМИНАЛ ID: " + РезультатОплаты.ТерминалИД;
			  
	
	Возврат РезультатОплаты;
	
	
КонецФункции

&НаКлиенте
Функция ИнициализироватьКомпоненту()
	Попытка
		ОбъектДрайвера = Новый COMОбъект("AddIn.PrivatBankAcquirer");
	Исключение
		ОбъектДрайвера=Неопределено;
	КонецПопытки;
	
	//КонецЕсли;
	Если ОбъектДрайвера=Неопределено Тогда
		Сообщить("Ошибка подключения компоненты!");
		Возврат Неопределено;
	КонецЕсли;
	КомпонентаКлиент = ОбъектДрайвера;
	Возврат ОбъектДрайвера;	
КонецФункции


&НаКлиенте
Функция ПродажаДЖСОН(ПараметрыПлатежа)
	РезультатОплаты = Новый Структура("Успешно,Сумма,ОписаниеОшибки,Банк,НомерКарты,КодRRN,НомерЧека,КодАвторизации,ДействительнаДо,Клиент,Эмитент,ТерминалИД,МерчантИД,СлипЧек",Ложь,0);
	
	ПараметрыДрайвера = ПараметрыПлатежа.ПараметрыДрайвера;
	
	//Из-за того что библиотека работающая с TCP - является ДЕМО, у нее есть ограничение на количество обрабатываемых символов.
	//Около 30 кб. Поэтому будем переинициализировать компоненту перед каждой операцией заново.
	ОбъектДрайвера = ИнициализироватьКомпоненту();
	Если ОбъектДрайвера = Неопределено Тогда
		РезультатОплаты.ОписаниеОшибки = "Не вдалося ініціалізувати драйвер банківського терміналу";
		Возврат РезультатОплаты;
	КонецЕсли;
	
	Успешно = ИнициализироватьУстройство(ОбъектДрайвера,ПараметрыДрайвера);
	Если Не Успешно Тогда
		РезультатОплаты.ОписаниеОшибки = ПараметрыДрайвера.ОписаниеОшибки;
		Возврат РезультатОплаты;
	КонецЕсли;
	
	СуммаПлатежа = СтрЗаменить(ПараметрыПлатежа.Сумма,Символы.НПП,"");
	//СуммаПлатежа = 0.5;
	
	КомандаОплаты = "{""method"":""Purchase"",""step"": 0,""params"": {""amount"": """+СуммаПлатежа+""",""discount"": """",""merchantId"": ""0"",""facepay"": ""false""}}";
	
	ОтветТерминала = ВыполнитьКомандуТерминала(ОбъектДрайвера,КомандаОплаты,ПараметрыДрайвера);
	ОбъектДрайвера.Disconnect();
	ОбъектДрайвера = Неопределено;
	Если Не ОтветТерминала.Успешно Тогда
		РезультатОплаты.Вставить("ОписаниеОшибки", ОтветТерминала.ОписаниеОшибки);
		СообщитьОтладка("ОШИБКА Purchase: "+ОтветТерминала.ОписаниеОшибки,ПараметрыДрайвера.Отладка);
		Возврат РезультатОплаты;
	КонецЕсли;
	
	//2024-11-13 sa Еше может быть вариант что устройство занято
	Если ТипЗнч(ОтветТерминала.СтруктураОтвета) = Тип("Структура") И ОтветТерминала.СтруктураОтвета.Свойство("method") И ОтветТерминала.СтруктураОтвета.method = "ServiceMessage" Тогда
		Если ОтветТерминала.СтруктураОтвета.params.msgType = "deviceBusy" Тогда
			РезультатОплаты.Вставить("ОписаниеОшибки","Устройство занято");
			Возврат РезультатОплаты;
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураОтвета = ОтветТерминала.СтруктураОтвета;
	РезультатыПлатежа = СтруктураОтвета.params;	
	РезультатОплаты.Успешно = Не СтруктураОтвета.error;
	РезультатОплаты.Сумма = СуммаПлатежа;
	РезультатОплаты.Банк = РезультатыПлатежа.bankAcquirer;
	РезультатОплаты.НомерКарты = РезультатыПлатежа.pan;
	РезультатОплаты.КодRRN = РезультатыПлатежа.rrn;
	РезультатОплаты.НомерЧека = РезультатыПлатежа.invoiceNumber;
	РезультатОплаты.КодАвторизации = РезультатыПлатежа.approvalCode;
	РезультатОплаты.ДействительнаДо = РезультатыПлатежа.cardExpiryDate;
	РезультатОплаты.Клиент = РезультатыПлатежа.cardHolderName;
	РезультатОплаты.Эмитент = РезультатыПлатежа.issuerName;
	РезультатОплаты.ТерминалИД = РезультатыПлатежа.terminalId;
	РезультатОплаты.МерчантИД = РезультатыПлатежа.merchant;
	 
	
	РезультатОплаты.СлипЧек = ""+РезультатОплаты.Банк + Символы.ПС + 
			  Символы.ПС +
			  Строка(РезультатОплаты.Эмитент) + Символы.ПС +
			  //"ДЕЙСТВ. ДО: " + Прав(ДействительнаДо,2) + "/" + Лев(ДействительнаДо,2) + Символы.ПС +
			  Строка(РезультатОплаты.НомерКарты) + Символы.ПС +
			  "КОД АВТОРИЗАЦИИ: " + РезультатОплаты.КодАвторизации + Символы.ПС +
			  "RRN: " + РезультатОплаты.КодRRN + Символы.ПС +
			  "СУММА: " + СТРОКА(СуммаПлатежа) + " ГРН" + Символы.ПС +
			  "КЛИЕНТ: " + РезультатОплаты.Клиент + Символы.ПС+
			  "МЕРЧАНТ ID: " + РезультатОплаты.МерчантИД + Символы.ПС+
			  "ТЕРМИНАЛ ID: " + РезультатОплаты.ТерминалИД;
			  
	
	Возврат РезультатОплаты;
КонецФункции // ПродажаДЖСОН()

&НаКлиенте
Функция ВозвратДЖСОН(ПараметрыПлатежа)
	РезультатОплаты = Новый Структура("Успешно,Сумма,ОписаниеОшибки,Банк,НомерКарты,КодRRN,НомерЧека,КодАвторизации,ДействительнаДо,Клиент,Эмитент,ТерминалИД,МерчантИД,СлипЧек",Ложь,0);
	
	ПараметрыДрайвера = ПараметрыПлатежа.ПараметрыДрайвера;
	
	ОбъектДрайвера = ИнициализироватьКомпоненту();
	Если ОбъектДрайвера = Неопределено Тогда
		РезультатОплаты.ОписаниеОшибки = "Не вдалося ініціалізувати драйвер банківського терміналу";
		Возврат РезультатОплаты;
	КонецЕсли;
	
	Успешно = ИнициализироватьУстройство(ОбъектДрайвера,ПараметрыДрайвера);
	Если Не Успешно Тогда
		РезультатОплаты.ОписаниеОшибки = ПараметрыДрайвера.ОписаниеОшибки;
		Возврат РезультатОплаты;
	КонецЕсли;
	
	СуммаПлатежа = СтрЗаменить(ПараметрыПлатежа.Сумма,Символы.НПП,"");
	//СуммаПлатежа = 0.5;
	
	НомерТранзакции = ПараметрыПлатежа.НомерТранзакции;
	КомандаВозврата = "{""method"":""Refund"",""step"": 0,""params"": {""amount"": """+СуммаПлатежа+""",""discount"": """",""merchantId"": ""0"",""rrn"": """+НомерТранзакции+"""}}";

	ОтветТерминала = ВыполнитьКомандуТерминала(ОбъектДрайвера,КомандаВозврата,ПараметрыДрайвера);
	ОбъектДрайвера.Disconnect();
	ОбъектДрайвера = Неопределено;
	Если Не ОтветТерминала.Успешно Тогда
		РезультатОплаты.Вставить("ОписаниеОшибки", ОтветТерминала.ОписаниеОшибки);
		СообщитьОтладка("ОШИБКА Refund: "+ОтветТерминала.ОписаниеОшибки,ПараметрыДрайвера.Отладка);
		Возврат РезультатОплаты;
	КонецЕсли;
	
	СтруктураОтвета = ОтветТерминала.СтруктураОтвета;
	
	СтруктураОтвета = ОтветТерминала.СтруктураОтвета;
	РезультатыПлатежа = СтруктураОтвета.params;	
	РезультатОплаты.Успешно = Не СтруктураОтвета.error;
	РезультатОплаты.Сумма = СуммаПлатежа;
	РезультатОплаты.Банк = РезультатыПлатежа.bankAcquirer;
	РезультатОплаты.НомерКарты = РезультатыПлатежа.pan;
	РезультатОплаты.КодRRN = РезультатыПлатежа.rrn;
	РезультатОплаты.НомерЧека = РезультатыПлатежа.invoiceNumber;
	РезультатОплаты.КодАвторизации = ?(РезультатыПлатежа.approvalCode="","00000",РезультатыПлатежа.approvalCode);
	РезультатОплаты.ДействительнаДо = РезультатыПлатежа.cardExpiryDate;
	РезультатОплаты.Клиент = РезультатыПлатежа.cardHolderName;
	РезультатОплаты.Эмитент = РезультатыПлатежа.issuerName;
	РезультатОплаты.ТерминалИД = РезультатыПлатежа.terminalId;
	РезультатОплаты.МерчантИД = РезультатыПлатежа.merchant;
	 
	
	РезультатОплаты.СлипЧек = ""+РезультатОплаты.Банк + Символы.ПС + 
			  Строка(РезультатОплаты.Эмитент) + Символы.ПС +
			  //"ДЕЙСТВ. ДО: " + Прав(ДействительнаДо,2) + "/" + Лев(ДействительнаДо,2) + Символы.ПС +
			  Строка(РезультатОплаты.НомерКарты) + Символы.ПС +
			  "КОД АВТОРИЗАЦИИ: " + РезультатОплаты.КодАвторизации + Символы.ПС +
			  "RRN: " + РезультатОплаты.КодRRN + Символы.ПС +
			  "СУММА: " + СТРОКА(СуммаПлатежа) + " ГРН" + Символы.ПС +
			  "КЛИЕНТ: " + РезультатОплаты.Клиент + Символы.ПС+
			  "МЕРЧАНТ ID: " + РезультатОплаты.МерчантИД + Символы.ПС+
			  "ТЕРМИНАЛ ID: " + РезультатОплаты.ТерминалИД;
			  
	
	Возврат РезультатОплаты;
КонецФункции // ПродажаДЖСОН()

&НаКлиенте
Функция ПреобразоватьВХекс(Знач Число_)
	Если Число_ = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	СимволыХекс = "0123456789ABCDEF";
	ЦелаяЧасть = Цел(Число_/16);
	ДробнаяЧасть = Число_%16;
	СимволХекс_ = Сред(СимволыХекс,ЦелаяЧасть+1,1) + Сред(СимволыХекс,ДробнаяЧасть+1,1);
	
	
	Возврат СимволХекс_;
	
КонецФункции

&НаКлиенте
Процедура Пауза(ЗадержкаВМиллисекундах,Прогресс = 100)
	ТаймерМС = ТекущаяУниверсальнаяДатаВМиллисекундах() + ЗадержкаВМиллисекундах;
	Пока ТаймерМС > ТекущаяУниверсальнаяДатаВМиллисекундах() Цикл
		Если Прогресс > 0 Тогда
        	Состояние("Ожидание банковского терминала",Прогресс);
		КонецЕсли;
    КонецЦикла;		
КонецПроцедуры
