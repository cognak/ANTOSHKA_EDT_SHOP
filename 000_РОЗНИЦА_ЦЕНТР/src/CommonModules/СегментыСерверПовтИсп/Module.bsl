//	LNK 21.05.2019 10:02:35
Функция МагазиныФормированияСегментов(Разрешено)	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПолитикаМагазина.Магазин КАК Магазин,
	|	1 КАК Маркер
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаМагазинов.СрезПоследних(
	|			,
	|			НЕ Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
	|				И Магазин.ВведенВЭксплуатацию
	|				И НЕ(Магазин.СкладУправляющейСистемы
	|						ИЛИ Магазин.ПометкаУдаления
	|						ИЛИ Магазин.Блокирован)) КАК ПолитикаМагазина
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.ПоМагазину КАК ТаблицаУзлы
	|		ПО ПолитикаМагазина.Магазин = ТаблицаУзлы.Магазин
	|			И (НЕ ТаблицаУзлы.ПометкаУдаления)
	|ГДЕ
	|	ПолитикаМагазина.НоменклатуруСегментаФормироватьНаПериферии = &Разрешено
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТаблицаМагазины.Ссылка,
	|	2
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаМагазинов.СрезПоследних(, Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)) КАК ПолитикаОбщая
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Магазины КАК ТаблицаМагазины
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаМагазинов.СрезПоследних(
	|					,
	|					НЕ Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
	|						И Магазин.ВведенВЭксплуатацию
	|						И НЕ(Магазин.СкладУправляющейСистемы
	|								ИЛИ Магазин.ПометкаУдаления
	|								ИЛИ Магазин.Блокирован)) КАК ПолитикаМагазина
	|			ПО ТаблицаМагазины.Ссылка = ПолитикаМагазина.Магазин
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.ПоМагазину КАК ТаблицаУзлы
	|			ПО ТаблицаМагазины.Ссылка = ТаблицаУзлы.Магазин
	|				И (НЕ ТаблицаУзлы.ПометкаУдаления)
	|		ПО (ТаблицаМагазины.ВведенВЭксплуатацию)
	|			И (НЕ(ТаблицаМагазины.СкладУправляющейСистемы
	|					ИЛИ ТаблицаМагазины.ПометкаУдаления
	|					ИЛИ ТаблицаМагазины.Блокирован))
	|ГДЕ
	|	ПолитикаОбщая.НоменклатуруСегментаФормироватьНаПериферии = &Разрешено
	|	И ПолитикаМагазина.Магазин ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Маркер"
	);
	Запрос.УстановитьПараметр("Разрешено", Разрешено);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Магазин");

КонецФункции
