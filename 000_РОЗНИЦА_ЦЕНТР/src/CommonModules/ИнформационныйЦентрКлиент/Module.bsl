////////////////////////////////////////////////////////////////////////////////
// Подсистема "Информационный центр".
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Открывает форму отображения отдельной новости.
//
// Параметры:
//	ВнешняяСсылка - Строка - внешняя ссылка.
//	Идентификатор - УникальныйИдентификатор - идентификатор новости.
//
Процедура ПоказатьНовость(ВнешняяСсылка, Идентификатор) Экспорт
	
	Если Не ПустаяСтрока(ВнешняяСсылка) Тогда 
		ПерейтиПоНавигационнойСсылке(ВнешняяСсылка);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОткрытьНовость");
	ПараметрыФормы.Вставить("Идентификатор", Идентификатор);
	ОткрытьФорму("ОбщаяФорма.ОтображениеНовостей", ПараметрыФормы);
	
КонецПроцедуры

// Открывает форму отображения всех новостей.
//
//
Процедура ПоказатьВсеСообщения() Экспорт
	
	ПараметрыФормы = Новый Структура("ОткрытьВсеНовости");
	ОткрытьФорму("ОбщаяФорма.ОтображениеНовостей", ПараметрыФормы);
	
КонецПроцедуры

// Возвращает размер в мегабайтах, размер вложения не больше 25 Мегабайта.
//
// Возвращаемое значение:
//	Число - размер вложений в мегабайтах.
//
Функция МаксимальныйРазмерВложенийДляОтправкиСообщенияВПоддержкуСервиса() Экспорт
	
	Возврат 20;
	
КонецФункции

// Открывает форму по отправке сообщения получателю.
//
Процедура ОткрытьФормуОтправкиСообщенияВТехподдержку(ИспользоватьРазделениеПоОбластямДанных, ПараметрыСообщения = Неопределено) Экспорт
	
	Если ИспользоватьРазделениеПоОбластямДанных Тогда
		ОткрытьФорму("ОбщаяФорма.ОтправкаСообщенияВТехподдержку", ПараметрыСообщения);
	КонецЕсли;
	
КонецПроцедуры

// Процедура-обработчик при нажатии на информационную ссылку.
//
// Параметры:
//	Форма - УправляемаяФорма - контекст управляемой формы.
//	Элемент - ЭлементФормы - группа формы.
//
Процедура НажатиеНаИнформационнуюСсылку(Форма, Элемент) Экспорт
	
	Гиперссылка = Форма.ИнформационныеСсылки.НайтиПоЗначению(Элемент.Имя);
	
	Если Гиперссылка <> Неопределено Тогда
		
		ПерейтиПоНавигационнойСсылке(Гиперссылка.Представление);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура-обработчик при нажатии на "Все" информационные ссылки.
//
// Параметры:
//	ПутьКФорме - Строка - полный путь к форме.
//
Процедура НажатиеНаСсылкуВсеИнформационныеСсылки(ПутьКФорме) Экспорт
	
	ПараметрыФормы = Новый Структура("ПутьКФорме", ПутьКФорме);
	ОткрытьФорму("Обработка.ИнформационныйЦентр.Форма.ИнформационныеСсылкиВКонтексте", ПараметрыФормы);
	
КонецПроцедуры