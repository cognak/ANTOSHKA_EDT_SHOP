//	LNK 05.05.2022 08:47:20
Процедура ОбменРозницаMagentoЗарегистрироватьИзменение(Источник, Отказ) Экспорт

	Возврат;	//	LNK 06.05.2022 05:51:15 - ОТКЛЮЧЕНО

	Если НЕ ОбщиеДействияПередРегистрацией(Источник) Тогда

		Возврат;

	КонецЕсли;


КонецПроцедуры

//	LNK 21.04.2022 10:54:17
Процедура ОбменРозницаMagentoЗарегистрироватьИзменениеДокумента(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт

	Возврат;	//	LNK 06.05.2022 05:51:15 - ОТКЛЮЧЕНО
		
	Если НЕ ОбщиеДействияПередРегистрацией(Источник) Тогда

		Возврат;

	КонецЕсли;

	#Если _ Тогда
	Источник = Справочники.Номенклатура.СоздатьЭлемент();
	#КонецЕсли

	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Номенклатура") Тогда

		Если НЕ Источник.ЭтоГруппа ИЛИ НЕ Источник.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда

			Возврат;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

//	LNK 21.04.2022 10:49:28
Процедура ОбменРозницаMagentoЗарегистрироватьИзменениеНабораЗаписей(Источник, Отказ, Замещение) Экспорт

	#Если _ Тогда
	Источник = РегистрыНакопления.ТоварыНаСкладах.СоздатьНаборЗаписей();
	#КонецЕсли
		
	Если НЕ ОбщиеДействияПередРегистрацией(Источник) Тогда

		Возврат;

	КонецЕсли;

	Запрос = Новый Запрос(ПолучитьТекстЗапросаРегистрация(ТипЗнч(Источник)));

	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда

		Запрос.УстановитьПараметр("ТаблицаНабора", Источник.Выгрузить());
		Запрос.УстановитьПараметр("Регистратор"  , Источник.Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Упрощённый"   , Истина);	//	только Номенклатура (Измерение_2)
		ДанныеРегистрации = Запрос.Выполнить().Выбрать();

		НаборЗаписей  = РегистрыСведений.КомплексОбменаMagento.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;

		Пока ДанныеРегистрации.Следующий() Цикл

			НаборЗаписей.Очистить();

			НаборЗаписей.Отбор.ТипРегистрации.Установить(ДанныеРегистрации.ТипРегистрации);
			НаборЗаписей.Отбор.Измерение_1.Установить(ДанныеРегистрации.Измерение_1);
			НаборЗаписей.Отбор.Измерение_2.Установить(ДанныеРегистрации.Измерение_2);
			НаборЗаписей.Отбор.Измерение_3.Установить(ДанныеРегистрации.Измерение_3);

			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ДанныеРегистрации);

			ЗаписьНабора.ДатаИзменения = ТекущаяДата();

			НаборЗаписей.Записать();

			ПланыОбмена.ЗарегистрироватьИзменения(ОбменMagentoПовтИсп.ПолучитьМассивУзлов(), НаборЗаписей);

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

#Область СлужебныеПроцедуры

Функция ПолучитьТекстЗапросаРегистрация(ТипИсточника)

	ТекстЗапроса = "";

	Если ТипИсточника = Тип("РегистрНакопленияНаборЗаписей.ТоварыНаСкладах") Тогда

	//	Выбираем только отличия между новым и существующим составом набора записей.
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТаблицаРегистра.Склад КАК Справочник.Склады) КАК Склад,
		|	ВЫРАЗИТЬ(ТаблицаРегистра.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
		|	ТаблицаРегистра.Характеристика КАК Характеристика,
		|	ТаблицаРегистра.Количество КАК Количество,
		|	ТаблицаРегистра.Период КАК Значение
		|ПОМЕСТИТЬ ТекущийНабор
		|ИЗ
		|	&ТаблицаНабора КАК ТаблицаРегистра
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРегистрацииMagento.ПустаяСсылка)
		|		ИНАЧЕ ТаблицаДанных.ТипРегистрации
		|	КОНЕЦ КАК ТипРегистрации,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаДанных.Измерение_1
		|	КОНЕЦ КАК Измерение_1,
		|	ТаблицаДанных.Измерение_2 КАК Измерение_2,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаДанных.Измерение_3
		|	КОНЕЦ КАК Измерение_3,
		|	СУММА(ТаблицаДанных.Количество) КАК Количество,
		|	МАКСИМУМ(ТаблицаДанных.Значение) КАК Значение,
		|	&Регистратор КАК Объект
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРегистрацииMagento.ТоварыНаСкладах) КАК ТипРегистрации,
		|		ЕСТЬNULL(ТаблицаРегистра.Склад.Магазин, ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)) КАК Измерение_1,
		|		ТаблицаРегистра.Номенклатура КАК Измерение_2,
		|		ТаблицаРегистра.Характеристика КАК Измерение_3,
		|		-ТаблицаРегистра.Количество КАК Количество,
		|		ТаблицаРегистра.Период КАК Значение
		|	ИЗ
		|		РегистрНакопления.ТоварыНаСкладах КАК ТаблицаРегистра
		|	ГДЕ
		|		ТаблицаРегистра.Регистратор = &Регистратор
		|		И ТаблицаРегистра.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|		И ТаблицаРегистра.Номенклатура.WEB_Выгружать
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРегистрацииMagento.ТоварыНаСкладах),
		|		ЕСТЬNULL(ТекущийНабор.Склад.Магазин, ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)),
		|		ТекущийНабор.Номенклатура,
		|		ТекущийНабор.Характеристика,
		|		ТекущийНабор.Количество,
		|		ТекущийНабор.Значение
		|	ИЗ
		|		ТекущийНабор КАК ТекущийНабор
		|	ГДЕ
		|		ТекущийНабор.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|		И ТекущийНабор.Номенклатура.WEB_Выгружать) КАК ТаблицаДанных
		|ГДЕ
		|	НЕ ТаблицаДанных.Измерение_1 = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|	И НЕ ТаблицаДанных.Измерение_2 = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаДанных.Измерение_2,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРегистрацииMagento.ПустаяСсылка)
		|		ИНАЧЕ ТаблицаДанных.ТипРегистрации
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаДанных.Измерение_1
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаДанных.Измерение_3
		|	КОНЕЦ
		|
		|ИМЕЮЩИЕ
		|	НЕ СУММА(ТаблицаДанных.Количество) = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТекущийНабор"
		;

	ИначеЕсли ТипИсточника = Тип("РегистрНакопленияНаборЗаписей.ЗаказыПокупателей") Тогда

	//	Выбираем только отличия между новым и существующим составом набора записей.
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаРегистра.Заказ КАК Заказ,
		|	ВЫРАЗИТЬ(ТаблицаРегистра.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
		|	ТаблицаРегистра.Характеристика КАК Характеристика,
		|	ТаблицаРегистра.Количество КАК Количество,
		|	ТаблицаРегистра.Период КАК Значение
		|ПОМЕСТИТЬ ТекущийНабор
		|ИЗ
		|	&ТаблицаНабора КАК ТаблицаРегистра
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРегистрацииMagento.ПустаяСсылка)
		|		ИНАЧЕ ТаблицаДанных.ТипРегистрации
		|	КОНЕЦ КАК ТипРегистрации,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаДанных.Измерение_1
		|	КОНЕЦ КАК Измерение_1,
		|	ТаблицаДанных.Измерение_2 КАК Измерение_2,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаДанных.Измерение_3
		|	КОНЕЦ КАК Измерение_3,
		|	СУММА(ТаблицаДанных.Количество) КАК Количество,
		|	МАКСИМУМ(ТаблицаДанных.Значение) КАК Значение,
		|	&Регистратор КАК Объект
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРегистрацииMagento.ЗаказыПокупателей) КАК ТипРегистрации,
		|		ТаблицаРегистра.Заказ КАК Измерение_1,
		|		ТаблицаРегистра.Номенклатура КАК Измерение_2,
		|		ТаблицаРегистра.Характеристика КАК Измерение_3,
		|		-ТаблицаРегистра.Количество КАК Количество,
		|		ТаблицаРегистра.Период КАК Значение
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей КАК ТаблицаРегистра
		|	ГДЕ
		|		ТаблицаРегистра.Регистратор = &Регистратор
		|		И ТаблицаРегистра.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|		И ТаблицаРегистра.Номенклатура.WEB_Выгружать
		|
		|	ОБЪЕДИНИТЬ
		|
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРегистрацииMagento.ЗаказыПокупателей),
		|		ТекущийНабор.Заказ,
		|		ТекущийНабор.Номенклатура,
		|		ТекущийНабор.Характеристика,
		|		ТекущийНабор.Количество,
		|		ТекущийНабор.Значение
		|	ИЗ
		|		ТекущийНабор КАК ТекущийНабор
		|	ГДЕ
		|		ТекущийНабор.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|		И ТекущийНабор.Номенклатура.WEB_Выгружать) КАК ТаблицаДанных
		|ГДЕ
		|	НЕ ТаблицаДанных.Измерение_1 = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаДанных.Измерение_2,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРегистрацииMagento.ПустаяСсылка)
		|		ИНАЧЕ ТаблицаДанных.ТипРегистрации
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаДанных.Измерение_1
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &Упрощённый = ИСТИНА
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаДанных.Измерение_3
		|	КОНЕЦ
		|ИМЕЮЩИЕ
		|	НЕ СУММА(ТаблицаДанных.Количество) = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТекущийНабор"
		;

	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.ЗаказПокупателя") Тогда


	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции

Функция ОбщиеДействияПередРегистрацией(Источник)

	Если Источник.ДополнительныеСвойства.Свойство("ОтключитьМеханизмРегистрацииРозницаMagento") Тогда

			НужнаРегистрация = НЕ Источник.ДополнительныеСвойства.ОтключитьМеханизмРегистрацииРозницаMagento = Истина;

	Иначе	НужнаРегистрация = Истина;

	КонецЕсли;
	
	Если НужнаРегистрация Тогда

		НужнаРегистрация = ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() И ОбменMagentoПовтИсп.ОбменАктивен();

		Если НужнаРегистрация И НЕ ПривилегированныйРежим() Тогда

			УстановитьПривилегированныйРежим(Истина);

		КонецЕсли;

	КонецЕсли;

	Возврат НужнаРегистрация;

КонецФункции

#КонецОбласти











