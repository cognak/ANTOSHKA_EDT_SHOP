///////////////////////////////////////////////////////////////////////////////////////////////////
// БЛОК ЭКСПОРТНЫХ ФУНКЦИЙ

// Обработчик события "ПередЗаписьюОбъекта".
// Проверяет установленность свойства "ИзменениеРеквизитов".
// Если свойство не установлено - объект не записывается.
//
Процедура ЗапретРедактированияРеквизитовОбъектовПередЗаписьюОбъекта(Источник, Отказ) Экспорт
	
	Если ЗначениеЗаполнено(Источник.Ссылка)
	   И НЕ Источник.ОбменДанными.Загрузка Тогда
		
		ПараметрыЗапретаРедактирования = Неопределено;
		Источник.ДополнительныеСвойства.Свойство("__ПараметрыЗапретаРедактирования", ПараметрыЗапретаРедактирования);
		
		Если ПроверитьВсеРеквизитыРазрешеныДляРедактирования(Источник, ПараметрыЗапретаРедактирования) Тогда
			ТребуетсяПоискСсылокВИБ = Ложь;
		Иначе
			ТребуетсяПоискСсылокВИБ = НЕ ПроверитьИзменениеРеквизитовОбъекта(Источник, ПараметрыЗапретаРедактирования);
		КонецЕсли;
		
		Если ТребуетсяПоискСсылокВИБ И ЗапретРедактированияРеквизитовОбъектов.ПроверитьНаОбъектИмеютсяСсылкиВИБ(Источник.Ссылка) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'Попытка изменения реквизитов объекта, на который имеются ссылки в информационной базе'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Запрет редактирования реквизитов объекта'"),
									УровеньЖурналаРегистрации.Ошибка, , ,
									ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// БЛОК ВСПОМОГАТЕЛЬНЫХ ФУНКЦИЙ

Функция ПроверитьИзменениеРеквизитовОбъекта(знач Объект, знач ПараметрыЗапретаРедактирования)
	
	ИмяТипаОбъекта = Объект.Ссылка.Метаданные().Имя;
	
	Если ПараметрыЗапретаРедактирования = Неопределено Тогда
		ПараметрыЗапретаРедактирования = Новый Соответствие;
	КонецЕсли;
	
	ПроверяемыеРеквизитыМассив = Новый Массив;
	
	БлокируемыеРеквизитыМассив = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Ссылка).ПолучитьБлокируемыеРеквизитыОбъекта();
	
	Для Каждого БлокируемыйРеквизит Из БлокируемыеРеквизитыМассив Цикл
		
		ИмяРеквизита = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(БлокируемыйРеквизит, ";")[0];
		
		РедактированиеРазрешено = ПараметрыЗапретаРедактирования.Получить(ИмяРеквизита);
		Если РедактированиеРазрешено = Неопределено ИЛИ РедактированиеРазрешено = Ложь Тогда
			ПроверяемыеРеквизитыМассив.Добавить(ИмяРеквизита);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПроверяемыеРеквизитыМассив.Количество() > 0 Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Объект.Ссылка,
			СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(ПроверяемыеРеквизитыМассив));
		Для Каждого ИмяРеквизита Из ПроверяемыеРеквизитыМассив Цикл
			Если Объект.Метаданные().ТабличныеЧасти.Найти(ИмяРеквизита) <> Неопределено Тогда
				ТаблицаПредыдущая = ЗначенияРеквизитов[ИмяРеквизита].Выгрузить();
				ТаблицаПредыдущая.Колонки.Удалить("Ссылка");
				ТаблицаЗаписываемая = Объект[ИмяРеквизита].Выгрузить();
				
				ТаблицаСравнения = ПолучитьТаблицуСравнения(ТаблицаПредыдущая.Колонки);
				СкопироватьТаблицу(ТаблицаСравнения, ТаблицаПредыдущая, -1);
				СкопироватьТаблицу(ТаблицаСравнения, ТаблицаЗаписываемая, +1);
				
				КолонкиСравнения = "";
				
				Для Каждого ОписаниеКолонки Из ТаблицаСравнения.Колонки Цикл
					Если ОписаниеКолонки.Имя = "_Признак" Тогда
						Продолжить;
					КонецЕсли;
					КолонкиСравнения = ОписаниеКолонки.Имя + "," + КолонкиСравнения;
				КонецЦикла;
				
				КолонкиСравнения = Лев(КолонкиСравнения, СтрДлина(КолонкиСравнения)-1);
				
				ТаблицаСравнения.Свернуть(КолонкиСравнения, "_Признак");
				Если ТаблицаСравнения.НайтиСтроки(Новый Структура("_Признак", -1)).Количество() > 0
				 ИЛИ ТаблицаСравнения.НайтиСтроки(Новый Структура("_Признак", +1)).Количество() > 0 Тогда
					
					Возврат Ложь;
					
				КонецЕсли;
				
			Иначе
				Если Объект[ИмяРеквизита] <> ЗначенияРеквизитов[ИмяРеквизита] Тогда
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПроверитьВсеРеквизитыРазрешеныДляРедактирования(знач Источник, знач ПараметрыЗапретаРедактирования)
	
	Если ПараметрыЗапретаРедактирования = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МенеджерОМД = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Источник.Ссылка);
	БлокируемыеРеквизиты = МенеджерОМД.ПолучитьБлокируемыеРеквизитыОбъекта();
	
	Для Каждого БлокируемыйРеквизит Из БлокируемыеРеквизиты Цикл
		
		ИмяРеквизита = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(БлокируемыйРеквизит, ";")[0];
		
		РазрешеноРедактирование = ПараметрыЗапретаРедактирования.Получить(ИмяРеквизита);
		
		Если РазрешеноРедактирование = Неопределено ИЛИ РазрешеноРедактирование = Ложь Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьТаблицуСравнения(Колонки)
	
	Таблица = Новый ТаблицаЗначений;
	
	Для Каждого ОписаниеКолонки Из Колонки Цикл
		Таблица.Колонки.Добавить(ОписаниеКолонки.Имя, ОписаниеКолонки.ТипЗначения);
	КонецЦикла;
	
	Таблица.Колонки.Добавить("_Признак",
							Новый ОписаниеТипов("Число", , Новый КвалификаторыЧисла(1,0,ДопустимыйЗнак.Любой)));
	
	Возврат Таблица;
	
КонецФункции

Функция СкопироватьТаблицу(ТаблицаПриемник, ТаблицаИсточник, Признак)
	
	Для Каждого ЭлементСтрока Из ТаблицаИсточник Цикл
		
		НоваяСтрока = ТаблицаПриемник.Добавить();
		
		Для Каждого ОписаниеКолонки Из ТаблицаПриемник.Колонки Цикл
			Если ОписаниеКолонки.Имя = "_Признак" Тогда
				НоваяСтрока["_Признак"] = Признак;
				Продолжить;
			КонецЕсли;
			НоваяСтрока[ОписаниеКолонки.Имя] = ЭлементСтрока[ОписаниеКолонки.Имя];
		КонецЦикла;
		
	КонецЦикла;
	
КонецФункции
