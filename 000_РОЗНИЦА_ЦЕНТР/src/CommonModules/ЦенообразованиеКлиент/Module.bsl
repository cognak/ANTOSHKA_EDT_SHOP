////////////////////////////////////////////////////////////////////////////////
// Модуль "ЦенообразованиеКлиент", содержит процедуры и функции для 
// обработки действий пользователя в процессе работы с ценами
//

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПРОВЕРКИ ВОЗМОЖНОСТИ ВЫПОЛНЕНИЯ ДЕЙСТВИЙ

// Проверяет заполненность реквизитов, необходимых для пересчета из валюты в валюту
//
// Параметры:
// Документ - ДокументОбъект, для которого выполняются проверки
// СтараяВалюта - Предыдущая валюта документа
//
// Возвращаемое значение:
// Булево
// Ложь, если необходимые данные не заполнены
//
Функция ПроверитьНеобходимостьПересчетаВВалюту(Документ,СтараяВалюта) Экспорт

	Если Не ЗначениеЗаполнено(Документ.Валюта) Тогда
		Возврат Ложь;
	ИначеЕсли Не ЗначениеЗаполнено(СтараяВалюта) Тогда
		Возврат Ложь;
	ИначеЕсли СтараяВалюта = Документ.Валюта Тогда
		Возврат Ложь;
	ИначеЕсли Документ.Товары.Итог("Цена") = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru='Пересчитать суммы в документе в валюту ""%Валюта%""?'");
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Валюта%", Документ.Валюта);
	
	ОтветНаВопрос = Вопрос(ТекстСообщения,РежимДиалогаВопрос.ДаНет);

	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции // ПроверитьНеобходимостьПересчетаВВалюту()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОПОВЕЩЕНИЯ ПОЛЬЗОВАТЕЛЯ О ВЫПОЛНЕННЫХ ДЕЙСТВИЯХ

//Показывает оповещение пользователя об окончании пересчета сумм из валюты в валюту
//
// Параметры:
// ВалютаИсточник - СправочникСсылка.Валюты - валюта, из которой осуществлялся пересчет
// ВалютаПриемник - СправочникСсылка.Валюты - валюта, в которую осуществляется пересчет
//
Процедура ОповеститьОбОкончанииПересчетаСуммВВалюту(ВалютаИсточник, ВалютаПриемник) Экспорт

	СтрокаСообщения = НСтр("ru='Суммы в документе пересчитаны из валюты %ВалютаИсточник% в валюту %ВалютаПриемник%'");
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%ВалютаИсточник%", ВалютаИсточник);
	СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%ВалютаПриемник%", ВалютаПриемник);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Суммы пересчитаны'"),
		,
		СтрокаСообщения,
		БиблиотекаКартинок.Информация32
	);

КонецПроцедуры // ОповеститьОбОкончанииПересчетаСуммВВалюту()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПРОВЕРКИ ЗАПОЛНЕНИЯ ТЧ ДОКУМЕНТОВ

//Процедура передает управление серверной процедуре, формирующей список упаковок
//Параметры:
//ДанныеВыбора - Значение, в которое будет помещен список упаковок
//СтандартнаяОбработка - Булево
//ТекущаяСтрока - Текущие данные таблицы
//ДобавлятьПустуюУпаковку - Булево
Процедура ВыбратьУпаковкуНоменклатуры(ДанныеВыбора, СтандартнаяОбработка, ТекущаяСтрока, ДобавлятьПустуюУпаковку = Истина) Экспорт

	СтандартнаяОбработка = Ложь;

	Если ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		ДанныеВыбора = Новый СписокЗначений;
		Ценообразование.ПолучитьСписокДляВыбораУпаковок(ТекущаяСтрока.Номенклатура, ДанныеВыбора, Неопределено, ДобавлятьПустуюУпаковку);
	КонецЕсли;
	
КонецПроцедуры

//Процедура открывает форму справочника "Характеристики номенклатуры" с отбором по владельцу
//
Процедура ВыбратьХарактеристикуНоменклатуры(Форма, Элемент, СтандартнаяОбработка, ТекущаяСтрока) Экспорт

	СтандартнаяОбработка = Ложь;

	Если ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		ВладелецХарактеристики = Неопределено;
		Если Ценообразование.ПроверитьИспользованиеХарактеристикИПолучитьВладельцаДляВыбора(ТекущаяСтрока.Номенклатура, ВладелецХарактеристики) Тогда
			Если ВладелецХарактеристики = Неопределено Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Для данной номенклатуры характеристики не заданы.'"));
			Иначе
				ПараметрыФормыВыбора = Новый Структура;
				ПараметрыФормыВыбора.Вставить("ТекущийЭлемент"  , ТекущаяСтрока.Характеристика);
				ПараметрыФормыВыбора.Вставить("ПараметрВладелец", ВладелецХарактеристики);
				ПараметрыФормыВыбора.Вставить("Номенклатура"    , ТекущаяСтрока.Номенклатура);

				ОткрытьФорму("Справочник.ХарактеристикиНоменклатуры.ФормаВыбора", ПараметрыФормыВыбора, Элемент);
			КонецЕсли;
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Для данной номенклатуры отключено использование характеристик.'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры



