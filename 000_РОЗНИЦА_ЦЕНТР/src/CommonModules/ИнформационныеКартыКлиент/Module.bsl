
// Функция вызывает вопрос о подтверждении создания информационной карты
// Параметры
//		ТекстВопросаНовойКарты - Строка - текст вопроса пользователю
//		РежимРМКУправляемый - Булево - определяет, каким образом будет вызываться диалог вопроса:
//										встроенной функцией или специализированной формой.
// Возвращаемое значение
// 		РазрешеноСоздатьКарту - Булево - Истина, если пользователь подтвердил создание карты, Ложь - иначе
Функция ПолучитьРазрешениеПользователяНаСозданиеКарты(ТекстВопросаНовойКарты, РежимРМКУправляемый = Ложь) Экспорт
	
	РазрешеноСоздатьКарту = Ложь;
	
	Если НЕ ЗначениеЗаполнено(ТекстВопросаНовойКарты) Тогда
		ТекстВопросаНовойКарты = НСтр("ru = 'Создать новую информационную карту?'")
	КонецЕсли;
	
	Если РежимРМКУправляемый Тогда
		
		ОтветПользователя = ОбщегоНазначенияРТКлиент.ВывестиВопросДляРМК(ТекстВопросаНовойКарты);
		
		Если ВРЕГ(ОтветПользователя) = "ДА" Тогда
			РазрешеноСоздатьКарту = Истина;
		КонецЕсли;
		
	Иначе
		Если Вопрос(ТекстВопросаНовойКарты, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			РазрешеноСоздатьКарту = Истина;
		КонецЕсли;
	КонецЕсли;
	
	
	Возврат РазрешеноСоздатьКарту;
	
КонецФункции 

// Функция подготавливает текст оповещения о создании дисконтной карты
// и вызывает окно оповещения пользователя
// Параметры
//		ИнформационнаяКарта - СправочникСсылка.ИнформационныеКарты - ссылка на созданную карту
// Возвращаемое значение
// 		нет
Функция ОповеститьОСозданииНовойКарты(ИнформационнаяКарта) Экспорт
	
	Если ЗначениеЗаполнено(ИнформационнаяКарта) Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Создана информационная карта'"),
			,
			СтрЗаменить(НСтр("ru = 'Создана информационная карта ""%Карта%""'"), "%Карта%", ИнформационнаяКарта),
			БиблиотекаКартинок.Информация32);
		
	КонецЕсли;	
		
КонецФункции	

//	LNK 09.10.2017 16:00:52
// Процедура подготавливает параметры открытия формы
// и открывает форму опроса владельца дисконтной карты
// если на указанную дату нужно проводить опрос
// Параметры
//		Карта - СправочникСсылка.ИнформационныеКарты - ссылка на проверяемую карту
//		ДатаСобытия - Дата - дата проверки
Процедура ПровестиОпросВладельца(Карта, Магазин, ДатаСобытия, ЭтотОбъект = Неопределено, ОписаниеОповещения, ОпросПользователюРазрешён = Неопределено, Принудительно = Ложь) Экспорт
	
	НуженОпрос = ИнформационныеКарты.НеобходимостьОпросаВладельца(Карта, ДатаСобытия, ОпросПользователюРазрешён);

	Если НуженОпрос ИЛИ Принудительно = Истина Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ИнформационнаяКарта", Карта);
		ПараметрыОткрытия.Вставить("Магазин", Магазин);
		
		ОткрытьФорму("Обработка.ДанныеПокупателя.Форма.ВерификацияДержателяКарты"
			, ПараметрыОткрытия
			, ЭтотОбъект
			, "ВерификацияДержателяКарты"
			,
			,
			, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс
		);
		
	КонецЕсли;

КонецПроцедуры

