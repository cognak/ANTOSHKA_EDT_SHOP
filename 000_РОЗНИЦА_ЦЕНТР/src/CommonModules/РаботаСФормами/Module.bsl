#Область УправлениеСортировкойТабличнойЧасти

Процедура ДобавитьКнопкиПорядкаКодАртикул(Форма, КоманднаяПанель, СписокКлючей = "")	Экспорт

	Если СписокКлючей = "" Тогда

		СписокКлючей = РаботаСФормамиПовтИсп.КлючиСортировки();

	КонецЕсли;

	ОписаниеКнопокФормы = Новый Массив;
	ДобавитьОписаниеКнопки("Код", "Код", ОписаниеКнопокФормы, СписокКлючей);
	ДобавитьОписаниеКнопки("Артикул", "Артикул", ОписаниеКнопокФормы, СписокКлючей);
	ДобавитьОписаниеКнопки("Производитель", "Производитель", ОписаниеКнопокФормы, СписокКлючей);
	ДобавитьОписаниеКнопки("ТорговаяМарка", "Торговая марка", ОписаниеКнопокФормы, СписокКлючей);
	ДобавитьОписаниеКнопки("ЦенаРазница", "Цена разница", ОписаниеКнопокФормы, СписокКлючей);

	СписокРеквизитов = Новый Массив;

	Для каждого ОписаниеКнопки Из ОписаниеКнопокФормы Цикл

	    КомандаФормы = Форма.Команды.Добавить(ОписаниеКнопки.Имя);
		КомандаФормы.ИзменяетСохраняемыеДанные = Истина;
	    КомандаФормы.Действие  = ОписаниеКнопки.Имя + "Нажатие";
		КомандаФормы.Заголовок = ОписаниеКнопки.Заголовок;
		КомандаФормы.Подсказка = ОписаниеКнопки.Подсказка;
		КомандаФормы.Картинка  = ОписаниеКнопки.Картинка;

	    КнопкаФормы  = Форма.Элементы.Добавить(ОписаниеКнопки.Имя, Тип("КнопкаФормы"), КоманднаяПанель);
		КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	    КнопкаФормы.ИмяКоманды  = ОписаниеКнопки.Имя;

		СписокРеквизитов.Добавить(Новый РеквизитФормы(ОписаниеКнопки.Имя, Новый ОписаниеТипов("Булево")));

	КонецЦикла;

	Форма.ИзменитьРеквизиты(СписокРеквизитов);

КонецПроцедуры

Процедура ДобавитьОписаниеКнопки(Имя, Заголовок, ОписаниеКнопокФормы, СписокКлючей)

	СоставНаправлений = Новый Массив;
	СоставНаправлений.Добавить("ПоВозрастанию");
	СоставНаправлений.Добавить("ПоУбыванию");

	Если РаботаСФормамиПовтИсп.СоответствиеКлючейСортировки(СписокКлючей).Получить(Имя) = Истина Тогда

		Для каждого Направление Из СоставНаправлений Цикл

			ОписаниеКнопокФормы.Добавить(ОписаниеКнопки(Имя, Направление, Заголовок));

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

Функция ОписаниеКнопки(Имя, Направление, Заголовок)

	ОписаниеКнопки = Новый Структура(
		"Имя", "ТоварыПорядок" + Имя + Направление);
	ОписаниеКнопки.Вставить("Заголовок", "Сортировать по «" + Заголовок + "» "
		+ ?(Направление = "ПоВозрастанию", " возрастание", "убывание"));
	ОписаниеКнопки.Вставить("Подсказка", "Упорядочить табличную часть в соответствии с указанным порядком");
	ОписаниеКнопки.Вставить("Картинка", ?(Направление = "ПоВозрастанию"
		, БиблиотекаКартинок.СортироватьСписокПоВозрастанию
		, БиблиотекаКартинок.СортироватьСписокПоУбыванию));

	Возврат ОписаниеКнопки;

КонецФункции // ОписаниеКнопки()

Процедура ТоварыСортироватьПоРеквизитуНоменклатуры(ИмяРеквизита, Направление, Товары)	Экспорт
	Если ИмяРеквизита = "ТорговаяМарка" тогда
		Запрос = Новый Запрос(СтрЗаменить(  		
		"ВЫБРАТЬ
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
		|ПОМЕСТИТЬ Источник
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Источник.НомерСтроки - 1 КАК Индекс,
		|	НоменклатураДополнительныеРеквизиты.Значение КАК Значение
		|ИЗ
		|	Источник КАК Источник
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
		|		ПО Источник.Номенклатура = НоменклатураДополнительныеРеквизиты.Ссылка
		|			И (НоменклатураДополнительныеРеквизиты.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ТорговыеМарки))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Значение УБЫВ"
		, "УБЫВ",  Направление)
		);
		
	ИначеЕсли ИмяРеквизита = "ЦенаРазница" тогда
		Запрос = Новый Запрос(СтрЗаменить(
		"ВЫБРАТЬ
		|	Товары.НомерСтроки,
		|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
		|	Товары.ЦенаНовая - Товары.Цена КАК ЦенаРазница
		|ПОМЕСТИТЬ Источник
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Источник.НомерСтроки - 1 КАК Индекс,
		|	Источник.Номенклатура,
		|	Источник.Номенклатура.Код КАК КлючПорядка,
		|	Источник.ЦенаРазница КАК ЦенаРазница
		|ИЗ
		|	Источник КАК Источник
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЦенаРазница  УБЫВ"
		, "УБЫВ",  Направление)
		);
	Иначе
		Запрос = Новый Запрос(СтрЗаменить(СтрЗаменить(
		"ВЫБРАТЬ
		|	Товары.НомерСтроки,
		|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
		|ПОМЕСТИТЬ Источник
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Источник.НомерСтроки - 1 КАК Индекс,
		|	Источник.Номенклатура,
		|	Источник.Номенклатура.Код КАК КлючПорядка
		|ИЗ
		|	Источник КАК Источник
		|
		|УПОРЯДОЧИТЬ ПО
		|	КлючПорядка УБЫВ"
		, ".Номенклатура.Код", ".Номенклатура." + ИмяРеквизита)
		, "КлючПорядка УБЫВ", "КлючПорядка " + Направление)
		);
	КонецЕсли;
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИндексПорядка = 0;

	Пока Выборка.Следующий() Цикл

		Товары[Выборка.Индекс].НомерСтрокиПорядок = ИндексПорядка;
		ИндексПорядка = ИндексПорядка + 1;

	КонецЦикла;
	
	Товары.Сортировать("НомерСтрокиПорядок");

КонецПроцедуры

#КонецОбласти




