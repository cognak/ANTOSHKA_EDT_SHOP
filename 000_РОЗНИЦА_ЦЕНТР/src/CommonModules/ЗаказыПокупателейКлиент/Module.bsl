#Область ФункционалЗапросДоступностиТоваров

Процедура КомандаИзменитьСтатусДокумента(ДокументСсылка, Форма, КлючФормы)	Экспорт  
	
	Если КлючФормы = "ФормаДокумента" Тогда
		
		Попытка
			ПрошлыйСтатус = Форма.Объект.СтатусЗапроса;
			ДанныеСостояния = ЗаказыПокупателейСервер.ПолучитьСледующийСтатусЗапросаДоступностиТоваров(Форма.Объект.Ссылка); 
			Форма.Объект.СтатусЗапроса = ДанныеСостояния;
			Форма.Модифицированность = Истина;
			Форма.Записать();
		Исключение
			Форма.Объект.СтатусЗапроса = ПрошлыйСтатус;
		КонецПопытки;
		Оповестить("ИзменилСледующийСтатусЗапроса");
		
	Иначе
		
		ДанныеСостояния = ЗаказыПокупателейСервер.ПолучитьСледующийСтатусЗапросаДоступностиТоваров(ДокументСсылка); 
		ЗаказыПокупателейСервер.УстановитьСостояниеЗапросаДоступностиТоваров(ДокументСсылка, ДанныеСостояния);
		
	КонецЕсли; 
	
	ОформитьКнопкуИзменениеСтатусаДокумента(ДокументСсылка, Форма, КлючФормы); 
	Оповестить("ЗаписанЗапросДоступностиТоваров");

КонецПроцедуры

Процедура ОформитьКнопкуИзменениеСтатусаДокумента(ДокументСсылка, Форма, КлючФормы)	Экспорт

	//Кнопка = Форма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаДокументЗапросДоступностиТоваровИзменениеСтатусаДокумента;
	Если Форма.Элементы.Найти("ФормаДокументЗапросДоступностиТоваровИзменениеСтатусаДокумента") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Кнопка = Форма.Элементы.ФормаДокументЗапросДоступностиТоваровИзменениеСтатусаДокумента;
	Кнопка.Видимость = Истина;

	Если ДокументСсылка = Неопределено
		Или Не ЗаказыПокупателейСервер.ДоступностьДляРедактирования(ДокументСсылка) Тогда

		Кнопка.Заголовок = "Нет действий";
		Кнопка.Доступность = Ложь;

	Иначе
		
		ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента();

		ЗапросДанногоМагазина = (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "МагазинОтправитель")
				 = ПараметрыРаботыКлиента.ТекущийМагазин);

		ИсключительныйРежим = ТехническаяПоддержкаВызовСервера.ИсключительныйРежим();
	
		Если КлючФормы = "ФормаСписка" Тогда
			
			СледующееСостояние = ЗаказыПокупателейСервер.ПолучитьСледующийСтатусЗапросаДоступностиТоваров(ДокументСсылка);
			
		Иначе 
			
			СледующееСостояние = ЗаказыПокупателейСервер.ПолучитьСледующийСтатусЗапросаДоступностиТоваров(Форма.Объект.Ссылка);
			
		КонецЕсли;
		
		Кнопка.Доступность = Истина; 
		
		Если СледующееСостояние = ПредопределенноеЗначение("Перечисление.СтатусыЗапросовДоступностиТоваровШапка.ВРаботе")
				И (ЗапросДанногоМагазина Или ИсключительныйРежим) Тогда 
			
			Кнопка.Заголовок = "Взять документ в работу";
			
		ИначеЕсли СледующееСостояние = ПредопределенноеЗначение("Перечисление.СтатусыЗапросовДоступностиТоваровШапка.Отменён")
				И (ЗапросДанногоМагазина Или ИсключительныйРежим) Тогда
					
			Кнопка.Заголовок = "Подтвердить отмену";
			
		ИначеЕсли СледующееСостояние = ПредопределенноеЗначение("Перечисление.СтатусыЗапросовДоступностиТоваровШапка.Согласован")
				И (ЗапросДанногоМагазина Или ИсключительныйРежим) Тогда
					
			Кнопка.Заголовок = "Согласовать документ";
			
		ИначеЕсли СледующееСостояние = ПредопределенноеЗначение("Перечисление.СтатусыЗапросовДоступностиТоваровШапка.ПустаяСсылка") Тогда 
			
			Кнопка.Заголовок = "Нет действий";
			Кнопка.Доступность = Ложь;
			
		ИначеЕсли СледующееСостояние = ПредопределенноеЗначение("Перечисление.СтатусыЗапросовДоступностиТоваровШапка.КОтправке")
				И (ЗапросДанногоМагазина Или ИсключительныйРежим) Тогда
					
			Кнопка.Заголовок = "Заказ собран";
			
		ИначеЕсли СледующееСостояние = ПредопределенноеЗначение("Перечисление.СтатусыЗапросовДоступностиТоваровШапка.Получен") Тогда
					
			Кнопка.Заголовок = "Подтвердить получение";
			
		ИначеЕсли СледующееСостояние = ПредопределенноеЗначение("Перечисление.СтатусыЗапросовДоступностиТоваровШапка.Выдан") Тогда 
			
			Кнопка.Заголовок = "Выдать заказ";
			
		ИначеЕсли СледующееСостояние = ПредопределенноеЗначение("Перечисление.СтатусыЗапросовДоступностиТоваровШапка.ВыданДоставке") Тогда 
			
			Кнопка.Заголовок = "Выдать на доставку";
			
		Иначе

			Кнопка.Заголовок = "Нет действий";
			Кнопка.Доступность = Ложь;
			
		КонецЕсли;

	КонецЕсли;
	
	Если ОбменДаннымиПовтИсп.ЭтоГлавныйУзел()
			И Не ТехническаяПоддержкаВызовСервера.ИсключительныйРежим() Тогда
		Кнопка.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры

Процедура ОформитьКнопкуСозданиеНаОснованииПеремещения(Форма)	Экспорт

	//Кнопка = Форма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаДокументЗапросДоступностиТоваровИзменениеСтатусаДокумента;
	Если Форма.Элементы.Найти("ФормаДокументПеремещениеТоваровСоздатьНаОсновании") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Кнопка = Форма.Элементы.ФормаДокументПеремещениеТоваровСоздатьНаОсновании;
	
	ИсключительныйРежим = ТехническаяПоддержкаВызовСервера.ИсключительныйРежим();

	Кнопка.Доступность = ИсключительныйРежим;
	Кнопка.Видимость = ИсключительныйРежим;

КонецПроцедуры

Процедура ОформитьКнопкуСозданиеНаОснованииЭлектроннойНакладной(Форма)	Экспорт

	//Кнопка = Форма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаДокументЗапросДоступностиТоваровИзменениеСтатусаДокумента;
	Если Форма.Элементы.Найти("ФормаДокументЭлектроннаяНакладнаяСоздатьНаОсновании") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Кнопка = Форма.Элементы.ФормаДокументЭлектроннаяНакладнаяСоздатьНаОсновании;

	ЭННаОсновании = (Форма.Объект.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусыЗапросовДоступностиТоваровШапка.КОтправке"))
		И (Форма.НомерТТН = ПредопределенноеЗначение("Документ.ЭлектроннаяНакладная.ПустаяСсылка"));
	ИсключительныйРежим = ТехническаяПоддержкаВызовСервера.ИсключительныйРежим();

	Кнопка.Доступность = ЭННаОсновании Или ИсключительныйРежим;
	Кнопка.Видимость = ЭННаОсновании Или ИсключительныйРежим;

КонецПроцедуры

#КонецОбласти












