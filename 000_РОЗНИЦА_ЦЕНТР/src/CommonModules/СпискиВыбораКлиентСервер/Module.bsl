
Процедура Загрузить(ИмяНастройки, СписокВыбора)Экспорт
	
	ИсторияПоиска = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(ИмяНастройки,);
	Если ИсторияПоиска <> Неопределено Тогда
		СписокВыбора.ЗагрузитьЗначения(ИсторияПоиска);
	КонецЕсли;
	
КонецПроцедуры

Процедура Сохранить(ИмяНастройки, СписокВыбора) Экспорт
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(ИмяНастройки, ,СписокВыбора.ВыгрузитьЗначения());
	
КонецПроцедуры // Сохранить()

Процедура ОбновитьСписокВыбора(СписокВыбора, ЭлементСписка, ПредставлениеЭлемента = "", РазмерСписка = 30) Экспорт
	
	#Если _ Тогда
	СписокВыбора = Новый СписокЗначений;
	#КонецЕсли

	// Удалим элемент из истории поиска если он там был
	НомерНайденногоЭлементаСписка = СписокВыбора.НайтиПоЗначению(ЭлементСписка);
	Пока НомерНайденногоЭлементаСписка <> Неопределено Цикл
		СписокВыбора.Удалить(НомерНайденногоЭлементаСписка);
		НомерНайденногоЭлементаСписка = СписокВыбора.НайтиПоЗначению(ЭлементСписка);
	КонецЦикла;

//	И поставим его на первое место
	СписокВыбора.Вставить(0, ЭлементСписка, ?(ПустаяСтрока(ПредставлениеЭлемента), СокрЛП(ЭлементСписка), ПредставлениеЭлемента));

	Пока СписокВыбора.Количество() > РазмерСписка Цикл

		СписокВыбора.Удалить(СписокВыбора.Количество() - 1);

	КонецЦикла;

КонецПроцедуры // ОбновитьСписокВыбора()

Процедура АвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	КоличествоНайденных = 0;
	Для каждого ЭлементСписка Из Элемент.СписокВыбора Цикл
		Если ЛЕВ(ВРег(ЭлементСписка.Значение),СтрДлина(СокрЛП(Текст))) = ВРег(СокрЛП(Текст)) Тогда
			ДанныеВыбора.Добавить(ЭлементСписка.Значение);
			КоличествоНайденных = КоличествоНайденных + 1;
			Если КоличествоНайденных > 7 Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // АвтоПодбор()