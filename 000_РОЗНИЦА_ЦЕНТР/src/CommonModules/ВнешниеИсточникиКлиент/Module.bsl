Функция ПодтвердитьРешениеУчетВNavision(Форма, ВнешнееОписаниеОповещения = Неопределено)	Экспорт

	Если ВнешнееОписаниеОповещения = Неопределено Тогда

		ОписаниеОповещения = Новый ОписаниеОповещения("ПодтвердитьРешениеУчетВNavisionПодтверждение"
							//	, ОбщегоНазначенияКлиентСервер.ОбщийМодуль("ВнешниеИсточникиКлиент"), Новый Структура("ЭтотОбъект", Форма))
								, ВнешниеИсточникиКлиент, Новый Структура("ЭтотОбъект", Форма))

	Иначе

		ОписаниеОповещения = ВнешнееОписаниеОповещения;

	КонецЕсли;

	ПоказатьВопрос(ОписаниеОповещения
		, "Документ будет учтён в КСУ (общей системе учёта). Любые изменения
		  |этого документа будут невозможны. Подтвердите своё решение:
		  |[Да]  - установить признак учёта (операция необратимая после записи документа)
		  |[Нет] - работа с документом будет возможна в соответствии с общими правилами"
		, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да
		, "Учесть в Navision?", КодВозвратаДиалога.Нет
	);

КонецФункции

Процедура ПодтвердитьРешениеУчетВNavisionПодтверждение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Если КодВозврата = КодВозвратаДиалога.Да Тогда

		ДополнительныеПараметры.ЭтотОбъект.РегистрацияПередачиВNavisionНаСервере();

	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьОповещениеПередачиДокументаВNavision(Источник, СсылкаНаОбъект, УчестьДокументВNavision)	Экспорт

	Оповестить("РегистрацияПередачиВNavision"
			, Новый Структура(
				"Объект, РегистрацияПередачиВNavision"
				, СсылкаНаОбъект
				, УчестьДокументВNavision)
			, Источник);

КонецПроцедуры

Процедура ПроверитьКнопкуУчестьВNavision(Форма, УстановкаРеквизитаФормы = Ложь, ОснованиеПередано = Истина)	Экспорт

	ВнешниеИсточникиСобытия.ПроверитьКнопкуУчестьВNavision(Форма, УстановкаРеквизитаФормы, ОснованиеПередано);

КонецПроцедуры

//	LNK 18.06.2023 06:13:52
Процедура ПодтвердитьПовторнуюВыгрузкуВNavision(Форма, ПроверитьДопустимость = Ложь)	Экспорт

	Если ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		ДополнительныеПараметры = Новый Структура(
			"Ссылка, ПроверитьДопустимость"
			, Форма.Объект.Ссылка
			, ПроверитьДопустимость
		);

		ОписаниеОповещения = Новый ОписаниеОповещения("ПодтвердитьПовторнуюВыгрузкуВNavisionПодтверждение", ВнешниеИсточникиКлиент, ДополнительныеПараметры);

		ПоказатьВопрос(ОписаниеОповещения
			, "Вы действительно хотите выполнить повторную выгрузку объекта?"
			, РежимДиалогаВопрос.ОКОтмена
			, 60
			, КодВозвратаДиалога.ОК
			, "Выгрузка объекта в NAV"
			, КодВозвратаДиалога.Отмена
		);

	Иначе

		ПоказатьОповещениеПользователя("Выполнение невозможно!"
			,
			, "Действие может быть выполнено только в центральном узле!"
			, БиблиотекаКартинок.Информация32
		);

	КонецЕсли;

КонецПроцедуры

//	LNK 18.06.2023 06:48:04
Процедура ПодтвердитьПовторнуюВыгрузкуВNavisionПодтверждение(КодВозврата, ДополнительныеПараметры)	Экспорт

	Если КодВозврата = КодВозвратаДиалога.ОК Тогда

		Если ДополнительныеПараметры.ПроверитьДопустимость = Истина Тогда

				Результат = ВнешниеИсточникиСобытия.ОбеспечитьПовторнуюПередачуВNavision(ДополнительныеПараметры.Ссылка, Истина);

		Иначе	Результат = Новый Структура("Ошибка", Ложь);

		КонецЕсли;

		Если НЕ Результат.Ошибка Тогда
			
			Результат = ВнешниеИсточникиСобытия.ОбеспечитьПовторнуюПередачуВNavision(ДополнительныеПараметры.Ссылка);

			Если Результат.Ошибка Тогда

				ПоказатьОповещениеПользователя("Ошибка очистки!"
					,
					, "Попытка очистки таблиц вызвала ошибку:" + Символы.ПС
					+ Результат.Описание + Символы.ПС
					+ "(все Ваши действия запротоколированны)"
					, БиблиотекаКартинок.Ошибка32
				);

			Иначе

				ПоказатьОповещениеПользователя("Выполнено!"
					,
					, "Транспортные таблицы NAV очищены, объект зарегистрирован для выгрузки!" + Символы.ПС
					+ "(все Ваши действия запротоколированны)"
					, БиблиотекаКартинок.RFDIОперацииВыполнены
				);

			КонецЕсли;

		Иначе

			ПоказатьОповещениеПользователя("Проверка не выполнена!"
				,
				, "Проверка допустимости очистки таблиц выявила невозможность этой операции:" + Символы.ПС
				+ Результат.Описание
				, БиблиотекаКартинок.Ошибка32
			);

		КонецЕсли;

	Иначе

		ПоказатьОповещениеПользователя("Отмена!",, "Действие выполнено не будет", БиблиотекаКартинок.Информация32);

	КонецЕсли;

КонецПроцедуры

















