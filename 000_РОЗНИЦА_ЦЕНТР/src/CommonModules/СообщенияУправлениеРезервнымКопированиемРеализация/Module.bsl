////////////////////////////////////////////////////////////////////////////////
// ОБЩАЯ РЕАЛИЗАЦИЯ ОБРАБОТКИ СООБЩЕНИЙ УПРАВЛЕНИЯ РЕЗЕРВНЫМ КОПИРОВАНИЕМ
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Обработка входящих сообщений с типом {http://www.1c.ru/SaaS/ManageZonesBackup/a.b.c.d}PlanZoneBackup
//
// Параметры:
//  КодОбластиДанных - число(7,0),
//  ИдентификаторРезервнойКопии - УникальныйИдентификатор,
//  МоментВремениРезервнойКопии - дата (дата и время),
//  Принудительно - булево, флаг принудительного создания резервной копии.
//
Процедура ПланироватьСозданиеРезервнойКопииОбласти(Знач КодОбластиДанных,
		Знач ИдентификаторРезервнойКопии, Знач МоментРезервнойКопии,
		Знач Принудительно) Экспорт
	
	ПараметрыВыгрузки = РезервноеКопированиеОбластейДанных.СоздатьПустыеПараметрыВыгрузки();
	ПараметрыВыгрузки.ОбластьДанных = КодОбластиДанных;
	ПараметрыВыгрузки.ИДКопии = ИдентификаторРезервнойКопии;
	ПараметрыВыгрузки.МоментЗапуска = МестноеВремя(МоментРезервнойКопии, // !Перевести универсальное в местное -
		РаботаВМоделиСервиса.ПолучитьЧасовойПоясОбластиДанных(КодОбластиДанных)); // в очередях на вход должно поступать местное.
	ПараметрыВыгрузки.Принудительно = Принудительно;
	ПараметрыВыгрузки.ПоТребованию = Ложь;
	
	РезервноеКопированиеОбластейДанных.ЗапланироватьАрхивациюВОчереди(ПараметрыВыгрузки);
	
КонецПроцедуры

// Обработка входящих сообщений с типом {http://www.1c.ru/SaaS/ManageZonesBackup/a.b.c.d}CancelZoneBackup
//
// Параметры:
//  КодОбластиДанных - число(7,0),
//  ИдентификаторРезервнойКопии - УникальныйИдентификатор.
//
Процедура ОтменитьСозданиеРезервнойКопииОбласти(Знач КодОбластиДанных, Знач ИдентификаторРезервнойКопии) Экспорт
	
	ПараметрыОтмены = Новый Структура("ОбластьДанных, ИДКопии", КодОбластиДанных, ИдентификаторРезервнойКопии);
	РезервноеКопированиеОбластейДанных.ОтменитьСозданиеРезервнойКопииОбласти(ПараметрыОтмены);
	
КонецПроцедуры