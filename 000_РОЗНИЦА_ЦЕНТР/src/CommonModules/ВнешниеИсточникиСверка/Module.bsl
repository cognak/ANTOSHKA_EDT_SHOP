#Область ПрограммныйИнтерфейсМодуля

//	LNK 11.06.2017 08:28:48
Процедура НазначитьДокументДляСверки(СсылкаНаОбъект, Параметры = Неопределено, Примечание = "")	Экспорт

	Если НЕ ПривилегированныйРежим() Тогда

		УстановитьПривилегированныйРежим(Истина);

	КонецЕсли;

	ТипЗначения = ТипЗнч(СсылкаНаОбъект);

	Если ТипЗначения = Тип("Строка") И НЕ ПустаяСтрока(СсылкаНаОбъект) Тогда

	//	При внешнем запросе на сверку из Navision в нашей базе такого документа может и не оказаться!
	//	В этом случае будет передана строка вида "IDN#ВидДокумента"
		IDN = ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(Параметры, "IDN", ОбщегоНазначенияКлиентСервер.lx_GetTooken(СсылкаНаОбъект, "#", 1));

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);

	Иначе

		МетаданныеСсылки  = ВнешниеИсточникиПовтИсп.ТипыСверкиNavision().Получить(ТипЗначения);

		Если МетаданныеСсылки = Неопределено Тогда

			Возврат;

		КонецЕсли;

	//	Отработаем на всякий случай предохранитель... мало ли, где-то не будет идентификатора!
		Если НЕ (ОбщегоНазначенияПовтИсп.ЕстьОбщийРеквизит("IDN", МетаданныеСсылки.ПолноеИмя()) ИЛИ ОбщегоНазначенияРТ.ЕстьРеквизитОбъекта("IDN", МетаданныеСсылки)) Тогда

			Возврат;

		КонецЕсли;

		Запрос = Новый Запрос(СтрЗаменить(
		"ВЫБРАТЬ
		|	ТаблицаДокументы.IDN
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК ТаблицаДокументы
		|ГДЕ
		|	ТаблицаДокументы.Ссылка = &СсылкаНаОбъект
		|	И ТаблицаДокументы.IDN <> """""
		, "Документ.ПеремещениеТоваров", "Документ." + МетаданныеСсылки.Имя)
		);
		Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
		
		Выборка = Запрос.Выполнить().Выбрать();
	
		Если Выборка.Следующий() Тогда

				IDN = Выборка.IDN;
		Иначе	IDN = ""

		КонецЕсли;

	КонецЕсли;

//	\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\/
	
	Если НЕ ПустаяСтрока(IDN) Тогда

		СписокМагазинов  = ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(Параметры, "Магазины", Неопределено);
		РезультатСверки  = ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(Параметры, "РезультатСверки", Перечисления.РезультатСверкиNavision.НеПроверялся);
		ВидДокумента     = ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(Параметры, "ВидДокумента", "");
		СообщитьNavision = ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(Параметры, "СообщитьNavision", Неопределено);
		ДополнительнаяИнформация = ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(Параметры, "ДополнительнаяИнформация", Неопределено);

	//	---------------------------------------------------------------------------------------
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Магазины.Ссылка КАК Магазин
		|ИЗ
		|	Справочник.Магазины КАК Магазины
		|ГДЕ
		|	Магазины.Ссылка В(&СписокМагазинов)
		|	И (&ПринятьЛюбойМагазин = ИСТИНА
		|			ИЛИ НЕ(Магазины.СкладУправляющейСистемы
		|					ИЛИ Магазины.Блокирован))
		|	И НЕ &ПоВсемМагазинам = ИСТИНА
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
		|ГДЕ
		|	&ПоВсемМагазинам = ИСТИНА"
		;
		Запрос.УстановитьПараметр("СписокМагазинов", ?(ТипЗнч(СписокМагазинов) = Тип("Массив"), СписокМагазинов, ОбщегоНазначенияКлиентСервер.AAD(СписокМагазинов)));
		Запрос.УстановитьПараметр("ПоВсемМагазинам", СписокМагазинов = Неопределено);
		Запрос.УстановитьПараметр("ПринятьЛюбойМагазин", ТипЗначения = Тип("Строка"));	//	Когда в нашей базе нет документа, а есть только внешний IDN

		Выборка = Запрос.Выполнить().Выбрать();
		
		Если НЕ ТранзакцияАктивна() И НЕ Выборка.Количество() = 0 Тогда

				НачатьТранзакцию();
		//		Флаг, определяющий использование "локальной" транзакции (вызов не из общей транзакции, например, из обработчика "ПриЗаписи")
				ТранзакцияИспользуется = Истина;

		Иначе	ТранзакцияИспользуется = Ложь;

		КонецЕсли;

		Попытка

			Пока Выборка.Следующий() Цикл

				МенеджерЗаписи = РегистрыСведений.СоответствиеДокументовNavision.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.Объект  = СсылкаНаОбъект;
				МенеджерЗаписи.Магазин = Выборка.Магазин;

				МенеджерЗаписи.Прочитать();

				Если МенеджерЗаписи.Магазин.Пустая() Тогда

				//	Такая запись еще не существует...
					МенеджерЗаписи.Объект  = СсылкаНаОбъект;
					МенеджерЗаписи.Магазин = Выборка.Магазин;

				КонецЕсли;

				МенеджерЗаписи.IDN = IDN;
				МенеджерЗаписи.РезультатСверки = РезультатСверки;
				МенеджерЗаписи.ДатаОбновления  = ТекущаяДатаСеанса();

				Если НЕ ПустаяСтрока(ВидДокумента) Тогда

					МенеджерЗаписи.ВидДокумента = ВидДокумента;

				КонецЕсли;

				Если НЕ СообщитьNavision = Неопределено Тогда

					МенеджерЗаписи.СообщитьNavision = СообщитьNavision;

				КонецЕсли;

				Если НЕ ПустаяСтрока(Примечание) Тогда

					МенеджерЗаписи.Примечание = Примечание;

				КонецЕсли;

				Если МенеджерЗаписи.ДатаРегистрации = '00010101' Тогда

					МенеджерЗаписи.ДатаРегистрации = ТекущаяДатаСеанса();

				КонецЕсли;

				Если НЕ ДополнительнаяИнформация = Неопределено Тогда

					МенеджерЗаписи.ДополнительнаяИнформация = Новый ХранилищеЗначения(ДополнительнаяИнформация, Новый СжатиеДанных(9));

				КонецЕсли;

				МенеджерЗаписи.Записать(Истина);

			КонецЦикла;

			Если ТранзакцияИспользуется И ТранзакцияАктивна() Тогда

				ЗафиксироватьТранзакцию();

			КонецЕсли;

		Исключение

			ТекстОшибки = ОписаниеОшибки();

			Если ТранзакцияИспользуется И ТранзакцияАктивна() Тогда

				ОтменитьТранзакцию();

			КонецЕсли;

			ВызватьИсключение "При фиксации сверки с данными КСУ возникла ошибка:" + Символы.ПС + ТекстОшибки;

		КонецПопытки;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

//	LNK 20.06.2017 12:25:06
Процедура РегистрацияВыгрузкиВNavision(Объект, Магазин)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДанных.IDN
	|ИЗ
	|	РегистрСведений.СоответствиеДокументовNavision КАК ТаблицаДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеДокументовNavision.Изменения КАК ТаблицаРегистраций
	|		ПО (ТаблицаДанных.Объект = ТаблицаРегистраций.Объект
	|				И ТаблицаРегистраций.Узел ССЫЛКА ПланОбмена.ОбменNavisionРозница)
	|ГДЕ
	|	ТаблицаДанных.Объект = &Объект
	|	И ТаблицаДанных.Магазин = &Магазин
	|	И ТаблицаДанных.СообщитьNavision = ИСТИНА
	|	И ТаблицаРегистраций.Узел ЕСТЬ NULL "
	);
	Запрос.УстановитьПараметр("Объект" , Объект);
	Запрос.УстановитьПараметр("Магазин", Магазин);

	НаборЗаписей = РегистрыСведений.СоответствиеДокументовNavision.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Отбор.Магазин.Установить(Магазин);
	
	ДанныеВыборка = Запрос.Выполнить().Выбрать();
	
	Если ДанныеВыборка.Следующий() Тогда

		ПланыОбмена.ЗарегистрироватьИзменения(ВнешниеИсточникиПовтИсп.СписокУзловNavision(), НаборЗаписей);

	КонецЕсли;

	Для каждого Узел Из ВнешниеИсточникиПовтИсп.СписокУзловNavision() Цикл

		Если НЕ ПланыОбмена.ИзменениеЗарегистрировано(Узел, НаборЗаписей) Тогда

			ВызватьИсключение "Регистрация в узел «" + Узел + "» отсутствует!";

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

#Область НепосредственноеВыполнениеСверочныхОпераций

//	LNK 16.06.2017 08:52:50
Процедура ПровестиСверкуНазначенныхДокументов()	Экспорт

	Если НЕ ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		Возврат;

	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);

//	Для начала проверим документы, запрошенные на сверку из Navision, но, на тот момент,
//	отсутствующие в нашей базе данных.
//	=======================================================================================

	Запрос = Новый Запрос(ТекстЗапросаПоискОтсутствующихОбъектов());
	ОбъектыВыборка = Запрос.Выполнить().Выбрать();

	Пока ОбъектыВыборка.Следующий() Цикл

		НачатьТранзакцию();

		Попытка

		//	Удаляем текущую запись регистра с текстовым значением объекта...

			МенеджерЗаписи = РегистрыСведений.СоответствиеДокументовNavision.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Объект  = ОбъектыВыборка.ОбъектУдалить;
			МенеджерЗаписи.Магазин = ОбъектыВыборка.МагазинУдалить;

			МенеджерЗаписи.Удалить();

		//	Добавляем новую запись в регистр, уже "полноценную".

			ПараметрыСверки = Новый Структура(
				"Магазины, РезультатСверки, ВидДокумента"
				, ОбъектыВыборка.Магазин
				, ОбъектыВыборка.РезультатСверки
				, ОбъектыВыборка.ВидДокумента);

			НазначитьДокументДляСверки(ОбъектыВыборка.Объект, ПараметрыСверки, "Восстановлен запрос из Navision");

			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();

			ТекстОшибки = ОписаниеОшибки();
			ЗаписьЖурналаРегистрации("ВыполнениеСверкиNavision.ПоискОтсутствующих", УровеньЖурналаРегистрации.Предупреждение,,, ТекстОшибки);

		КонецПопытки;

	КонецЦикла;

//	Выполняем сверку документов.
//	=======================================================================================

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаСоответствий.Объект КАК Объект,
	|	ТаблицаСоответствий.Магазин КАК Магазин,
	|	ТаблицаСоответствий.IDN КАК IDN,
	|	ТаблицаСоответствий.ДатаРегистрации КАК ДатаРегистрации,
	|	ЕСТЬNULL(ТаблицаУточнения.ВозвратПоставщику, ЛОЖЬ) КАК ВозвратПоставщику,
	|	ТаблицаСоответствий.РезультатСверки КАК РезультатСверки,
	|	ТаблицаСоответствий.Примечание КАК Примечание
	|ИЗ
	|	РегистрСведений.СоответствиеДокументовNavision КАК ТаблицаСоответствий
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК ТаблицаУточнения
	|		ПО ТаблицаСоответствий.Объект = ТаблицаУточнения.Ссылка
	|ГДЕ
	|	НЕ ТИПЗНАЧЕНИЯ(ТаблицаСоответствий.Объект) В (ТИП(СТРОКА), ТИП(Документ.КассовыйДень))
	|	И ТаблицаСоответствий.РезультатСверки В (ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеПроверялся), ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.ОтсутствуетРозница), ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.ОтсутствуетNavision))
	|	И ТаблицаСоответствий.ДатаРегистрации >= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ), НЕДЕЛЯ, -2)
	|	И ВЫБОР
	|			КОГДА &IDN = """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ТаблицаСоответствий.IDN = &IDN
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаРегистрации УБЫВ"
	);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	Запрос.УстановитьПараметр("IDN", "");	//	отладочный параметр
//	|	И ТаблицаСоответствий.РезультатСверки = ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеСоответствует)
//	Кассовый день на текущий момент отключим, так как нет соответствия в Navision

	ОбъектыВыборка = Запрос.Выполнить().Выбрать();

	Счётчик = 0;	//	отладочная переменная

	Пока ОбъектыВыборка.Следующий() Цикл

		ВидОперации = ВнешниеИсточникиПовтИсп.ВидыДокументовСверкиNavision().Получить(
			ОбъектыВыборка.Объект.Метаданные().ПолноеИмя() + ?(ОбъектыВыборка.ВозвратПоставщику, ".Возврат", ""));

		Если ВидОперации = Неопределено Тогда

			ЗаписьЖурналаРегистрации("ВыполнениеСверкиNavision.ВидОперации", УровеньЖурналаРегистрации.Предупреждение
				, ОбъектыВыборка.Объект.Метаданные(), ОбъектыВыборка.Объект, "Не удалось определить операцию Navision!");
			Продолжить;

		Иначе

			ПроверитьСоответствиеДокументаNavision(ОбъектыВыборка, ВидОперации);

		КонецЕсли;

		Счётчик = Счётчик + 1;

	КонецЦикла;

КонецПроцедуры

//	LNK 16.06.2017 12:13:16
Процедура ПроверитьСоответствиеДокументаNavision(ВедущаяВыборка, ВидОперации)

//	Первым делом определим документ для сверки (для перемещений - это ордер) и признак
//	отправки этой ссылки в Navision.

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Данные.Объект,
	|	ВЫБОР
	|		КОГДА Данные.Объект ССЫЛКА Документ.ПеремещениеТоваров
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходныйОрдер.Ссылка ЕСТЬ НЕ NULL 
	|						ТОГДА ПриходныйОрдер.Ссылка
	|					КОГДА РасходныйОрдер.Ссылка ЕСТЬ НЕ NULL 
	|						ТОГДА РасходныйОрдер.Ссылка
	|					ИНАЧЕ НЕОПРЕДЕЛЕНО
	|				КОНЕЦ
	|		ИНАЧЕ Данные.Объект
	|	КОНЕЦ КАК ОбъектСверки,
	|	Данные.Магазин КАК Магазин,
	|	Данные.IDN
	|ПОМЕСТИТЬ Источник
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаДокументы.Ссылка КАК Объект,
	|		ТаблицаДокументы.IDN КАК IDN,
	|		ВЫБОР
	|			КОГДА ТаблицаДокументы.МагазинОтправитель.СкладУправляющейСистемы
	|				ТОГДА ТаблицаДокументы.МагазинПолучатель
	|			ИНАЧЕ ТаблицаДокументы.МагазинОтправитель
	|		КОНЕЦ КАК Магазин
	|	ИЗ
	|		Документ.ПеремещениеТоваров КАК ТаблицаДокументы
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ПеремещениеТоваров)
	|		И ТаблицаДокументы.Ссылка = &Объект
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаДокументы.Ссылка,
	|		ТаблицаДокументы.IDN,
	|		ВЫБОР
	|			КОГДА ТаблицаДокументы.МагазинОтправитель.СкладУправляющейСистемы
	|				ТОГДА ТаблицаДокументы.МагазинПолучатель
	|			ИНАЧЕ ТаблицаДокументы.МагазинОтправитель
	|		КОНЕЦ
	|	ИЗ
	|		Документ.КорректировкаПеремещения КАК ТаблицаДокументы
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.КорректировкаПеремещения)
	|		И ТаблицаДокументы.Ссылка = &Объект
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаДокументы.Ссылка,
	|		ТаблицаДокументы.IDN,
	|		ТаблицаДокументы.Магазин
	|	ИЗ
	|		Документ.ЗаказПоставщику КАК ТаблицаДокументы
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ЗаказПоставщику)
	|		И ТаблицаДокументы.Ссылка = &Объект
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаДокументы.Ссылка,
	|		ТаблицаДокументы.IDN,
	|		ТаблицаДокументы.Магазин
	|	ИЗ
	|		Документ.ОприходованиеТоваров КАК ТаблицаДокументы
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ОприходованиеТоваров)
	|		И ТаблицаДокументы.Ссылка = &Объект
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаДокументы.Ссылка,
	|		ТаблицаДокументы.IDN,
	|		ТаблицаДокументы.Магазин
	|	ИЗ
	|		Документ.СписаниеТоваров КАК ТаблицаДокументы
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.СписаниеТоваров)
	|		И ТаблицаДокументы.Ссылка = &Объект
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаДокументы.Ссылка,
	|		ТаблицаДокументы.IDN,
	|		ТаблицаДокументы.Магазин
	|	ИЗ
	|		Документ.ПересортицаТоваров КАК ТаблицаДокументы
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ПересортицаТоваров)
	|		И ТаблицаДокументы.Ссылка = &Объект) КАК Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходныйОрдерНаТовары КАК ПриходныйОрдер
	|		ПО Данные.Объект = ПриходныйОрдер.ДокументОснование
	|			И (ПриходныйОрдер.Проведен)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары КАК РасходныйОрдер
	|		ПО Данные.Объект = РасходныйОрдер.ДокументОснование
	|			И (РасходныйОрдер.Проведен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Источник.Объект,
	|	Источник.ОбъектСверки,
	|	Источник.IDN,
	|	ВЫБОР
	|		КОГДА ТаблицаУчтенВNavision.УчтеноNavision ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Учтен
	|ИЗ
	|	Источник КАК Источник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектУчтенВNavision КАК ТаблицаУчтенВNavision
	|		ПО Источник.ОбъектСверки = ТаблицаУчтенВNavision.Объект
	|			И Источник.Магазин = ТаблицаУчтенВNavision.Магазин"
	);
	Запрос.УстановитьПараметр("Объект", ВедущаяВыборка.Объект);
	
	ОбъектыВыборка = Запрос.Выполнить().Выбрать();
	
	Если ОбъектыВыборка.Следующий() И НЕ ОбъектыВыборка.ОбъектСверки = Неопределено Тогда

		Если ОбъектыВыборка.Учтен Тогда

			ДанныеNavision = ПолучитьДанныеNavisionДляСверки(ОбъектыВыборка.Объект, ОбъектыВыборка.IDN, ВидОперации);

			Если ПустаяСтрока(ДанныеNavision.Ошибка) Тогда

			//	Если НЕ ДанныеNavision.Шапка.Количество() = 0 И ДанныеNavision.Шапка[0].Учтен = 1 Тогда
			//	Результат сверки важен для Navision, так как показывает состояние документа в 1С для его учёта в Navision.
			//	Поэтому не учитываем флаг "Учтен" в Navision... а что дальше? после возможного изменения?
				Если НЕ ДанныеNavision.Шапка.Количество() = 0 Тогда

					ДанныеЛокальные = ПолучитьДанныеЛокальныеДляСверки(ОбъектыВыборка.ОбъектСверки);

					ДополнительнаяИнформация = "";
					РезультатСверки = СравнитьТаблицыТоваров(ДанныеNavision.Товары, ДанныеЛокальные.Товары, ДополнительнаяИнформация);

					Если НЕ ВедущаяВыборка.РезультатСверки = РезультатСверки Тогда

						ПараметрыСверки = Новый Структура(
							"Магазины, РезультатСверки, ВидДокумента, ДополнительнаяИнформация"
							, ВедущаяВыборка.Магазин
							, РезультатСверки
							, ВидОперации
							, ДополнительнаяИнформация);

						НачатьТранзакцию();

						Попытка	//	LNK 29.12.2021 10:41:28

							НазначитьДокументДляСверки(ОбъектыВыборка.Объект, ПараметрыСверки, "Сверка выполнена по [" + ОбъектыВыборка.ОбъектСверки + "] (операция [" + ВидОперации + "])");
							РегистрацияВыгрузкиВNavision(ОбъектыВыборка.Объект, ВедущаяВыборка.Магазин);

							ЗафиксироватьТранзакцию();

						Исключение

							ТекстОшибки = ОписаниеОшибки();
							ОтменитьТранзакцию();

						КонецПопытки;

					КонецЕсли;

				Иначе

					Если НЕ ВедущаяВыборка.РезультатСверки = Перечисления.РезультатСверкиNavision.ОтсутствуетNavision Тогда

						ПараметрыСверки = Новый Структура(
							"Магазины, РезультатСверки, ВидДокумента"
							, ВедущаяВыборка.Магазин
							, Перечисления.РезультатСверкиNavision.ОтсутствуетNavision
							, ВидОперации);

						НазначитьДокументДляСверки(ОбъектыВыборка.Объект, ПараметрыСверки, "IDN [" + ОбъектыВыборка.IDN + "] (операция [" + ВидОперации + "]) в Navision не обнаружен!");

					КонецЕсли;

				КонецЕсли;

			Иначе

				ЖурналСобытий.Регистрация("NAV.Сверка.error", УровеньЖурналаРегистрации.Ошибка
					, Метаданные.РегистрыСведений.СоответствиеДокументовNavision
					, ОбъектыВыборка.Объект
					,
					, ДанныеNavision.Ошибка
					,
					, Ложь
				);

			КонецЕсли;

		Иначе

			Если НЕ ВедущаяВыборка.РезультатСверки = Перечисления.РезультатСверкиNavision.НеГотовКСверке Тогда

				ПараметрыСверки = Новый Структура(
					"Магазины, РезультатСверки, ВидДокумента"
					, ВедущаяВыборка.Магазин
					, Перечисления.РезультатСверкиNavision.НеГотовКСверке
					, ВидОперации);

				НачатьТранзакцию();

				Попытка	//	LNK 29.12.2021 10:41:28

					НазначитьДокументДляСверки(ОбъектыВыборка.Объект, ПараметрыСверки, "Объект [" + ОбъектыВыборка.ОбъектСверки + "] не проведен либо не отправлен в Navision!");
					РегистрацияВыгрузкиВNavision(ОбъектыВыборка.Объект, ВедущаяВыборка.Магазин);

					ЗафиксироватьТранзакцию();

				Исключение

					ТекстОшибки = ОписаниеОшибки();
					ОтменитьТранзакцию();

				КонецПопытки;

			КонецЕсли;

		КонецЕсли;

	ИначеЕсли ОбъектыВыборка.ОбъектСверки = Неопределено Тогда

		Если ТипЗнч(ОбъектыВыборка.Объект) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда

			Примечание = "Вероятно, отсутствует товарный ордер - предмет сверки не обнаружен! Проверка не выполнена.";

			Если НЕ ВедущаяВыборка.Примечание = Примечание Тогда

				ПараметрыСверки = Новый Структура(
					"Магазины, РезультатСверки, ВидДокумента, ДополнительнаяИнформация"
					, ВедущаяВыборка.Магазин
					, ВедущаяВыборка.РезультатСверки
					, ВидОперации
					, ДополнительнаяИнформация);

				НазначитьДокументДляСверки(ОбъектыВыборка.Объект, ПараметрыСверки, Примечание);

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

//	LNK 16.06.2017 10:10:03
Функция ТекстЗапросаПоискОтсутствующихОбъектов()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаСоответствий.Объект,
	|	ТаблицаСоответствий.Магазин,
	|	ТаблицаСоответствий.IDN КАК IDN,
	|	ТаблицаСоответствий.ВидДокумента КАК ВидДокумента,
	|	ТаблицаСоответствий.ДатаРегистрации КАК КлючСортировки
	|ПОМЕСТИТЬ Фильтр
	|ИЗ
	|	РегистрСведений.СоответствиеДокументовNavision КАК ТаблицаСоответствий
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ТаблицаСоответствий.Объект) = ТИП(СТРОКА)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	IDN,
	|	ВидДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Фильтр.Объект КАК ОбъектУдалить,
	|	Фильтр.Магазин КАК МагазинУдалить,
	|	ТаблицаДокументы.Ссылка КАК Объект,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументы.МагазинОтправитель.СкладУправляющейСистемы
	|			ТОГДА ТаблицаДокументы.МагазинПолучатель
	|		ИНАЧЕ ТаблицаДокументы.МагазинОтправитель
	|	КОНЕЦ КАК Магазин,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокументы.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеГотовКСверке)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеПроверялся)
	|	КОНЕЦ КАК РезультатСверки,
	|	Фильтр.IDN,
	|	Фильтр.ВидДокумента,
	|	Фильтр.КлючСортировки КАК КлючСортировки
	|ИЗ
	|	Фильтр КАК Фильтр
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК ТаблицаДокументы
	|		ПО Фильтр.IDN = ТаблицаДокументы.IDN
	|			И (Фильтр.ВидДокумента = ""ПЕРЕМЕЩ"")
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Фильтр.Объект КАК ОбъектУдалить,
	|	Фильтр.Магазин КАК МагазинУдалить,
	|	ТаблицаДокументы.Ссылка КАК Объект,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументы.МагазинОтправитель.СкладУправляющейСистемы
	|			ТОГДА ТаблицаДокументы.МагазинПолучатель
	|		ИНАЧЕ ТаблицаДокументы.МагазинОтправитель
	|	КОНЕЦ КАК Магазин,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокументы.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеГотовКСверке)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеПроверялся)
	|	КОНЕЦ КАК РезультатСверки,
	|	Фильтр.IDN,
	|	Фильтр.ВидДокумента,
	|	Фильтр.КлючСортировки КАК КлючСортировки
	|ИЗ
	|	Фильтр КАК Фильтр
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПеремещения КАК ТаблицаДокументы
	|		ПО Фильтр.IDN = ТаблицаДокументы.IDN
	|			И (Фильтр.ВидДокумента = ""ПЕРЕМЕЩ"")
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Фильтр.Объект,
	|	Фильтр.Магазин,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Магазин,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокументы.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеГотовКСверке)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеПроверялся)
	|	КОНЕЦ,
	|	Фильтр.IDN,
	|	Фильтр.ВидДокумента,
	|	Фильтр.КлючСортировки
	|ИЗ
	|	Фильтр КАК Фильтр
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК ТаблицаДокументы
	|		ПО Фильтр.IDN = ТаблицаДокументы.IDN
	|			И (ТаблицаДокументы.ВозвратПоставщику = ЛОЖЬ)
	|			И (Фильтр.ВидДокумента = ""ПОКУПКА"")
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Фильтр.Объект,
	|	Фильтр.Магазин,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Магазин,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокументы.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеГотовКСверке)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеПроверялся)
	|	КОНЕЦ,
	|	Фильтр.IDN,
	|	Фильтр.ВидДокумента,
	|	Фильтр.КлючСортировки
	|ИЗ
	|	Фильтр КАК Фильтр
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК ТаблицаДокументы
	|		ПО Фильтр.IDN = ТаблицаДокументы.IDN
	|			И (ТаблицаДокументы.ВозвратПоставщику = ИСТИНА)
	|			И (Фильтр.ВидДокумента = ""ВЗВР.ПОКУП"")
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Фильтр.Объект,
	|	Фильтр.Магазин,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Магазин,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокументы.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеГотовКСверке)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеПроверялся)
	|	КОНЕЦ,
	|	Фильтр.IDN,
	|	Фильтр.ВидДокумента,
	|	Фильтр.КлючСортировки
	|ИЗ
	|	Фильтр КАК Фильтр
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОприходованиеТоваров КАК ТаблицаДокументы
	|		ПО Фильтр.IDN = ТаблицаДокументы.IDN
	|			И (Фильтр.ВидДокумента = ""ПРИХОД"")
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Фильтр.Объект,
	|	Фильтр.Магазин,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Магазин,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокументы.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеГотовКСверке)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеПроверялся)
	|	КОНЕЦ,
	|	Фильтр.IDN,
	|	Фильтр.ВидДокумента,
	|	Фильтр.КлючСортировки
	|ИЗ
	|	Фильтр КАК Фильтр
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеТоваров КАК ТаблицаДокументы
	|		ПО Фильтр.IDN = ТаблицаДокументы.IDN
	|			И (Фильтр.ВидДокумента = ""СПИСАНИЕ"")
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Фильтр.Объект,
	|	Фильтр.Магазин,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Магазин,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокументы.Проведен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеГотовКСверке)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РезультатСверкиNavision.НеПроверялся)
	|	КОНЕЦ,
	|	Фильтр.IDN,
	|	Фильтр.ВидДокумента,
	|	Фильтр.КлючСортировки
	|ИЗ
	|	Фильтр КАК Фильтр
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПересортицаТоваров КАК ТаблицаДокументы
	|		ПО Фильтр.IDN = ТаблицаДокументы.IDN
	|			И (Фильтр.ВидДокумента = ""ПЕРЕСОРТ"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлючСортировки"
	;
	Возврат ТекстЗапроса;

КонецФункции

//	LNK 16.06.2017 11:22:29
Функция ПолучитьДанныеNavisionДляСверки(Объект, IDN, ВидОперации)
	
	ДанныеNavision = Новый Структура(
		"Шапка, Товары, Ошибка"
		, Новый ТаблицаЗначений, Новый ТаблицаЗначений, "");

	Если ТипЗнч(Объект) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда

			ТекстЗапросаШапка  = "EXEC PURDOC_HEAD '#TYPE#', '#NUM#'";
			ТекстЗапросаТовары = "EXEC PURDOC_LINE '#TYPE#', '#NUM#'";

	Иначе	ТекстЗапросаШапка  = "EXEC WHDOC_HEAD_1C '#TYPE#', '#NUM#'";
			ТекстЗапросаТовары = "EXEC WHDOC_LINE_1C '#TYPE#', '#NUM#'";

	КонецЕсли;

	ТекстЗапросаШапка  = СтрЗаменить(ТекстЗапросаШапка , "#NUM#" , IDN);
	ТекстЗапросаШапка  = СтрЗаменить(ТекстЗапросаШапка , "#TYPE#", ВидОперации);
	ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "#NUM#" , IDN);
	ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "#TYPE#", ВидОперации);

	Попытка

		ДанныеNavision.Шапка  = ВнешниеИсточники.ПолучитьДанныеОтNavision(ТекстЗапросаШапка,,, Истина);
		ДанныеNavision.Товары = ВнешниеИсточники.ПолучитьДанныеОтNavision(ТекстЗапросаТовары,,, Истина);

		ДанныеNavision.Товары.Колонки.Удалить("IDN");
		ДанныеNavision.Товары.Колонки.ТоварНо_.Имя   = "IDN";
		ДанныеNavision.Товары.Колонки.Кол_воБаз_.Имя = "Количество";

	Исключение

		ДанныеNavision.Ошибка = ОписаниеОшибки();

	КонецПопытки;

	Возврат ДанныеNavision;

КонецФункции

//	LNK 18.06.2017 09:23:48
Функция ПолучитьДанныеЛокальныеДляСверки(Объект)

	ДанныеЛокальные = Новый Структура(
		"Шапка, Товары"
		, Новый Структура, Новый ТаблицаЗначений);
	ДанныеЛокальные.Товары.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ДанныеЛокальные.Товары.Колонки.Добавить("IDN"         , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(20)));
	ДанныеЛокальные.Товары.Колонки.Добавить("Количество"  , Новый ОписаниеТипов("Число"));

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Дата КАК Период,
	|	ТаблицаДокументы.Ссылка КАК Регистратор,
	|	ТаблицаДокументы.Проведен КАК Проведен,
	|	ТаблицаТовары.Номенклатура.IDN КАК IDN,
	|	ТаблицаТовары.Номенклатура,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ИЗ
	|	Документ.ПриходныйОрдерНаТовары КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходныйОрдерНаТовары.Товары КАК ТаблицаТовары
	|		ПО ТаблицаДокументы.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ПриходныйОрдерНаТовары)
	|	И ТаблицаДокументы.Ссылка = &Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура,
	|	СУММА(ТаблицаТовары.Количество)
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары.Товары КАК ТаблицаТовары
	|		ПО ТаблицаДокументы.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.РасходныйОрдерНаТовары)
	|	И ТаблицаДокументы.Ссылка = &Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура,
	|	СУММА(ТаблицаТовары.Количество)
	|ИЗ
	|	Документ.КорректировкаПеремещения КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПеремещения.Товары КАК ТаблицаТовары
	|		ПО ТаблицаДокументы.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.КорректировкаПеремещения)
	|	И ТаблицаДокументы.Ссылка = &Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура,
	|	СУММА(ТаблицаТовары.Количество)
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ТаблицаТовары
	|		ПО ТаблицаДокументы.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ЗаказПоставщику)
	|	И ТаблицаДокументы.Ссылка = &Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура,
	|	СУММА(ТаблицаТовары.Количество)
	|ИЗ
	|	Документ.ОприходованиеТоваров КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОприходованиеТоваров.Товары КАК ТаблицаТовары
	|		ПО ТаблицаДокументы.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ОприходованиеТоваров)
	|	И ТаблицаДокументы.Ссылка = &Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура,
	|	СУММА(ТаблицаТовары.Количество)
	|ИЗ
	|	Документ.СписаниеТоваров КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеТоваров.Товары КАК ТаблицаТовары
	|		ПО ТаблицаДокументы.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.СписаниеТоваров)
	|	И ТаблицаДокументы.Ссылка = &Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.Номенклатура.IDN,
	|	ТаблицаТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.IDN,
	|	ТаблицаТовары.Номенклатура,
	|	СУММА(ТаблицаТовары.Количество)
	|ИЗ
	|	Документ.ПересортицаТоваров КАК ТаблицаДокументы,
	|	(ВЫБРАТЬ
	|		ТаблицаСписать.Номенклатура.IDN КАК IDN,
	|		ТаблицаСписать.Номенклатура КАК Номенклатура,
	|		-СУММА(ТаблицаСписать.Количество) КАК Количество
	|	ИЗ
	|		Документ.ПересортицаТоваров.Товары КАК ТаблицаСписать
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ПересортицаТоваров)
	|		И ТаблицаСписать.Ссылка = &Объект
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаСписать.Номенклатура.IDN,
	|		ТаблицаСписать.Номенклатура
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаОприходовать.НоменклатураОприходование.IDN,
	|		ТаблицаОприходовать.НоменклатураОприходование,
	|		СУММА(ТаблицаОприходовать.Количество)
	|	ИЗ
	|		Документ.ПересортицаТоваров.Товары КАК ТаблицаОприходовать
	|	ГДЕ
	|		ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ПересортицаТоваров)
	|		И ТаблицаОприходовать.Ссылка = &Объект
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаОприходовать.НоменклатураОприходование.IDN,
	|		ТаблицаОприходовать.НоменклатураОприходование) КАК ТаблицаТовары
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Документ.ПересортицаТоваров)
	|	И ТаблицаДокументы.Ссылка = &Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументы.Дата,
	|	ТаблицаДокументы.Ссылка,
	|	ТаблицаДокументы.Проведен,
	|	ТаблицаТовары.IDN,
	|	ТаблицаТовары.Номенклатура
	|ИТОГИ
	|	МАКСИМУМ(Период),
	|	МАКСИМУМ(Проведен),
	|	СУММА(Количество)
	|ПО
	|	Регистратор"
	);
	Запрос.УстановитьПараметр("Объект", Объект);
	
	СверкаРезультат = Запрос.Выполнить();
	
	Если НЕ СверкаРезультат.Пустой() Тогда

		ОбъектыВыборка = СверкаРезультат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ОбъектыВыборка.Следующий() Цикл

			ОбщегоНазначенияРТ.ПеренестиСтрокуВыборкиВСтруктуру(СверкаРезультат, ОбъектыВыборка, ДанныеЛокальные.Шапка
				, ОбщегоНазначенияРТ.ПолучитьСоответствиеПоСтроке("Период, Регистратор, Проведен"));

			ТоварыВыборка = ОбъектыВыборка.Выбрать();

			Пока ТоварыВыборка.Следующий() Цикл

				СтрокаТовары = ДанныеЛокальные.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТовары, ТоварыВыборка);

			КонецЦикла;

		КонецЦикла;

	КонецЕсли;

	Возврат ДанныеЛокальные;

КонецФункции // ПолучитьДанныеЛокальныеДляСверки()

//	LNK 18.06.2017 14:25:40
Функция СравнитьТаблицыТоваров(ТоварыNavision, ТоварыЛокальные, ДополнительнаяИнформация)

	РезультатСверки = Перечисления.РезультатСверкиNavision.Соответствует;
	ОтличныКоличества = Новый Соответствие;

//	Свернём на всякий случай...
	ТоварыNavision.Свернуть("IDN", "Количество");
	ТоварыЛокальные.Свернуть("IDN", "Количество");

//	1. Идем по таблице "ТоварыNavision" и проверяем, чего нет в таблице "ТоварыЛокальные"

	Для каждого СтрокаТаблицы Из ТоварыNavision Цикл

		Если СтрокаТаблицы.Количество = 0 Тогда

		//	Количество не указано - всё равно, что нет строки! Пропускаем.
			Продолжить;

		КонецЕсли;

		ТекстПроблемы  = "";
		СтрокаОтветная = ТоварыЛокальные.Найти(СтрокаТаблицы.IDN, "IDN");

		Если СтрокаОтветная = Неопределено Тогда

			ТекстПроблемы = "В 1С:Розница нет товара [" + СтрокаТаблицы.IDN + "]";

		Иначе

			Если НЕ СтрокаТаблицы.Количество = СтрокаОтветная.Количество Тогда

				ОтличныКоличества.Вставить(СтрокаТаблицы.IDN, Истина);
				ТекстПроблемы = "Разное количество [" + СтрокаТаблицы.IDN
				+ "]> 1С:Розница=" + Формат(СтрокаОтветная.Количество, "ЧН=0,000; ЧГ=")
				+ ", а в Navision=" + Формат(СтрокаТаблицы.Количество, "ЧН=0,000; ЧГ=");

			КонецЕсли;

		КонецЕсли;

		Если НЕ ПустаяСтрока(ТекстПроблемы) Тогда

			РезультатСверки = Перечисления.РезультатСверкиNavision.НеСоответствует;
			ДополнительнаяИнформация = ДополнительнаяИнформация + ?(ПустаяСтрока(ДополнительнаяИнформация), "", Символы.ПС) + ТекстПроблемы;

		КонецЕсли;

	КонецЦикла;

//	2. Идем по таблице "ТоварыЛокальные" и проверяем, чего нет в таблице "ТоварыNavision"

	Для каждого СтрокаТаблицы Из ТоварыЛокальные Цикл

		Если СтрокаТаблицы.Количество = 0 Тогда

		//	Количество не указано - всё равно, что нет строки! Пропускаем.
			Продолжить;

		КонецЕсли;

		ТекстПроблемы  = "";
		СтрокаОтветная = ТоварыNavision.Найти(СтрокаТаблицы.IDN, "IDN");

		Если СтрокаОтветная = Неопределено Тогда

			ТекстПроблемы = "В Navision нет товара [" + СтрокаТаблицы.IDN + "]";

		Иначе

			Если НЕ СтрокаТаблицы.Количество = СтрокаОтветная.Количество Тогда

				ОтличныКоличества.Вставить(СтрокаТаблицы.IDN, Истина);
				ТекстПроблемы = "Разное количество [" + СтрокаТаблицы.IDN
				+ "]> Navision=" + Формат(СтрокаОтветная.Количество, "ЧН=0,000; ЧГ=")
				+ ", а в 1С:Розница=" + Формат(СтрокаТаблицы.Количество, "ЧН=0,000; ЧГ=");

			КонецЕсли;

		КонецЕсли;

		Если НЕ ПустаяСтрока(ТекстПроблемы) Тогда

			РезультатСверки = Перечисления.РезультатСверкиNavision.НеСоответствует;
			ДополнительнаяИнформация = ДополнительнаяИнформация + ?(ПустаяСтрока(ДополнительнаяИнформация), "", Символы.ПС) + ТекстПроблемы;

		КонецЕсли;

	КонецЦикла;

	Возврат РезультатСверки;

КонецФункции // СравнитьТаблицыТоваров()

//	LNK 22.07.2017 10:02:39
Функция НаличиеДокументаNavision(IDN, ВидОперации, ТипОбъекта = Неопределено)	Экспорт

	Если ТипОбъекта = Тип("ДокументСсылка.ЗаказПоставщику") Тогда

			ТекстЗапроса = "EXEC PURDOC_HEAD '#TYPE#', '#NUM#'";

	Иначе	ТекстЗапроса = "EXEC WHDOC_HEAD_1C '#TYPE#', '#NUM#'";

	КонецЕсли;

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "#NUM#" , IDN);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "#TYPE#", ВидОперации);

	ТаблицаШапки = ВнешниеИсточники.ПолучитьДанныеОтNavision(ТекстЗапроса,,, Истина);

	Возврат НЕ ТаблицаШапки.Количество() = 0;

КонецФункции // НаличиеДокументаNavision()

#КонецОбласти

#Область НоваяСхемаСверкиДокументов

Процедура РегистрацияДокументаПриЗаписи(Объект, Отказ)	Экспорт

	Если ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		СписокУзлов = ВнешниеИсточникиПовтИсп.СписокУзловNavision(Истина);

		Если НЕ СписокУзлов.Количество() = 0 Тогда

			ПланыОбмена.ЗарегистрироватьИзменения(СписокУзлов, Объект.Ссылка);

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти







