////////////////////////////////////////////////////////////////////////////////
// Подсистема "Варианты отчетов" (клиент)
// 
// Выполняется на клиенте.
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Открывает панель отчетов. Используется в модуле общей команды.
//
// Параметры:
//   ПутьКПодсистеме (Строка) Имя раздела или путь к подсистеме, для которой открывается панель отчетов.
//       Задается в формате: "ИмяРаздела[.ИмяВложеннойПодсистемы1][.ИмяВложеннойПодсистемы2][...]".
//       NB! Раздел должен быть описан в ВариантыОтчетовПереопределяемый.ОпределитьРазделыСВариантамиОтчетов.
//   ПараметрыВыполненияКоманды (ПараметрыВыполненияКоманды) Передается "как есть" из параметров обработчика команды.
//   Заголовок (Строка) Необязательный. Заголовок панели отчетов.
//
Процедура ПоказатьПанельОтчетов(ПутьКПодсистеме, ПараметрыВыполненияКоманды, Заголовок = "") Экспорт
	ФормаПараметры = Новый Структура("ПутьКПодсистеме", ПутьКПодсистеме);
	Если ЗначениеЗаполнено(Заголовок) Тогда
		ФормаПараметры.Вставить("Заголовок", Заголовок);
	КонецЕсли;
	
	ФормаВладелец     = ПараметрыВыполненияКоманды.Источник;
	ФормаУникальность = ПутьКПодсистеме + "." + ПараметрыВыполненияКоманды.Уникальность;
	ФормаОкно         = ПараметрыВыполненияКоманды.Окно;
	
	ОткрытьФорму("ОбщаяФорма.ПанельОтчетов", ФормаПараметры, ФормаВладелец, ФормаУникальность, ФормаОкно);
КонецПроцедуры

// Устарела: следует использовать процедуру ПоказатьПанельОтчетов.
//
Процедура ОткрытьПанельОтчетов(ИмяРаздела, ЗаголовокПанелиОтчетов, ПараметрыВыполненияКоманды,
	КартинкаФормы = Неопределено) Экспорт
	ПоказатьПанельОтчетов(ИмяРаздела, ПараметрыВыполненияКоманды, ЗаголовокПанелиОтчетов);
КонецПроцедуры

// Открывает диалог настройки размещения нескольких вариантов в разделах.
// Проверки рекомендуется осуществлять до вызова.
//
// Параметры:
//   МассивВариантов (Массив) из (СправочникСсылка.ВариантыОтчетов)
//   ДополнительныеПараметры (*) Необязательный.
//
Процедура ОткрытьДиалогРазмещенияВариантовВРазделах(МассивВариантов, ДополнительныеПараметры = Неопределено, Модально = Ложь) Экспорт
	
	Если ТипЗнч(МассивВариантов) <> Тип("Массив") ИЛИ МассивВариантов.Количество() < 1 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите варианты отчетов, которые необходимо разместить в разделах.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("МассивВариантов, ДополнительныеПараметры", МассивВариантов, ДополнительныеПараметры);
	Если Модально Тогда
		ОткрытьФормуМодально("Справочник.ВариантыОтчетов.Форма.РазмещениеВРазделах", ПараметрыОткрытия);
	Иначе
		ОткрытьФорму("Справочник.ВариантыОтчетов.Форма.РазмещениеВРазделах", ПараметрыОткрытия);
	КонецЕсли;
	
КонецПроцедуры

// Оповещает открытые панели отчетов, формы списков и элементов о изменениях.
//
// Параметры:
//  см. справку к Оповестить (Глобальный контекст)
//
Процедура ОбновитьОткрытыеФормы(Параметр = Неопределено, Источник = Неопределено) Экспорт
	
	Оповестить(ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта(), Параметр, Источник);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Открывает вариант отчета.
//
// Параметры:
//   Форма (УправляемаяФорма)
//
Процедура ОткрытьВариантОтчета(Форма) Экспорт
	Если Форма.ИмяФормы = "Справочник.ВариантыОтчетов.Форма.ФормаСписка" Тогда
		Вариант = Форма.Элементы.Список.ТекущиеДанные;
	ИначеЕсли Форма.ИмяФормы = "Справочник.ВариантыОтчетов.Форма.ФормаЭлемента" Тогда
		Вариант = Форма.Объект;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если Вариант = Неопределено ИЛИ Вариант.Ссылка.Пустая() Тогда
		                      
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите вариант отчета.'"));
		
	ИначеЕсли Вариант.ТипОтчета = ПредопределенноеЗначение("Перечисление.ТипыОтчетов.Внешний") Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Вариант внешнего отчета можно открыть только из формы отчета.'"));
		
	ИначеЕсли Вариант.ТипОтчета = ПредопределенноеЗначение("Перечисление.ТипыОтчетов.Дополнительный") Тогда
		
		ПараметрыОткрытия = Новый Структура("Ссылка, Отчет, КлючВарианта");
		ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, Вариант);
		ОткрытьВариантДополнительногоОтчета(ПараметрыОткрытия);
		
	Иначе
		
		Если Форма.ИмяФормы = "Справочник.ВариантыОтчетов.Форма.ФормаСписка" Тогда
			ИмяОтчета = Вариант.ИмяОтчета;
		Иначе
			ИмяОтчета = Форма.ИмяОтчета;
		КонецЕсли;
		
		Уникальность = "Отчет." + ИмяОтчета;
		Если ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
			Уникальность = Уникальность + "/КлючВарианта." + Вариант.КлючВарианта;
		КонецЕсли;
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("КлючВарианта", Вариант.КлючВарианта);
		ПараметрыОткрытия.Вставить("КлючПараметровПечати", Уникальность);
		ПараметрыОткрытия.Вставить("КлючСохраненияПоложенияОкна", Уникальность);
		
		ВариантыОтчетовВызовСервера.ЗаписьВРегистрИспользованияОтчета(Вариант.Ссылка);
		
		ОткрытьФорму("Отчет." + ИмяОтчета + ".Форма", ПараметрыОткрытия, Неопределено, Уникальность);
		
	КонецЕсли;
КонецПроцедуры

// Выводит пользователю результат выполнения какой-либо операции, при необходимости
//   оповещает об изменении типа или ссылки.
//
Процедура ПоказатьРезультат(Результат) Экспорт
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить  = ?(Результат.Свойство("Оповестить"),  Результат.Оповестить,  Истина);
	Заголовок   = ?(Результат.Свойство("Заголовок"),   Результат.Заголовок,   "");
	Текст       = ?(Результат.Свойство("Текст"),       Результат.Текст,       "");
	Картинка    = ?(Результат.Свойство("Картинка"),    Результат.Картинка,    Неопределено);
	ТекстОшибок = ?(Результат.Свойство("ТекстОшибок"), Результат.ТекстОшибок, "");
	Ссылка      = ?(Результат.Свойство("Ссылка"),      Результат.Ссылка,      "");
	
	Если Оповестить = Истина Тогда
		ПоказатьОповещениеПользователя(Заголовок, Ссылка, Текст, Картинка);
	Иначе
		Если ЗначениеЗаполнено(ТекстОшибок) Тогда
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(1, НСтр("ru = 'Показать ошибки'"));
			Кнопки.Добавить(КодВозвратаДиалога.Отмена);
			
			Ответ = Вопрос(Текст, Кнопки, , 1, Заголовок);
			
			Если Ответ = 1 Тогда
				ТекстовыйДокумент = Новый ТекстовыйДокумент;
				ТекстовыйДокумент.УстановитьТекст(ТекстОшибок);
				Если ЗначениеЗаполнено(Заголовок) Тогда
					ТекстовыйДокумент.Показать(Заголовок);
				Иначе
					ТекстовыйДокумент.Показать();
				КонецЕсли;
			КонецЕсли;
		Иначе
			ПоказатьПредупреждение(, Текст);
		КонецЕсли;
	КонецЕсли;
	
	Если Результат.Свойство("ТипДляОбновления") Тогда
		ОповеститьОбИзменении(Результат.ТипДляОбновления);
	КонецЕсли;
	
КонецПроцедуры

// Процедура обслуживает событие реквизита ДеревоПодсистем в формах редактирования.
//
Процедура ДеревоПодсистемИспользованиеПриИзменении(Форма, Элемент) Экспорт
	СтрокаДерева = Форма.Элементы.ДеревоПодсистем.ТекущиеДанные;
	Если СтрокаДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Пропуск корневой строки
	Если СтрокаДерева.Приоритет = "" Тогда
		СтрокаДерева.Использование = 0;
		Возврат;
	КонецЕсли;
	
	Если СтрокаДерева.Использование = 2 И СтрокаДерева.ИспользованиеПоУмолчанию <> 2 Тогда
		СтрокаДерева.Использование = 0;
	КонецЕсли;
	
	УстановитьИзмененПользователем(Форма, СтрокаДерева);
КонецПроцедуры

// Процедура обслуживает событие реквизита ДеревоПодсистем в формах редактирования.
//
Процедура ДеревоПодсистемВажностьПриИзменении(Форма, Элемент) Экспорт
	СтрокаДерева = Форма.Элементы.ДеревоПодсистем.ТекущиеДанные;
	Если СтрокаДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Пропуск корневой строки
	Если СтрокаДерева.Приоритет = "" Тогда
		СтрокаДерева.Важность = "";
		Возврат;
	КонецЕсли;
	
	Если СтрокаДерева.Важность <> СтрокаДерева.ВажностьПоУмолчанию Тогда
		СтрокаДерева.Использование = 1;
	КонецЕсли;
	
	УстановитьИзмененПользователем(Форма, СтрокаДерева);
КонецПроцедуры

// Вспомогательная процедура для обработки изменений ДеревоПодсистем.
//
Процедура УстановитьИзмененПользователем(Форма, СтрокаДерева)
	Если СтрокаДерева.Использование = СтрокаДерева.ИспользованиеПоУмолчанию
		И СтрокаДерева.Важность = СтрокаДерева.ВажностьПоУмолчанию Тогда
		СтрокаДерева.ИзмененПользователем = Ложь;
	Иначе
		СтрокаДерева.ИзмененПользователем = Истина;
		Если Форма.ИмяФормы = "Справочник.ВариантыОтчетов.Форма.ФормаЭлемента" Тогда
			Форма.Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Вызывает диалог сохранения редактируемых данных произвольной формы
//   Вызывается из обработчиков "ПередЗакрытием" и "ПриЗакрытии".
//
// Параметры:
//   Модифицированность (Булево) Флаг модифицированности формы.
//       Если форма не модифицирована, то сохранять нечего и запрос не требуется
//   Отказ (...) Флаг отказа от закрытия формы.
//       |- (Булево)    для обработчика "ПередЗакрытием", используется для толстого и тонкого клиента.
//       |- (Неопределено) для обработчика "ПриЗакрытии", используется для веб клиента.
//
// Возвращаемое значение: 
//   (Булево) Необходимость сохранения данных формы.
//
Функция СохранитьДанныеПроизвольнойФормы(Модифицированность, Отказ = Неопределено) Экспорт
	// Изменение сохраняемых данных должно устанавливать модифицированность формы
	Если Модифицированность <> Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Определение клиента
	#Если ВебКлиент Тогда
		ЭтоВебКлиент = Истина;
	#Иначе
		ЭтоВебКлиент = Ложь;
	#КонецЕсли
	
	// Предупреждение необходимо вызвать только один раз для одного клиента
	Если ЭтоВебКлиент <> (Отказ = Неопределено) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
	Кнопки = ?(ЭтоВебКлиент, РежимДиалогаВопрос.ДаНет, РежимДиалогаВопрос.ДаНетОтмена);
	Ответ = Вопрос(ТекстВопроса, Кнопки, 60, КодВозвратаДиалога.Нет); 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЭтоВебКлиент И Ответ = КодВозвратаДиалога.Отмена Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики условных вызовов в другие подсистемы

// Открывает форму дополнительного отчета с заданным вариантом.
//
// Параметры
//   Вариант (Структура)
//       |- Ссылка (СправочникСсылка.ВариантыОтчетов)
//       |- Отчет (*) см. тип реквизита Отчет справочника ВариантыОтчетов.
//       |- КлючВарианта (Строка)
//
Процедура ОткрытьВариантДополнительногоОтчета(Вариант) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		Модуль = ОбщегоНазначенияКлиентСервер.ОбщийМодуль("ДополнительныеОтчетыИОбработкиКлиент");
		Модуль.ОткрытьВариантДополнительногоОтчета(Вариант.Отчет, Вариант.КлючВарианта);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик команд, добавленных динамически.
//
// Параметры:
//   Форма  (УправляемаяФорма) Форма отчета.
//   Команда   (КомандаФормы)     Команда, которая была вызвана.
//   Результат (Булево)           Истина, если вызов команды обработан.
//
Процедура ФормаОтчетаОбработчикКоманды(Форма, Команда, Результат) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("СтандартныеПодсистемы.РассылкаОтчетов") Тогда
		Модуль = ОбщегоНазначенияКлиентСервер.ОбщийМодуль("РассылкаОтчетовКлиент");
		Модуль.ФормаОтчетаОбработчикКоманды(Форма, Команда, Результат);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик результата выбора подчиненной формы.
//
// Параметры:
//   Форма          (УправляемаяФорма) Форма отчета.
//   ВыбранноеЗначение (*)                Результат выбора в подчиненной форме.
//   ИсточникВыбора    (УправляемаяФорма) Форма, где осуществлен выбор. 
//   Результат         (Булево)           Истина, если результат выбора обработан.
//
Процедура ФормаОтчетаОбработкаВыбора(Форма, ВыбранноеЗначение, ИсточникВыбора, Результат) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("СтандартныеПодсистемы.РассылкаОтчетов") Тогда
		Модуль = ОбщегоНазначенияКлиентСервер.ОбщийМодуль("РассылкаОтчетовКлиент");
		Модуль.ФормаОтчетаОбработкаВыбора(Форма, ВыбранноеЗначение, ИсточникВыбора, Результат);
	КонецЕсли;
	
КонецПроцедуры
