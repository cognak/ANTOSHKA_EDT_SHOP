//	LNK 12.08.2019 08:55:10
Функция РесурсMicrosoftDynamicsДоступен(Обновить = Ложь)	Экспорт

	Возврат Ложь;

КонецФункции

//	LNK 12.08.2019 11:18:47
Функция РесурсWebRetailДоступен(Обновить = Ложь)	Экспорт

	РесурсДоступен = Константы.РесурсWebRetailДоступен.Получить();

	Если Обновить = Истина ИЛИ НЕ РесурсДоступен Тогда

		Попытка

			ФоновыеЗадания.Выполнить("ОбменWebRetailСервер.ПроверкаДоступностиWebRetail"
				, ОбщегоНазначенияКлиентСервер.AAD(Ложь)
				, "ПРОВЕРКА_СОЕДИНЕНИЯ"
			);

		Исключение

		//	запуск фонового технический.. не случилось и хорошо.
			ТекстОшибки = ОписаниеОшибки();

		КонецПопытки;

	КонецЕсли;

	Возврат РесурсДоступен;

КонецФункции

#Область КомплексРаботСДаннымиКонтрагентовВРознице

//	LNK 16.08.2019 09:41:58
Функция ВыполнитьКомплексДанныхКонтрагента(КодКарты, НомерТелефона, ДанныеКонтрагента, НазначениеДанных, ДокументПродажи, ВыполнениеРегламента = Ложь, КонтрольНаличия = Ложь)	Экспорт

	Если НЕ ПривилегированныйРежим() Тогда

		УстановитьПривилегированныйРежим(Истина);

	КонецЕсли;

	СоставДанных = ОбменMicrosoftDynamicsСервер.ИнициализироватьСоставДанных();

	Если Справочники.Контрагенты.КлючиПоискаУказаны(ДанныеКонтрагента) Тогда

		СоставДанных = Справочники.Контрагенты.НайтиКонтрагентаПоДаннымCRM(ДанныеКонтрагента);

		ТекстДанныеОтложены = "";

		Если СоставДанных.Контрагент = Неопределено И НЕ КонтрольНаличия Тогда

			Если НЕ ВыполнениеРегламента И НЕ ДокументПродажи = Неопределено Тогда

			//	Данные контрагента "откладываем" в том случае, если контрагент НЕ найден.
				Справочники.Контрагенты.ОтложитьДанныеКонтрагента(КодКарты, НомерТелефона, ДанныеКонтрагента, НазначениеДанных, ДокументПродажи, ОбменДаннымиПовтИсп.ПолучитьДанныеУзла().Магазин);
				ТекстДанныеОтложены = Символы.ПС + "Отложены данные КАРТА[" + КодКарты + "], ТЕЛ[" + НомерТелефона + "]";

			КонецЕсли;

			Если ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

				Если ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(ДанныеКонтрагента, "РазрешеноСозданиеНового", Истина) = Истина Тогда

					СоставДанных = Справочники.Контрагенты.ПрименитьКонтрагентаПоДаннымCRM(ДанныеКонтрагента, СоставДанных,, Ложь);

				КонецЕсли;

			Иначе

				Если РозничныеПродажиСлужебный.РесурсWebRetailДоступен() Тогда

					Попытка

						Подключение  = СервисыСервер.Подключение("RetailPack");
						СоставДанных = ОбщегоНазначенияКлиентСервер.ДесериализоватьJSON(Подключение.GetBuyerFromCRMData(ОбщегоНазначенияКлиентСервер.СериализоватьJSON(Новый ХранилищеЗначения(ДанныеКонтрагента, Новый СжатиеДанных(9)))));

						Если СоставДанных.Ошибка = Ложь Тогда

							Если СоставДанных.Свойство("Объекты") Тогда

							//	Из ЦБ получены объекты - Контрагент и его карта!
							//	Рассматриваем эти данные, как вариант синхронизации.. обновим локальную информацию.

								СоставДанных.Объекты.Контрагент.ОбменДанными.Загрузка = Истина;
								СоставДанных.Объекты.Контрагент.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
								СоставДанных.Объекты.Контрагент.ДополнительныеСвойства.Вставить("СлужебнаяЗапись", Истина);

								СоставДанных.Объекты.Контрагент.Записать();	//	теперь локальный объект соответствует данным ЦБ!

							//	LNK 13.09.2022 17:39:36 -	Карту, полученную из ЦБ, переписываем в любом случае!
								Если ЗначениеЗаполнено(СоставДанных.ДисконтнаяКарта) Тогда

									СоставДанных.Объекты.ДисконтнаяКарта.ОбменДанными.Загрузка = Истина;
									СоставДанных.Объекты.ДисконтнаяКарта.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
									СоставДанных.Объекты.ДисконтнаяКарта.ДополнительныеСвойства.Вставить("СлужебнаяЗапись", Истина);

									СоставДанных.Объекты.ДисконтнаяКарта.Записать();

								КонецЕсли;

							КонецЕсли;

						Иначе

							ЖурналСобытий.Регистрация("Контрагенты.MSCRM.RetailPack", УровеньЖурналаРегистрации.Ошибка
							,,,, "Ошибка получения данных:" + Символы.ПС + СоставДанных.ОписаниеОшибки,, Ложь);

						КонецЕсли;

					Исключение

						СоставДанных = ОбменMicrosoftDynamicsСервер.ИнициализироватьСоставДанных();

						СоставДанных.Ошибка = Истина;
						СоставДанных.ОписаниеОшибки = ОписаниеОшибки();

						ЖурналСобытий.Регистрация("Контрагенты.MSCRM.RetailPack", УровеньЖурналаРегистрации.Ошибка,,,, "Ошибка передачи:" + Символы.ПС + СоставДанных.ОписаниеОшибки,, Ложь);

					КонецПопытки;

				Иначе

					ЖурналСобытий.Регистрация("Контрагенты.MSCRM.RetailPack", УровеньЖурналаРегистрации.Информация,,,, "Ресурс «WebRetail» НЕДОСТУПЕН. Получить данные из ЦБ невозможно." + ТекстДанныеОтложены,, Ложь);

				КонецЕсли;

			КонецЕсли;

			Если СоставДанных.Свойство("Объекты") Тогда

				СоставДанных.Удалить("Объекты");

			КонецЕсли;

		КонецЕсли;

	Иначе

		СоставДанных.Ошибка = Истина;
		СоставДанных.ОписаниеОшибки = "Внешняя система не вернула обязательные ключи поиска.";

	КонецЕсли;

	Возврат СоставДанных;

КонецФункции

#КонецОбласти

//	LNK 07.12.2021 05:55:45
Функция ПроверитьПереданныеВЦентрЧеки(Контрагент)	Экспорт

	Отказ = Ложь;

	Если НЕ ПривилегированныйРежим() Тогда

		УстановитьПривилегированныйРежим(Истина);

	КонецЕсли;

	Если НЕ ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТаблицаНазначение.Объект КАК Объект
		|ИЗ
		|	РегистрСведений.РегистрацияОбъектов КАК ТаблицаНазначение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМ КАК Фильтр
		|		ПО ТаблицаНазначение.Объект = Фильтр.Ссылка
		|ГДЕ
		|	ТаблицаНазначение.ДействиеКоманда = ""ПЕРЕДАТЬ_В_ЦЕНТР""
		|	И Фильтр.ВладелецДисконтнойКарты = &Контрагент
		|	И Фильтр.СтатусЧекаККМ В (ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробитый), ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Архивный))"
		);
		Запрос.УстановитьПараметр("Контрагент", Контрагент);

		Отказ = НЕ Запрос.Выполнить().Пустой();

	КонецЕсли;

	Возврат НЕ Отказ;

КонецФункции

#Область КомпонентаГенерацииQRкода

//	LNK 10.04.2023 11:21:39
Функция ПолучитьГенераторQRкода()	Экспорт

	Макет = ПолучитьОбщийМакет("КомпонентаПечатиQRКода");
	ГенераторQRкода = Неопределено;

	Если ПодключитьВнешнююКомпоненту(ПоместитьВоВременноеХранилище(Макет), "QR") тогда

		ГенераторQRкода = Новый("AddIn.QR.QRCodeExtension");

	Иначе

		ВызватьИсключение "Не вдалося підключити компонент генерації QR коду";		

	КонецЕсли;

	Возврат Новый ХранилищеЗначения(ГенераторQRкода);

КонецФункции


#КонецОбласти