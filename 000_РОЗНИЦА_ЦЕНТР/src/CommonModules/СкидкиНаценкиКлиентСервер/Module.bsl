#Область ПрограммныйИнтерфейс

// Функция подготавливает структуру,
// заполненную пустыми значениями,
// которая используется для проверки запрета розничной продажи
// Возвращаемое значение
//		СтруктураПроверки - Структура
Функция ПараметрыПроверкиЗапретаРозничнойПродажи() Экспорт 
	
	СтруктураПроверки = Новый Структура;
	
	СтруктураПроверки.Вставить("Магазин",			Неопределено);
	СтруктураПроверки.Вставить("Дата",				Неопределено);
	СтруктураПроверки.Вставить("ТекстСообщения",	Неопределено);
	
	Возврат СтруктураПроверки;
	
КонецФункции

#Область ОкруглениеСуммыФискальногоЧека

//	LNK 22.10.2019 09:42:40
Функция ОкруглитьТоварыПоСуммеДокумента(СуммаДокумента, Товары, ЦенаВключаетНДС, СоставТиповНоменклатуры = Неопределено, ТолькоВозможностьОкругления = Ложь)	Экспорт

	СуммаКупонов    = ПолучитьСуммуКупонов(Товары, СоставТиповНоменклатуры); 
	СуммаТоварыЧека = Макс(0, СуммаДокумента - СуммаКупонов);

//	Округляем до 10 копеек по арифметическим правилам (от 5 к большему)
	СуммаОкругленнаяИтог = Окр(СуммаТоварыЧека, 1, РежимОкругления.Окр15как20);
	СуммаРазницыИтог     = СуммаОкругленнаяИтог - СуммаТоварыЧека;

	Если НЕ ТолькоВозможностьОкругления И НЕ СуммаРазницыИтог = 0 Тогда

		СтрокаМаксимум = Неопределено;	//	для "вброса" возможного остатка

		Для каждого СтрокаТовары Из Товары Цикл

			Если СоставТиповНоменклатуры = Неопределено Тогда
				
					ТипНоменклатуры = СтрокаТовары.ТипНоменклатуры;

			Иначе	ТипНоменклатуры = СоставТиповНоменклатуры.Получить(СтрокаТовары.Номенклатура);

			КонецЕсли;

			Если СуммаРазницыИтог = 0
			ИЛИ  СтрокаТовары.ПродажаПодарка = Истина
			ИЛИ  ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.СкидочныйКупон")
			ИЛИ  ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПодарочныйСертификат")	//	LNK 21.02.2020 13:44:33
			Тогда

			//	.. позиции по фильтру выше будут удалены из ТЧ "Товары"
				Продолжить;

			КонецЕсли;

			Коэффициент     = СтрокаТовары.Сумма / СуммаТоварыЧека;
			СуммаОкругления = Окр(СуммаРазницыИтог * Коэффициент + 0.004, 2);

			Если (СтрокаТовары.Сумма + СуммаОкругления) > 0 Тогда	//	предохранитель на всякий случай

				СтрокаТовары.СуммаОкругления = СуммаОкругления;
				СтрокаТовары.Сумма      = СтрокаТовары.Сумма + СтрокаТовары.СуммаОкругления;

				Если ТипЗнч(Товары) = Тип("ДанныеФормыКоллекция") Тогда

					СтрокаТовары.СуммаВсего = СтрокаТовары.Сумма;

				КонецЕсли;

				#Если Клиент ИЛИ ТонкийКлиент Тогда
				СтрокаТовары.СуммаНДС   = СкидкиНаценкиКлиент.РассчитатьСуммуНДС(СтрокаТовары.Сумма, СтрокаТовары.СтавкаНДС, ЦенаВключаетНДС);
				#Иначе
				СтрокаТовары.СуммаНДС   = СкидкиНаценкиСервер.РассчитатьСуммуНДС(СтрокаТовары.Сумма, СтрокаТовары.СтавкаНДС, ЦенаВключаетНДС);
				#КонецЕсли

				СуммаРазницыИтог = СуммаРазницыИтог - СтрокаТовары.СуммаОкругления;

				Если СтрокаМаксимум = Неопределено ИЛИ СтрокаМаксимум.Сумма < СтрокаТовары.Сумма Тогда

					СтрокаМаксимум = СтрокаТовары;

				КонецЕсли;

			КонецЕсли;

		КонецЦикла;

		Если НЕ (СуммаРазницыИтог = 0 ИЛИ СтрокаМаксимум = Неопределено) Тогда

			СтрокаМаксимум.СуммаОкругления = СтрокаМаксимум.СуммаОкругления + СуммаРазницыИтог;
			СтрокаМаксимум.Сумма      = СтрокаМаксимум.Сумма + СуммаРазницыИтог;

			Если ТипЗнч(Товары) = Тип("ДанныеФормыКоллекция") Тогда

				СтрокаМаксимум.СуммаВсего = СтрокаМаксимум.Сумма;

			КонецЕсли;

			#Если Клиент ИЛИ ТонкийКлиент Тогда
			СтрокаМаксимум.СуммаНДС   = СкидкиНаценкиКлиент.РассчитатьСуммуНДС(СтрокаМаксимум.Сумма, СтрокаМаксимум.СтавкаНДС, ЦенаВключаетНДС);
			#Иначе
			СтрокаМаксимум.СуммаНДС   = СкидкиНаценкиСервер.РассчитатьСуммуНДС(СтрокаМаксимум.Сумма, СтрокаМаксимум.СтавкаНДС, ЦенаВключаетНДС);
			#КонецЕсли
			СуммаРазницыИтог = 0;

		КонецЕсли;

	КонецЕсли;

	Возврат НЕ СуммаРазницыИтог = 0;

КонецФункции

//	LNK 31.01.2020 10:25:21
Функция ОчиститьОкругленияТоваров(Товары, СообщитьПользователю = Истина)	Экспорт

	БылаОчистка = Ложь;

	Для каждого СтрокаТовары Из Товары Цикл

		Если НЕ СтрокаТовары.СуммаОкругления = 0 Тогда

			БылаОчистка = Истина;
			СтрокаТовары.Сумма = СтрокаТовары.Сумма - СтрокаТовары.СуммаОкругления;
			СтрокаТовары.СуммаОкругления = 0;

		КонецЕсли;

	КонецЦикла;

	Если БылаОчистка И СообщитьПользователю = Истина Тогда

		Сообщить("Округления таблицы «Товары» очищены!");

	КонецЕсли;

	Возврат БылаОчистка;

КонецФункции

//	LNK 22.10.2019 09:52:00
Функция ПолучитьСуммуКупонов(Товары, СоставТиповНоменклатуры = Неопределено)	Экспорт

	СуммаКупонов  = 0;

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл

		Если СоставТиповНоменклатуры = Неопределено Тогда
			
				ТипНоменклатуры = СтрокаТабличнойЧасти.ТипНоменклатуры;

		Иначе	ТипНоменклатуры = СоставТиповНоменклатуры.Получить(СтрокаТабличнойЧасти.Номенклатура);

		КонецЕсли;

		Если НЕ ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.СкидочныйКупон") Тогда

			Продолжить;

		КонецЕсли;

		СуммаКупонов = СуммаКупонов + СтрокаТабличнойЧасти.Сумма;

	КонецЦикла; 

	Возврат СуммаКупонов;

КонецФункции

#КонецОбласти

//	LNK 01.07.2021 11:08:01
Процедура РасчитатьПроцентСкидкиПоТабличнойЧасти(ТабличнаяЧасть, УчитыватьНДС, ЦенаВключаетНДС)	Экспорт

	Для каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл

		РасчитатьПроцентСкидкиДляСтроки(СтрокаТабличнойЧасти, УчитыватьНДС, ЦенаВключаетНДС);

	КонецЦикла;

КонецПроцедуры

//	LNK 01.07.2021 09:15:19
Процедура РасчитатьПроцентСкидкиДляСтроки(СтрокаТабличнойЧасти, УчитыватьНДС, ЦенаВключаетНДС)	Экспорт

	СуммаБезСкидки = СтрокаТабличнойЧасти.КоличествоУпаковок * СтрокаТабличнойЧасти.Цена;	//	это без НДС, если что

	Если СуммаБезСкидки = 0 Тогда

		СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 0;

	Иначе

		Если УчитыватьНДС = Истина И ЦенаВключаетНДС = Ложь Тогда

			СуммаБезСкидки = СуммаБезСкидки + НДСИсходящийСервер.РассчитатьСуммуНДСсУчетомПогрешности(СуммаБезСкидки, ЦенаВключаетНДС, СтрокаТабличнойЧасти.СтавкаНДС);

		КонецЕсли;

		СуммаВсехСкидок = СуммаБезСкидки
						- (СтрокаТабличнойЧасти.Сумма + ?(УчитыватьНДС = Истина И ЦенаВключаетНДС = Ложь, СтрокаТабличнойЧасти.СуммаНДС, 0));

		СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = СуммаВсехСкидок / СуммаБезСкидки * 100;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти






