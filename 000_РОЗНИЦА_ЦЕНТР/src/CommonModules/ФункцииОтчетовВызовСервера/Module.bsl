Функция ТелефоныКонтрагента(Контрагент)	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаТелефон.Представление КАК НомерТелефона
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК ТаблицаТелефон
	|ГДЕ
	|	ТаблицаТелефон.Ссылка = &Контрагент
	|	И ТаблицаТелефон.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТаблицаТелефон.Представление
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК ТаблицаТелефон
	|ГДЕ
	|	ТаблицаТелефон.Ссылка = &Контрагент
	|	И ТаблицаТелефон.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагентаСлужебный)"
	);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СписокТелефонов = "";

	Пока Выборка.Следующий() Цикл

		СписокТелефонов = СписокТелефонов
			+ ?(ПустаяСтрока(СписокТелефонов), "", Символы.ПС)
			+ СокрЛП(Выборка.НомерТелефона);

	КонецЦикла;

	Возврат СписокТелефонов;

КонецФункции // ТелефоныКонтрагента()

Функция КартыКонтрагента(Контрагент)	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИнформационныеКарты.КодКарты КАК КодКарты
	|ИЗ
	|	Справочник.ИнформационныеКарты КАК ИнформационныеКарты
	|ГДЕ
	|	ИнформационныеКарты.ВладелецКарты = &Контрагент
	|	И ИнформационныеКарты.ВладелецКарты ССЫЛКА Справочник.Контрагенты
	|	И НЕ ИнформационныеКарты.ПометкаУдаления
	|	И НЕ ИнформационныеКарты.Блокирован
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодКарты"
	);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СписокКарт = "";

	Пока Выборка.Следующий() Цикл

		СписокКарт = СписокКарт
			+ ?(ПустаяСтрока(СписокКарт), "", Символы.ПС)
			+ СокрЛП(Выборка.КодКарты);

	КонецЦикла;

	Возврат СписокКарт;

КонецФункции // КартыКонтрагента()
