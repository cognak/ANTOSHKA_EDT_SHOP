//	LNK 01.08.2019 14:09:45
Функция ПолученНомерДисконтнойКарты(СтрокаКода)	Экспорт

	ПолученНомер = Ложь;
	ДлинаСтроки  = СтрДлина(СтрокаКода);

	КлючиНомера = Новый Соответствие;
	КлючиНомера.Вставить("0", Истина);
	КлючиНомера.Вставить("1", Истина);
	КлючиНомера.Вставить("2", Истина);

	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаКода) Тогда

		Если ДлинаСтроки = 8 Тогда

			ПолученНомер = КлючиНомера.Получить(Лев(СтрокаКода, 1)) = Истина;

		КонецЕсли;

	КонецЕсли;

	Возврат ПолученНомер;

КонецФункции // ПолученНомерДисконтнойКарты()

//	LNK 23.09.2022 13:01:31
Процедура ПротоколОС(ТекстСообщения, Период = '00010101')	Экспорт

	Если ОбщегоНазначенияРТКлиент.ПолучитьПравилаПротокола(Период).ПротоколВерсииОС = Истина Тогда

		СИ = Новый СистемнаяИнформация;
		СИ_данные = Новый Структура(
			"ВерсияОС, ВерсияПриложения, ИдентификаторКлиента, ИнформацияПрограммыПросмотра, ОперативнаяПамять, Процессор, ТипПлатформы"
			, СИ.ВерсияОС
			, СИ.ВерсияПриложения
			, СокрЛП(СИ.ИдентификаторКлиента)
			, СИ.ИнформацияПрограммыПросмотра
			, СИ.ОперативнаяПамять
			, СИ.Процессор
			, СокрЛП(СИ.ТипПлатформы)
		);

		РозничныеПродажиСервер.ПротоколВерсииОС(СИ_данные, ТекстСообщения);

	КонецЕсли;

КонецПроцедуры

#Если ТонкийКлиент Тогда

#Область ОбщиеВызовыПередачиБонусовНаБлаготворительныйФонд

//	LNK 20.09.2023 08:36:31
//	Параметры
//		НомерТелефона			- обязательно
//		Контрагент				- обязательно
//		ИмяВнешнейОбработки		- пустая строка, если не из РМК
//		УникальныйИдентификатор	- любой UUID, если вызов не из формы. Иначе Форма.УникальныйИдентификатор
//		Форма					- Неопределено, если вызов не из формы
Процедура ПередатьБонусныеБаллыНаБлаготворительныйФонд(НомерТелефона, Контрагент, ИмяВнешнейОбработки, УникальныйИдентификатор, Форма = Неопределено)	Экспорт

//	Обновим флаг доступа принудительно..
	РесурсWebRetailДоступен = РозничныеПродажиСлужебный.РесурсWebRetailДоступен();

	Если РесурсWebRetailДоступен = Истина И НЕ ПустаяСтрока(НомерТелефона) Тогда

		ДополнительныеПараметры = Новый Структура(
			"НомерТелефона, Контрагент, ИмяВнешнейОбработки, УникальныйИдентификатор, Форма"
			, НомерТелефона, Контрагент, ИмяВнешнейОбработки, УникальныйИдентификатор, Форма
		);

		ОписаниеОповещения = Новый ОписаниеОповещения("ПередатьБонусныеБаллыНаБлаготворительныйФондАутентификация", ЭтотОбъект, ДополнительныеПараметры);

		ОткрытьФорму("Обработка.ДанныеПокупателя.Форма.Аутентификация"
			, Новый Структура("НомерТелефона", НомерТелефона)
			, ЭтотОбъект
			, УникальныйИдентификатор
			,
			,
			, ОписаниеОповещения
			, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
		);

	Иначе

		ПоказатьОповещениеПользователя("Відмовлено"
			,
			, "Центральний сервер зараз поза доступом"
			, БиблиотекаКартинок.Предупреждение32
		);

	КонецЕсли;

КонецПроцедуры

//	LNK 11.09.2023 05:28:08
Процедура ПередатьБонусныеБаллыНаБлаготворительныйФондАутентификация(ДанныеВозврата, ДополнительныеПараметры)	Экспорт

	Если ТипЗнч(ДанныеВозврата) = Тип("Структура") И ДанныеВозврата.Подтверждён = Истина Тогда

		ОписаниеОповещения = Новый ОписаниеОповещения("ПередатьБонусныеБаллыНаБлаготворительныйФондЗавершение", ЭтотОбъект, ДополнительныеПараметры);

		ОткрытьФорму(РМККлиент.КонтекстИмениФормы("Обработка.РМК.Форма.ФормаПередачиБонусныхБалловНаБлаготворительныйФонд", ДополнительныеПараметры.ИмяВнешнейОбработки)
			, Новый Структура("НомерТелефона, Контрагент", ДополнительныеПараметры.НомерТелефона, ДополнительныеПараметры.Контрагент)
			, ЭтотОбъект
			, ДополнительныеПараметры.УникальныйИдентификатор,
			,
			, ОписаниеОповещения
			, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
		);

	Иначе

		ПоказатьОповещениеПользователя("Відмовлено"
			,
			, "Аутентифікація не виконана"
			, БиблиотекаКартинок.Предупреждение32
		);

	КонецЕсли;

КонецПроцедуры

//	LNK 11.09.2023 06:47:24
Процедура ПередатьБонусныеБаллыНаБлаготворительныйФондЗавершение(ДанныеВозврата, ДополнительныеПараметры)	Экспорт

	Если НЕ ДополнительныеПараметры.Форма = Неопределено Тогда

		ДополнительныеПараметры.Форма.УстановитьОформлениеЭлементов();

		Если ДополнительныеПараметры.Форма.ИмяФормы = "Обработка.ДанныеПокупателя.Форма.ВыборКонтрагента" Тогда

			ДополнительныеПараметры.Форма.Закрыть();

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли











