//	LNK 21.04.2017 12:03:34
Процедура УстановитьОрганизациюПоУмолчанию(Организация, Знач Пользователь = Неопределено, УстановитьНастройкуПользователя = Истина)	Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Если Пользователь = Неопределено Тогда

		Пользователь = Пользователи.ТекущийПользователь();

	КонецЕсли;

	ОрганизацияМагазина = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыСеанса.ТекущийМагазин, "Организация", Справочники.Организации.ПустаяСсылка(), Ложь);

	Если НЕ Организация = ОрганизацияМагазина Тогда

		Если ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		//	В Центральной базе мы не сможет однозначно установить магазин по организации... таких тут много.
		//	Поэтому откатываемся к организации, указанной для магазина.
			Организация = ОрганизацияМагазина;

		Иначе

		//	А здесь магазины, имеющие место в узле, хранятся в элементе структуры данных.
		//	Вот из этих магазинов и выбираем по нужной организации. Если такой не найдём, то
		//	результат будет такой же, как и в "верхней" ветке условия.
			ДанныеУзла = ОбменДаннымиПовтИсп.ПолучитьДанныеУзла();

			Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
			|	МагазиныУзла.Магазин,
			|	МагазиныУзла.Магазин.НомерМагазина КАК КлючПорядка
			|ИЗ
			|	Справочник.СтруктураУзлов.Магазины КАК МагазиныУзла
			|ГДЕ
			|	МагазиныУзла.Ссылка = &ЭлементСтруктуры
			|	И МагазиныУзла.Магазин.Организация = &Организация
			|			И НЕ МагазиныУзла.Магазин.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
			|
			|УПОРЯДОЧИТЬ ПО
			|	КлючПорядка"
			);
			Запрос.УстановитьПараметр("Организация", Организация);
			Запрос.УстановитьПараметр("ЭлементСтруктуры", ДанныеУзла.ЭлементСтруктуры);

			Выборка = Запрос.Выполнить().Выбрать();

			Если Выборка.Следующий() Тогда

				ПараметрыСеанса.ТекущийМагазин = Выборка.Магазин;

			Иначе

				Организация = ОрганизацияМагазина;

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	ПараметрыСеанса.ТекущаяОрганизация = Организация;

	Если УстановитьНастройкуПользователя Тогда

		МенеджерЗаписи = РегистрыСведений.НастройкиПользователей.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.Магазин      = ПараметрыСеанса.ТекущийМагазин;
		МенеджерЗаписи.Настройка    = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация;
		МенеджерЗаписи.Значение     = ПараметрыСеанса.ТекущаяОрганизация;

		МенеджерЗаписи.Записать();

	КонецЕсли;

КонецПроцедуры


