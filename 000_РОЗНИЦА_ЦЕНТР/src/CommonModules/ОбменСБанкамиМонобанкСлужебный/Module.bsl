//	LNK 04.03.2020 09:07:51
Функция ПолучитьСигнатуру(ТекстЗапроса, СекретныйКлюч)	Экспорт

//	Формат шифрования, указанный по адресу "https://u2-demo-ext.mono.st4g3.com/docs/index.html#"
//	encodeBase64(
//	   HmacSHA256(
//	       getBytesUTF-8(
//	           request_body
//	       )
//	   )
//	)

    ОбъектUTF8   = Новый COMОбъект("System.Text.UTF8Encoding");
    Криптография = Новый COMОбъект("System.Security.Cryptography.HMACSHA256");

//	устанавливаем ключ хеша
    Криптография.Key = ОбъектUTF8.GetBytes_4(СекретныйКлюч);
//	получаем сам хеш - массив байтов
	ХешБайты = Криптография.ComputeHash_2(ОбъектUTF8.GetBytes_4(ТекстЗапроса)).Выгрузить();

//	загоняем полученный массив байтов в буффер
	БуферДанных = Новый БуферДвоичныхДанных(ХешБайты.Количество());
	Счётчик     = 0;

	Для Каждого Элемент Из ХешБайты Цикл

	    БуферДанных.Установить(Счётчик, Элемент);
	    Счётчик = Счётчик + 1;

	КонецЦикла;

	Возврат Base64Строка(ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(БуферДанных));

КонецФункции

Функция ВыполнитьЗапросКСерверу(ИмяПроцедуры, ТекстЗапроса, ДанныеСоединения, Метод = "POST", Знач Таймаут = 40)	Экспорт

	Результат = Новый Структура(
		"КодОтвета, Ошибка, ОписаниеОшибки, ДанныеОтвета"
		, 0, Ложь, "", Неопределено);
	//
	Таймаут = Макс(5, Таймаут);

	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда

		Попытка

			Сигнатура = ОбменСБанкамиМонобанкСлужебный.ПолучитьСигнатуру(ТекстЗапроса, ДанныеСоединения.Ключ);

			HTTP = Новый HTTPСоединение(ДанныеСоединения.Адрес,,,,, Таймаут, Новый ЗащищенноеСоединениеOpenSSL);

		//	формирование заголовка
			Заголовки = Новый Соответствие;
			Заголовки.Вставить("store-id" , ДанныеСоединения.Идентификатор);
			Заголовки.Вставить("signature", Сигнатура);
			Заголовки.Вставить("Content-Type", "application/json");
			Заголовки.Вставить("Accept", "application/json");

			HTTPЗапрос = Новый HTTPЗапрос(ИмяПроцедуры, Заголовки);
			HTTPЗапрос.УстановитьТелоИзСтроки(ТекстЗапроса);

			Если Метод = "POST" Тогда

				HTTPОтвет = HTTP.ОтправитьДляОбработки(HTTPЗапрос);

			Иначе	//	Метод = "GET"

				HTTPОтвет = HTTP.Получить(HTTPЗапрос);

			КонецЕсли;

			Результат.КодОтвета = HTTPОтвет.КодСостояния;
			ДанныеRFC2616 = СервисыСерверПовтИсп.КодыRFC2616(HTTPОтвет.КодСостояния);

			Результат.Ошибка = НЕ (Результат.КодОтвета = 200 ИЛИ Результат.КодОтвета = 201);
			Результат.ОписаниеОшибки = Формат(Результат.КодОтвета, "ЧДЦ=; ЧН=000; ЧГ=") + Символы.ПС + ДанныеRFC2616.Описание;

			ТекстОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();	//	LNK 10.09.2019 13:54:49

			Попытка

				ЧтениеJSON = Новый ЧтениеJSON;
				ЧтениеJSON.УстановитьСтроку(ТекстОтвета);
				Результат.ДанныеОтвета = ПрочитатьJSON(ЧтениеJSON);

				Если Результат.Ошибка И Результат.ДанныеОтвета.Свойство("message") И НЕ ПустаяСтрока(Результат.ДанныеОтвета.message) Тогда

					Результат.ОписаниеОшибки = СокрЛП(Результат.ОписаниеОшибки) + Символы.ПС + "Ответ банка:" + Символы.ПС + Результат.ДанныеОтвета.message;

				КонецЕсли;

			Исключение

				Результат.Ошибка = Истина;
				Результат.ОписаниеОшибки = "Парсер JSON:" + Символы.ПС + ОписаниеОшибки();

				ЖурналСобытий.Регистрация("JSON.error", УровеньЖурналаРегистрации.Ошибка
				, Метаданные.Справочники.КлассификаторБанков
				, ДанныеСоединения.Банк
				,
				, Результат.ОписаниеОшибки + Символы.ПС + ОбщегоНазначенияКлиентСервер.REPEAT("-", 50) + Символы.ПС + ТекстОтвета
				,
				, Ложь);

			КонецПопытки;

		Исключение

			Результат.Ошибка = Истина;
			Результат.ОписаниеОшибки = "ошибка запроса к серверу (" + ИмяПроцедуры + "):" + Символы.ПС + ОписаниеОшибки();

		КонецПопытки;

	Иначе

		Результат.Ошибка = Истина;
		Результат.ОписаниеОшибки = "ошибка запроса к серверу (" + ИмяПроцедуры + "):" + Символы.ПС + "текст запроса НЕ заполнен (пустая строка)";

	КонецЕсли;

	Возврат Результат;

КонецФункции












