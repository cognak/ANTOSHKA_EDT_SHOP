// Программный интерфейс подключения украинских фискальных регистраторов.
// Использует в своей работе Универсальный драйвер фискальных регистраторов компании АртСофт
// Документация по работе драйвера и примеры подключения http://www.artsoft.ua/ArtSoftFiscalPrinter.php

Функция Подключить(ОбъектДрайвера, Параметры) Экспорт
	
	Перем Порт, Скорость, Модель;
	  
	Параметры.Свойство("Порт", Порт);
	Параметры.Свойство("Скорость", Скорость);
	Параметры.Свойство("Модель", Модель);
	
	Результат = Истина;
	
	// Проверка параметров
	Если Порт = Неопределено Или Скорость = Неопределено Тогда
		Результат = Ложь;
	Иначе
		// Начало работы с универсальным драйвером АртСофт
		Успешно = ОбъектДрайвера.start(Лев(Модель,1));
		Если НЕ Успешно Тогда
			// Завершение работы с универсальным драйвером АртСофт
			ОбъектДрайвера.stop();
			Возврат Ложь;
		КонецЕсли;
		
		// Открытие СОМ-порта и соединение с регистратором
		Успешно = ОбъектДрайвера.openPort(Порт,Скорость); 
		Если НЕ Успешно Тогда
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьОшибку(ОбъектДрайвера, ОписаниеОшибки) Экспорт
	
	ОписаниеОшибки = ОбъектДрайвера.LastErrorExText;
	Возврат ОбъектДрайвера.LastError;
	
КонецФункции

Функция Отключить(ОбъектДрайвера) Экспорт
	
	// Отключение устройства.
	ОбъектДрайвера.closePort();
	ОбъектДрайвера.stop();
	
КонецФункции

Функция ОткрытьЧек(ОбъектДрайвера, ФискальныйЧек, ЧекВозврата,  АннулироватьОткрытыйЧек, НомерЧека, НомерСмены) Экспорт
	
	Результат  = Истина;

	ИмяКассира = "";
	Если ФискальныйЧек Тогда
		// Открытие фискального чека ФР
		Если НЕ ЧекВозврата Тогда
			Успешно = ОбъектДрайвера.beginFiscalReceipt(0, ИмяКассира);
		Иначе
			Успешно = ОбъектДрайвера.beginFiscalReceipt(1, ИмяКассира);
		КонецЕсли;
	Иначе
		// Открытие нефискального (чека комментариев) ФР
		Успешно = ОбъектДрайвера.beginNonFiscal();
	КонецЕсли;
	
	Если НЕ Успешно Тогда 
		Результат = Ложь;
	Иначе

		НомерСмены = ОбъектДрайвера.SmenNum;			// Получение номера смены фискального регистратора (номер Z-отчета)
		НомерЧека = ОбъектДрайвера.LastReceiptNum + 1;	// Получение номера последнего фискального чека
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция ЗакрытьЧек(ОбъектДрайвера, НаличнаяОплата, ОплатаКартой, ОплатаКредитом, ОплатаСертификатом, ОплатаПослеплата, ФискальныйЧек, СлужебнаяИнфа = Неопределено) Экспорт
	
	Результат  = Истина;

	// Закрытие чека
	
	// Выполнение оплат только для фискального чека
	Если ФискальныйЧек Тогда
		// + HVOYA 11.10.2016 15:11:15, Латышев А.А.
		Если Не СлужебнаяИнфа = Неопределено Тогда
			Для НомерСтроки = 1 По СтрЧислоСтрок(СлужебнаяИнфа) Цикл
				ВыделеннаяСтрока = СтрПолучитьСтроку(СлужебнаяИнфа, НомерСтроки);
				Успешно = ОбъектДрайвера.printText(ВыделеннаяСтрока);
				Если НЕ Успешно Тогда 
					Результат = Ложь;
					Возврат Результат;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли; 
		// - HVOYA 11.10.2016 15:11:15, Латышев А.А. 
		
		// ОПЛАТА НАЛИЧНЫМ ТИПОМ
		Если НаличнаяОплата > 0 Тогда
			Успешно = ОбъектДрайвера.printRecTotal(НаличнаяОплата, 0);
			Если НЕ Успешно Тогда 
				Результат = Ложь;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
		// ОПЛАТА БЕЗНАЛИЧНЫМ ТИПОМ
		Если ОплатаКредитом > 0 Тогда
			Успешно = ОбъектДрайвера.printRecTotal(ОплатаКредитом, 1);
			Если НЕ Успешно Тогда 
				Результат = Ложь;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
		// ОПЛАТА БЕЗНАЛИЧНЫМ ТИПОМ
		Если ОплатаСертификатом > 0 Тогда
			Успешно = ОбъектДрайвера.printRecTotal(ОплатаСертификатом, 2);
			Если НЕ Успешно Тогда 
				Результат = Ложь;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
		// ОПЛАТА БЕЗНАЛИЧНЫМ ТИПОМ
		Если ОплатаКартой > 0 Тогда
			Успешно = ОбъектДрайвера.printRecTotal(ОплатаКартой, 3);
			Если НЕ Успешно Тогда 
				Результат = Ложь;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
		// ОПЛАТА БЕЗНАЛИЧНЫМ ТИПОМ
		Если ОплатаПослеплата > 0 Тогда
			Успешно = ОбъектДрайвера.printRecTotal(ОплатаПослеплата, 4);
			Если НЕ Успешно Тогда 
				Результат = Ложь;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;	
	
	// ЗАКРЫТИЕ ЧЕКА
	Если ФискальныйЧек Тогда
		Успешно = ОбъектДрайвера.endFiscalReceipt(); 	// Закрытие фискального чека
	Иначе
		Успешно = ОбъектДрайвера.endNonFiscal(); 		// Закрытие нефискального чека (чека комментариев)
	КонецЕсли;
	
	Если НЕ Успешно Тогда 
		Результат = Ложь;
	КонецЕсли;

	
	Возврат Результат;
	
КонецФункции
														
Функция ОтменитьЧек(ОбъектДрайвера) Экспорт
	
	Результат  = Истина;

	// Аннулировать чек
	Результат = ОбъектДрайвера.printRecVoid();

	Возврат Результат;
	
КонецФункции

Функция НапечататьОтчетБезГашения(ОбъектДрайвера) Экспорт
	
	Результат  = Истина;

	// Печать X-отчёта фискального регистратора
	Успешно = ОбъектДрайвера.printXReport();
	Если НЕ Успешно Тогда 
		Результат = Ложь;
	Иначе
		НомерСмены = ОбъектДрайвера.SmenNum;		// Получение номера смены фискального регистратора (номер Z-отчета)
		НомерЧека = ОбъектДрайвера.LastReceiptNum;  // Получение номера последнего фискального чека
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НапечататьОтчетСГашением(ОбъектДрайвера) Экспорт
	
	Результат  = Истина;

	// Снятие Z-отчёта фискального регистратора
    Успешно = ОбъектДрайвера.printZReport();
	Если НЕ Успешно Тогда 
		Результат = Ложь;
	Иначе
		НомерСмены = ОбъектДрайвера.SmenNum;		// Получение номера смены фискального регистратора (номер Z-отчета)
		НомерЧека = ОбъектДрайвера.LastReceiptNum;  // Получение номера последнего фискального чека
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция НапечататьФискСтроку(ОбъектДрайвера, Наименование, Количество, Цена, Сумма, НомерСекции, НДС, ДопРеквизиты, Параметры) Экспорт

	ТаблицаСоответствий = Новый Массив;

	Результат = Истина;
	
	СуммоваяСкидка = - Окр(Количество*Цена - Сумма, 2);
	ПроцентнаяСкидка = 0;
	НалоговаяГруппа = 0;

	ДопРеквизиты = МенеджерОборудованияСервер.ПолучитьТаблицуДопРеквизитов(ДопРеквизиты);
	
	Параметры.Свойство("ТаблицаСоответствийНалоговыхГрупп", ТаблицаСоответствий);
	Для каждого Строка из ТаблицаСоответствий Цикл
		Если Строка[1] = ДопРеквизиты[0] И Строка[2] = ДопРеквизиты[1] Тогда
			НалоговаяГруппа = Строка[0];
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Печать строки чека
	Результат = ОбъектДрайвера.printRecItem(Наименование, Цена, Количество, НалоговаяГруппа, СуммоваяСкидка, ПроцентнаяСкидка);
	
	Возврат Результат;
	
КонецФункции
																										
Функция НапечататьНефискСтроку(ОбъектДрайвера, СтрокаТекста, ФискальныйЧек) Экспорт
	
	Результат = Истина;
	
	Если ФискальныйЧек Тогда
			Результат = ОбъектДрайвера.printText(СтрокаТекста);
	Иначе
			Результат = ОбъектДрайвера.printNonFiscalText(СтрокаТекста);
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция НапечататьЧекВнесенияВыемки(ОбъектДрайвера, Сумма) Экспорт

	Результат  = Истина;

	// Положительная сумма - внесение суммы, отрицательная - изъятие суммы
	Результат = ОбъектДрайвера.printRecCash(Сумма);

	Возврат Результат;
	
КонецФункции

Функция НапечататьШтрихКод(ОбъектДрайвера, ТипШтрихКода, ШтрихКод) Экспорт
	
	 
	Результат  = Истина;
	
	ТипШК = 2; // Тип штрих-кода по умолчанию CODE128
	
	Если ТипШтрихКода = "EAN8" Тогда
		ТипШК = 0; // 0 - EAN8 
	ИначеЕсли ТипШтрихКода = "EAN13" Тогда
		ТипШК = 1; // 1 - EAN13
	ИначеЕсли ТипШтрихКода = "CODE128" Тогда
		ТипШК = 2; // 2 - CODE128
	//Иначе
	//	Результат  = Ложь;
	//	Возврат Результат;
	КонецЕсли;

	// Функция предназначена для печати штрих-кода в чеке. 
	// Типы поддерживаемых штрих-кодов зависят от моделей и версий фискальных регистраторов. 
	// ШтрихКод – Строка текста до 128 символов для печати штрих-кода на фискальном регистраторе. 
	// ТипШК – целое число, тип штрих-кода (0 - EAN8, 1 - EAN13, 2 - CODE128) 
	Результат = ОбъектДрайвера.printBarCode(ШтрихКод, ТипШК);

	Возврат Результат;

КонецФункции

Функция ОткрытьДенежныйЯщик(ОбъектДрайвера) Экспорт
	Результат  = Истина;

	// Открытие денежного ящика (сейфа)
	Результат = ОбъектДрайвера.openCashDrawer();

	Возврат Результат;
	
КонецФункции

Функция ПолучитьШиринуСтроки(ОбъектДрайвера, ШиринаСтроки) Экспорт
	Возврат 34;	
КонецФункции

Функция ТестУстройства(ОбъектДрайвера, РезультатТеста, АктивированДемоРежим, Параметры) Экспорт
	
	Результат = Подключить(ОбъектДрайвера, Параметры);

	// Дополнительные действия по проверке устройства

	Если НЕ Результат Тогда
		ПолучитьОшибку(ОбъектДрайвера, РезультатТеста);
	КонецЕсли;
	
	Отключить(ОбъектДрайвера);

	Возврат Результат;
	
КонецФункции

Функция ПолучитьНомерВерсии(ОбъектДрайвера) Экспорт
	Возврат "4.0";
КонецФункции

Функция ПолучитьОписание(ОбъектДрайвера, Наименование, Описание, ТипОборудования, РевизияИнтерфейса, ИнтеграционнаяБиблиотека, ОсновнойДрайверУстановлен, ПолучитьURLCкачивания) Экспорт
	Наименование = "АртСофт: Универсальный драйвер фискальных регистраторов";
	Описание = "OLE-сервер ArtSoft Универсальный драйвер РРО – предназначена для работы прикладных программ с украинскими фискальными регистраторами в операционных системах MS Windows";
	ТипОборудования = "ФискальныйРегистратор";
	ИнтеграционнаяБиблиотека = "ArtSoftFiscalPrinter.dll";
	РевизияИнтерфейса = "1.0";
	ОсновнойДрайверУстановлен = Истина;
	ПолучитьURLCкачивания = "www.artsoft.ua/download/ArtSoftFiscalPrinter.rar";
КонецФункции
																			
Функция ПолучитьПараметры(ОбъектДрайвера, ТаблицаПараметров) Экспорт
	
	ТаблицаПараметров = "";
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьДополнительныеДействия(ОбъектДрайвера, ДополнительныеДействия) Экспорт
	
	ДополнительныеДействия = "";
	
	Возврат Истина;
	
КонецФункции

Функция НапечататьПериодическийОтчетПоДатам(ОбъектДрайвера, ПериодНачало, ПериодКонец, Сокращенный) Экспорт
	
	// Тип отчета	Название отчета
	// 0			отчет по дате полный
	// 1			отчет по дате краткий
	// 2			отчет по номеру полный
	// 3			отчет по номеру краткий

	Результат  = Истина;
	Если Сокращенный Тогда
		ТипОтчета = 1;
	Иначе
		ТипОтчета = 0;
	КонецЕсли;
		
	НП = Формат(ПериодНачало, "ДФ=ддММгг");
	КП = Формат(ПериодКонец, "ДФ=ддММгг");

	Результат = ОбъектДрайвера.printPeriodicReport(ТипОтчета, НП, КП);

	Возврат Результат;
	
КонецФункции

Функция НапечататьПериодическийОтчетПоНомерам(ОбъектДрайвера, НомерНачало, НомерКонец, Сокращенный) Экспорт
	
	// Тип отчета	Название отчета
	// 0			отчет по дате полный
	// 1			отчет по дате краткий
	// 2			отчет по номеру полный
	// 3			отчет по номеру краткий

	Результат  = Истина;
	
	Если Сокращенный Тогда
		ТипОтчета = 3;
	Иначе
		ТипОтчета = 2;
	КонецЕсли;
		
	Результат = ОбъектДрайвера.printPeriodicReport(ТипОтчета, НомерНачало, НомерКонец);

	Возврат Результат;
	
КонецФункции

Функция НапечататьНулевойЧек(ОбъектДрайвера) Экспорт
	
	Результат  = Истина;

	Результат = ОбъектДрайвера.printNullReceipt();

	Возврат Результат;
	
КонецФункции

//	LNK 22.03.2017 16:26:02
Функция УстановитьВремяРегистратора(ОбъектДрайвера)	Экспорт

	Результат  = Истина;

	ТекстДаты = Формат(ОбщегоНазначенияВызовСервера.ТекущаяДатаСервера(), "ДФ=ЧЧммсс");
	Результат = ОбъектДрайвера.setTime(ТекстДаты);

	Возврат Результат;

КонецФункции // УстановитьВремяРегистратора()

Функция НапечататьОтчетОПроданныхТоварах(ОбъектДрайвера) Экспорт
	
	//	Тип отчета	Название отчета
	//	0			отчет реализованных товаров
	//	1			отчет запрограммированных товаров
	//	2			отчет по скидкам
	//	3			отчет по налоговым ставкам
	//	4			отчет по товарным группам
	//	5			отчет по отделам
	//	6			отчет по операторам
	//	7			отчет по контрольной ленте

	Результат  = Истина;

	Результат = ОбъектДрайвера.printReport(0);

	Возврат Результат;
	
КонецФункции

Функция ВывестиИнформациюНаДисплейПокупателя(ОбъектДрайвера, Параметры, СтрокаТекста, Сумма) Экспорт
	Если  Параметры.Модель="501" или Параметры.Модель="205" тогда 
		ДлинаПоля = 16;
	Иначе
		ДлинаПоля = 20;
	КонецЕсли;
	//ДлинаПоля = ?(Параметры.Модель="501",16,20);  // 16 для модели МІНІ-ФП54.01
	
	Результат = ОбъектДрайвера.displayText(ПостроитьПоле(СтрПолучитьСтроку(СтрокаТекста, 1), ДлинаПоля), Число(Сумма) );
	Если Результат Тогда
		Если Параметры.Модель="205" тогда			
		Результат = ОбъектДрайвера.displayTextAt(1, СтрПолучитьСтроку(СтрокаТекста, 1))
				  И ОбъектДрайвера.displayTextAt(2, СтрПолучитьСтроку(СтрокаТекста, 2)); 			
		Иначе
		Результат = ОбъектДрайвера.displayTextAt(1, ПостроитьПоле(СтрПолучитьСтроку(СтрокаТекста, 1), ДлинаПоля))
				  И ОбъектДрайвера.displayTextAt(2, ПостроитьПоле(СтрПолучитьСтроку(СтрокаТекста, 2), ДлинаПоля));
		КонецЕсли;
				  
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОчиститьДисплейПокупателя(ОбъектДрайвера) Экспорт
	
	Результат = ОбъектДрайвера.clearText();
	
	Возврат Результат;
	
КонецФункции

// Подготавливает поле, если поле слишком короткое - дополняет пробелами
Функция ПостроитьПоле(Текст, ДлинаПоля)
	
	Если СтрДлина(Текст) < ДлинаПоля Тогда
		ТекстПолный = Текст;
		КолвоПробелов = ДлинаПоля - СтрДлина(ТекстПолный);
		Для й = 1 По КолвоПробелов Цикл
			ТекстПолный = " " + ТекстПолный;
		КонецЦикла;
	Иначе
		ТекстПолный = Лев(Текст, ДлинаПоля);
	КонецЕсли;
	
	Возврат ТекстПолный;
	
КонецФункции


//	LNK 22.03.2017 16:26:02
Функция ОткрытьЧекКомментариев(ОбъектДрайвера)	Экспорт

	Результат  = Истина;

	Результат = ОбъектДрайвера.beginNonFiscal();

	Возврат Результат;

КонецФункции 

Функция ЗакрытьЧекКомментариев(ОбъектДрайвера)	Экспорт

	Результат  = Истина;

	Результат = ОбъектДрайвера.endNonFiscal();

	Возврат Результат;

КонецФункции

Функция ПечататьТекстКомментариев(ОбъектДрайвера,Параметры)	Экспорт

	Результат  = Истина;

	Результат = ОбъектДрайвера.printNonFiscalText(Параметры[2]);

	Возврат Результат;

КонецФункции

Функция ПечататьШтрикодВЧеке(ОбъектДрайвера,Параметры)	Экспорт

	Результат  = Истина;

	Результат = ОбъектДрайвера.printBarCode(Параметры,2);

	Возврат Результат;

КонецФункции


