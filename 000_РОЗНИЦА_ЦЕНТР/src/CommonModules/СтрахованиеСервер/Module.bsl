Процедура ОтразитьТоварыЗастрахованные(ДополнительныеСвойства, Движения, Отказ)	Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыЗастрахованные;

	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда

		Возврат;

	КонецЕсли;

	Движения.ТоварыЗастрахованные.Записывать = Истина;
	Движения.ТоварыЗастрахованные.Загрузить(Таблица);

КонецПроцедуры

Процедура ОтразитьДоговорыСтрахованияОплаченные(ДополнительныеСвойства, Движения, Отказ)	Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоговорыСтрахованияОплаченные;

	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда

		Возврат;

	КонецЕсли;

	Движения.ДоговорыСтрахованияОплаченные.Записывать = Истина;
	Движения.ДоговорыСтрахованияОплаченные.Загрузить(Таблица);

КонецПроцедуры

Функция ПолучитьУслугуСтрахования_()	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаСправочник.Ссылка КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК ТаблицаСправочник
	|ГДЕ
	|	ТаблицаСправочник.IDN = &IDN
	|	И НЕ(ТаблицаСправочник.ПометкаУдаления
	|				ИЛИ ТаблицаСправочник.Блокирован)"
	);
	Запрос.УстановитьПараметр("IDN", "7011600");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда

			УслугаСтрахования = Выборка.Номенклатура;

	Иначе	УслугаСтрахования = Справочники.Номенклатура.ПустаяСсылка();

	КонецЕсли;

	Возврат УслугаСтрахования;

КонецФункции // ПолучитьУслугуСтрахования()

Функция ПолучитьУслугуСтрахования(Магазин, ДатаСреза = '00010101')	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ПолитикаМагазина.УслугаСтрахования, Ведущая.УслугаСтрахования) КАК УслугаСтрахования,
	|	ВЫБОР
	|		КОГДА ПолитикаМагазина.Магазин = &Магазин
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КлючПорядка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК УслугаСтрахования) КАК Ведущая
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаМагазинов.СрезПоследних(
	|				&ДатаСреза,
	|				Магазин = &Магазин
	|					ИЛИ Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)) КАК ПолитикаМагазина
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлючПорядка УБЫВ"
	);
	Запрос.УстановитьПараметр("Магазин"  , Магазин);
	Запрос.УстановитьПараметр("ДатаСреза", ?(ДатаСреза = '00010101', ТекущаяДатаСеанса(), ДатаСреза));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	Возврат Выборка.УслугаСтрахования;

КонецФункции // ПолучитьУслугуСтрахования()
