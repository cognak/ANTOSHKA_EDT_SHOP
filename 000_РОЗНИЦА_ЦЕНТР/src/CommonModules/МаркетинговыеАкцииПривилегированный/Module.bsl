//	LNK 16.06.2020 14:40:17
Функция ПроверитьСерийныеНомераПогашенияКупонов(ТаблицаТовары, ТаблицаПогашенныеКупоны, Отказ, ТекстОшибки)	Экспорт

	Если НЕ ТипЗнч(ТекстОшибки) = Тип("Строка") Тогда

		ТекстОшибки = "";

	КонецЕсли;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&ТаблицаТовары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Погашение.СкидочныйКупон КАК Номенклатура,
	|	Погашение.СерийныйНомер КАК СерийныйНомер,
	|	Погашение.НоминалКупона КАК НоминалКупона
	|ПОМЕСТИТЬ ПогашенныеКупоны
	|ИЗ
	|	&ТаблицаПогашенныеКупоны КАК Погашение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВладельцы.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаВладельцы.Номенклатура) КАК Номенклатура
	|ИЗ
	|	(ВЫБРАТЬ
	|		Товары.НомерСтроки КАК НомерСтроки,
	|		ТаблицаСправочник.Ссылка КАК Номенклатура
	|	ИЗ
	|		Справочник.Номенклатура КАК ТаблицаСправочник
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Товары КАК Товары
	|			ПО ТаблицаСправочник.Ссылка = Товары.Номенклатура
	|	ГДЕ
	|		ТаблицаСправочник.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.СкидочныйКупон)
	|		И ТаблицаСправочник.ИспользоватьСерийныеНомера) КАК ТаблицаВладельцы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПогашенныеКупоны КАК ТаблицаПогашенныеКупоны
	|		ПО ТаблицаВладельцы.Номенклатура = ТаблицаПогашенныеКупоны.Номенклатура
	|ГДЕ
	|	ТаблицаПогашенныеКупоны.СерийныйНомер ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВладельцы.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаВладельцы.Номенклатура) КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаПогашенныеКупоны.СерийныйНомер) КАК СерийныйНомер,
	|	ТаблицаДвижений.Магазин КАК Магазин,
	|	ТаблицаДвижений.Дата КАК Дата
	|ИЗ
	|	(ВЫБРАТЬ
	|		Товары.НомерСтроки КАК НомерСтроки,
	|		ТаблицаСправочник.Ссылка КАК Номенклатура
	|	ИЗ
	|		Справочник.Номенклатура КАК ТаблицаСправочник
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Товары КАК Товары
	|			ПО ТаблицаСправочник.Ссылка = Товары.Номенклатура
	|	ГДЕ
	|		ТаблицаСправочник.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.СкидочныйКупон)
	|		И ТаблицаСправочник.ИспользоватьСерийныеНомера) КАК ТаблицаВладельцы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПогашенныеКупоны КАК ТаблицаПогашенныеКупоны
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДвиженияСерийныхНомеровКупонов КАК ТаблицаДвижений
	|			ПО ТаблицаПогашенныеКупоны.Номенклатура = ТаблицаДвижений.Купон
	|				И ТаблицаПогашенныеКупоны.СерийныйНомер = ТаблицаДвижений.СерийныйНомер
	|		ПО ТаблицаВладельцы.Номенклатура = ТаблицаПогашенныеКупоны.Номенклатура
	|ГДЕ
	|	ТаблицаДвижений.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПогашенныеКупоны"
	);
	Запрос.УстановитьПараметр("ТаблицаТовары", ТаблицаТовары);
	Запрос.УстановитьПараметр("ТаблицаПогашенныеКупоны", ТаблицаПогашенныеКупоны);
	
	Результаты = Запрос.ВыполнитьПакет();

	Если НЕ Результаты[2].Пустой() Тогда

		Отказ = Истина;
		Выборка = Результаты[2].Выбрать();

		Пока Выборка.Следующий() Цикл

			ТекстОшибки = ТекстОшибки + ?(ПустаяСтрока(ТекстОшибки), "", Символы.ПС)
				+  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Серийный номер скидочного купона номенклатуры ""%1"" (стр.%2) , не найден!'")
					, СокрЛП(Выборка.Номенклатура), Выборка.НомерСтроки
				);

		КонецЦикла;

	КонецЕсли;
	
	Если НЕ Отказ И НЕ Результаты[3].Пустой() Тогда

		Отказ = Истина;
		Выборка = Результаты[3].Выбрать();

		Пока Выборка.Следующий() Цикл

			ТекстОшибки = ТекстОшибки + ?(ПустаяСтрока(ТекстОшибки), "", Символы.ПС)
				+  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Номер скидочного купона %3 номенклатуры ""%1"" (стр.%2) уже погашен в магазине %4. Дата погашения - %5.'")
						, Выборка.Номенклатура
						, Выборка.НомерСтроки
						, Выборка.СерийныйНомер
						, Выборка.Магазин
						, Формат(Выборка.Дата, "ДФ=dd.MM.yyyy"
					)
				);

		КонецЦикла;

	КонецЕсли;

	Возврат НЕ Отказ;

КонецФункции






