////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Логин") Тогда 
		Логин = Параметры.Логин;
	КонецЕсли;
	
	Если Параметры.Свойство("ИдентификаторПожелания") Тогда 
		ИдентификаторПожелания = Параметры.ИдентификаторПожелания;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Отправить(Команда)
	
	Если ПустаяСтрока(Комментарий) Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Комментарий не может быть пустым'"));
		Возврат;
	КонецЕсли;
	
	
	Результат = ДобавитьКомментрий();
	Если Результат Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Комментарий добавлен.'"));
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Комментарий не добавлен. Повторите попытку позднее.'"));
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ДобавитьКомментрий()
	
	ПроксиИнформационногоЦентра = ИнформационныйЦентрСервер.ПолучитьПроксиИнформационногоЦентра();
	ФабрикаXDTOВебСервиса       = ПроксиИнформационногоЦентра.ФабрикаXDTO;
	ОписаниеКомментария         = ПривестиКОбъектуXDTOКомментарийПользователя(ФабрикаXDTOВебСервиса);
	
	Возврат ПроксиИнформационногоЦентра.AddCommentSuggestion(ОписаниеКомментария);
	
КонецФункции

&НаСервере
Функция ПривестиКОбъектуXDTOКомментарийПользователя(ФабрикаXDTOВебСервиса)
	
	ТипКомментария      = ФабрикаXDTOВебСервиса.Тип("http://www.1c.ru/SaaS/1.0/XMLSchema/ManageInfoCenter", "CommentListElement");
	ОписаниеКомментария = ФабрикаXDTOВебСервиса.Создать(ТипКомментария);
	
	ОписаниеКомментария.Text           = Комментарий;
	ОписаниеКомментария.Date           = ТекущаяДата(); // Проектное решение БСП
	ОписаниеКомментария.Author         = Логин;
	ОписаниеКомментария.RefSuggestion  = ИдентификаторПожелания;
	ОписаниеКомментария.Vote           = 0;
	
	Возврат ОписаниеКомментария;
	
КонецФункции

