
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ПечатьИзДокумента Тогда
		МассивНепроверяемыхРеквизитов.Добавить("МагазинДляЦен");
		МассивНепроверяемыхРеквизитов.Добавить("ВидЦены");
	Иначе
		Если ЦеныПоВидуЦены Тогда
			МассивНепроверяемыхРеквизитов.Добавить("МагазинДляЦен");
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("ВидЦены");
		КонецЕсли;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.ШаблонЦенника");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.ШаблонЭтикетки");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоЦенников");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоЭтикеток");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если ЭтотОбъект.НажатаПечать Тогда
		ПроверитьЗаполнениеШаблоновЦенниковИЭтикеток(Отказ);
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ПроверитьЗаполнениеШаблоновЦенниковИЭтикеток(Отказ)

	ПроверятьЦенники = (ЭтотОбъект.Режим = "ПечатьЦенниковИЭтикеток") ИЛИ (ЭтотОбъект.Режим = "ПечатьЦенников");
	ПроверятьЭтикетки = (ЭтотОбъект.Режим = "ПечатьЦенниковИЭтикеток") ИЛИ (ЭтотОбъект.Режим = "ПечатьЭтикеток");
	
	ШаблонСообщенияЦенники  = НСтр("ru='Не заполнена колонка ""Шаблон ценника"" в строке %НомерСтроки%.'");
	ШаблонСообщенияЭтикетки = НСтр("ru='Не заполнена колонка ""Шаблон этикетки"" в строке %НомерСтроки%.'");
	
	ШаблонСообщенияЦенникиКоличество  = НСтр("ru='Не заполнена колонка ""Количество ценников"" в строке %НомерСтроки%.'");
	ШаблонСообщенияЭтикеткиКоличество = НСтр("ru='Не заполнена колонка ""Количество этикеток"" в строке %НомерСтроки%.'");
	
	СтрокиВыбран = ЭтотОбъект.Товары.НайтиСтроки(Новый Структура("Выбран", Истина));
	
	Для каждого СтрокаТаблицы Из СтрокиВыбран Цикл
	
		Если ПроверятьЦенники И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ШаблонЦенника) Тогда
			
			Отказ = Истина;
			ТекстСообщения = СтрЗаменить(ШаблонСообщенияЦенники, "%НомерСтроки%", СтрокаТаблицы.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТаблицы.НомерСтроки, "ШаблонЦенника");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект", Отказ);
			
		КонецЕсли;
		
		Если ПроверятьЦенники И НЕ ЗначениеЗаполнено(СтрокаТаблицы.КоличествоЦенников) Тогда
			
			Отказ = Истина;
			ТекстСообщения = СтрЗаменить(ШаблонСообщенияЦенникиКоличество, "%НомерСтроки%", СтрокаТаблицы.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТаблицы.НомерСтроки, "КоличествоЦенников");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект", Отказ);
			
		КонецЕсли;
		
		Если ПроверятьЭтикетки И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ШаблонЭтикетки) Тогда
			
			Отказ = Истина;
			ТекстСообщения = СтрЗаменить(ШаблонСообщенияЭтикетки, "%НомерСтроки%", СтрокаТаблицы.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТаблицы.НомерСтроки, "ШаблонЭтикетки");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект", Отказ);
			
		КонецЕсли;
			
		Если ПроверятьЭтикетки И НЕ ЗначениеЗаполнено(СтрокаТаблицы.КоличествоЭтикеток) Тогда
			
			Отказ = Истина;
			ТекстСообщения = СтрЗаменить(ШаблонСообщенияЭтикеткиКоличество, "%НомерСтроки%", СтрокаТаблицы.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТаблицы.НомерСтроки, "КоличествоЭтикеток");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект", Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры
