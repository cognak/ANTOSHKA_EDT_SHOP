#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;

	Параметры.Свойство("Номенклатура", Объект.Номенклатура);
	ТекстОшибки = "";

	Если Объект.Номенклатура.Пустая() Тогда

		Отказ = Истина;
		ТекстОшибки = "Владелец сертификатов НЕ указан.";

	Иначе

		Элементы.ТипСертификата.СписокВыбора.ЗагрузитьЗначения(ПодарочныеСертификатыПовтИсп.ТипыСертификатов(Истина));

		ЗаполнитьОпределенияДиапазона();

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте	//	LNK 12.04.2021 18:52:01
Процедура ТипСертификатаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Объект.ТипСертификата = ВыбранноеЗначение;

КонецПроцедуры

&НаКлиенте
Процедура ТипСертификатаПриИзменении(Элемент)

	ЗаполнитьОпределенияДиапазона(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)

	ЗаполнитьОпределенияДиапазона(Элемент.Имя);

КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьДиапазонСертификатов(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьДиапазонСертификатовЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения
		, "Будет запущен процесс заполнения диапазона
		  |серийных номеров сертификатов «" + Объект.ТипСертификата + "».
		  |Подтвердите своё решение:"
		, РежимДиалогаВопрос.ОКОтмена, 60, КодВозвратаДиалога.Отмена
		, "Генератор серийных номеров", КодВозвратаДиалога.ОК
	);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДиапазонСертификатовЗавершение(КодВозврата, ДополнительныеПараметры)	Экспорт

	Если КодВозврата = КодВозвратаДиалога.ОК Тогда

		ТекстОшибки = "";

		Если ЗаполнитьДиапазонСертификатовНаСервере(ТекстОшибки) Тогда

			ПоказатьОповещениеПользователя("Процесс завершён",, "Процесс заполнения сертификатов завершен нормально."
			, БиблиотекаКартинок.Информация32);

		Иначе

			Если НЕ ПустаяСтрока(ТекстОшибки) Тогда

				Сообщить(ТекстОшибки);

			КонецЕсли;

			ПоказатьОповещениеПользователя("Ошибка выполнения",, "Процесс заполнения не был завершён."
			, БиблиотекаКартинок.Ошибка32);

		КонецЕсли;

		Оповестить("СозданыСертификаты");

	Иначе

		ПоказатьОповещениеПользователя("Действие отменено",, "Начало процесса не подтверждено пользователем"
		, БиблиотекаКартинок.Ошибка32);

	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ЗаполнитьДиапазонСертификатовНаСервере(ТекстОшибки)

	ПодарочныеСертификатыСервер.ЗаполнитьДиапазонСерийныхНомеров(
		Объект.Номенклатура, Объект.ТипСертификата, Номинал, Объект.НомерПервый, Объект.Количество, ТекстОшибки
	);
	Выполнено = ПустаяСтрока(ТекстОшибки);

	ЗаполнитьОпределенияДиапазона();

	Возврат Выполнено;

КонецФункции

	
#КонецОбласти

#Область ПоддержкаФункционалаФормы

&НаСервере
Процедура ЗаполнитьОпределенияДиапазона(ПриИзменении = "")

	КритерииВладельца  = ПодарочныеСертификатыСервер.ПолучитьКритерииВладельца(Объект.Номенклатура);
	Объект.НомерПервый = КритерииВладельца.НомерПервый;
	Номинал = КритерииВладельца.Номинал;

	Если НЕ ПриИзменении = "ТипСертификата" Тогда

		Если ПустаяСтрока(КритерииВладельца.ТипСертификата) Тогда

			Объект.ТипСертификата = Элементы.ТипСертификата.СписокВыбора[0].Значение;

		Иначе

			Объект.ТипСертификата = КритерииВладельца.ТипСертификата;

		КонецЕсли;

	КонецЕсли;

	Элементы.ТипСертификата.ТолькоПросмотр = НЕ ПустаяСтрока(КритерииВладельца.КодСерийногоНомера);

	Если ПустаяСтрока(КритерииВладельца.КодСерийногоНомера) Тогда

		КритерииВладельца.КодСерийногоНомера = ПодарочныеСертификатыСервер.СформироватьКодСерийногоНомера(Объект.ТипСертификата, КритерииВладельца.Номинал, Объект.НомерПервый - 1);

	КонецЕсли;

	КодСерийногоНомера = КритерииВладельца.КодСерийногоНомера;

КонецПроцедуры
	
#КонецОбласти











