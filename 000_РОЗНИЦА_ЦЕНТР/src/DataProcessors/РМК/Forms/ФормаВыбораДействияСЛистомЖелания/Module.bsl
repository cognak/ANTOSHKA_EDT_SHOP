
&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КорзиныЛистаЖеланий.Ссылка
	|ПОМЕСТИТЬ ВТ_Корзины
	|ИЗ
	|	Справочник.КорзиныЛистаЖеланий КАК КорзиныЛистаЖеланий
	|ГДЕ
	|	НЕ КорзиныЛистаЖеланий.ПометкаУдаления
	|	И КорзиныЛистаЖеланий.Магазин = &Магазин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Корзины.Ссылка КАК Корзина,
	|	ЛистыЖеланийОстатки.ЛистЖеланий КАК Заказ,
	|	ЛистыЖеланийОстатки.ЛистЖеланий.Номер КАК Код,
	|	ЛистыЖеланийОстатки.ЛистЖеланий.Анкета.ИмяИмениника КАК ИмяИмениника,
	|	ЛистыЖеланийОстатки.ЛистЖеланий.Анкета.ДатаРожденияИменинника КАК ДатаРожденияИменинника,
	|	ЕСТЬNULL(ЛистыЖеланийОстатки.ЛистЖеланий.СрокДействия, ДАТАВРЕМЯ(1, 1, 1)) КАК СрокДействия
	|ИЗ
	|	ВТ_Корзины КАК ВТ_Корзины
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЛистыЖеланий.Остатки КАК ЛистыЖеланийОстатки
	|		ПО ВТ_Корзины.Ссылка = ЛистыЖеланийОстатки.Корзина
	|
	|УПОРЯДОЧИТЬ ПО
	|	Корзина"
	);
	Запрос.УстановитьПараметр("Магазин", ?(Параметры.Свойство("Магазин"), Параметры.Магазин, ПараметрыСеанса.ТекущийМагазин));
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СписокЛЖ.Добавить();
		НоваяСтрока.Корзина = Выборка.Корзина;
		НоваяСтрока.ПредставлениеЛистаЖеланий = Строка(Выборка.Код) +" "+ Строка(Выборка.ИмяИмениника) + " " + Формат(Выборка.ДатаРожденияИменинника, "ДФ=dd.MM.yyyy");
		НоваяСтрока.Заказ = Выборка.Заказ;
		НоваяСтрока.Просрочен = ?(Не ЗначениеЗаполнено(Выборка.СрокДействия), Ложь ,Выборка.СрокДействия < ТекущаяДата());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СписокЛЖВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Заказ = Элемент.ТекущиеДанные.Заказ;
	Корзина = Элемент.ТекущиеДанные.Корзина;
	Если ЗначениеЗаполнено(Заказ) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заказ", Заказ);
		ПараметрыФормы.Вставить("Операция", "Корректировка");
		ПараметрыФормы.Вставить("Корзина", Корзина);
		ОткрытьФорму("Обработка.РМК.Форма.ФормаЛистаЖеланий", ПараметрыФормы, ВладелецФормы, ВладелецФормы,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заказ", Заказ);
		ПараметрыФормы.Вставить("Операция", "Создание");
		ПараметрыФормы.Вставить("Корзина", Корзина);
		ОткрытьФорму("Обработка.РМК.Форма.ФормаЛистаЖеланий", ПараметрыФормы, ВладелецФормы, ВладелецФормы,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	КонецЕсли;
	Закрыть();
КонецПроцедуры
