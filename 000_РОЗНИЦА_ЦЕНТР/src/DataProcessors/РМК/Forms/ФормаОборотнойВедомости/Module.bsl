#Область ОбработчикиСобытийФормы

&НаСервере	//	LNK 12.11.2022 18:37:14
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Параметры.Свойство("ТекКассаККМ", ТекКассаККМ);

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура КомандаВыбораПериода(Команда)

	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Заголовок"		, "Визначте період звіту");
	ПараметрыОткрытия.Вставить("ВведенныйПериод", ПериодПериодическогоОтчета);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаВыбораПериодаЗавершение", ЭтотОбъект);

	ОткрытьФорму(РМККлиент.КонтекстИмениФормы("Обработка.РМК.Форма.ФормаВводаПериода", Объект.ИмяВнешнейОбработки)
		, ПараметрыОткрытия
		, ЭтотОбъект
		, ЭтотОбъект
		,,
		, ОписаниеОповещения
		, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);

КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбораПериодаЗавершение(РезультатВыбора, ДополнительныеПараметры)	Экспорт

	Если НЕ РезультатВыбора = Неопределено Тогда

		ЗначениеВыбора = РезультатВыбора.ВведенныйПериод;

		Если ЗначениеЗаполнено(ЗначениеВыбора) Тогда

				ПериодПериодическогоОтчета = ЗначениеВыбора;

		Иначе	ПериодПериодическогоОтчета = Новый СтандартныйПериод();

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры


&НаСервере
Процедура СформироватьНаСервере()
	
	ТаблицаОтчета.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КассоваяСмена.Ссылка
	|ИЗ
	|	Документ.КассоваяСмена КАК КассоваяСмена
	|ГДЕ
	|	КассоваяСмена.КассаККМ = &КассаККМ
	|	И КассоваяСмена.НачалоКассовойСмены >= &НачалоПериода
	|	И КассоваяСмена.ОкончаниеКассовойСмены <= &КонецПериода";
	Запрос.УстановитьПараметр("НачалоПериода",НачалоДня(ПериодПериодическогоОтчета.ДатаНачала));
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(ПериодПериодическогоОтчета.ДатаОкончания));
	Запрос.УстановитьПараметр("КассаККМ",ТекКассаККМ);
	Выгрузка = Запрос.Выполнить().Выгрузить();
	Для каждого Стр из Выгрузка Цикл
		ПолучитьДанныеПоКассовойСмене(Стр.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеПоКассовойСмене(КС)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&НомерКассовойСмены КАК НомерСмены,
	|	&ДатаКассовойСмены КАК ДатаСмены,
	|	ВыемкаСредств.Выемка КАК СуммаВыдачи,
	|	ВнесениеСредств.Внесение КАК СуммаВзноса,
	|	ОстаткиСредств.Остаток КАК СуммаОстатка,
	|	ТекПродажи.СтоимостьПродаж КАК СуммаПродаж,
	|	ТекПродажиКредит.СтоимостьПродажКредит КАК СуммаПродажВКредит
	|ИЗ
	|	(ВЫБРАТЬ
	|		СУММА(ДенежныеСредстваККМОбороты.СуммаОборот) КАК Выемка
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваККМ.Обороты(&НачалоДня, &КонецДня, Регистратор, КассаККМ = &КассаККМ) КАК ДенежныеСредстваККМОбороты
	|	ГДЕ
	|		ДенежныеСредстваККМОбороты.Регистратор ССЫЛКА Документ.ВыемкаДенежныхСредствИзКассыККМ) КАК ВыемкаСредств,
	|	(ВЫБРАТЬ
	|		СУММА(ДенежныеСредстваККМОбороты.СуммаОборот) КАК Внесение
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваККМ.Обороты(&НачалоДня, &КонецДня, Регистратор, КассаККМ = &КассаККМ) КАК ДенежныеСредстваККМОбороты
	|	ГДЕ
	|		ДенежныеСредстваККМОбороты.Регистратор ССЫЛКА Документ.ВнесениеДенежныхСредствВКассуККМ) КАК ВнесениеСредств,
	|	(ВЫБРАТЬ
	|		СУММА(ДенежныеСредстваККМОстатки.СуммаОстаток) КАК Остаток
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваККМ.Остатки(&КонецДня, КассаККМ = &КассаККМ) КАК ДенежныеСредстваККМОстатки) КАК ОстаткиСредств,
	|	(ВЫБРАТЬ
	|		СУММА(ПродажиОбороты.СтоимостьОборот) КАК СтоимостьПродаж
	|	ИЗ
	|		РегистрНакопления.Продажи.Обороты(&НачалоДня, &КонецДня, , ДокументПродажи.КассаККМ = &КассаККМ) КАК ПродажиОбороты
	|	ГДЕ
	|		ПродажиОбороты.ДокументПродажи.КассаККМ = &КассаККМ) КАК ТекПродажи,
	|	(ВЫБРАТЬ
	|		СУММА(ПродажиПоПлатежнымКартамОбороты.СуммаОборот) КАК СтоимостьПродажКредит
	|	ИЗ
	|		РегистрНакопления.ПродажиПоПлатежнымКартам.Обороты(&НачалоДня, &КонецДня, Регистратор, ЭквайринговыйТерминал.Касса = &КассаККМ) КАК ПродажиПоПлатежнымКартамОбороты) КАК ТекПродажиКредит";
	Запрос.УстановитьПараметр("КассаККМ",КС.КассаККМ);
	Запрос.УстановитьПараметр("НомерКассовойСмены",КС.Номер);
	Запрос.УстановитьПараметр("ДатаКассовойСмены",КС.Дата);
	Запрос.УстановитьПараметр("НачалоДня",НачалоДня(КС.НачалоКассовойСмены));
	Запрос.УстановитьПараметр("КонецДня",Новый Граница( ?(ЗначениеЗаполнено(КС.ОкончаниеКассовойСмены),КС.ОкончаниеКассовойСмены,КонецДня(ОбщегоНазначенияВызовСервера.ТекущаяДатаСервера() )),ВидГраницы.Включая));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НС = ТаблицаОтчета.Добавить();
		ЗаполнитьЗначенияСвойств(НС,Выборка);
	КонецЦикла;

	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если НЕ ЗначениеЗаполнено(ПериодПериодическогоОтчета) Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьНаСервере();
	
КонецПроцедуры

