
///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Печать

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПроверкаИспользуемыхКодов") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПроверкаИспользуемыхКодов", 
			"Проверка используемых кодов", 
			ПечатьПроверкиИспользуемыхКодов(МассивОбъектов, ОбъектыПечати, ПараметрыПечати["СтруктураТаблиц"], ПараметрыВывода),,,Истина);

	КонецЕсли;
		
КонецПроцедуры

Функция ПечатьПроверкиИспользуемыхКодов(МассивОбъектов, ОбъектыПечати, СтруктураТаблиц, ПараметрыВывода)
		
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;

	ТабличныйДокумент = Новый ТабличныйДокумент;
		
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаполнениеДиапазонаСерийныхНомеров_ПроверкаИспользуемыхКодов";
		
	Макет = УправлениеПечатью.ПолучитьМакет("Обработка.ЗаполнениеДиапазонаСерийныхНомеров.ПФ_MXL_ПроверкаИспользуемыхКодов", КодЯзыкаПечать);
	
	ОбластьМакета 	= Макет.ПолучитьОбласть("Шапка");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета   = Макет.ПолучитьОбласть("Строка");
	ТаблицаКодов    = Неопределено;
	НомерСтроки     = 0;
		
	Если СтруктураТаблиц.Свойство("ТаблицаМагнитныхКодов", ТаблицаКодов) Тогда
		
		Для каждого СтрокаТаблицыКодов Из ТаблицаКодов Цикл
			
			НомерСтроки = НомерСтроки + 1;
			ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыКодов);
			ОбластьМакета.Параметры.Номер = НомерСтроки;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если СтруктураТаблиц.Свойство("ТаблицаШтриховыхКодов", ТаблицаКодов) Тогда
		
		Для каждого СтрокаТаблицыКодов Из ТаблицаКодов Цикл
			НомерСтроки = НомерСтроки + 1;
			ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыКодов);
			ОбластьМакета.Параметры.Номер = НомерСтроки;
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		
	КонецЕсли;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции
