
///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//	LNK 08.11.2016 08:46:59
//	Аналог ОбщегоНазначенияРТСервер.ОпределитьТекущийМагазин()
Функция ДоступныеМагазины(УчитыватьТекущийМагазин = Ложь, Пользователь = Неопределено) Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Магазины.Ссылка КАК Магазин
	|ИЗ
	|	Справочник.Магазины КАК Магазины"
	);
	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда

		Возврат Новый ТаблицаЗначений;

	Иначе

		УстановитьПривилегированныйРежим(Истина);
		ДоступныеМагазины = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Магазин");

		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоступныеМагазины.Магазин КАК Магазин,
		|	ВЫБОР
		|		КОГДА НЕ Пользователи.Магазин ЕСТЬ NULL 
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Пометка,
		|	СправочникМагазины.Наименование КАК Наименование
		|ИЗ
		|	(ВЫБРАТЬ
		|		МагазиныУзла.Магазин КАК Магазин
		|	ИЗ
		|		ПланОбмена.ПоМагазину.Магазины КАК МагазиныУзла
		|	ГДЕ
		|		НЕ &ГлавныйУзел
		|				И МагазиныУзла.Ссылка = &ЭтотУзел
		|		И МагазиныУзла.Магазин В(&ДоступныеМагазины)
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаУзлы.Магазин
		|	ИЗ
		|		ПланОбмена.ПоМагазину КАК ТаблицаУзлы
		|	ГДЕ
		|		НЕ &ГлавныйУзел
		|				И ТаблицаУзлы.Ссылка = &ЭтотУзел
		|		И ТаблицаУзлы.Магазин В(&ДоступныеМагазины)
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		Магазины.Ссылка
		|	ИЗ
		|		Справочник.Магазины КАК Магазины
		|	ГДЕ
		|		&ГлавныйУзел
		|		И НЕ Магазины.СкладУправляющейСистемы
		|		И Магазины.Ссылка В(&ДоступныеМагазины)) КАК ДоступныеМагазины
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО (Пользователи.Ссылка = &Пользователь)
		|			И (Пользователи.Магазин = ДоступныеМагазины.Магазин)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Магазины КАК СправочникМагазины
		|		ПО (СправочникМагазины.Ссылка = ДоступныеМагазины.Магазин)
		|ГДЕ
		|	(&УчитыватьТекущийМагазин
		|			ИЛИ ДоступныеМагазины.Магазин <> &ТекущийМагазин)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование"
		;
		Запрос.УстановитьПараметр("ЭтотУзел"    , ПланыОбмена.ПоМагазину.ЭтотУзел());
		Запрос.УстановитьПараметр("ГлавныйУзел" , ОбменДаннымиПовтИсп.ГлавныйУзел() = Неопределено);
		Запрос.УстановитьПараметр("Пользователь", ?(Пользователь = Неопределено, Пользователи.ТекущийПользователь(), Пользователь));
		Запрос.УстановитьПараметр("УчитыватьТекущийМагазин", УчитыватьТекущийМагазин);
		Запрос.УстановитьПараметр("ТекущийМагазин"   , ПараметрыСеанса.ТекущийМагазин);
		Запрос.УстановитьПараметр("ДоступныеМагазины", ДоступныеМагазины);

		Возврат Запрос.Выполнить().Выгрузить();

	КонецЕсли;

КонецФункции // ДоступныеМагазины()

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
