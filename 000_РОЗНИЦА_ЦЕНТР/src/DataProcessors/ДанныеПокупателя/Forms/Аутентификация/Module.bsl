#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;

//	Этот реквизит формы используем, как отладочный, снимающий "фатальные" ограничения.
	ОтладочныйРежим = ТехническаяПоддержкаВызовСервера.ОтладочныйРежимРаботы();

	Параметры.Свойство("НомерТелефона", НомерТелефона);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьДоступностьЭлементовФормы();

КонецПроцедуры

&НаКлиенте	//	LNK 05.09.2023 06:31:48
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если НЕ ЗакрытиеСанкционировано Тогда

		Отказ = Истина;
		ТекстПредупреждения = "Воспользуйтесь кнопками управления аутентификацией!";

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОтправкаСообщенияSMS

&НаКлиенте
Процедура ОтправитьКодПодтвержденияSMS(Команда)

	Если ПроверитьЗаполнение() Тогда

		ПоказатьВопрос(Новый ОписаниеОповещения("ОтправитьКодПодтвержденияSMSЗавершение", ЭтотОбъект)
			, "Будет отправлено SMS по указанному номеру. Подтвердите своё решение:", РежимДиалогаВопрос.ОКОтмена
			, 60, КодВозвратаДиалога.ОК, "ИДЕНТИФИКАЦИЯ", КодВозвратаДиалога.Отмена);

	Иначе

		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМК("В действии отказано!", "Необходимо заполнить все обязательные поля:" + ТекстОшибокЗаполнения);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьКодПодтвержденияSMSЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда

		Если ОтладочныйРежим ИЛИ ОтправитьКодПодтвержденияSMSНаСервере(НомерТелефона, ИмяКомпьютера(), Контрагент) Тогда

			ПодтверждениеОтправлено = Истина;
			ПоказатьОповещениеПользователя("Код успешно отправлен на телефон" + Символы.ПС + "«" + НомерТелефона + "»");

		КонецЕсли;

		УстановитьДоступностьЭлементовФормы();

		ТекущийЭлемент = Элементы.КодПодтверждения;

	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтправитьКодПодтвержденияSMSНаСервере(НомерТелефона, ИмяКлиентскогоКомпьютера, Контрагент)

	ДополнительныеПараметры = Новый Структура(
		"ИмяКомпьютера, Контрагент", ИмяКлиентскогоКомпьютера, Контрагент);

	Возврат ВерификацияКлиентовСервер.ОтправитьКодПодтверждения(ОтправкаSMS.ПодготовитьНомерТелефона(НомерТелефона), ДополнительныеПараметры, Истина);

КонецФункции

#КонецОбласти

#Область ПолучениеКодаПодтверждения

&НаКлиенте
Процедура КодПодтвержденияПриИзменении(Элемент)

	ПоказатьОповещениеПользователя("Проверка номера телефона"
	,, "Отправлен запрос на получение последнего кода подтверждения ..."
	, БиблиотекаКартинок.Вопрос32, СтатусОповещенияПользователя.Информация);

	ПодтверждениеПолучено = Ложь;	//	сбросим возможное подтверждение

	Если ОтладочныйРежим Тогда

		ДанныеКлиента = Новый Структура(
			"Указан, КодКлиента"
			, Истина
			, КодПодтверждения
		);

	Иначе

		ДанныеКлиента = КодПодтвержденияПриИзмененииНаСервере(НомерТелефона);

	КонецЕсли;

	Если ДанныеКлиента.Указан Тогда

		Если СокрЛП(ДанныеКлиента.КодКлиента) = СокрЛП(КодПодтверждения) Тогда

			ПодтверждениеПолучено = Истина;

			ПоказатьОповещениеПользователя("Проверка номера телефона"
			,, "Поздравляем! Код подтверждения соответствует"
			, БиблиотекаКартинок.Информация32, СтатусОповещенияПользователя.Информация);

			НовыйТекущийЭлемент = Элементы.ПрименитьИЗакрыть;
			ЗакрытиеСанкционировано = Истина;

		Иначе

			ПоказатьОповещениеПользователя("Проверка номера телефона"
			,, "Код подтверждения не совпадает с отправленным на номер телефона «" + НомерТелефона + "»"
			, БиблиотекаКартинок.Ошибка32, СтатусОповещенияПользователя.Важное);

			НовыйТекущийЭлемент = Элементы.КодПодтверждения;

		КонецЕсли;

	Иначе

		ПоказатьОповещениеПользователя("Проверка номера телефона"
		,, "Код подтверждения не получен. Возможно, не соответствует номер телефона «" + НомерТелефона + "»"
		, БиблиотекаКартинок.Ошибка32, СтатусОповещенияПользователя.Важное);

		НовыйТекущийЭлемент = Элементы.КодПодтверждения;
	
	КонецЕсли;

	УстановитьДоступностьЭлементовФормы(НовыйТекущийЭлемент);

КонецПроцедуры

&НаСервереБезКонтекста
Функция КодПодтвержденияПриИзмененииНаСервере(НомерТелефона)

	Возврат ВерификацияКлиентовСервер.ПолучитьКодПодтверждения(ОтправкаSMS.ПодготовитьНомерТелефона(НомерТелефона), Истина);

КонецФункции // КодПодтвержденияПриИзмененииНаСервере()
	
#КонецОбласти

&НаКлиенте	//	LNK 05.09.2023 06:28:21
Процедура ПрименитьИЗакрыть(Команда)

	ЗакрытиеСанкционировано = Истина;
	Закрыть(Новый Структура("НомерТелефона, Подтверждён", НомерТелефона, Истина));

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИЗакрыть(Команда)

	ЗакрытиеСанкционировано = Истина;
	Закрыть(Новый Структура("НомерТелефона, Подтверждён", НомерТелефона, Ложь));

КонецПроцедуры

#Область ПоддержкаОбщегоФункционалаФормы

&НаКлиенте	//	LNK 24.07.2019 09:48:08
Процедура УстановитьДоступностьЭлементовФормы(НовыйТекущийЭлемент = Неопределено)

	Элементы.ОтправитьКодПодтвержденияSMS.Доступность = НЕ ПодтверждениеОтправлено;
	Элементы.КодПодтверждения.ТолькоПросмотр          = НЕ ПодтверждениеОтправлено;

	Элементы.ПрименитьИЗакрыть.Доступность = ЗакрытиеСанкционировано;
	Элементы.ОтменитьИЗакрыть.Доступность  = НЕ ЗакрытиеСанкционировано;

	Если НЕ НовыйТекущийЭлемент = Неопределено Тогда	//	LNK 21.09.2023 08:38:43

		ТекущийЭлемент = НовыйТекущийЭлемент;

	КонецЕсли;

КонецПроцедуры

&НаСервере	//	LNK 23.07.2019 13:21:56
Функция ЭтотОбъект(ТекущийОбъект = Неопределено) 

	Если ТекущийОбъект = Неопределено Тогда

		Возврат РеквизитФормыВЗначение("Объект");

	КонецЕсли;

	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");

	Возврат Неопределено;

КонецФункции

#КонецОбласти














