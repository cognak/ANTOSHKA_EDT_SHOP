
&НаКлиенте
Процедура ПодключитьУстройство()
	ОписаниеОшибки = "";
			
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("БиометрическийСканер");
	
	МенеджерОборудованияКлиент.ПодключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО, ОписаниеОшибки);				
	
	Если ОписаниеОшибки <> "" Тогда
		Предупреждение(ОписаниеОшибки);	
	Иначе  
		ИдентификаторУстройства = МенеджерОборудованияКлиент.ВыбратьУстройство("БиометрическийСканер","Выберите устройство","Устройство не подключено!"); 
		СтруктураПараметров = ПолучитьПараметрыУстройства(ИдентификаторУстройства);
		ВходныеПараметры  = Неопределено;
		ВыходныеПараметры = Неопределено;

		мДрайвер = МенеджерОборудованияКлиент.ПолучитьОбъектДрайвера(СтруктураПараметров.ОбработчикДрайвера,"",СтруктураПараметров.Параметры);
		Объект.Подключен = Истина;
	КонецЕсли;        
	
КонецПроцедуры  

&НаКлиенте
Процедура ТестУстройства(Команда)
	Результат = Ложь;
	ОписаниеОшибки = "";	

	Если Не ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
		ИдентификаторУстройства = МенеджерОборудованияКлиент.ВыбратьУстройство("БиометрическийСканер","Выберите устройство","Устройство не подключено!");
	КонецЕсли;

	Результат = МенеджерОборудованияКлиент.ПодключитьОборудованиеПоИдентификатору(ЭтотОбъект, ИдентификаторУстройства, ОписаниеОшибки);

	Если Результат Тогда

		ВходныеПараметры  = Неопределено;
		ВыходныеПараметры = Неопределено;

		Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
		                                                        "ТестУстройства",
		                                                        ВходныеПараметры,
		                                                        ВыходныеПараметры);

		Если НЕ Результат Тогда
			Предупреждение("Ошибка при тестировании устройства!");
		Иначе
			Предупреждение(ВыходныеПараметры[1]);
		КонецЕсли;

		МенеджерОборудованияКлиент.ОтключитьОборудованиеПоИдентификатору(ЭтотОбъект,
		                                                                 ИдентификаторУстройства);

	Иначе

		Сообщить("При підключенні пристрою (Біосканер) сталася помилка." + Символы.ПС + ОписаниеОшибки);

	КонецЕсли;

КонецПроцедуры


&НаСервере
Функция ПолучитьПараметрыУстройства(ИдентификаторУстройства)
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("Параметры",ИдентификаторУстройства.Параметры.Получить());
	СтруктураПараметры.Вставить("ОбработчикДрайвера",ИдентификаторУстройства.ОбработчикДрайвера);
	
	Возврат СтруктураПараметры;
КонецФункции


&НаКлиенте
Процедура ОтключитьУстройство()
	Результат = Ложь;
	ОписаниеОшибки = "";	

	Если Не ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
		ИдентификаторУстройства = МенеджерОборудованияКлиент.ВыбратьУстройство("БиометрическийСканер","Выберите устройство","Устройство не подключено!");
	КонецЕсли;

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
	                                                        "ОтключитьУстройство",
	                                                        ВходныеПараметры,
	                                                        ВыходныеПараметры);

	Если НЕ Результат Тогда
		Предупреждение(ВыходныеПараметры[1]);
	Иначе
		Элементы.СтатусСканера.Заголовок = "Не подключен";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Сотрудник = Параметры.Сотрудник;
	Если РольДоступна("АдминистраторСистемы") Тогда
		Элементы.ФормаКомандаПодключить.Видимость = Истина;
		Элементы.ФормаКомандаОтключить.Видимость = Истина;
		Элементы.ФормаКомандаЗарегистрировать.Видимость = Истина; 
		Элементы.ФормаТестУстройства.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Зарегистрировать(Команда)
	Результат = Ложь;
	ОписаниеОшибки = "";	

	
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда
		Предупреждение("Укажите сотрудника у которого снимается отпечаток!");
		Возврат;
	КонецЕсли;
	
	Элементы.СтатусСканера.Заголовок = "Регистрация";
	Если Не ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
		ИдентификаторУстройства = МенеджерОборудованияКлиент.ВыбратьУстройство("БиометрическийСканер","Выберите устройство","Устройство не подключено!");
	КонецЕсли;

	СканерПодключен = МенеджерОборудованияКлиент.ПодключитьОборудованиеПоИдентификатору(ЭтотОбъект, ИдентификаторУстройства, ОписаниеОшибки);

	Если СканерПодключен Тогда

		ВходныеПараметры  = Неопределено;
		ВыходныеПараметры = Неопределено;
		
		//в х64 разрядной версии проблема с подключением к контексту 1С. Драйвер не может генерировать внешнее событие. Поэтому регистрация отпечатка будет происходить
		//по следующей схеме: снимаем 3 отпечатка, закидываем в библиотеку и получаем зарегистрированный вариант.
		//Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
		//                                                        "РегистрацияОтпечатка",
		//                                                        ВходныеПараметры,
		//                                                        ВыходныеПараметры);

		НомерРегистрации = 0;     
		ПредпоследнийОтпечаток = "";
		Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
		                                                        "ЗахватОтпечатка",
		                                                        ВходныеПараметры,
		                                                        ВыходныеПараметры);
		Если НЕ Результат Тогда
			Предупреждение(ВыходныеПараметры[1]);
			МенеджерОборудованияКлиент.ОтключитьОборудованиеПоИдентификатору(ЭтотОбъект,ИдентификаторУстройства);
		Иначе   
			ПодключитьОбработчикОжидания("РегистрацияОтпечаткаХ64",1);
		КонецЕсли;   
		
		
	Иначе
		Сообщить("При підключенні пристрою (Біосканер) сталася помилка." + Символы.ПС + ОписаниеОшибки);
		МенеджерОборудованияКлиент.ОтключитьОборудованиеПоИдентификатору(ЭтотОбъект,ИдентификаторУстройства);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияОтпечаткаХ64() Экспорт     
	
	ОтключитьОбработчикОжидания("РегистрацияОтпечаткаХ64");
	//Элементы.Информация.Заголовок = "Приложите палец к сенсору. Попытка №"+(НомерРегистрации+1);
	ЭтаФорма.Информация = "Прикладіть палець до сенсора. Спроба № "+(НомерРегистрации+1)+" з 3"; 
	ЭтаФорма.ОбновитьОтображениеДанных(Элементы.Информация);
	Если Не ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
		ИдентификаторУстройства = МенеджерОборудованияКлиент.ВыбратьУстройство("БиометрическийСканер","Выберите устройство","Устройство не подключено!");
	КонецЕсли;

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;
	
	Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
	                                                        "ПоследнийОтпечаток",
	                                                        ВходныеПараметры,
	                                                        ВыходныеПараметры);
	Если НЕ Результат Тогда
		Предупреждение(ВыходныеПараметры[1]);
		Сообщить("Регистрация неуспешно!");
		Возврат;
	КонецЕсли;
	
	ПоследнийОтпечаток = ВыходныеПараметры[0];
	Если ((ПоследнийОтпечаток <> ПредпоследнийОтпечаток) И (ЗначениеЗаполнено(ПоследнийОтпечаток))) Тогда  
		//Элементы.Информация.Заголовок = "Зафиксировано!";  
		//проверим, что это не палец какого-то другого сотрудника
		Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
		                                                        "НайтиПоБД",
		                                                        ВходныеПараметры,
		                                                        ВыходныеПараметры);

		Если Не Результат Тогда      
			//Проверим что прикладывается один и тот же палец 
			РазрешаемРегистрироватьПопытку = Истина;
			Если НомерРегистрации > 0 Тогда
				ВходныеПараметры = Новый Массив;
				ВходныеПараметры.Добавить(ПоследнийОтпечаток);
				ВходныеПараметры.Добавить(ПредпоследнийОтпечаток);
				Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
			                                                        "СравнитьОтпечатки",
			                                                        ВходныеПараметры,
			                                                        ВыходныеПараметры);
				Если Результат Тогда
					Если ВыходныеПараметры[0]<90 Тогда
						РазрешаемРегистрироватьПопытку = Ложь;
						ЭтаФорма.Информация = "Потрібно прикладати той самий палець!";    
					КонецЕсли;
				КонецЕсли;   
			КонецЕсли;                                                        
			
			Если РазрешаемРегистрироватьПопытку Тогда
				ЭтаФорма.Информация = "Зафиксовано";
				ПредпоследнийОтпечаток = ПоследнийОтпечаток;
				Если АдресРегистрации = "" Тогда
					МассивОтпечатков = Новый Массив;
				Иначе
					МассивОтпечатков  = ПолучитьИзВременногоХранилища(АдресРегистрации);
					Если МассивОтпечатков = Неопределено Тогда
						Сообщить("Помилка - Чомусь в сховищі нічого нет");  
						МассивОтпечатков = Новый Массив;
					КонецЕсли;
				КонецЕсли;
				МассивОтпечатков.Добавить(ПоследнийОтпечаток);  
				НомерРегистрации = НомерРегистрации + 1;   
				
				АдресРегистрации = ПоместитьВоВременноеХранилище(МассивОтпечатков,ЭтаФорма.УникальныйИдентификатор);
			КонецЕсли;			
		Иначе          
			НомерСотрудника = ВыходныеПараметры[0]-1;
			//Сообщить("Номер сотрудника в БД "+НомерСотрудника);
			
			//СтрокаСотрудник = Сотрудники[НомерСотрудника];
			ЭтаФорма.Информация = "Пересадка пальца? Данные уже зарегистрированы за другим сотрудником!";			
		КонецЕсли;
	КонецЕсли;
	
	Если НомерРегистрации < 3 Тогда
		Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
	                                                        "ЗахватОтпечатка",
	                                                        ВходныеПараметры,
	                                                        ВыходныеПараметры); 
															
		ПодключитьОбработчикОжидания("РегистрацияОтпечаткаХ64",1); 
	Иначе
        ВходныеПараметры = Новый Массив;
		ВходныеПараметры.Добавить(МассивОтпечатков);
		Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
	                                                        "РегистрацияПоДанным",
	                                                        ВходныеПараметры,
	                                                        ВыходныеПараметры);
		Если Результат Тогда
			СохранитьОтпечатокВБазе(ВыходныеПараметры[0]);
			ЭтаФорма.Информация = "";
		Иначе
			ЭтаФорма.Информация = ""+ВыходныеПараметры[1];
		КонецЕсли;													

		ЭтаФорма.ОбновитьОтображениеДанных(Элементы.Информация);
	КонецЕсли;  

	
КонецПроцедуры

 &НаКлиенте
Процедура СохранитьОтпечатокВБазе(Отпечаток)
	Если СохранитьОтпечатокВБазеСервер(Отпечаток) Тогда
		Сообщить("Отпечаток сохранен в базе данных!");
		ЭтаФорма.Закрыть(Новый Структура("Успешно",Истина));
	Иначе
		Сообщить("Не удалось сохранить отпечаток в базе данных");
		ЭтаФорма.Закрыть(Новый Структура("Успешно",Ложь));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СохранитьОтпечатокВБазеСервер(Отпечаток)
	Возврат РаботаСБиометричекимиДанными.УстановитьБиометриюСотруднику(Сотрудник,Отпечаток);
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)  
	Версия = 1.21;
	Если Не Объект.Подключен Тогда
		ПодключитьУстройство();
	КонецЕсли;                  
	ИнициализироватьБДСканера();
	Зарегистрировать(Неопределено); 
	
	ЭтаФорма.Ширина = 100;
	ЭтаФорма.Высота = 12;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	//ОтключитьУстройство();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПодключить(Команда)
	ПодключитьУстройство();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтключить(Команда)
	ОтключитьУстройство();
КонецПроцедуры


 &НаСервереБезКонтекста
Функция ПолучитьБиометриюСотрудников()
	ТаблицаСотрудников = РаботаСБиометричекимиДанными.ПолучитьСотрудниковСБиометрией();
	
	Возврат ТаблицаСотрудников.ВыгрузитьКолонку("Биометрия");
	
КонецФункции

&НаКлиенте
Процедура ИнициализироватьБДСканера()
	МассивБиометрия = ПолучитьБиометриюСотрудников(); 


	Результат = Ложь;
	ОписаниеОшибки = "";	

	
	Если Не ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
		ИдентификаторУстройства = МенеджерОборудованияКлиент.ВыбратьУстройство("БиометрическийСканер","Выберите устройство","Устройство не подключено!");
	КонецЕсли;

	ВходныеПараметры  = Новый Массив;
	ВыходныеПараметры = Неопределено;
	
	ВходныеПараметры.Добавить(МассивБиометрия);

	Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
	                                                        //"ИнициализацияБДпоУИД",
	                                                        "ИнициализацияБД",
	                                                        ВходныеПараметры,
	                                                        ВыходныеПараметры);

	Если Не Результат Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не вдалося ініціалізувати БД: "+ВыходныеПараметры[1];
		Сообщение.Сообщить();
	Иначе
		//Сообщение.Текст = "Ініціалізація БД успішно!";
	КонецЕсли;
	
	
КонецПроцедуры

