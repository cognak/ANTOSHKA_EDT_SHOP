Процедура СформироватьТабличныйДокумент(ТабличныйДокумент)	Экспорт

	ТабличныйДокумент.Очистить();

	УстановитьПривилегированныйРежим(Истина);

	Макет = ПолучитьМакет("Реестр");
	ОбластьШапка    = Макет.ПолучитьОбласть("Шапка");
	ОбластьВладелец = Макет.ПолучитьОбласть("Владелец");
	ОбластьСтрока   = Макет.ПолучитьОбласть("Строка");

	ОбластьШапка.Параметры.ПериодПредставление = ПредставлениеПериода(ДатаНачала, ДатаОкончания, "ДЛФ=D");
	ТабличныйДокумент.Вывести(ОбластьШапка);

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаСправочник.Владелец КАК Владелец,
	|	ТаблицаСправочник.Владелец.Наименование КАК ВладелецПредставление,
	|	ТаблицаСправочник.Владелец.Номинал КАК ВладелецНоминал,
	|	ТаблицаСправочник.Ссылка КАК Ссылка,
	|	ТаблицаСправочник.КодСерийногоНомера КАК КодСерийногоНомера,
	|	ТаблицаШтрихкоды.Штрихкод КАК Штрихкод,
	|	ТаблицаКлючи.КлючНомера КАК КлючНомераСекретный,
	|	ТаблицаСправочник.Код КАК Код,
	|	ТаблицаСправочник.ДатаСоздания КАК ДатаСоздания
	|ИЗ
	|	Справочник.СерийныеНомера КАК ТаблицаСправочник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ТаблицаШтрихкоды
	|		ПО ТаблицаСправочник.Ссылка = ТаблицаШтрихкоды.Владелец
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СерийныеНомераКлючи КАК ТаблицаКлючи
	|		ПО ТаблицаСправочник.Ссылка = ТаблицаКлючи.СерийныйНомер
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ТаблицаСправочник.Владелец = &Номенклатура
	|		КОНЕЦ
	|	И ТаблицаСправочник.ДатаСоздания МЕЖДУ &ДатаНачала И ВЫБОР
	|			КОГДА &ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА КОНЕЦПЕРИОДА(&ДатаТекущая, ГОД)
	|			ИНАЧЕ КОНЕЦПЕРИОДА(&ДатаОкончания, ДЕНЬ)
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВладелецНоминал,
	|	Код
	|ИТОГИ
	|	МАКСИМУМ(ВладелецПредставление),
	|	МАКСИМУМ(ВладелецНоминал)
	|ПО
	|	Владелец"
	);
	Запрос.УстановитьПараметр("Номенклатура" , Номенклатура);
	Запрос.УстановитьПараметр("ДатаНачала"   , ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаТекущая"  , ТекущаяДата());

	ВладелецВыборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВладелецВыборка.Следующий() Цикл

		ОбластьВладелец.Параметры.Номенклатура = ВладелецВыборка.ВладелецПредставление;
		ТабличныйДокумент.Вывести(ОбластьВладелец);

		КлючиВыборка = ВладелецВыборка.Выбрать();

		НомерСтроки = 0;

		Пока КлючиВыборка.Следующий() Цикл

			НомерСтроки = НомерСтроки + 1;

			ОбластьСтрока.Параметры.НомерСтроки  = НомерСтроки;
			ОбластьСтрока.Параметры.Штрихкод     = КлючиВыборка.Штрихкод;
			ОбластьСтрока.Параметры.КлючНомера   = ПодарочныеСертификатыСервер.РасшифроватьКлючНомера(КлючиВыборка.КлючНомераСекретный);
			ОбластьСтрока.Параметры.ДатаСоздания = КлючиВыборка.ДатаСоздания;

			ТабличныйДокумент.Вывести(ОбластьСтрока);

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры







