
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	Отчет.Диаграмма = Найти(ВРЕГ(НаименованиеТекущегоВарианта), "ДИАГРАММА") > 0;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	Перем АдресХранилищаНастроекДляРасшифровки;
	
	Если Параметры.Свойство("ЭтоРасшифровка") Тогда
		
		Параметры.Свойство("Инвервал"          , Отчет.Интервал);
		Параметры.Свойство("МаркетинговаяАкция", Отчет.МаркетинговаяАкция);
		
		Если Параметры.Свойство("АдресХранилищаНастроекДляРасшифровки", АдресХранилищаНастроекДляРасшифровки) Тогда
			
			НастройкаРасшифровки = ПолучитьИзВременногоХранилища(АдресХранилищаНастроекДляРасшифровки);
			МаркетинговыеАкцииСервер.ЗаполнитьОтборПоОтбору(Отчет.КомпоновщикНастроек, НастройкаРасшифровки.Отбор);
			МаркетинговыеАкцииСервер.ЗаполнитьПараметрыКомпоновщика(Отчет.КомпоновщикНастроек, НастройкаРасшифровки.ПараметрыДанных);
			
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Отчет.МаркетинговаяАкция) Тогда
		
		ПриИзмененииМаркетинговойАкцииСервер();
		
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаполнитьОтборПоАкции(Команда)
	
	ВыбранноеЗначение = ОткрытьФормуМодально("Документ.МаркетинговаяАкция.ФормаВыбора", , ЭтотОбъект);
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Отчет.МаркетинговаяАкция = ВыбранноеЗначение;
		ПриИзмененииМаркетинговойАкцииСервер();
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПриИзмененииМаркетинговойАкцииСервер()
	Перем НачалоПериода, КонецПериода;
	
	Если ЗначениеЗаполнено(Отчет.МаркетинговаяАкция) Тогда
		ДатаНачалаАкции = Отчет.МаркетинговаяАкция.ДатаНачалаДействия;
		ДатаКонцаАкции  = Отчет.МаркетинговаяАкция.ДатаОкончанияДействия;
		Если Не ЗначениеЗаполнено(ДатаКонцаАкции)  Тогда
			ДатаКонцаАкции = КонецДня(ТекущаяДатаСеанса());
		КонецЕсли;
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(ДатаКонцаАкции) И ЗначениеЗаполнено(ДатаНачалаАкции) И ДатаКонцаАкции >= ДатаНачалаАкции Тогда
		Если НачалоМесяца(ДатаНачалаАкции) = ДатаНачалаАкции И
			КонецМесяца(ДатаКонцаАкции) = КонецДня(ДатаКонцаАкции) Тогда
			КоличествоМесяцев = Год(ДатаКонцаАкции) * 12 + Месяц(ДатаКонцаАкции) - Год(ДатаНачалаАкции) * 12 - Месяц(ДатаНачалаАкции) + 1;
			НачалоПериода     = ДобавитьМесяц(ДатаНачалаАкции, - КоличествоМесяцев);
			КонецПериода      = ДобавитьМесяц(ДатаКонцаАкции , КоличествоМесяцев);
		Иначе
			КоличествоСекунд = (КонецДня(ДатаКонцаАкции) - ДатаНачалаАкции+1);
			НачалоПериода    = ДатаНачалаАкции - КоличествоСекунд;
			КонецПериода     = ДатаКонцаАкции  + КоличествоСекунд;
		КонецЕсли;
	Иначе
		КонецПериода  = ДатаКонцаАкции;
		НачалоПериода = ДатаНачалаАкции;
	КонецЕсли;
	
	Периоды = Новый Структура;
	Периоды.Вставить("НачалоПериода"  , НачалоПериода);
	Периоды.Вставить("КонецПериода"   , КонецПериода);
	
	МаркетинговыеАкцииСервер.УстановитьОтборСегментовПоМаркетинговойАкции(Отчет.КомпоновщикНастроек, Отчет.МаркетинговаяАкция, Периоды);
	
КонецПроцедуры
