#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос(
	// 0 ТаблицаЦеныНоменклатуры
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	ТаблицаТовары.ВидЦены КАК ВидЦены,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Акция КАК Акция,
	|	ТаблицаТовары.Ссылка.Дата КАК Период
	|ИЗ
	|	Документ.УстановкаЦенНоменклатуры.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И (ТаблицаТовары.Номенклатура.ВидНоменклатуры.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать)
	|			ИЛИ ТаблицаТовары.Номенклатура.ВидНоменклатуры.ИспользованиеХарактеристик <> ЗНАЧЕНИЕ(Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать)
	|				И ТаблицаТовары.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|	И НЕ ТаблицаТовары.Цена = 0"
	);

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результат = Запрос.ВыполнитьПакет();

	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЦеныНоменклатуры", Результат[0].Выгрузить());

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Печать

// Процедура печати документа.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
			
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УстановкаЦенНоменклатуры") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"УстановкаЦенНоменклатуры",
			"Установка цен номенклатуры",
			ПечатьУстановкаЦенНоменклатуры(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода),,,Истина);

	КонецЕсли;
    	
КонецПроцедуры

// Функция формирует табличный документ с печатной формой установки цен номенклатуры.
//
// Возвращаемое значение:
//  Табличный документ.
//
Функция ПечатьУстановкаЦенНоменклатуры(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода) Экспорт
		
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УстановкаЦенНоменклатуры";
	
	Макет = УправлениеПечатью.ПолучитьМакет("Документ.УстановкаЦенНоменклатуры.ПФ_MXL_УстановкаЦенНоменклатуры", КодЯзыкаПечать);
	
	ВидыЦен = Новый Массив;
	ДеревоНастроек = ПолучитьИзВременногоХранилища(ПараметрыПечати.ДеревоНастроек);
	Для Каждого СтрокаДерева Из ДеревоНастроек.Строки Цикл
		Если СтрокаДерева.ВыводитьНаПечать Тогда
			ВидыЦен.Добавить(СтрокаДерева.Параметр);
		КонецЕсли;
	КонецЦикла;
	
	ПервыйДокумент = Истина;
	Для Каждого УстановкаЦенНоменклатуры Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УстановкаЦенНоменклатуры.Ссылка       КАК Ссылка,
		|	УстановкаЦенНоменклатуры.Ссылка.Номер КАК Номер,
		|	УстановкаЦенНоменклатуры.Ссылка.Дата  КАК Дата,
		|   УстановкаЦенНоменклатуры.Ответственный.ФизЛицо КАК Ответственный
		|ИЗ
		|	Документ.УстановкаЦенНоменклатуры КАК УстановкаЦенНоменклатуры
		|ГДЕ
		|	УстановкаЦенНоменклатуры.Ссылка = &УстановкаЦенНоменклатуры
		|;
		|
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Товары.НомерСтроки                       КАК НомерСтроки,
		|	Товары.Номенклатура                      КАК Номенклатура,
		|	Товары.Характеристика                    КАК Характеристика,
		|	Товары.Упаковка                          КАК Упаковка,
		|	Товары.Цена                              КАК Цена,
		|	Товары.ВидЦены                           КАК ВидЦены,
		|	
		|	ВЫБОР КОГДА ЦеныНоменклатурыСрезПоследних.Упаковка = Товары.Упаковка ТОГДА
		|		ЦеныНоменклатурыСрезПоследних.Цена
		|	ИНАЧЕ
		|		ЦеныНоменклатурыСрезПоследних.Цена/ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Упаковка.Коэффициент,1)*ЕСТЬNULL(Товары.Упаковка.Коэффициент,1)
		|	КОНЕЦ КАК ДействующаяЦена,
		|	
		|	ВЫРАЗИТЬ(ВЫБОР КОГДА ЦеныНоменклатурыСрезПоследних.Цена <> 0 Тогда 100*(Товары.Цена - ВЫБОР КОГДА ЦеныНоменклатурыСрезПоследних.Упаковка = Товары.Упаковка ТОГДА
		|		ЦеныНоменклатурыСрезПоследних.Цена
		|	ИНАЧЕ
		|		ЦеныНоменклатурыСрезПоследних.Цена/ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Упаковка.Коэффициент,1)*ЕСТЬNULL(Товары.Упаковка.Коэффициент,1)
		|	КОНЕЦ)/ 
		|	
		|	ВЫБОР КОГДА ЦеныНоменклатурыСрезПоследних.Упаковка = Товары.Упаковка ТОГДА
		|		ЦеныНоменклатурыСрезПоследних.Цена
		|	ИНАЧЕ
		|		ЦеныНоменклатурыСрезПоследних.Цена/ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Упаковка.Коэффициент,1)*ЕСТЬNULL(Товары.Упаковка.Коэффициент,1)
		|	КОНЕЦ ИНАЧЕ 0.00 КОНЕЦ КАК Число(10,2)) КАК ПроцентИзменения
		|	
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	Документ.УстановкаЦенНоменклатуры.Товары КАК Товары
		|	
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ДатаСреза, (Номенклатура, Характеристика, ВидЦены) В (ВЫБРАТЬ Т.Номенклатура, Т.Характеристика, Т.ВидЦены ИЗ Документ.УстановкаЦенНоменклатуры.Товары КАК Т ГДЕ Т.Ссылка = &УстановкаЦенНоменклатуры И Т.ВидЦены В (&ВидыЦен))) КАК ЦеныНоменклатурыСрезПоследних
		|
		|		ПО Товары.Номенклатура      = ЦеныНоменклатурыСрезПоследних.Номенклатура
		|			И Товары.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
		|			И Товары.ВидЦены        = ЦеныНоменклатурыСрезПоследних.ВидЦены
		|
		|ГДЕ
		|	Товары.Ссылка = &УстановкаЦенНоменклатуры
		|	И Товары.ВидЦены В (&ВидыЦен)
		|;
		|
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Товары.ВидЦены КАК ВидЦены
		|ИЗ
		|	Товары КАК Товары
		|ГДЕ
		|	Товары.ВидЦены В (&ВидыЦен)
		|	И Товары.ПроцентИзменения <> 0 ИЛИ &Все
		|УПОРЯДОЧИТЬ ПО
		|	Товары.ВидЦены.РеквизитДопУпорядочивания
		|;
		|
		|ВЫБРАТЬ
		|	Товары.Номенклатура                      КАК Номенклатура,
		|	Товары.Характеристика                    КАК Характеристика,
		|	Товары.Номенклатура.НаименованиеПолное   КАК ПолноеНаименованиеНоменклатуры,
		|	Товары.Характеристика.Наименование       КАК ПолноеНаименованиеХарактеристики,
		|	Товары.Номенклатура.Код                  КАК Код,
		|	Товары.Номенклатура.Артикул              КАК Артикул,
		|	Товары.Упаковка                          КАК Упаковка,
		|	Товары.Номенклатура.ЕдиницаИзмерения     КАК ЕдиницаИзмерения,
		|	Товары.Цена                              КАК Цена,
		|	Товары.ВидЦены                           КАК ВидЦены,
		|	Товары.ДействующаяЦена                   КАК СтараяЦена,
		|	Товары.ПроцентИзменения                  КАК ПроцентИзменения
		|	
		|ИЗ
		|	Товары КАК Товары
		|ГДЕ
		|	Товары.ПроцентИзменения <> 0 ИЛИ &Все
		|УПОРЯДОЧИТЬ ПО
		|	Товары.НомерСтроки
		|
		|ИТОГИ ПО
		|	Номенклатура, Характеристика");
		
		Запрос.УстановитьПараметр("УстановкаЦенНоменклатуры", УстановкаЦенНоменклатуры);
		Запрос.УстановитьПараметр("ДатаСреза",                УстановкаЦенНоменклатуры.Дата - 1);
		Запрос.УстановитьПараметр("ВидыЦен",                  ВидыЦен);
		Запрос.УстановитьПараметр("Все",                      Не ПараметрыПечати.ПечататьТолькоИзмененныеЦены);
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		
		Шапка = РезультатЗапроса[0].Выбрать();
		Шапка.Следующий();
		
		Если ПараметрыПечати.ВыводитьШапку Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
			ОбластьМакета.Параметры.ТекстЗаголовка = ФормированиеПечатныхФормСервер.СформироватьЗаголовокДокумента(Шапка, НСтр("ru = 'Установка цен номенклатуры';uk='Установка цін номенклатури'", КодЯзыкаПечать), КодЯзыкаПечать);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		// Шапка таблицы
		КолонкаКодов = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
		ВыводитьКоды = ?(ЗначениеЗаполнено(КолонкаКодов),Истина, Ложь);
		
		ШапкаНомера  = Макет.ПолучитьОбласть("ШапкаТаблицы|ОбластьНомера");
		ШапкаКодов   = Макет.ПолучитьОбласть("ШапкаТаблицы|ОбластьКодов");
		ШапкаТовар   = Макет.ПолучитьОбласть("ШапкаТаблицы|ОбластьТовар");
		ШапкаВидЦены = Макет.ПолучитьОбласть("ШапкаТаблицы|ОбластьВидЦены" + ?(Не ПараметрыПечати.ВыводитьШапку,"УникальныйИдентификатор",""));
		ШапкаУникальныеИдентификаторы = Макет.ПолучитьОбласть("ШапкаТаблицы|УникальныеИдентификаторы");
		
		ТабДокумент.Вывести(ШапкаНомера);
		Если ВыводитьКоды Тогда
			ШапкаКодов.Параметры.ИмяКолонкиКодов = КолонкаКодов;
			ТабДокумент.Присоединить(ШапкаКодов);
		КонецЕсли;
		Если Не ПараметрыПечати.ВыводитьШапку Тогда
			ТабДокумент.Присоединить(ШапкаУникальныеИдентификаторы);
		КонецЕсли;
		ТабДокумент.Присоединить(ШапкаТовар);
		
		
		Табл = Новый ТаблицаЗначений;
		Табл.Колонки.Добавить("ВидЦены");
		Табл.Колонки.Добавить("МакетОбласти");
				
		ВыборкаПоВидамЦен = РезультатЗапроса[2].Выбрать();
		Пока ВыборкаПоВидамЦен.Следующий() Цикл
			
			Новая                          = Табл.Добавить();
			Новая.ВидЦены                  = ВыборкаПоВидамЦен.ВидЦены;
		
					
			ШапкаВидЦены       = Макет.ПолучитьОбласть("ШапкаТаблицы|ОбластьВидЦены" + ?(Не ПараметрыПечати.ВыводитьШапку,"УникальныйИдентификатор",""));
			Новая.МакетОбласти = Макет.ПолучитьОбласть("Строка|ОбластьВидЦены" + ?(Не ПараметрыПечати.ВыводитьШапку,"УникальныйИдентификатор",""));
						
			ШапкаВидЦены.Параметры.ВидЦены = ВыборкаПоВидамЦен.ВидЦены;
			ТабДокумент.Присоединить(ШапкаВидЦены);
			
		КонецЦикла;		
		
		ОбластьНомера = Макет.ПолучитьОбласть("Строка|ОбластьНомера");
		ОбластьКодов  = Макет.ПолучитьОбласть("Строка|ОбластьКодов");
		ОбластьТовар  = Макет.ПолучитьОбласть("Строка|ОбластьТовар");
		ОбластьУникальныеИдентификаторы = Макет.ПолучитьОбласть("Строка|УникальныеИдентификаторы");
		
		НомерСтроки = 0;
		ВыборкаПоНоменклатуре = РезультатЗапроса[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоНоменклатуре.Следующий() Цикл
			
			Если Не ЗначениеЗаполнено(ВыборкаПоНоменклатуре.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
			ВыборкаПоХарактеристикам = ВыборкаПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоХарактеристикам.Следующий() Цикл
			
				НомерСтроки = НомерСтроки + 1;
				
				ОбластьНомера.Параметры.НомерСтроки = НомерСтроки;
				ТабДокумент.Вывести(ОбластьНомера);
				Если ВыводитьКоды Тогда
					
					Если КолонкаКодов = "Артикул" Тогда
						ОбластьКодов.Параметры.Артикул = ВыборкаПоНоменклатуре.Артикул;
					Иначе
						ОбластьКодов.Параметры.Артикул = ВыборкаПоНоменклатуре.Код;
					КонецЕсли;
					
					ТабДокумент.Присоединить(ОбластьКодов);
					
				КонецЕсли;
				
				Если Не ПараметрыПечати.ВыводитьШапку Тогда
					ОбластьУникальныеИдентификаторы.Параметры.УникальныйИдентификаторНоменклатура   = ВыборкаПоХарактеристикам.Номенклатура.УникальныйИдентификатор();
					ОбластьУникальныеИдентификаторы.Параметры.УникальныйИдентификаторХарактеристика = ВыборкаПоХарактеристикам.Характеристика.УникальныйИдентификатор();
					ТабДокумент.Присоединить(ОбластьУникальныеИдентификаторы);
				КонецЕсли;
				
				ОбластьТовар.Параметры.Заполнить(ВыборкаПоХарактеристикам);
				ОбластьТовар.Параметры.Товар = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(ВыборкаПоХарактеристикам.ПолноеНаименованиеНоменклатуры, ВыборкаПоХарактеристикам.ПолноеНаименованиеХарактеристики);
				ТабДокумент.Присоединить(ОбластьТовар);
				
				ВыборкаПоВидамЦен = ВыборкаПоХарактеристикам.Выбрать();
				Для Каждого СтрокаТЗ Из Табл Цикл
					
					ВыборкаПоВидамЦен.Сбросить();
					Если ВыборкаПоВидамЦен.НайтиСледующий(Новый Структура("ВидЦены", СтрокаТЗ.ВидЦены)) Тогда
						
						ОбластьВидЦены = СтрокаТЗ.МакетОбласти;
						ОбластьВидЦены.Параметры.Цена = ВыборкаПоВидамЦен.Цена;											
						
						Если ЗначениеЗаполнено(ВыборкаПоВидамЦен.Упаковка) Тогда
							ОбластьВидЦены.Параметры.ЕдиницаИзмерения = ВыборкаПоВидамЦен.Упаковка;
						Иначе
							ОбластьВидЦены.Параметры.ЕдиницаИзмерения = ВыборкаПоВидамЦен.ЕдиницаИзмерения;
						КонецЕсли;
						
						Если Не ПараметрыПечати.ВыводитьШапку Тогда
							ОбластьВидЦены.Параметры.УникальныйИдентификаторЕдиницаИзмерения = ВыборкаПоВидамЦен.Упаковка.УникальныйИдентификатор();
						КонецЕсли;
						
					Иначе
						
						ОбластьВидЦены = СтрокаТЗ.МакетОбласти;
						ОбластьВидЦены.Параметры.Цена = 0;																		
						ОбластьВидЦены.Параметры.ЕдиницаИзмерения = Неопределено;
						
						Если Не ПараметрыПечати.ВыводитьШапку Тогда
							ОбластьВидЦены.Параметры.УникальныйИдентификаторЕдиницаИзмерения = Справочники.УпаковкиНоменклатуры.ПустаяСсылка().УникальныйИдентификатор();
						КонецЕсли;
						
					КонецЕсли;
					
					ТабДокумент.Присоединить(ОбластьВидЦены);
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Если ПараметрыПечати.ВыводитьШапку Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
			ОбластьМакета.Параметры.ОтветственныйПредставление = ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(Шапка.Ответственный);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, УстановкаЦенНоменклатуры);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

//	LNK 05.09.2016 09:30:48
Процедура УстановитьОбменДаннымиПолучатели(Получатели, СписокВидовЦен)	Экспорт

	Если ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		СписокУзлов = Новый Массив;
		СписокУзлов.Добавить(ПланыОбмена.ПоМагазину.ЭтотУзел());
		СписокУзлов.Добавить(ПланыОбмена.ПоРабочемуМесту.ЭтотУзел());

		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТаблицаМагазины.Ссылка КАК Магазин
		|ПОМЕСТИТЬ Магазины
		|ИЗ
		|	Справочник.Магазины КАК ТаблицаМагазины
		|ГДЕ
		|	ТаблицаМагазины.ПравилоЦенообразования.ВидЦен В(&СписокВидовЦен)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ТаблицаМагазины.Ссылка
		|ИЗ
		|	Справочник.Магазины КАК ТаблицаМагазины
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаЦенообразования.ЦеновыеГруппы КАК ТаблицаПравила
		|		ПО ТаблицаМагазины.ПравилоЦенообразования = ТаблицаПравила.Ссылка
		|ГДЕ
		|	ТаблицаПравила.ВидЦен В(&СписокВидовЦен)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ТаблицаМагазины.Ссылка
		|ИЗ
		|	Справочник.Магазины КАК ТаблицаМагазины
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитика.СрезПоследних(&ТекущаяДата, ) КАК УчетнаяПолитика
		|		ПО (УчетнаяПолитика.ВидЦенИнтернет В (&СписокВидовЦен))
		|ГДЕ
		|	НЕ ТаблицаМагазины.СкладУправляющейСистемы
		|	И НЕ ТаблицаМагазины.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаУзлы.Ссылка КАК Узел
		|ИЗ
		|	ПланОбмена.ПоМагазину КАК ТаблицаУзлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Магазины КАК ТаблицаМагазины
		|		ПО ТаблицаУзлы.Магазин = ТаблицаМагазины.Магазин
		|ГДЕ
		|	НЕ ТаблицаУзлы.Ссылка В (&ЭтотУзел)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ТаблицаУзлы.Ссылка
		|ИЗ
		|	ПланОбмена.ПоМагазину.Магазины КАК ТаблицаУзлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Магазины КАК ТаблицаМагазины
		|		ПО ТаблицаУзлы.Магазин = ТаблицаМагазины.Магазин
		|ГДЕ
		|	НЕ ТаблицаУзлы.Ссылка В (&ЭтотУзел)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ТаблицаУзлы.Ссылка
		|ИЗ
		|	ПланОбмена.ПоРабочемуМесту КАК ТаблицаУзлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Магазины КАК ТаблицаМагазины
		|		ПО ТаблицаУзлы.Магазин = ТаблицаМагазины.Магазин
		|ГДЕ
		|	НЕ ТаблицаУзлы.Ссылка В (&ЭтотУзел)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ТаблицаУзлы.Ссылка
		|ИЗ
		|	ПланОбмена.ПоМагазину КАК ТаблицаУзлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитика.СрезПоследних КАК УчетнаяПолитика
		|		ПО (УчетнаяПолитика.ВидЦенИнтернет В (&СписокВидовЦен))
		|ГДЕ
		|	ТаблицаУзлы.ВидУзла = ЗНАЧЕНИЕ(Перечисление.ВидыУзлов.ПустойУзел)
		|	И НЕ ТаблицаУзлы.Ссылка В (&ЭтотУзел)"
		);
		Запрос.УстановитьПараметр("СписокВидовЦен", СписокВидовЦен);
		Запрос.УстановитьПараметр("ТекущаяДата"   , ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("ЭтотУзел"      , СписокУзлов);

		УзлыВыборка = Запрос.Выполнить().Выбрать();

		Пока УзлыВыборка.Следующий() Цикл

			Получатели.Добавить(УзлыВыборка.Узел);

		КонецЦикла;

	Иначе
		
		ОбменДаннымиСервер.ДобавитьВСписокГлавныйУзел(Получатели);

	КонецЕсли;

КонецПроцедуры

#КонецЕсли
