#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;

	ПриСозданииЧтенииНаСервере();

	ОбщегоНазначенияРТ.ЗаполнитьШапкуДокумента(Объект, КартинкаСостоянияДокумента, СостояниеДокумента, РазрешеноПроведение);

//	NAV:БлокировкаДанныхУчестьДокументВNavision
//	Обработчик подсистемы "NAV"
	ВнешниеИсточникиСобытия.ДобавитьКнопкуУчестьВNavision(ЭтотОбъект, ЭтотОбъект.КоманднаяПанель);
//	Оформление элементов в обработчике подсистемы "NAV"
	ВнешниеИсточникиСобытия.ПроверитьКнопкуУчестьВNavision(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);

	ПриСозданииЧтенииНаСервере();

	ОбщегоНазначенияРТКлиентСервер.ОбновитьСостояниеДокумента(Объект, СостояниеДокумента, КартинкаСостоянияДокумента, РазрешеноПроведение);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьОформлениеЭлементов();

КонецПроцедуры

&НаКлиенте	//	LNK 29.07.2021 05:11:29
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

//	NAV:БлокировкаДанныхУчестьДокументВNavision
	Если ИмяСобытия = "РегистрацияПередачиВNavision" Тогда

		ОбработкаОповещенияНаСервере(ИмяСобытия, Параметр, Источник);

	КонецЕсли;

КонецПроцедуры

&НаСервере	//	LNK 20.09.2023 08:13:33
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	Если НЕ Объект.Начисление.Количество() = 0 Тогда

		ПроверяемыеРеквизиты.Добавить("Объект.ПрограммаЛояльности");

	КонецЕсли;

КонецПроцедуры

&НаСервере	//	LNK 29.07.2021 05:11:29
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

//	NAV:БлокировкаДанныхУчестьДокументВNavision
//	Управление блокировкой "УчётВNavision" подсистемы "NAV" (реквизит "РегистрацияПередачиВNavision" создается в обработчике ПриСозданииНаСервере)
	ТекущийОбъект.ДополнительныеСвойства.Вставить("РегистрацияПередачиВNavision", ЭтотОбъект["РегистрацияПередачиВNavision"]);

КонецПроцедуры

&НаСервере	//	LNK 29.07.2021 05:11:29
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

//	NAV:БлокировкаДанныхУчестьДокументВNavision
//	Оформление элементов в обработчике подсистемы "NAV"
	ВнешниеИсточникиСобытия.ПроверитьКнопкуУчестьВNavision(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте	//	LNK 29.07.2021 05:11:29
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбщегоНазначенияРТКлиентСервер.ОбновитьСостояниеДокумента(Объект, СостояниеДокумента, КартинкаСостоянияДокумента, РазрешеноПроведение);

//	NAV:БлокировкаДанныхУчестьДокументВNavision
	ВнешниеИсточникиКлиент.ВыполнитьОповещениеПередачиДокументаВNavision(Объект.Ссылка, Объект.Ссылка, ЭтотОбъект["РегистрацияПередачиВNavision"]);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ПрограммаЛояльностиПриИзменении(Элемент)

	ПрограммаЛояльностиПриИзмененииНаСервере();

	УстановитьОформлениеЭлементов();

КонецПроцедуры

&НаКлиенте	//	LNK 20.09.2023 08:03:04
Процедура МагазинПриИзменении(Элемент)

	Объект.КодБюджета = РозничныеПродажиСерверПовтИсп.РеквизитыМагазина(Объект.Магазин).КодБюджета;
	УстановитьОформлениеЭлементов();

КонецПроцедуры

&НаКлиенте	//	LNK 20.09.2023 08:22:27
Процедура НачислениеПриИзменении(Элемент)

	УстановитьОформлениеЭлементов();

КонецПроцедуры

&НаКлиенте	//	LNK 20.09.2023 08:26:37
Процедура НачислениеПослеУдаления(Элемент)

	УстановитьОформлениеЭлементов();

КонецПроцедуры

#Область ПериодыДействия

&НаКлиенте
Процедура ИспользоватьПериодДействияПриИзменении(Элемент)

	УстановитьОформлениеЭлементов();

	Если ИспользоватьПериодДействия = 2 Тогда

		Объект.КоличествоПериодовДействия = 0;
		Объект.ПериодДействия = Неопределено;

	Иначе

		Объект.КоличествоПериодовДействия = ИспользоватьПериодДействия;
		Объект.ДатаОкончанияСрокаДействия = '00010101';

		Если ИспользоватьПериодДействия = 0 Тогда

			Объект.ПериодДействия = Неопределено;

		ИначеЕсли ИспользоватьПериодДействия = 1 И Объект.ПериодДействия.Пустая() Тогда

			Объект.ПериодДействия = ПредопределенноеЗначение("Перечисление.Периодичность.Год");

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтсрочкуНачалаДействияПриИзменении(Элемент)

	УстановитьОформлениеЭлементов();

	Объект.КоличествоПериодовОтсрочкиНачалаДействия = ИспользоватьОтсрочкуНачалаДействия;

	Если Объект.КоличествоПериодовОтсрочкиНачалаДействия = 0 Тогда

		Объект.ПериодОтсрочкиНачалаДействия = Неопределено;

	Иначе

		Объект.ПериодОтсрочкиНачалаДействия = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя");

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

//	NAV:БлокировкаДанныхУчестьДокументВNavision
#Область ОбслуживаниеКнопкиУчестьДокументВNavision

&НаКлиенте	//	LNK 29.07.2021 05:02:55
Процедура РегистрацияПередачиВNavisionНажатие(Элемент)

	ВнешниеИсточникиКлиент.ПодтвердитьРешениеУчетВNavision(ЭтотОбъект);
	
	//А++ 20241210 по задаче  https://awdev.atlassian.net/browse/RETAIL1C-1006
	Если  ПроверитьРоль() Тогда
		Объект.Дата = ТекущаяДата();
	КонецЕсли;
	//А++ 20241210 по задаче  https://awdev.atlassian.net/browse/RETAIL1C-1006
	

КонецПроцедуры

&НаКлиенте	//	LNK 18.06.2023 06:12:51
Процедура РегистрацияПередачиВNavisionПовторнаяНажатие(Элемент)

	ВнешниеИсточникиКлиент.ПодтвердитьПовторнуюВыгрузкуВNavision(ЭтотОбъект);

КонецПроцедуры

//	Обработчик события кнопки подсистемы "NAV"
&НаСервере	//	LNK 29.07.2021 05:02:55
Процедура РегистрацияПередачиВNavisionНаСервере()	Экспорт

//	Оформление элементов в обработчике подсистемы "NAV"
	ВнешниеИсточникиСобытия.ПроверитьКнопкуУчестьВNavision(ЭтотОбъект, Истина);

КонецПроцедуры

&НаСервере	//	LNK 29.07.2021 05:02:55
Процедура ОбработкаОповещенияНаСервере(ИмяСобытия, Параметр, Источник)

//	LNK 10.09.2016 10:14:54
//	NAV:БлокировкаДанныхУчестьДокументВNavision
	Если ИмяСобытия = "РегистрацияПередачиВNavision" Тогда

		Если Источник = Объект.Ссылка Тогда

		//	Оформление элементов в обработчике подсистемы "NAV"
			ВнешниеИсточникиСобытия.ПроверитьКнопкуУчестьВNavision(ЭтотОбъект);

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрограммаЛояльностиПриИзмененииНаСервере()

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаСправочник.ПериодДействия КАК ПериодДействия,
	|	ТаблицаСправочник.ПериодОтсрочкиНачалаДействия КАК ПериодОтсрочкиНачалаДействия,
	|	ТаблицаСправочник.КоличествоПериодовДействия КАК КоличествоПериодовДействия,
	|	ТаблицаСправочник.КоличествоПериодовОтсрочкиНачалаДействия КАК КоличествоПериодовОтсрочкиНачалаДействия
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ПериодДействия,
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ПериодОтсрочкиНачалаДействия,
	|		0 КАК КоличествоПериодовДействия,
	|		0 КАК КоличествоПериодовОтсрочкиНачалаДействия) КАК Ведущая
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПрограммыЛояльности КАК ТаблицаСправочник
	|		ПО (ТаблицаСправочник.Ссылка = &ПрограммаЛояльности)"
	);
	Запрос.УстановитьПараметр("ПрограммаЛояльности", Объект.ПрограммаЛояльности);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	ЗаполнитьЗначенияСвойств(Объект, Выборка);

	ПриСозданииЧтенииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()

	ИспользоватьОтсрочкуНачалаДействия = ?(Объект.КоличествоПериодовОтсрочкиНачалаДействия > 0, 1, 0);
	
	Если Объект.КоличествоПериодовДействия > 0 Тогда

		ИспользоватьПериодДействия = 1;

	ИначеЕсли НЕ Объект.ДатаОкончанияСрокаДействия = '00010101' Тогда

		ИспользоватьПериодДействия = 2;

	Иначе

		ИспользоватьПериодДействия = 0;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОформлениеЭлементов()

	Элементы.ГруппаСрокДействияПраво.Доступность	= (ИспользоватьПериодДействия = 1);
	Элементы.ДатаОкончанияСрокаДействия.Доступность	= (ИспользоватьПериодДействия = 2);
	Элементы.ГруппаОтсрочкаПраво.Доступность		= (ИспользоватьОтсрочкуНачалаДействия = 1);
//	LNK 20.09.2023 08:21:41
	Элементы.ПрограммаЛояльности.АвтоОтметкаНезаполненного = НЕ Объект.Начисление.Количество() = 0;
	
	
	//А++ 20241210 по задаче  https://awdev.atlassian.net/browse/RETAIL1C-1006
	Если  ПроверитьРоль() Тогда
		Элементы.ГруппаБонуснаяСистема.ТолькоПросмотр = ЗначениеЗаполнено(Объект.ПрограммаЛояльности)
	КонецЕсли;
	//А++ 20241210 по задаче  https://awdev.atlassian.net/browse/RETAIL1C-1006

КонецПроцедуры

//А++ 20241210 по задаче  https://awdev.atlassian.net/browse/RETAIL1C-1006
&НаСервере
Функция ПроверитьРоль()
	Возврат РольДоступна("ДобавлениеИзменениеНачислениеИСписаниеБонусныхБаллов");
КонецФункции


#КонецОбласти










