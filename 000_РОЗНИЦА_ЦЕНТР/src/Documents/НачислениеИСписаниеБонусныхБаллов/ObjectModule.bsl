#Область ОбработчикиСобытийОбъекта

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	ИнициализироватьДокумент();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если НЕ Начисление.Количество() = 0 Тогда

		ПроверяемыеРеквизиты.Добавить("ПрограммаЛояльности");

	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ЗаполнениеОбъектовСобытия.ОбщиеДействияПередЗаписью(ЭтотОбъект, Отказ);

	ДополнительныеСвойства.Вставить("ЭтоНовый"   , ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	ДополнительныеСвойства.Вставить("Проведен"   , ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Проведен", Ложь));

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	ПроведениеСервер.УстановитьРежимПроведения(Проведен, РежимЗаписи, РежимПроведения);

//	NAV:БлокировкаДанныхУчестьДокументВNavision
	Если НЕ ТехническаяПоддержкаВызовСервера.ИсключительныйРежим() И НЕ ЭтоНовый() Тогда

		Если ВнешниеИсточникиСобытия.ПередачаNavision(Ссылка) Тогда

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ «" + СокрЛП(Ссылка) + "» учтён в КСУ Navision! Изменения запрещены. Отказано.", Ссылка,,, Отказ);
			Возврат;

		КонецЕсли;

	КонецЕсли;

	Если НЕ Отказ Тогда

		Если КоличествоПериодовОтсрочкиНачалаДействия = 0 Тогда

			ПериодОтсрочкиНачалаДействия = Неопределено;

		КонецЕсли;

		Если КоличествоПериодовДействия = 0 Тогда

			ПериодДействия = Неопределено;

		Иначе

			ДатаОкончанияСрокаДействия = '00010101';

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

//	NAV:БлокировкаДанныхУчестьДокументВNavision
	Если ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(ДополнительныеСвойства, "РегистрацияПередачиВNavision", Ложь) Тогда

		ВнешниеИсточникиСобытия.УстановитьПереданоNavision(Ссылка, ОбщегоНазначенияРТСервер.ПолучитьМагазиныПоОбъекту(Ссылка)
			, ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(ДополнительныеСвойства, "РесурсВидПередачиВNavision", Перечисления.ВидыПередачиNavision.ПереданВNavision),,,, Истина);

	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.НачислениеИСписаниеБонусныхБаллов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	ПроведениеСервер.ОтразитьДвиженияПоРегистру("БонусныеБаллы"			, ДополнительныеСвойства, Движения, Отказ); 
	ПроведениеСервер.ОтразитьДвиженияПоРегистру("БонусныеБаллыВРезерве"	, ДополнительныеСвойства, Движения, Отказ); 
	ПроведениеСервер.ОтразитьДвиженияПоРегистру("СписанныеБонусныеБаллы", ДополнительныеСвойства, Движения, Отказ, "НомерСтрокиДокумента"); 

	СформироватьСписокРегистровДляКонтроля();

	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Автор = Неопределено;
	УзелСоздания = Неопределено;

	ИнициализироватьДокумент();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент()

	Ответственный = Пользователи.ТекущийПользователь();
	ВидОперации	  = Перечисления.ВидыОперацийНачислениеИСписаниеБонусныхБаллов.ОперационныйДокумент;

КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;

	Массив.Добавить(Движения.БонусныеБаллы);

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

Функция ПолучитьСуммуДокумента()	Экспорт	//	LNK 30.07.2021 10:01:07

	СуммаОбщая = Начисление.Итог("СуммаБонусныхБаллов") + Списание.Итог("СуммаБонусныхБаллов");

	Возврат СуммаОбщая;

КонецФункции

#КонецОбласти







