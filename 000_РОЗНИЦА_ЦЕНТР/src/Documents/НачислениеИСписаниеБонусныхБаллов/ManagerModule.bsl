#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Дата КАК Период,
	|	ТаблицаДокумента.ПрограммаЛояльности КАК ПрограммаЛояльности,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ДЕНЬ, ТаблицаДокумента.КоличествоПериодовОтсрочкиНачалаДействия), ДЕНЬ)
	|		КОГДА ТаблицаДокумента.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, НЕДЕЛЯ, ТаблицаДокумента.КоличествоПериодовОтсрочкиНачалаДействия), ДЕНЬ)
	|		КОГДА ТаблицаДокумента.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, МЕСЯЦ, ТаблицаДокумента.КоличествоПериодовОтсрочкиНачалаДействия), ДЕНЬ)
	|		КОГДА ТаблицаДокумента.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, КВАРТАЛ, ТаблицаДокумента.КоличествоПериодовОтсрочкиНачалаДействия), ДЕНЬ)
	|		КОГДА ТаблицаДокумента.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ГОД, ТаблицаДокумента.КоличествоПериодовОтсрочкиНачалаДействия), ДЕНЬ)
	|		КОГДА ТаблицаДокумента.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ДЕКАДА, ТаблицаДокумента.КоличествоПериодовОтсрочкиНачалаДействия), ДЕНЬ)
	|		КОГДА ТаблицаДокумента.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ПОЛУГОДИЕ, ТаблицаДокумента.КоличествоПериодовОтсрочкиНачалаДействия), ДЕНЬ)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ЧАС, 1)
	|	КОНЕЦ КАК ДатаНачисления,
	|	НАЧАЛОПЕРИОДА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ДатаОкончанияСрокаДействия = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ВЫБОР
	|						КОГДА ТаблицаДокумента.КоличествоПериодовДействия = 0
	|							ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|						КОГДА ТаблицаДокумента.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|							ТОГДА ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ДЕНЬ, ТаблицаДокумента.КоличествоПериодовДействия)
	|						КОГДА ТаблицаДокумента.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|							ТОГДА ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, НЕДЕЛЯ, ТаблицаДокумента.КоличествоПериодовДействия)
	|						КОГДА ТаблицаДокумента.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|							ТОГДА ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, МЕСЯЦ, ТаблицаДокумента.КоличествоПериодовДействия)
	|						КОГДА ТаблицаДокумента.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|							ТОГДА ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, КВАРТАЛ, ТаблицаДокумента.КоличествоПериодовДействия)
	|						КОГДА ТаблицаДокумента.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|							ТОГДА ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ГОД, ТаблицаДокумента.КоличествоПериодовДействия)
	|						КОГДА ТаблицаДокумента.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|							ТОГДА ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ДЕКАДА, ТаблицаДокумента.КоличествоПериодовДействия)
	|						КОГДА ТаблицаДокумента.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|							ТОГДА ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ПОЛУГОДИЕ, ТаблицаДокумента.КоличествоПериодовДействия)
	|						ИНАЧЕ ДОБАВИТЬКДАТЕ(ТаблицаДокумента.Дата, ДЕНЬ, 1)
	|					КОНЕЦ
	|			ИНАЧЕ ТаблицаДокумента.ДатаОкончанияСрокаДействия
	|		КОНЕЦ, ДЕНЬ) КАК ДатаСписания
	|ПОМЕСТИТЬ Шапка
	|ИЗ
	|	Документ.НачислениеИСписаниеБонусныхБаллов КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Шапка.ДатаНачисления КАК Период,
	|	Шапка.Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаНачисления.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНачисления.Контрагент КАК Контрагент,
	|	Шапка.ПрограммаЛояльности КАК ПрограммаЛояльности,
	|	Шапка.Ссылка КАК ДокументОперации,
	|	Шапка.Период КАК ПериодОперации,
	|	ТаблицаНачисления.СуммаБонусныхБаллов КАК СуммаБонусныхБаллов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийБонусныхБаллов.Начисление) КАК ВидОперации,
	|	Шапка.ДатаНачисления КАК ДатаНачисления,
	|	Шапка.ДатаСписания КАК ДатаСписания,
	|	ЛОЖЬ КАК СписаниеПартий,
	|	1 КАК КлючПорядка
	|ПОМЕСТИТЬ ТаблицаБонусы
	|ИЗ
	|	Шапка КАК Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачислениеИСписаниеБонусныхБаллов.Начисление КАК ТаблицаНачисления
	|		ПО Шапка.Ссылка = ТаблицаНачисления.Ссылка
	|ГДЕ
	|	НЕ ТаблицаНачисления.СуммаБонусныхБаллов = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Шапка.ДатаСписания,
	|	Шапка.Ссылка,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаНачисления.НомерСтроки,
	|	ТаблицаНачисления.Контрагент,
	|	Шапка.ПрограммаЛояльности,
	|	Шапка.Ссылка,
	|	Шапка.Период,
	|	ТаблицаНачисления.СуммаБонусныхБаллов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийБонусныхБаллов.Аннуляция),
	|	Шапка.ДатаНачисления,
	|	Шапка.ДатаСписания,
	|	ИСТИНА,
	|	3
	|ИЗ
	|	Шапка КАК Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачислениеИСписаниеБонусныхБаллов.Начисление КАК ТаблицаНачисления
	|		ПО Шапка.Ссылка = ТаблицаНачисления.Ссылка
	|ГДЕ
	|	НЕ ТаблицаНачисления.СуммаБонусныхБаллов = 0
	|	И НЕ Шапка.ДатаСписания = ДАТАВРЕМЯ(1, 1, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Шапка.Период,
	|	Шапка.Ссылка,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаСписания.НомерСтроки,
	|	ТаблицаСписания.Контрагент,
	|	Шапка.ПрограммаЛояльности,
	|	Шапка.Ссылка,
	|	Шапка.Период,
	|	ТаблицаСписания.СуммаБонусныхБаллов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийБонусныхБаллов.Использование),
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ИСТИНА,
	|	2
	|ИЗ
	|	Шапка КАК Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачислениеИСписаниеБонусныхБаллов.Списание КАК ТаблицаСписания
	|		ПО Шапка.Ссылка = ТаблицаСписания.Ссылка
	|ГДЕ
	|	НЕ ТаблицаСписания.СуммаБонусныхБаллов = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Шапка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаБонусы.Период КАК Период,
	|	ТаблицаБонусы.Регистратор КАК Регистратор,
	|	ТаблицаБонусы.ВидДвижения КАК ВидДвижения,
	|	МИНИМУМ(ТаблицаБонусы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаБонусы.Контрагент КАК Контрагент,
	|	ТаблицаБонусы.ПрограммаЛояльности КАК ПрограммаЛояльности,
	|	ВЫБОР
	|		КОГДА ТаблицаБонусы.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийБонусныхБаллов.Начисление), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийБонусныхБаллов.Аннуляция))
	|			ТОГДА ТаблицаБонусы.ДокументОперации
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументНачисления,
	|	СУММА(ТаблицаБонусы.СуммаБонусныхБаллов) КАК СуммаБонусныхБаллов,
	|	ТаблицаБонусы.ВидОперации КАК ВидОперации,
	|	ТаблицаБонусы.ПериодОперации КАК ДатаОперации,
	|	ТаблицаБонусы.ДокументОперации КАК ДокументОперации,
	|	ТаблицаБонусы.ДатаНачисления КАК ДатаНачисления,
	|	ТаблицаБонусы.ДатаСписания КАК ДатаСписания,
	|	ТаблицаБонусы.КлючПорядка КАК КлючПорядка
	|ИЗ
	|	ТаблицаБонусы КАК ТаблицаБонусы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаБонусы.Период,
	|	ТаблицаБонусы.Регистратор,
	|	ТаблицаБонусы.ВидДвижения,
	|	ТаблицаБонусы.Контрагент,
	|	ТаблицаБонусы.ПрограммаЛояльности,
	|	ТаблицаБонусы.ВидОперации,
	|	ТаблицаБонусы.ПериодОперации,
	|	ТаблицаБонусы.ДокументОперации,
	|	ТаблицаБонусы.ДатаНачисления,
	|	ТаблицаБонусы.ДатаСписания,
	|	ТаблицаБонусы.КлючПорядка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент,
	|	КлючПорядка,
	|	ПрограммаЛояльности,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаБонусы.ПериодОперации КАК Период,
	|	ТаблицаБонусы.Регистратор КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	МИНИМУМ(ТаблицаБонусы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаБонусы.Контрагент КАК Контрагент,
	|	ТаблицаБонусы.ПрограммаЛояльности КАК ПрограммаЛояльности,
	|	ТаблицаБонусы.Регистратор КАК ДокументНачисления,
	|	СУММА(ТаблицаБонусы.СуммаБонусныхБаллов) КАК СуммаБонусныхБаллов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийБонусныхБаллов.Резерв) КАК ВидОперации,
	|	ТаблицаБонусы.ПериодОперации КАК ДатаОперации,
	|	ТаблицаБонусы.ДокументОперации КАК ДокументОперации,
	|	1 КАК КлючПорядка
	|ИЗ
	|	ТаблицаБонусы КАК ТаблицаБонусы
	|ГДЕ
	|	ТаблицаБонусы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийБонусныхБаллов.Начисление)
	|	И НЕ ТаблицаБонусы.Период = ТаблицаБонусы.ПериодОперации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаБонусы.ПериодОперации,
	|	ТаблицаБонусы.Регистратор,
	|	ТаблицаБонусы.Контрагент,
	|	ТаблицаБонусы.ПрограммаЛояльности,
	|	ТаблицаБонусы.Регистратор,
	|	ТаблицаБонусы.ПериодОперации,
	|	ТаблицаБонусы.ДокументОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаБонусы.Период,
	|	ТаблицаБонусы.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	МИНИМУМ(ТаблицаБонусы.НомерСтроки),
	|	ТаблицаБонусы.Контрагент,
	|	ТаблицаБонусы.ПрограммаЛояльности,
	|	ТаблицаБонусы.Регистратор,
	|	СУММА(-ТаблицаБонусы.СуммаБонусныхБаллов),
	|	ТаблицаБонусы.ВидОперации,
	|	ТаблицаБонусы.ПериодОперации,
	|	ТаблицаБонусы.ДокументОперации,
	|	2
	|ИЗ
	|	ТаблицаБонусы КАК ТаблицаБонусы
	|ГДЕ
	|	ТаблицаБонусы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийБонусныхБаллов.Начисление)
	|	И НЕ ТаблицаБонусы.Период = ТаблицаБонусы.ПериодОперации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаБонусы.Период,
	|	ТаблицаБонусы.Регистратор,
	|	ТаблицаБонусы.Контрагент,
	|	ТаблицаБонусы.ПрограммаЛояльности,
	|	ТаблицаБонусы.ВидОперации,
	|	ТаблицаБонусы.Регистратор,
	|	ТаблицаБонусы.ПериодОперации,
	|	ТаблицаБонусы.ДокументОперации
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент,
	|	КлючПорядка,
	|	ПрограммаЛояльности,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаБонусы.Период КАК Период,
	|	ТаблицаБонусы.Регистратор КАК Регистратор,
	|	МИНИМУМ(ТаблицаБонусы.НомерСтроки) КАК НомерСтроки,
	|	0 КАК НомерСтрокиДокумента,
	|	ТаблицаБонусы.ВидОперации КАК ВидОперации,
	|	ТаблицаБонусы.Период КАК ПериодДвижения,
	|	ТаблицаБонусы.ПериодОперации КАК ДатаОперации,
	|	ТаблицаБонусы.ДатаНачисления КАК ДатаНачисления,
	|	ТаблицаБонусы.ДатаСписания КАК ДатаСписания,
	|	ТаблицаБонусы.ДокументОперации КАК ДокументОперации,
	|	НЕОПРЕДЕЛЕНО КАК ДокументПродажи,
	|	ТаблицаБонусы.Контрагент КАК Контрагент,
	|	ТаблицаБонусы.ПрограммаЛояльности КАК ПрограммаЛояльности,
	|	СУММА(ТаблицаБонусы.СуммаБонусныхБаллов) КАК СуммаБонусныхБаллов,
	|	ТаблицаБонусы.СписаниеПартий КАК СписаниеПартий,
	|	ТаблицаБонусы.КлючПорядка КАК КлючПорядка
	|ИЗ
	|	ТаблицаБонусы КАК ТаблицаБонусы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаБонусы.Период,
	|	ТаблицаБонусы.Регистратор,
	|	ТаблицаБонусы.ВидОперации,
	|	ТаблицаБонусы.ПериодОперации,
	|	ТаблицаБонусы.ДатаНачисления,
	|	ТаблицаБонусы.ДатаСписания,
	|	ТаблицаБонусы.ДокументОперации,
	|	ТаблицаБонусы.Контрагент,
	|	ТаблицаБонусы.ПрограммаЛояльности,
	|	ТаблицаБонусы.СписаниеПартий,
	|	ТаблицаБонусы.КлючПорядка,
	|	ТаблицаБонусы.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент,
	|	КлючПорядка,
	|	ПрограммаЛояльности,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаБонусы"
	);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаБонусныеБаллы"			, Результат[3].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаБонусныеБаллыВРезерве"	, Результат[4].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСписанныеБонусныеБаллы"	, Результат[5].Выгрузить());	//	LNK 26.09.2021 13:09:58

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти
	
#КонецЕсли
