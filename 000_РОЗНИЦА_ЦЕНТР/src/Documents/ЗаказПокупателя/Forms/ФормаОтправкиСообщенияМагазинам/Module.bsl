
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ДокументПечати = Параметры.ДокументПечати;
	НомерДокумента = Параметры.НомерДокумента;
	Ответственный = Параметры.Ответственный;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Заголовок = "Письмо запроса наличия для магазинов по заказу " + НомерДокумента;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	ОтправитьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтправитьНаСервере()
	
	Если ТаблицаМагазинов.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	МассивОбъектов = Новый Массив();
	МассивОбъектов.Добавить(ДокументПечати);
	ОбъектыПечати = Новый СписокЗначений();
	ПараметрыВывода = Новый Структура();
	ПараметрыВывода.Вставить("КодЯзыкаДляМногоязычныхПечатныхФорм", "uk");
	ПараметрыВывода.Вставить("АвторПисьма", ПараметрыСеанса.ТекущийПользователь);
	ДокументДляМагазинов = Документы.ЗаказПокупателя.ПечатьСообщениеДляМагазинов(МассивОбъектов, ОбъектыПечати, ПараметрыВывода);
	Результат = Новый Структура("ПочтаОтправлена, ТекстОтвета", Ложь, "");;
	УчетныеДанныеОтправителя = РаботаСПочтовымиСообщениями.ПолучитьСистемнуюУчетнуюЗапись();

	Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда

		Модуль = ОбщегоНазначенияКлиентСервер.ОбщийМодуль("УправлениеКонтактнойИнформацией");
		
		ПараметрыПисьма = Новый Структура;
		ПараметрыПисьма.Вставить("Тема", "Заказ #"
						 + СокрЛП(НомерДокумента));
						 
		ИмяВрФайла = ПолучитьИмяВременногоФайла("html");
		ДокументДляМагазинов.Записать(ИмяВрФайла, ТипФайлаТабличногоДокумента.HTML);
		Файл = Новый ЧтениеТекста(ИмяВрФайла);
		Текст = Файл.Прочитать();

		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЗаказПокупателяТовары.Номенклатура.IDN КАК НоменклатураIDN
			|ИЗ
			|	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
			|ГДЕ
			|	ЗаказПокупателяТовары.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументПечати);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			АдресКартинки = "";

			СтруктураКодаКартинки = Новый Структура("sku", Выборка.НоменклатураIDN);
			МассивКодов = Новый Массив;
			МассивКодов.Добавить(СтруктураКодаКартинки);
			СтруктураЗапросаКартинки = Новый Структура("Skus", МассивКодов);
			
			ЗаписьJSON = Новый ЗаписьJSON; 
			ЗаписьJSON.УстановитьСтроку(); 
			ЗаписатьJSON(ЗаписьJSON,СтруктураЗапросаКартинки);
			СтрокаJSON = ЗаписьJSON.Закрыть();
	
			Соединение = Новый HTTPСоединение("backoffice.antoshka.ua",,,,,, Новый ЗащищенноеСоединениеOpenSSL);
			Заголовки = Новый Соответствие;
			Заголовки.Вставить("Content-Type", "application/json");
	
			HTTPЗапрос = Новый HTTPЗапрос("/api/rest/images/1c?token=ff75a594-73c6-4edc-96ae-67531e01ca0b", Заголовки);
	
			HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаJSON, КодировкаТекста.UTF8);
			HTTPОтвет = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
	
			Если HTTPОтвет.КодСостояния = 200 Или HTTPОтвет.КодСостояния = 201 Тогда
	
				ЧтениеJSON = Новый ЧтениеJSON;
				ЧтениеJSON.УстановитьСтроку(HTTPОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8));  		
				ТелоОтвета =ПрочитатьJSON(ЧтениеJSON);
				Если ТелоОтвета.Свойство("images") Тогда
					
					СтруктураАдресаКартинки = ТелоОтвета.images[0];
					
					Если СтруктураАдресаКартинки.Свойство("image") Тогда
	
						АдресКартинки = "<img
							|src=" + СтруктураАдресаКартинки.image + " 
							|width=""60"" height=""60"" />";

					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Текст = СтрЗаменить(Текст, "Фото" + СокрЛП(Выборка.НоменклатураIDN), АдресКартинки);
			
		КонецЦикла;

		ПараметрыПисьма.Вставить("Тело", Текст);
		ПараметрыПисьма.Вставить("ТипТекста", Перечисления.ТипыТекстовЭлектронныхПисем.HTMLСКартинками);

		ОтветственныйПочта = Модуль.ПолучитьКонтактнуюИнформацияОбъекта(Ответственный, Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		ПараметрыПисьма.Вставить("АдресОтвета", ОтветственныйПочта);

		Для Каждого СтрокаМагазинов Из ТаблицаМагазинов Цикл

			Адрес = Модуль.ПолучитьКонтактнуюИнформацияОбъекта(
				СтрокаМагазинов.Магазин, Справочники.ВидыКонтактнойИнформации.EmailМагазина);

			Если Не ПустаяСтрока(Адрес) Тогда
				
				ПараметрыПисьма.Вставить("Кому", Адрес);
				Результат.ТекстОтвета = ЭлектроннаяПочта.ОтправитьПочтовоеСообщение(УчетныеДанныеОтправителя, ПараметрыПисьма);
				Результат.ПочтаОтправлена = Истина;
				
			КонецЕсли;

		КонецЦикла;

		Файл.Закрыть();
		УдалитьФайлы(ИмяВрФайла);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
