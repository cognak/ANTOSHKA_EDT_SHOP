
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	МассивДокументов = Новый Массив();
	МассивДокументов.Добавить(ПараметрКоманды);
	
	Если УправлениеПечатьюКлиент.ПроверитьДокументыПроведены(МассивДокументов, ПараметрыВыполненияКоманды.Источник) Тогда

		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ЗаказПокупателя",
				"РазностьДляСклада",
				МассивДокументов,
				ПараметрыВыполненияКоманды.Источник,
				Неопределено);
				
		ИзменитьРегистрСостоянияСтрок(ПараметрКоманды);

	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИзменитьРегистрСостоянияСтрок(СсылкаНаЗаказ)

	СтрокаЗаписи = РегистрыСведений.СостояниеСтрокЗаказаПокупателя.ИнициализацияСтрокиЗаписи();

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументЗапросаТовары.КлючСвязи,
		|	ДокументЗапросаТовары.КоличествоТребование,
		|	ДокументЗапросаТовары.Ссылка
		|ИЗ
		|	Документ.ЗапросДоступностиТоваров.Товары КАК ДокументЗапросаТовары
		|ГДЕ
		|	ДокументЗапросаТовары.Ссылка.ДокументОснование = &ДокументОснование
		|	И НЕ (ДокументЗапросаТовары.СтатусЗапроса = ЗНАЧЕНИЕ(Перечисление.СтатусыЗапросовДоступностиТоваров.Отменён)
		|	ИЛИ ДокументЗапросаТовары.СтатусЗапроса = ЗНАЧЕНИЕ(Перечисление.СтатусыЗапросовДоступностиТоваров.Удалён))";
	
	Запрос.УстановитьПараметр("ДокументОснование", СсылкаНаЗаказ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл

		СтрокаЗаписи.ЗаказПокупателя = СсылкаНаЗаказ;   
		СтрокаЗаписи.ЗапросДоступностиТоваров = Выборка.Ссылка;   
		СтрокаЗаписи.КлючСвязи = Выборка.КлючСвязи; 
		СтрокаЗаписи.СтароеКоличество = Выборка.КоличествоТребование; 
		
		РегистрыСведений.СостояниеСтрокЗаказаПокупателя.ЗаписьВРегистрСтроки(СтрокаЗаписи);
		
	КонецЦикла;

	
КонецПроцедуры

// Код процедур и функций

#КонецОбласти
