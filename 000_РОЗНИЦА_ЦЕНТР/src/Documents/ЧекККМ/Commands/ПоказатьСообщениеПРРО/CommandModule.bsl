&НаКлиенте	//	LNK 25.12.2022 06:20:46
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Перем КодВозврата;
	
	ДанныеЧека = ПодключаемоеОборудованиеDeviceManagerФискальныйРегистратор.ПолучитьДанныеЧека(ПараметрКоманды);

	Если ТипЗнч(ДанныеЧека) = Тип("Структура")
	И	 ТипЗнч(ДанныеЧека.ДанныеPDF) = Тип("ДвоичныеДанные") Тогда

		Попытка

		//	LNK 27.02.2024 06:06:53
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
			ДанныеЧека.ДанныеPDF.Записать(ИмяВременногоФайла);

			ЗапуститьПриложение(ИмяВременногоФайла,, Истина, КодВозврата);

			Попытка

				УдалитьФайлы(ИмяВременногоФайла);

			Исключение

				ТекстОшибки = ОписаниеОшибки();	//	просто для информации

			КонецПопытки;

		Исключение

			ТекстОшибки = ОписаниеОшибки();

			ПоказатьПредупреждение(, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При открытии файла
				           |""%1""
				           |произошла ошибка:
				           |""%2"".'"),
				ИмяВременногоФайла,
				ТекстОшибки));

		КонецПопытки;

	Иначе

		ПоказатьОповещениеПользователя("Нет данных для просмотра"
			,
			, "Объект не содержит информации, необходимой для текущей операции"
			, БиблиотекаКартинок.Информация32
			,
			,
		);

	КонецЕсли;

КонецПроцедуры
