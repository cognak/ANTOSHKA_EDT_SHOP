#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Печать

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СводныйОтчет") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СводныйОтчет", "Сводный отчет", ПечатьСводногоОтчета(МассивОбъектов, ОбъектыПечати, ПараметрыВывода),,,Истина);
	КонецЕсли;
	
КонецПроцедуры



Функция ПечатьСводногоОтчета(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
		
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СводныйОтчетПоКассовойСмене.Номер,
	|	СводныйОтчетПоКассовойСмене.Дата КАК ДатаДокумента,
	|	СводныйОтчетПоКассовойСмене.Организация КАК Организация,
	|	СводныйОтчетПоКассовойСмене.Магазин.Представление КАК ПредставлениеМагазина,
	|	СводныйОтчетПоКассовойСмене.Ссылка КАК Ссылка,
	|	СводныйОтчетПоКассовойСмене.Организация.Префикс КАК Префикс
	|ИЗ
	|	Документ.СводныйОтчетПоКассовойСмене КАК СводныйОтчетПоКассовойСмене
	|ГДЕ
	|	СводныйОтчетПоКассовойСмене.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";

	РезультатЗапроса = Запрос.Выполнить();
	ДанныеПечати = РезультатЗапроса.Выбрать();
	
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СводныйОтчетПоКассовойСменеОтчетыОРозничныхПродажах.ОтчетОРозничныхПродажах,
	|	СводныйОтчетПоКассовойСменеОтчетыОРозничныхПродажах.Ссылка
	|ПОМЕСТИТЬ ТаблицаОтчетовОРозничнойПродаже
	|ИЗ
	|	Документ.СводныйОтчетПоКассовойСмене.ОтчетыОРозничныхПродажах КАК СводныйОтчетПоКассовойСменеОтчетыОРозничныхПродажах
	|ГДЕ
	|	СводныйОтчетПоКассовойСменеОтчетыОРозничныхПродажах.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СводнаяТаблица.НомерКассы,
	|	СводнаяТаблица.ДатаОтчета,
	|	СводнаяТаблица.НомерОтчета,
	|	СводнаяТаблица.ОтчетОРозничныхПродажах,
	|	СУММА(СводнаяТаблица.Выручка) КАК Выручка,
	|	СУММА(СводнаяТаблица.Возвраты) КАК Возвраты,
	|	ТаблицаОтчетовОРозничнойПродаже.Ссылка,
	|	СводнаяТаблица.ОтчетОРозничныхПродажах.КассаККМ КАК КассаККМ,
	|	СводнаяТаблица.ОтчетОРозничныхПродажах.КассаККМ.Владелец КАК Организация,
	|	СводнаяТаблица.ОтчетОРозничныхПродажах.КассаККМ.Владелец.Префикс КАК Префикс
	|ИЗ
	|	ТаблицаОтчетовОРозничнойПродаже КАК ТаблицаОтчетовОРозничнойПродаже
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВложенныйЗапрос.ОтчетОРозничныхПродажах.КассаККМ.Код КАК НомерКассы,
	|			ВложенныйЗапрос.ОтчетОРозничныхПродажах.Дата КАК ДатаОтчета,
	|			ВложенныйЗапрос.ОтчетОРозничныхПродажах.Номер КАК НомерОтчета,
	|			ВложенныйЗапрос.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах,
	|			ВложенныйЗапрос.Выручка КАК Выручка,
	|			ВложенныйЗапрос.Возвраты КАК Возвраты
	|		ИЗ
	|			(ВЫБРАТЬ
	|				СУММА(ВЫБОР
	|						КОГДА ОтчетОРозничныхПродажахТовары.Ссылка.ЦенаВключаетНДС
	|							ТОГДА ОтчетОРозничныхПродажахТовары.Сумма
	|						ИНАЧЕ ОтчетОРозничныхПродажахТовары.Сумма + ОтчетОРозничныхПродажахТовары.СуммаНДС
	|					КОНЕЦ) КАК Выручка,
	|				СУММА(ВЫБОР
	|						КОГДА ОтчетОРозничныхПродажахТовары.Количество < 0
	|							ТОГДА ВЫБОР
	|									КОГДА ОтчетОРозничныхПродажахТовары.Ссылка.ЦенаВключаетНДС
	|										ТОГДА -ОтчетОРозничныхПродажахТовары.Сумма
	|									ИНАЧЕ -(ОтчетОРозничныхПродажахТовары.Сумма + ОтчетОРозничныхПродажахТовары.СуммаНДС)
	|								КОНЕЦ
	|						ИНАЧЕ 0
	|					КОНЕЦ) КАК Возвраты,
	|				ОтчетОРозничныхПродажахТовары.Ссылка КАК ОтчетОРозничныхПродажах
	|			ИЗ
	|				Документ.ОтчетОРозничныхПродажах.Товары КАК ОтчетОРозничныхПродажахТовары
	|			ГДЕ
	|				ОтчетОРозничныхПродажахТовары.Ссылка В
	|						(ВЫБРАТЬ
	|							ТаблицаОтчетовОРозничнойПродаже.ОтчетОРозничныхПродажах
	|						ИЗ
	|							ТаблицаОтчетовОРозничнойПродаже КАК ТаблицаОтчетовОРозничнойПродаже)
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ОтчетОРозничныхПродажахТовары.Ссылка
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				СУММА(0),
	|				СУММА(ВЫБОР
	|						КОГДА ОтчетОРозничныхПродажахВозвращенныеТовары.Ссылка.ЦенаВключаетНДС
	|							ТОГДА ОтчетОРозничныхПродажахВозвращенныеТовары.Сумма
	|						ИНАЧЕ ОтчетОРозничныхПродажахВозвращенныеТовары.Сумма + ОтчетОРозничныхПродажахВозвращенныеТовары.СуммаНДС
	|					КОНЕЦ),
	|				ОтчетОРозничныхПродажахВозвращенныеТовары.Ссылка
	|			ИЗ
	|				Документ.ОтчетОРозничныхПродажах.ВозвращенныеТовары КАК ОтчетОРозничныхПродажахВозвращенныеТовары
	|			ГДЕ
	|				ОтчетОРозничныхПродажахВозвращенныеТовары.Ссылка В
	|						(ВЫБРАТЬ
	|							ТаблицаОтчетовОРозничнойПродаже.ОтчетОРозничныхПродажах
	|						ИЗ
	|							ТаблицаОтчетовОРозничнойПродаже КАК ТаблицаОтчетовОРозничнойПродаже)
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ОтчетОРозничныхПродажахВозвращенныеТовары.Ссылка) КАК ВложенныйЗапрос) КАК СводнаяТаблица
	|		ПО (СводнаяТаблица.ОтчетОРозничныхПродажах = ТаблицаОтчетовОРозничнойПродаже.ОтчетОРозничныхПродажах)
	|
	|СГРУППИРОВАТЬ ПО
	|	СводнаяТаблица.НомерКассы,
	|	СводнаяТаблица.ДатаОтчета,
	|	СводнаяТаблица.НомерОтчета,
	|	СводнаяТаблица.ОтчетОРозничныхПродажах,
	|	ТаблицаОтчетовОРозничнойПродаже.Ссылка,
	|	СводнаяТаблица.ОтчетОРозничныхПродажах.КассаККМ,
	|	СводнаяТаблица.ОтчетОРозничныхПродажах.КассаККМ.Владелец,
	|	СводнаяТаблица.ОтчетОРозничныхПродажах.КассаККМ.Владелец.Префикс";

	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаОтчетовОРозничныхПродажах = РезультатЗапроса.Выгрузить();
	
	ПервыйДокумент = Истина;
	
	Макет = УправлениеПечатью.ПолучитьМакет("Документ.СводныйОтчетПоКассовойСмене.ПФ_MXL_СводныйОтчет", КодЯзыкаПечать);
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СводныйОтчет";
	
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		СведенияОбОрганизации = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.ДатаДокумента);
		
		СтрокаКассовыхОтчетов = "";
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Ссылка", ДанныеПечати.Ссылка);
		
		СтрокиТаблицыОтчетов = ТаблицаОтчетовОРозничныхПродажах.НайтиСтроки(СтруктураПоиска);
		
		// Выводим общие реквизиты шапки
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны",, КодЯзыкаПечать);
		ТабличныйДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ИтогоВыручка  = 0;
		ИтогоВозвраты = 0;
		
		// Выводим строки
		ВыборкаСтрок = РезультатЗапроса.Выбрать();
		Для каждого СтрокаТаблицыОтчетов Из СтрокиТаблицыОтчетов Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
			ОбластьМакета.Параметры.Заполнить(СтрокаТаблицыОтчетов);
			ОбластьМакета.Параметры.Выручка = СтрокаТаблицыОтчетов.Выручка + СтрокаТаблицыОтчетов.Возвраты;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ИтогоВыручка  = ИтогоВыручка + СтрокаТаблицыОтчетов.Выручка + СтрокаТаблицыОтчетов.Возвраты;
			ИтогоВозвраты = ИтогоВозвраты + СтрокаТаблицыОтчетов.Возвраты;
			
		КонецЦикла;
		
		// Выводим итог таблицы
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогТаблицы");
		
		ОбластьМакета.Параметры.СуммаВыручки   = ИтогоВыручка;
		ОбластьМакета.Параметры.СуммаВозвратов = ИтогоВозвраты;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим подвал
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		
		ОбластьМакета.Параметры.СуммаПрописью   = ФормированиеПечатныхФормСервер.СформироватьСуммуПрописью(ИтогоВыручка + ИтогоВозвраты, , КодЯзыкаПечать);
		
		Руководители            = ФормированиеПечатныхФормСервер.ОтветственныеЛицаОрганизаций(ДанныеПечати.Организация, ДанныеПечати.ДатаДокумента);
		Руководитель            = Руководители.Руководитель;
		СтаршийКассир           = Руководители.Кассир;

		ОбластьМакета.Параметры.ФИОКассираОрганизации  = СтаршийКассир;
		ОбластьМакета.Параметры.ФИОРуководителя        = Руководитель;

		ТабличныйДокумент.Вывести(ОбластьМакета);
	
		
	КонецЦикла;
	
	// Зададим параметры макета
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;

КонецФункции

#КонецЕсли
