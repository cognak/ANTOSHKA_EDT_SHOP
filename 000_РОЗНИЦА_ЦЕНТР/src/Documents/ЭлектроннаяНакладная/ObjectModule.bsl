
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗапросДоступностиТоваров") Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЭлектроннаяНакладная.Ссылка
			|ИЗ
			|	Документ.ЭлектроннаяНакладная КАК ЭлектроннаяНакладная
			|ГДЕ
			|	ЭлектроннаяНакладная.ДокументОснование = &ДокументОснование";
		
		Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
		
		Если Не Запрос.Выполнить().Пустой() Тогда
			
			ТекстОшибки = НСтр("ru='Отказано! Для «%1» уже есть электронная накладная. Ввод на основании такого документа невозможен.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ДанныеЗаполнения);

			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		Если ДанныеЗаполнения.МагазинОтправитель = ДанныеЗаполнения.МагазинПолучатель Тогда
			
			ТекстОшибки = НСтр("ru='Отказано! У «%1» магазины отправителя и получателя одинаковы. Ввод на основании такого документа невозможен.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ДанныеЗаполнения);

			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;

		ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		Дата = ТекущаяДатаСеанса();
		ДокументОснование = ДанныеЗаполнения;
		ЗаказПокупателя = ДанныеЗаполнения.ДокументОснование;
		
		Если ДанныеЗаполнения.ОператорДоставки = Перечисления.ОператорыДоставки.ВнутренняяЛогистика Тогда
			ОператорДоставки = Перечисления.ОператорыДоставки.НоваяПочта;
		Иначе
			ОператорДоставки = ДанныеЗаполнения.ОператорДоставки;
		КонецЕсли;
		
		Если Не ДанныеЗаполнения.СуммаОплатыДоставки = 0 Тогда
			НашаСуммаДоставки = ДанныеЗаполнения.СуммаОплатыДоставки;
		КонецЕсли;
		
		//Данные получателя
		Если ДанныеЗаполнения.ОператорДоставки = Перечисления.ОператорыДоставки.ВнутренняяЛогистика Тогда
			
			МагазинПолучатель = ДанныеЗаполнения.МагазинПолучатель;
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	НастройкиСлужбДоставки.Контрагент,
				|	НастройкиСлужбДоставки.КонтактноеЛицо,
				|	НастройкиСлужбДоставки.НомерТелефона,
				|	НастройкиСлужбДоставки.Отделение,
				|	НастройкиСлужбДоставки.ОтправкаИзМагазина,
				|	КонтактныеЛицаСлужбыДоставки.КонтактноеЛицо,
				|	КонтактныеЛицаСлужбыДоставки.Телефон,
				|	КонтактныеЛицаСлужбыДоставки.Email,
				|	КонтактныеЛицаСлужбыДоставки.Контрагент,
				|	КонтактныеЛицаСлужбыДоставки.Фамилия,
				|	КонтактныеЛицаСлужбыДоставки.Имя,
				|	КонтактныеЛицаСлужбыДоставки.Отчество,
				|	КонтактныеЛицаСлужбыДоставки.Действие,
				|	КонтактныеЛицаСлужбыДоставки.Предопределенный,
				|	КонтактныеЛицаСлужбыДоставки.ИмяПредопределенныхДанных,
				|	КонтактныеЛицаСлужбыДоставки.Представление,
				|	АдресаСлужбыДоставки.ИДГорода,
				|	АдресаСлужбыДоставки.ИДУлицы,
				|	АдресаСлужбыДоставки.Дом,
				|	АдресаСлужбыДоставки.Адрес
				|ИЗ
				|	РегистрСведений.НастройкиСлужбДоставки КАК НастройкиСлужбДоставки
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаСлужбыДоставки КАК КонтактныеЛицаСлужбыДоставки
				|		ПО НастройкиСлужбДоставки.КонтактноеЛицо = КонтактныеЛицаСлужбыДоставки.Ссылка
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АдресаСлужбыДоставки КАК АдресаСлужбыДоставки
				|		ПО НастройкиСлужбДоставки.Адрес = АдресаСлужбыДоставки.Ссылка
				|ГДЕ
				|	НастройкиСлужбДоставки.Магазин = &Магазин
				|	И НастройкиСлужбДоставки.ОператорДоставки = &ОператорДоставки";
			
			Запрос.УстановитьПараметр("Магазин", ДанныеЗаполнения.МагазинПолучатель);
			Запрос.УстановитьПараметр("ОператорДоставки", ОператорДоставки);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				ДоставкаНаАдрес = Истина;
				ГородПолучатель = Выборка.ИДГорода;
				ОтделениеПолучатель = Выборка.Отделение;
				УлицаПолучатель = Выборка.ИДУлицы;
				ФамилияПолучатель = Выборка.Фамилия;
				ИмяПолучатель = Выборка.Имя;
				ОтчествоПолучатель = Выборка.Отчество;
				ТелефонПолучатель = Выборка.Телефон;
				ДомПолучатель = Выборка.Дом;
			
			КонецЦикла;
				
		Иначе
				
			МагазинПолучатель = Справочники.Магазины.ПустаяСсылка();
			ГородПолучатель = ДанныеЗаполнения.ДокументОснование.Город;
			ОтделениеПолучатель = ДанныеЗаполнения.ДокументОснование.Отделение;
			УлицаПолучатель = ДанныеЗаполнения.ДокументОснование.Улица;
			ДомПолучатель = ДанныеЗаполнения.ДокументОснование.Дом;
			КвартираПолучатель = ДанныеЗаполнения.ДокументОснование.Квартира;
			ФамилияПолучатель = ДанныеЗаполнения.ДокументОснование.Фамилия;
			ИмяПолучатель = ДанныеЗаполнения.ДокументОснование.Имя;
			ОтчествоПолучатель = ДанныеЗаполнения.ДокументОснование.Отчество;
			ТелефонПолучатель = ДанныеЗаполнения.ДокументОснование.Телефон;
			РайонПолучатель = ДанныеЗаполнения.ДокументОснование.Район;
			ОбластьПолучатель = ДанныеЗаполнения.ДокументОснование.Область;
			ДоставкаНаАдрес = ДанныеЗаполнения.ДокументОснование.ДоставкаНаАдрес;

		КонецЕсли;	

		//Данные отправителя
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	НастройкиСлужбДоставки.Контрагент,
			|	НастройкиСлужбДоставки.КонтактноеЛицо,
			|	НастройкиСлужбДоставки.НомерТелефона,
			|	НастройкиСлужбДоставки.Отделение,
			|	НастройкиСлужбДоставки.ОтправкаИзМагазина,
			|	АдресаСлужбыДоставки.ИДГорода,
			|	АдресаСлужбыДоставки.ИДУлицы,
			|	АдресаСлужбыДоставки.Дом,
			|	АдресаСлужбыДоставки.Ссылка
			|ИЗ
			|	РегистрСведений.НастройкиСлужбДоставки КАК НастройкиСлужбДоставки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АдресаСлужбыДоставки КАК АдресаСлужбыДоставки
			|		ПО НастройкиСлужбДоставки.Адрес = АдресаСлужбыДоставки.Ссылка
			|ГДЕ
			|	НастройкиСлужбДоставки.Магазин = &Магазин
			|	И НастройкиСлужбДоставки.ОператорДоставки = &ОператорДоставки";
		
		Если ТипЗнч(ДанныеЗаполнения.МагазинОтправитель) = Тип("СправочникСсылка.Склады") Тогда
			Запрос.УстановитьПараметр("Магазин", ДанныеЗаполнения.МагазинОтправитель.Магазин);
		Иначе
			Запрос.УстановитьПараметр("Магазин", ДанныеЗаполнения.МагазинОтправитель);
		КонецЕсли;

		Запрос.УстановитьПараметр("ОператорДоставки", ОператорДоставки);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			КонтрагентОтправитель = Выборка.Контрагент;
			ОтправкаИзМагазина = Выборка.ОтправкаИзМагазина;
			КонтактноеЛицоОтправитель = Выборка.КонтактноеЛицо;
			НомерТелефонаОтправитель = Выборка.НомерТелефона;
			ГородОтправитель = Выборка.ИДГорода;
			УлицаОтправитель = Выборка.ИДУлицы;
			НомерДомаОтправитель = Выборка.Дом;
			АдресОтправитель = Выборка.Ссылка;
			ОтделениеОтправитель = Выборка.Отделение;
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры
