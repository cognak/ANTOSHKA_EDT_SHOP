
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
		
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ОткрытаяКассоваяСмена = РозничныеПродажиСервер.ПолучитьОткрытуюКассовуюСмену(КассаККМ, Ссылка, НачалоКассовойСмены, ОкончаниеКассовойСмены);
		 
	ДополнительныеСвойства.Вставить("СписокОшибокЗаполнения", Новый Массив);

	Если ОткрытаяКассоваяСмена <> Неопределено Тогда
		
		ДополнительныеСвойства.СписокОшибокЗаполнения.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"По даній касі на дату %1 вже зареєстровано %2",
			Дата,
			ОткрытаяКассоваяСмена
		));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ДополнительныеСвойства.СписокОшибокЗаполнения[ДополнительныеСвойства.СписокОшибокЗаполнения.Количество() - 1]
			, ЭтотОбъект
			,
			,
			, Отказ
		);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОкончаниеКассовойСмены)
		 И ЗначениеЗаполнено(СтатусКассовойСмены)
		 И СтатусКассовойСмены <> Перечисления.СтатусыКассовойСмены.Открыта Тогда
		 
		ДополнительныеСвойства.СписокОшибокЗаполнения.Добавить("Поле «Закінчення зміни» не заповнено");

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ДополнительныеСвойства.СписокОшибокЗаполнения[ДополнительныеСвойства.СписокОшибокЗаполнения.Количество() - 1]
			, ЭтотОбъект
			, "ОкончаниеКассовойСмены"
			,
			, Отказ
		);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОкончаниеКассовойСмены)
		 И ОкончаниеКассовойСмены < НачалоКассовойСмены Тогда
		 
		ДополнительныеСвойства.СписокОшибокЗаполнения.Добавить("Час початку касової зміни більше часу закінчення касової зміни");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ДополнительныеСвойства.СписокОшибокЗаполнения[ДополнительныеСвойства.СписокОшибокЗаполнения.Количество() - 1]
			, ЭтотОбъект
			, "ОкончаниеКассовойСмены"
			,
			, Отказ
		);
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(СтатусКассовойСмены)
		 И СтатусКассовойСмены = Перечисления.СтатусыКассовойСмены.Открыта
		 И НачалоКассовойСмены <> Дата Тогда
		 
		ДополнительныеСвойства.СписокОшибокЗаполнения.Добавить("Час початку касової зміни відрізняється від дати документа");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ДополнительныеСвойства.СписокОшибокЗаполнения[ДополнительныеСвойства.СписокОшибокЗаполнения.Количество() - 1]
			, ЭтотОбъект
			, "НачалоКассовойСмены"
			,
			, Отказ
		);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтатусКассовойСмены)
		 И СтатусКассовойСмены <> Перечисления.СтатусыКассовойСмены.Открыта
		 И ОкончаниеКассовойСмены <> Дата Тогда
		 
		ДополнительныеСвойства.СписокОшибокЗаполнения.Добавить("Час закінчення касової зміни відрізняється від дати документа");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ДополнительныеСвойства.СписокОшибокЗаполнения[ДополнительныеСвойства.СписокОшибокЗаполнения.Количество() - 1]
			, ЭтотОбъект
			, "ОкончаниеКассовойСмены"
			,
			, Отказ
		);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
//	LNK 16.02.2017 14:33:05
	ЗаполнениеОбъектовСобытия.ОбщиеДействияПередЗаписью(ЭтотОбъект, Отказ);

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(Проведен, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
//	LNK 20.09.2016 09:50:56
//	NAV:БлокировкаДанныхУчестьДокументВNavision
	Если НЕ ТехническаяПоддержкаВызовСервера.ИсключительныйРежим() И НЕ ЭтоНовый() Тогда

		Если ВнешниеИсточникиСобытия.ПередачаNavision(Ссылка) Тогда

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ «" + СокрЛП(Ссылка) + "» враховано у КСУ Navision! Зміни заборонені. Відмовлено.", Ссылка,,, Отказ);
			Возврат;

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

//	LNK 20.09.2016 09:50:56
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда

		Возврат;
		
	КонецЕсли;

//	NAV:БлокировкаДанныхУчестьДокументВNavision
	Если ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(ДополнительныеСвойства, "РегистрацияПередачиВNavision", Ложь) Тогда

		ВнешниеИсточникиСобытия.УстановитьПереданоNavision(Ссылка, ОбщегоНазначенияРТСервер.ПолучитьМагазиныПоОбъекту(Ссылка), Перечисления.ВидыПередачиNavision.ТолькоБлокирован,,,, Истина);

	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение

// Инициализирует документ
//
Процедура ИнициализироватьДокумент()
	
	Кассир = Пользователи.ТекущийПользователь();

	Магазин     = ЗначениеНастроекПовтИсп.ПолучитьМагазинПоУмолчанию(Магазин);
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация, Кассир);
	КассаККМ    = ЗначениеНастроекПовтИсп.ПолучитьКассуОрганизацииПоУмолчанию(Организация, , КассаККМ, Магазин, );
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	НачалоКассовойСмены    = НачалоДня(ТекущаяДатаСеанса);
	ОкончаниеКассовойСмены = КонецДня(ТекущаяДатаСеанса);
	
КонецПроцедуры

// Заполняет отчет о розничных продажах в соответствии с отбором.
//
// Параметры
//  ДанныеЗаполнения - Структура со значениями отбора
//
Процедура ЗаполнитьДокументПоОтбору(ДанныеЗаполнения)
	
	Если ДанныеЗаполнения.Свойство("КассаККМ") Тогда
		
		ЗаполнитьДокументПоКассеККМ(ДанныеЗаполнения.КассаККМ);
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет документ по кассе ККМ 
//
Процедура ЗаполнитьДокументПоКассеККМ(КассаККМ)
	
	РеквизитыКассыККМ = Справочники.КассыККМ.РеквизитыКассыККМ(КассаККМ);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыКассыККМ);
	
КонецПроцедуры













