///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Обработчик подсистемы "Внешние обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	
	УстановитьОтборДинамическихСписков("Список,ДенежныеСредстваКПоступлению", "Организация,Касса");
	
	ЗаполнитьСписокХозяйственныхОпераций();

	Если НЕ ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() И Организация.Пустая() Тогда

		ПриЗагрузкеДанныхИзНастроекНаСервере(Новый Соответствие);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанПриходныйКассовыйОрдер" Тогда

		ОбновитьДиначескиеСписки();

	ИначеЕсли ИмяСобытия = "ПользовательИзменилТекущуюОрганизацию" Тогда

		Если НЕ Организация = Параметр.Организация Тогда

			Организация = Параметр.Организация;
			Касса = Неопределено;
			ОтборОрганизацияПриИзменении(Неопределено);

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

//	LNK 14.09.2017 13:12:44
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Если ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		Организация = Настройки.Получить("Организация");
		Касса = Настройки.Получить("Касса");

	Иначе

		Организация = ПараметрыСеанса.ТекущаяОрганизация;
		Касса = ЗначениеНастроекПовтИсп.ПолучитьКассуОрганизацииПоУмолчанию(Организация, Перечисления.ФормыОплаты.Наличная,, ПараметрыСеанса.ТекущийМагазин);

	КонецЕсли;
	
	Элементы.ОтборКасса.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Организация);
	
	УстановитьОтборДинамическихСписков("Список,ДенежныеСредстваКПоступлению", "Организация,Касса");

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ГруппаПКОВыемкаДСИзКассыККМПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОбновитьДиначескиеСписки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборДинамическихСписков("Список,ДенежныеСредстваКПоступлению", "Организация, Касса");
	
	Элементы.ОтборКасса.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКассаПриИзменении(Элемент)
	
	УстановитьОтборДинамическихСписков("Список,ДенежныеСредстваКПоступлению", "Касса");
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЧНОЙ ЧАСТИ "ДенежныеСредстваКПоступлению"

&НаКлиенте
Процедура ДенежныеСредстваКПоступлениюВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьЗначение(Элементы.ДенежныеСредстваКПоступлению.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЧНОЙ ЧАСТИ "Список"

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если НЕ Копирование Тогда
	
		НовыйДокументПриходныйКассовыйОрдер(0);
	
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура НовыйДокументПКОПоступлениеДенежныхСредствИзКассыККМ(Команда)
	
	НовыйДокументПриходныйКассовыйОрдер(1);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйДокументПКОВозвратДенежныхСредствОтПоставщика(Команда)
	
	НовыйДокументПриходныйКассовыйОрдер(2);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйДокументПКОПоступлениеДенежныхСредствИзБанка(Команда)
	
	НовыйДокументПриходныйКассовыйОрдер(3);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйДокументПКОПоступлениеДенежныхСредствИзДругойКассы(Команда)
	
	НовыйДокументПриходныйКассовыйОрдер(4);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйДокументПКОПоступлениеДенежныхСредствИзДругойОрганизации(Команда)
	
	НовыйДокументПриходныйКассовыйОрдер(5);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйДокументПКОПрочиеДоходы(Команда)
	
	НовыйДокументПриходныйКассовыйОрдер(6);
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаПринятьОплату(Команда)
	
	Если Элементы.ДенежныеСредстваКПоступлению.ТекущиеДанные = Неопределено Тогда
		возврат;
	Иначе
		НовыйДокументПриходныйКассовыйОрдер(1, Элементы.ДенежныеСредстваКПоступлению.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенныеДенежныеСредстваКПоступлению(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.ДенежныеСредстваКПоступлению);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура НовыйДокументПриходныйКассовыйОрдер(ХозяйственнаяОперацияИндекс, Основание = Неопределено)
	
	СтруктураПараметры = Новый Структура;
	Если Основание = Неопределено Тогда
		ХозяйственнаяОперация = СписокХозяйственныхОпераций[ХозяйственнаяОперацияИндекс].Значение;
		СтруктураПараметры.Вставить("Основание", Новый Структура("ХозяйственнаяОперация, Организация, Касса", ХозяйственнаяОперация, Организация, Касса));
	Иначе
		СтруктураПараметры.Вставить("Основание", Основание);
	КонецЕсли;
	
	ОткрытьФорму("Документ.ПриходныйКассовыйОрдер.ФормаОбъекта", СтруктураПараметры, Элементы.Список);
	
КонецПроцедуры

// Функция получает значение отбора по имени реквизита для динамического списка
//
&НаСервере
Функция ЗанчениеОтбораДляДинамическогоСписка(ИмяРеквизита, ПараметрВидСравнения, Список, ИмяСписка)
	Перем ЗначениеОтбора;
	
	Если ИмяРеквизита = "Касса" Тогда
		ЗначениеОтбора = Новый СписокЗначений();
		ЗначениеОтбора.Добавить(ЭтотОбъект[ИмяРеквизита]);
		ЗначениеОтбора.Добавить(Справочники.Кассы.ПустаяСсылка());
		ПараметрВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		Если ИмяСписка = "ДенежныеСредстваКПоступлению" Тогда
			ОтборыСписковКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Магазин", ?(ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизита]), ЭтотОбъект[ИмяРеквизита].Магазин, Неопределено), Истина);
		КонецЕсли;
	Иначе
		ЗначениеОтбора = ЭтотОбъект[ИмяРеквизита];
	КонецЕсли;
	возврат ЗначениеОтбора;
	
КонецФункции

// Процедура обновляет динамические списки формы
// - Список
// - ДенежныеСредстваКПоступлению
&НаКлиенте
Процедура ОбновитьДиначескиеСписки()
	
	Элементы.Список.Обновить();
	Элементы.ДенежныеСредстваКПоступлению.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокХозяйственныхОпераций()
	
	СписокХозяйственныхОпераций.Очистить();
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента); // 0
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ); // 1
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика); // 2
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка); // 3
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы); // 4
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации); // 5
	СписокХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПрочиеДоходы); // 6
	
КонецПроцедуры

// Процедура устанавливает отбор динамических списков формы
// Параметры:
//  ИменаДинамическихСписков - Строка - Имена динамических списков на которые устанавливается отбор. В случае, более одного имени списка, они разделяются запятыми.
//  ИменаРеквизитов - Строка - Имена реквизитов динамических списков. В случае, более одного имени списка, они разделяются запятыми.
&НаСервере
Процедура УстановитьОтборДинамическихСписков(ИменаДинамическихСписков, ИменаРеквизитов)
	Перем ПараметрВидСравнения, ЗначениеОтбора;
	
	СтруктураДинамическихСписков = Новый Структура(ИменаДинамическихСписков);
	СтруктураИменРеквизитов = Новый Структура(ИменаРеквизитов);
	Для Каждого НаименованиеДинамическогоСписка Из СтруктураДинамическихСписков Цикл
		ДинамическийСписок = ЭтотОбъект[НаименованиеДинамическогоСписка.Ключ];
		Для Каждого НаименованиеРеквизита Из СтруктураИменРеквизитов Цикл
			ИмяРеквизита = НаименованиеРеквизита.Ключ;
			ЗначениеОтбора = ЗанчениеОтбораДляДинамическогоСписка(ИмяРеквизита, ПараметрВидСравнения, ДинамическийСписок, НаименованиеДинамическогоСписка.Ключ);
			
			ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
				ДинамическийСписок,
				ИмяРеквизита,
				ЗначениеОтбора,
				ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизита]),
				ПараметрВидСравнения
				);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры



