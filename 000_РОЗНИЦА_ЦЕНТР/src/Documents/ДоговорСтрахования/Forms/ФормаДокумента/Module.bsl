
#Область ОбработчикиОсновныхСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;

//	Обработчик механизма "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	ОбщегоНазначенияРТ.ЗаполнитьШапкуДокумента(Объект, КартинкаСостоянияДокумента, СостояниеДокумента, РазрешеноПроведение);
	
	НастроитьФормуПоДополнительнымПравам();

	ОформитьЭлементыФормы();

	УправлениеДоступомРТ.ПриСозданииФормыНаСервере(ЭтотОбъект);	//	LNK 17.10.2019 14:30:01

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	НомерТелефона = ОбщегоНазначенияКлиентСервер.PAD(ТекущийОбъект.ТелефонКод, 2,, "0")
				  + ОбщегоНазначенияКлиентСервер.PAD(СтрЗаменить(ТекущийОбъект.ТелефонНомер, " ", ""), 7,, "0");

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)


КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Объект.Содержание = "№ " + СокрЛП(Объект.НомерДоговора) + " от " + Формат(Объект.Дата, "ДЛФ=DD");

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбщегоНазначенияРТКлиентСервер.ОбновитьСостояниеДокумента(Объект, СостояниеДокумента, КартинкаСостоянияДокумента, РазрешеноПроведение);
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура НомерТелефонаПриИзменении(Элемент)

	Объект.ТелефонКод   = Сред(НомерТелефона, 7, 2);
	Объект.ТелефонНомер = СтрЗаменить(Сред(НомерТелефона, 10), " ", "");

КонецПроцедуры

&НаКлиенте
Процедура ДокументПродажиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	СтруктураПараметрыОтбора = Новый Структура;
	СтруктураПараметрыОтбора.Вставить("Дата"   , НачалоДня(Объект.Дата));
	СтруктураПараметрыОтбора.Вставить("ВидОперации", ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Продажа"));
	СтруктураПараметрыОтбора.Вставить("ДоговорСтрахования", ПредопределенноеЗначение("Документ.ДоговорСтрахования.ПустаяСсылка"));
	СтруктураПараметрыОтбора.Вставить("Магазин", ?(Объект.Магазин.Пустая(), Неопределено, Объект.Магазин));
	
	СтруктураПараметры = Новый Структура("СтруктураПараметрыОтбора", СтруктураПараметрыОтбора);
	СтруктураПараметры.Вставить("РежимВыбора", Истина);

	ОписаниеОповещения = Новый ОписаниеОповещения("ДокументПродажиНачалоВыбораЗавершение", ЭтотОбъект);
	ОткрытьФорму("Документ.ЧекККМ.ФормаВыбора", СтруктураПараметры, ЭтотОбъект, ЭтотОбъект,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ДокументПродажиНачалоВыбораЗавершение(Значение, ДополнительныеПараметры)	Экспорт

	Если НЕ Значение = Неопределено Тогда

		Объект.ДокументПродажи = Значение;
		ДокументПродажиПриИзменении(Элементы.ДокументПродажи);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДокументПродажиПриИзменении(Элемент)

	Объект.Номенклатура = Неопределено;
	ДокументПродажиПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	Объект.ИнформационнаяКарта = Неопределено;

КонецПроцедуры

&НаКлиенте
Процедура ИнформационнаяКартаПриИзменении(Элемент)

	ИнформационнаяКартаПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ИнформационнаяКартаПриИзмененииНаСервере()

	Объект.Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ИнформационнаяКарта, "ВладелецКарты");

КонецПроцедуры

#КонецОбласти

#Область ВспомогательныйФункционалФормы

&НаСервере
Процедура НастроитьФормуПоДополнительнымПравам()


КонецПроцедуры

&НаСервере
Процедура ОформитьЭлементыФормы()

	Если НЕ Объект.Магазин.Пустая() Тогда

		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Новый СвязьПараметраВыбора("Отбор.Магазин", "Объект.Магазин"));
		Элементы.ДокументПродажи.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивПараметров);

	КонецЕсли;

	ДатаДоговора = НачалоДня(Объект.Дата);
	ДатаПродажи  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументПродажи, "Дата");

//	---------------------------------------------------------------------------------------

	СтруктураПараметров = Новый Массив;

	Если НЕ Объект.ДокументПродажи.Пустая() Тогда

		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТабличнаяЧасть.Номенклатура КАК Номенклатура
		|ИЗ
		|	Документ.ЧекККМ.Товары КАК ТабличнаяЧасть
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК ТаблицаСегмента
		|		ПО (ТаблицаСегмента.Сегмент = ЗНАЧЕНИЕ(Справочник.СегментыНоменклатуры.РазрешеноСтраховать))
		|			И ТабличнаяЧасть.Номенклатура = ТаблицаСегмента.Номенклатура
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &ДокументПродажи
		|	И НЕ ТабличнаяЧасть.Номенклатура.IDN = """""
		);
		Запрос.УстановитьПараметр("ДокументПродажи", Объект.ДокументПродажи);
		
		СтруктураПараметров.Добавить(
			Новый ПараметрВыбора("Отбор.Ссылка"
								, Новый ФиксированныйМассив(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура"))));

	КонецЕсли;

	Элементы.Номенклатура.ПараметрыВыбора = Новый ФиксированныйМассив(СтруктураПараметров);

КонецПроцедуры

&НаСервере
Процедура ДокументПродажиПриИзмененииНаСервере()

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокумент.Магазин,
	|	ТаблицаДокумент.ДисконтнаяКарта КАК ИнформационнаяКарта,
	|	ТаблицаДокумент.ВладелецДисконтнойКарты КАК Контрагент,
	|	ТаблицаДокумент.ВладелецДисконтнойКарты.Наименование КАК КонтрагентНаименование
	|ИЗ
	|	Документ.ЧекККМ КАК ТаблицаДокумент
	|ГДЕ
	|	ТаблицаДокумент.Ссылка = &ДокументПродажи"
	);
	Запрос.УстановитьПараметр("ДокументПродажи", Объект.ДокументПродажи);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если Объект.Магазин.Пустая() Тогда

			Объект.Магазин = Выборка.Магазин;

		КонецЕсли;

		Объект.Контрагент          = Выборка.Контрагент;
		Объект.ИнформационнаяКарта = Выборка.ИнформационнаяКарта;

		СоставФИО = ОбщегоНазначенияКлиентСервер.lx_FillValueList(, СтрЗаменить(СокрЛП(Выборка.КонтрагентНаименование), "  ", ""),, " ", 3,, Новый ОписаниеТипов("Строка"));

		Объект.Фамилия  = ТРег(СоставФИО[0]);
		Объект.Имя      = ТРег(СоставФИО[1]);
		Объект.Отчество = ТРег(СоставФИО[2]);

	КонецЕсли;

	ОформитьЭлементыФормы();

КонецПроцедуры

#КонецОбласти
















