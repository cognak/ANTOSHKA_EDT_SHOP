#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства)	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Дата КАК Период,
	|	ТаблицаДокументы.Ссылка КАК Регистратор,
	|	ТаблицаДокументы.Ссылка КАК ДоговорСтрахования,
	|	ТаблицаДокументы.Номенклатура,
	|	ТаблицаДокументы.ДокументПродажи,
	|	1 КАК Количество,
	|	ТаблицаДокументы.СуммаСтраховогоВзноса КАК Сумма,
	|	ТаблицаДокументы.Магазин
	|ПОМЕСТИТЬ Источник
	|ИЗ
	|	Документ.ДоговорСтрахования КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Период КАК Период,
	|	ТаблицаДокументы.Регистратор КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокументы.Номенклатура,
	|	ТаблицаДокументы.ДокументПродажи,
	|	ТаблицаДокументы.Количество КАК Количество,
	|	ТаблицаДокументы.Магазин
	|ИЗ
	|	Источник КАК ТаблицаДокументы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Период КАК Период,
	|	ТаблицаДокументы.Регистратор КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокументы.Номенклатура,
	|	ТаблицаДокументы.ДоговорСтрахования,
	|	ТаблицаДокументы.Сумма КАК Сумма,
	|	ТаблицаДокументы.Магазин
	|ИЗ
	|	Источник КАК ТаблицаДокументы"
	);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);

	Результаты = Запрос.ВыполнитьПакет();

	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыЗастрахованные", Результаты[1].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДоговорыСтрахованияОплаченные", Результаты[2].Выгрузить());

КонецПроцедуры

#КонецОбласти

#КонецЕсли



