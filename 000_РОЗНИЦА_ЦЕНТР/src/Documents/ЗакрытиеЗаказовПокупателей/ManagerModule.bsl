#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Дата КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Магазин КАК Магазин,
	|	ТаблицаДокумента.МоментВремени КАК МоментВремени,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Магазин = ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ НЕ ТаблицаДокумента.Магазин.СкладУправляющейСистемы
	|	КОНЕЦ КАК ФормироватьДвижения
	|ИЗ
	|	Документ.ЗакрытиеЗаказовПокупателей КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка"
	);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	РезультатЗапроса = Запрос.Выполнить();

	Реквизиты = РезультатЗапроса.Выбрать();
	Реквизиты.Следующий(); 
	
	ОбщегоНазначенияРТ.ПеренестиСтрокуВыборкиВПараметрыЗапроса(РезультатЗапроса, Реквизиты, Запрос);
	Запрос.УстановитьПараметр("ГраницаСреза", Новый Граница(Реквизиты.МоментВремени, ВидГраницы.Исключая));

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.ЗаказПокупателя КАК Заказ,
	|	ТабличнаяЧасть.ПричинаОтмены КАК ПричинаОтмены,
	|	ТабличнаяЧасть.НомерСтроки КАК КлючПорядка
	|ПОМЕСТИТЬ Источник
	|ИЗ
	|	Документ.ЗакрытиеЗаказовПокупателей.Реестр КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Магазин КАК Магазин,
	|	Источник.Заказ КАК Заказ,
	|	МИНИМУМ(Источник.ПричинаОтмены) КАК ПричинаОтмены,
	|	МИНИМУМ(Источник.КлючПорядка) КАК КлючПорядка
	|ПОМЕСТИТЬ Фильтр
	|ИЗ
	|	Источник КАК Источник
	|
	|СГРУППИРОВАТЬ ПО
	|	Источник.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Источник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаРегистра.Магазин КАК Магазин,
	|	ТаблицаРегистра.Заказ КАК Заказ,
	|	ТаблицаРегистра.Номенклатура КАК Номенклатура,
	|	ТаблицаРегистра.Характеристика КАК Характеристика,
	|	-ТаблицаРегистра.КоличествоОстаток КАК Количество,
	|	Фильтр.ПричинаОтмены КАК ПричинаОтмены,
	|	Фильтр.КлючПорядка КАК КлючПорядка
	|ИЗ
	|	Фильтр КАК Фильтр
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПокупателей.Остатки(
	|				&ГраницаСреза,
	|				(Магазин, Заказ) В
	|					(ВЫБРАТЬ
	|						Фильтр.Магазин,
	|						Фильтр.Заказ
	|					ИЗ
	|						Фильтр)) КАК ТаблицаРегистра
	|		ПО Фильтр.Магазин = ТаблицаРегистра.Магазин
	|			И Фильтр.Заказ = ТаблицаРегистра.Заказ
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлючПорядка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Ссылка КАК Заказ
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Фильтр КАК Фильтр
	|		ПО ТаблицаДокумента.Ссылка = Фильтр.Заказ
	|ГДЕ
	|	НЕ ТаблицаДокумента.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПокупателей.Закрыт)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Фильтр"
	;
	Результат = Запрос.ВыполнитьПакет();

	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПокупателей", Результат[3].Выгрузить());
	ТаблицыДляДвижений.Вставить("РеестрЗаказовПокупателей", Результат[4].Выгрузить());

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти

#КонецЕсли
