#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ВозможноВвестиВводОстатковДляУзла(Узел) Экспорт
	
	ВозможностьВвода = НЕ ОбменДаннымиПовтИсп.ЭтоПредопределенныйУзелПланаОбмена(Узел) И ОбменДаннымиПовтИсп.ГлавныйУзел() <> Узел;
	
	Возврат ВозможностьВвода;

КонецФункции

Функция ВозможноПровестиВводОстатковВУзле(Узел) Экспорт
	
	Возврат Узел = ПланыОбмена.ПоМагазину.ЭтотУзел(); 
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

// Инициализирует таблицы значений, содержащие данные документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Узел КАК Узел
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Количество КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла.ТоварыНаСкладах КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладах.НомерСтроки КАК НомерСтроки,
	|	ТоварыНаСкладах.ВидДвижения КАК ВидДвижения,
	|	ТоварыНаСкладах.Период КАК Период,
	|	ТоварыНаСкладах.Склад КАК Склад,
	|	ТоварыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладах.Характеристика КАК Характеристика,
	|	ТоварыНаСкладах.Количество КАК Количество
	|ИЗ
	|	Товары КАК ТоварыНаСкладах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТоварыНаМагазинах.НомерСтроки) КАК НомерСтроки,
	|	ТоварыНаМагазинах.ВидДвижения КАК ВидДвижения,
	|	ТоварыНаМагазинах.Период КАК Период,
	|	ТоварыНаМагазинах.Склад.Магазин КАК Магазин,
	|	ТоварыНаМагазинах.Номенклатура КАК Номенклатура,
	|	ТоварыНаМагазинах.Характеристика КАК Характеристика,
	|	СУММА(ТоварыНаМагазинах.Количество) КАК Количество
	|ИЗ
	|	Товары КАК ТоварыНаМагазинах
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыНаМагазинах.ВидДвижения,
	|	ТоварыНаМагазинах.Период,
	|	ТоварыНаМагазинах.Склад.Магазин,
	|	ТоварыНаМагазинах.Номенклатура,
	|	ТоварыНаМагазинах.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДенежныеСредства.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаДенежныеСредства.КассаККМ КАК КассаККМ,
	|	ТаблицаДенежныеСредства.Сумма КАК Сумма
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла.ДенежныеСредстваККМ КАК ТаблицаДенежныеСредства
	|ГДЕ
	|	ТаблицаДенежныеСредства.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЦеныНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатуры.Характеристика КАК Характеристика,
	|	ЦеныНоменклатуры.ВидЦены КАК ВидЦены,
	|	ЦеныНоменклатуры.Упаковка КАК Упаковка,
	|	ЦеныНоменклатуры.Цена КАК Цена
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|ГДЕ
	|	ЦеныНоменклатуры.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ДействующиеЦеныНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ДействующиеЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|	ДействующиеЦеныНоменклатуры.Характеристика КАК Характеристика,
	|	ДействующиеЦеныНоменклатуры.Магазин КАК ОбъектЦенообразования,
	|	ДействующиеЦеныНоменклатуры.Упаковка КАК Упаковка,
	|	ДействующиеЦеныНоменклатуры.Цена КАК Цена
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла.ДействующиеЦеныНоменклатуры КАК ДействующиеЦеныНоменклатуры
	|ГДЕ
	|	ДействующиеЦеныНоменклатуры.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДенежныеСредстваНаличные.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаДенежныеСредстваНаличные.Касса КАК Касса,
	|	ТаблицаДенежныеСредстваНаличные.Сумма КАК Сумма,
	|	ТаблицаДенежныеСредстваНаличные.Организация КАК Организация
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла.ДенежныеСредстваНаличные КАК ТаблицаДенежныеСредстваНаличные
	|ГДЕ
	|	ТаблицаДенежныеСредстваНаличные.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Организация КАК Организация
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла.ТоварыОрганизаций КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ТаблицаТовары.Магазин КАК Магазин,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Цена КАК Цена
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла.СебестоимостьНоменклатуры КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла.ТоварыКПоступлению КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ВводНачальныхОстатковУзла.ТоварыКОтгрузке КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Смещение = 1;
	Результат = Запрос.ВыполнитьПакет();
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;

	ТаблицыДляДвижений.Вставить("ТаблицаТоварыНаСкладах"            , Результат[Смещение + 0].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыНаМагазинах"          , Результат[Смещение + 1].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваККМ"        , Результат[Смещение + 2].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаЦеныНоменклатуры"           , Результат[Смещение + 3].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДействующиеЦеныНоменклатуры", Результат[Смещение + 4].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваНаличные"   , Результат[Смещение + 5].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыОрганизаций"          , Результат[Смещение + 6].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСебестоимостьНоменклатуры"  , Результат[Смещение + 7].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыКПоступлению"         , Результат[Смещение + 8].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыКОтгрузке"            , Результат[Смещение + 9].Выгрузить());
	
КонецПроцедуры

#КонецЕсли
