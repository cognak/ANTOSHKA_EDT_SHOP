#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

// Инициализирует таблицы значений, содержащие данные документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокумента.Магазин КАК Магазин
	|ИЗ
	|	Документ.РегистрацияПодсчетаПосетителей КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка"
	);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	РезультатЗапроса = Запрос.Выполнить();
	Реквизиты = РезультатЗапроса.Выбрать();
	Реквизиты.Следующий();
	ОбщегоНазначенияРТ.ПеренестиСтрокуВыборкиВПараметрыЗапроса(РезультатЗапроса, Реквизиты, Запрос);
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаСводная.СчетчикПодсчетаПосетителей КАК СчетчикПодсчетаПосетителей,
	|	ТаблицаСводная.ВремяРегистрации КАК ВремяРегистрации,
	|	СУММА(ТаблицаСводная.КоличествоВходящих) КАК КоличествоВходящих,
	|	СУММА(ТаблицаСводная.КоличествоВыходящих) КАК КоличествоВыходящих,
	|	СУММА(ТаблицаСводная.КоличествоПроходов) КАК КоличествоПроходов,
	|	ТаблицаСводная.Дата КАК Дата
	|ПОМЕСТИТЬ Источник
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеПодсчета.Ссылка.СчетчикПодсчетаПосетителей КАК СчетчикПодсчетаПосетителей,
	|		ДанныеПодсчета.ВремяРегистрации КАК ВремяРегистрации,
	|		ДанныеПодсчета.КоличествоВходящих КАК КоличествоВходящих,
	|		ДанныеПодсчета.КоличествоВыходящих КАК КоличествоВыходящих,
	|		0 КАК КоличествоПроходов,
	|		ДанныеПодсчета.Ссылка.Дата КАК Дата
	|	ИЗ
	|		Документ.РегистрацияПодсчетаПосетителей.ДанныеПодсчета КАК ДанныеПодсчета
	|	ГДЕ
	|		ДанныеПодсчета.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтатистикаПодсчета.Ссылка.СчетчикПодсчетаПосетителей,
	|		СтатистикаПодсчета.ВремяРегистрации,
	|		0,
	|		0,
	|		СтатистикаПодсчета.КоличествоПроходов,
	|		СтатистикаПодсчета.Ссылка.Дата
	|	ИЗ
	|		Документ.РегистрацияПодсчетаПосетителей.СтатистикаПодсчетаСотрудников КАК СтатистикаПодсчета
	|	ГДЕ
	|		СтатистикаПодсчета.Ссылка = &Ссылка) КАК ТаблицаСводная
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСводная.СчетчикПодсчетаПосетителей,
	|	ТаблицаСводная.ВремяРегистрации,
	|	ТаблицаСводная.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Магазин КАК Магазин,
	|	ТаблицаДанных.СчетчикПодсчетаПосетителей КАК СчетчикПодсчетаПосетителей,
	|	ТаблицаДанных.ВремяРегистрации КАК ВремяРегистрации,
	|	ТаблицаДанных.КоличествоВходящих КАК КоличествоВходящих,
	|	ТаблицаДанных.КоличествоВыходящих КАК КоличествоВыходящих,
	|	ТаблицаДанных.КоличествоПроходов КАК КоличествоПроходов,
	|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ТаблицаДанных.Дата, ДЕНЬ), ЧАС, ЧАС(ТаблицаДанных.ВремяРегистрации)), МИНУТА, МИНУТА(ТаблицаДанных.ВремяРегистрации)), СЕКУНДА, СЕКУНДА(ТаблицаДанных.ВремяРегистрации)) КАК Период,
	|	ВЫРАЗИТЬ((ТаблицаДанных.КоличествоВходящих + ТаблицаДанных.КоличествоВыходящих - ТаблицаДанных.КоличествоПроходов) / 2 КАК ЧИСЛО(6, 0)) КАК Количество,
	|	ДЕНЬНЕДЕЛИ(ТаблицаДанных.Дата) КАК ДеньНедели,
	|	ЧАС(ТаблицаДанных.ВремяРегистрации) КАК Час
	|ИЗ
	|	Источник КАК ТаблицаДанных
	|ГДЕ
	|	(ТаблицаДанных.КоличествоВходящих + ТаблицаДанных.КоличествоВыходящих - ТаблицаДанных.КоличествоПроходов) / 2 <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Источник";
	
	Результат          = Запрос.ВыполнитьПакет();
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаПосетители", Результат[1].Выгрузить());
			
КонецПроцедуры

#КонецЕсли
