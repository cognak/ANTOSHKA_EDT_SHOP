#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЛистЖеланий") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ЛистЖеланий", 
			"Лист желаний", 
			ПечатьЛистаЖеланий(МассивОбъектов, ОбъектыПечати, ПараметрыВывода),,,Истина);
			
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьЛистаЖеланий(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
		
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	СинонимДокумента   = НСтр("ru='Лист желаний';uk='Лист бажань'", КодЯзыкаПечать);

	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЛистЖеланий_ЛистЖеланий";
	
	Запрос = Новый Запрос;

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛистЖеланий.Ссылка.Магазин.Представление КАК Магазин,
	|	ЛистЖеланий.Ссылка.Анкета.Наименование КАК ФИОРодителя,
	|	ЛистЖеланий.Ссылка.Анкета.ИмяИмениника КАК Имениник,
	|	ВЫБОР
	|		КОГДА ЛистЖеланий.Ссылка.Анкета.ПолИмениника = 0
	|			ТОГДА ""М""
	|		ИНАЧЕ ""Ж""
	|	КОНЕЦ КАК ПолИмениника,
	|	ЛистЖеланий.Ссылка.Анкета.НомерТелефонаРодителя КАК НомерТелефонаРодителя,
	|	ЛистЖеланий.Ссылка.Анкета.ЭлектроннаяПочтаРодителя КАК Емаил,
	|	ЛистЖеланий.Ссылка.Анкета.ДатаРожденияИменинника КАК ДатаРожденияИменинника,
	|	ЛистЖеланий.Ссылка.Анкета.ДатаПразднования КАК ДатаПразднования,
	|	ВЫБОР
	|		КОГДА ЛистЖеланий.Ссылка.Анкета.РазрешениеНаИспользованиеДанных = 0
	|			ТОГДА ""Нет""
	|		ИНАЧЕ ""Да""
	|	КОНЕЦ КАК Разрешение,
	|	ЛистЖеланий.Ссылка.Корзина,
	|	ЛистЖеланий.Номер КАК НомерЛЖ,
	|	ЛистЖеланий.Дата КАК ДатаЛЖ,
	|	ЛистЖеланий.Магазин КАК МагазинcССылка,
	|	ЛистЖеланий.Ссылка
	|ИЗ
	|	Документ.ЛистЖеланий КАК ЛистЖеланий
	|ГДЕ
	|	ЛистЖеланий.Ссылка В(&МассивДокументов)";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	Результат = Запрос.Выполнить();
	
	ЗапросОстатки = Новый Запрос;
	ЗапросОстатки.Текст = 
		"ВЫБРАТЬ
		|	ЛистыЖеланийОстатки.Номенклатура,
		|	ЛистыЖеланийОстатки.КоличествоОстаток как Количество
		|ИЗ
		|	РегистрНакопления.ЛистыЖеланий.Остатки(, ЛистЖеланий В (&ЛистЖеланий)) КАК ЛистыЖеланийОстатки";
	
	ЗапросОстатки.УстановитьПараметр("ЛистЖеланий",МассивОбъектов );
	
	РезультатОстатки = ЗапросОстатки.Выполнить();
	
	Макет = УправлениеПечатью.ПолучитьМакет("Документ.ЛистЖеланий.ПФ_MXL_ЛистЖеланий", КодЯзыкаПечать);
	
	ПервыйДокумент = Истина;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		//РеквизитыМагазина = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(Выборка.МагазинcССылка, ,"ФактическийАдрес, Телефоны", , КодЯзыкаПечать);
		ОбластьМакета.Параметры.Адрес =ПолучитьАдресИзКонтактнойИнформации(Выборка.МагазинcССылка);
		ОбластьМакета.Параметры.Телефон = ПолучитьТелефонИзКонтактнойИнформации(Выборка.МагазинcССылка);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ДанныеКлиента");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ВыборкаОстатки = РезультатОстатки.Выбрать();
		
		Пока ВыборкаОстатки.Следующий() Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаНоменклатуры");
			ОбластьМакета.Параметры.Заполнить(ВыборкаОстатки);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция ПолучитьАдресИзКонтактнойИнформации(Объект)

	Если ЗначениеЗаполнено(Объект) Тогда
		
		ВладелецКонтактнойИнформации = Объект;
		
		ВидАдреса = Справочники.ВидыКонтактнойИнформации["ФактАдресМагазина"].Ссылка;

		ТабЗн = ВладелецКонтактнойИнформации.КонтактнаяИнформация.Выгрузить();
		
		НайденныеСтроки = ТабЗн.НайтиСтроки(Новый Структура("Тип,Вид", Перечисления.ТипыКонтактнойИнформации.Адрес, ВидАдреса));
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			Возврат НайденныеСтроки[0].Представление;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат "";

КонецФункции // ПолучитьАдресИзКонтактнойИнформации()

Функция ПолучитьТелефонИзКонтактнойИнформации(Объект)

	Если ЗначениеЗаполнено(Объект) Тогда
	
		ВладелецКонтактнойИнформации = Объект;

		ВидТелефона = Справочники.ВидыКонтактнойИнформации["ТелефонМагазина"].Ссылка;

		ТабЗн           = ВладелецКонтактнойИнформации.КонтактнаяИнформация.Выгрузить();
		НайденныеСтроки = ТабЗн.НайтиСтроки(Новый Структура("Тип,Вид", Перечисления.ТипыКонтактнойИнформации.Телефон, ВидТелефона));
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			Возврат НайденныеСтроки[0].Представление;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции // ПолучитьТелефонИзКонтактнойИнформации()

#КонецЕсли
