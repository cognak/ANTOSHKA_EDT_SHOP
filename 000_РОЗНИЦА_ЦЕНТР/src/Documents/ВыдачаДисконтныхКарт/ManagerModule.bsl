#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства)	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Дата КАК Период,
	|	ТаблицаДокументы.Ссылка КАК Регистратор,
	|	ТаблицаДокументы.Магазин КАК Магазин,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.ВладелецКарты КАК ВладелецКарты,
	|	ТабличнаяЧасть.ДисконтнаяКарта КАК ДисконтнаяКарта
	|ПОМЕСТИТЬ ДанныеВыдачи
	|ИЗ
	|	Документ.ВыдачаДисконтныхКарт КАК ТаблицаДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыдачаДисконтныхКарт.КартыПокупателей КАК ТабличнаяЧасть
	|		ПО ТаблицаДокументы.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	ВладелецКарты,
	|	ДисконтнаяКарта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеВыдачи.Магазин КАК Магазин,
	|	ДанныеВыдачи.Период КАК ДатаВыдачиКарты,
	|	ДанныеВыдачи.НомерСтроки КАК НомерСтроки,
	|	ДанныеВыдачи.ВладелецКарты КАК Контрагент,
	|	ДанныеВыдачи.ДисконтнаяКарта.ВидДисконтнойКарты КАК ВидДисконтнойКарты,
	|	ДанныеВыдачи.ДисконтнаяКарта КАК ИнформационнаяКарта
	|ИЗ
	|	ДанныеВыдачи КАК ДанныеВыдачи
	|ГДЕ
	|	НЕ ДанныеВыдачи.ДисконтнаяКарта.ВладелецКарты = ДанныеВыдачи.ВладелецКарты
	|	И ДанныеВыдачи.ДисконтнаяКарта.ВладелецКарты В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|	И ДанныеВыдачи.ДисконтнаяКарта.ВидДисконтнойКарты = ЗНАЧЕНИЕ(Справочник.ВидыДисконтныхКарт.ПредварительныеКартыЛояльности)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеВыдачи"
	);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();

	ТаблицыДляДвижений = СтруктураДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаНазначитьВладельцевКарт", МассивРезультатов[1].Выгрузить());

КонецПроцедуры

//	LNK 31.07.2019 13:57:47
Функция ВыдатьКартуКонтрагенту(ВладелецКарты, ДисконтнаяКарта, ТекстСообщения)	Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Выполнено = Ложь;

//	Сначала проверим, что есть такой владелец и карта не блокирован и является предварительной!
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаКонтрагенты.Ссылка КАК Контрагент,
	|	ТаблицаКарты.Ссылка КАК ДисконтнаяКарта
	|ИЗ
	|	Справочник.ИнформационныеКарты КАК ТаблицаКарты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК ТаблицаКонтрагенты
	|		ПО (ТаблицаКонтрагенты.Ссылка = &ВладелецКарты)
	|ГДЕ
	|	ТаблицаКарты.Ссылка = &ДисконтнаяКарта
	|	И НЕ(ТаблицаКарты.ПометкаУдаления
	|				ИЛИ ТаблицаКарты.Блокирован)
	|	И ТаблицаКарты.ВидДисконтнойКарты = ЗНАЧЕНИЕ(Справочник.ВидыДисконтныхКарт.ПредварительныеКартыЛояльности)"
	);
	Запрос.УстановитьПараметр("ВладелецКарты"  , ВладелецКарты);
	Запрос.УстановитьПараметр("ДисконтнаяКарта", ДисконтнаяКарта);
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда

		Попытка
			
			ДокументВыдачи = Документы.ВыдачаДисконтныхКарт.СоздатьДокумент();

			ДокументВыдачи.Дата = ТекущаяДата();
			ДокументВыдачи.Ответственный = Пользователи.ТекущийПользователь();
			ДокументВыдачи.Магазин = ПараметрыСеанса.ТекущийМагазин;

			Если ДокументВыдачи.Магазин.Пустая() Тогда

				ДокументВыдачи.Магазин = ОбменДаннымиПовтИсп.ПолучитьДанныеУзла().Магазин;

			КонецЕсли;

			СтрокаТабличнойЧасти = ДокументВыдачи.КартыПокупателей.Добавить();
			СтрокаТабличнойЧасти.ВладелецКарты   = ВладелецКарты;
			СтрокаТабличнойЧасти.ДисконтнаяКарта = ДисконтнаяКарта;

			ДокументВыдачи.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);

			Выполнено = Истина;
		
		Исключение

			ТекстСообщения = ОписаниеОшибки();

		КонецПопытки;

	Иначе

		ТекстСообщения = "Не удалось получить доступную предварительную карту";

	КонецЕсли;

	Возврат Выполнено;

КонецФункции // ВыдатьКартуКонтрагенту()

#КонецЕсли
