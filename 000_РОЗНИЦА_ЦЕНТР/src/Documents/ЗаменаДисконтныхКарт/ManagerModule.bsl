#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ПроверитьДопустимостьКарты(КартаДисконта, Назначение, ТекущийДокумент, ТекстСообщения)	Экспорт

	Отказ  = Ложь;

	Запрос = Новый Запрос(СтрЗаменить(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Дата КАК Дата,
	|	ТаблицаДокументы.Ссылка КАК ДокументЗамены,
	|	ТаблицаДокументы.Представление КАК ДокументЗаменыПредставление
	|ИЗ
	|	Документ.ЗаменаДисконтныхКарт КАК ТаблицаДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаменаДисконтныхКарт.КартыПокупателей КАК ТабличнаяЧасть
	|		ПО (ТаблицаДокументы.Ссылка = ТабличнаяЧасть.Ссылка
	|				И ТабличнаяЧасть.КартаИсточник = &КартаДисконта)
	|ГДЕ
	|	НЕ ТаблицаДокументы.Ссылка = &ТекущийДокумент
	|	И ТаблицаДокументы.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	ДокументЗамены"
	, "И ТабличнаяЧасть.КартаИсточник =", "И ТабличнаяЧасть.Карта" + Назначение + " =")
	);
	Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
	Запрос.УстановитьПараметр("КартаДисконта"  , КартаДисконта);

	Результат = Запрос.Выполнить();

	Если НЕ Результат.Пустой() Тогда

		ТекстСообщения = "Карта «" + КартаДисконта + "» уже использована в:";

		Отказ   = Истина;
		Выборка = Результат.Выбрать();

		Пока Выборка.Следующий() Цикл

			ТекстСообщения = ТекстСообщения + Символы.ПС + Выборка.ДокументЗаменыПредставление;

		КонецЦикла;

	КонецЕсли;

	Возврат НЕ Отказ;

КонецФункции


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Проведение

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Дата КАК Период,
	|	ТаблицаДокументы.Ссылка КАК Регистратор,
	|	ТаблицаДокументы.Магазин КАК Магазин,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.КартаИсточник КАК КартаИсточник,
	|	ТабличнаяЧасть.КартаПриемник КАК КартаПриемник
	|ПОМЕСТИТЬ ДанныеЗамены
	|ИЗ
	|	Документ.ЗаменаДисконтныхКарт КАК ТаблицаДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаменаДисконтныхКарт.КартыПокупателей КАК ТабличнаяЧасть
	|		ПО ТаблицаДокументы.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	КартаИсточник,
	|	КартаПриемник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Регистратор КАК Ссылка,
	|	ТабличнаяЧасть.Период КАК Период,
	|	ТабличнаяЧасть.КартаИсточник КАК КартаИсточник,
	|	ТабличнаяЧасть.КартаПриемник КАК КартаПриемник,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ДанныеЗамены КАК ТабличнаяЧасть
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеЗамены.Магазин КАК Магазин,
	|	ДанныеЗамены.Период КАК ДатаВыдачиКарты,
	|	ДанныеЗамены.НомерСтроки КАК НомерСтроки,
	|	ДанныеЗамены.КартаИсточник.ВладелецКарты КАК Контрагент,
	|	ДанныеЗамены.КартаПриемник.ВидДисконтнойКарты КАК ВидДисконтнойКарты,
	|	ДанныеЗамены.КартаПриемник КАК ИнформационнаяКарта
	|ИЗ
	|	ДанныеЗамены КАК ДанныеЗамены
	|ГДЕ
	|	НЕ ДанныеЗамены.КартаИсточник.ВладелецКарты = ДанныеЗамены.КартаПриемник.ВладелецКарты
	|	И ДанныеЗамены.КартаПриемник.ВладелецКарты В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|	И ДанныеЗамены.КартаПриемник.ВидДисконтнойКарты = ЗНАЧЕНИЕ(Справочник.ВидыДисконтныхКарт.ПредварительныеКартыЛояльности)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеЗамены.Период КАК Период,
	|	ДанныеЗамены.НомерСтроки КАК НомерСтроки,
	|	ДанныеЗамены.КартаИсточник КАК ИнформационнаяКарта
	|ИЗ
	|	ДанныеЗамены КАК ДанныеЗамены
	|ГДЕ
	|	НЕ ДанныеЗамены.КартаИсточник.Блокирован
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеЗамены"
	);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();

	ТаблицыДляДвижений = СтруктураДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаРегистрацияЗаменыКартПокупателей", МассивРезультатов[1].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаНазначитьВладельцевКарт", МассивРезультатов[2].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаБлокироватьИнформационныеКарты", МассивРезультатов[3].Выгрузить());

КонецПроцедуры

#КонецЕсли
