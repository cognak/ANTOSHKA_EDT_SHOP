&НаКлиенте	//	LNK 05.04.2019 09:30:50
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	КлючФормы = "ФормаДокумента";

	Если Прав(ПараметрыВыполненияКоманды.Источник.ИмяФормы, СтрДлина(КлючФормы)) = КлючФормы Тогда

		ЗаказыПокупателейКлиент.КомандаОтправитьВнешнейСистеме(ПараметрКоманды, ПараметрыВыполненияКоманды.Источник, КлючФормы);

	Иначе

		ДополнительныеПараметры = Новый Структура(
			"ДокументСсылка, Форма, КлючФормы"
			, ПараметрКоманды
			, ПараметрыВыполненияКоманды.Источник
			, "ФормаСписка"
		);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьДокументЗавершение", ЭтотОбъект, ДополнительныеПараметры);

		ПоказатьВопрос(ОписаниеОповещения
			, "Для отправки в CRM будет назначен документ" + Символы.ПС + "«" + ПараметрКоманды + "»"
			, РежимДиалогаВопрос.ОКОтмена, 60, КодВозвратаДиалога.ОК
			, "Выгрузка результата запроса доступности"
			, КодВозвратаДиалога.Отмена
		);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте	//	LNK 08.04.2019 10:19:42
Процедура ОтправитьДокументЗавершение(КодВозврата, ДополнительныеПараметры)	Экспорт

	Если КодВозврата = КодВозвратаДиалога.ОК Тогда

		ЗаказыПокупателейКлиент.КомандаОтправитьВнешнейСистеме(ДополнительныеПараметры.ДокументСсылка, ДополнительныеПараметры.Форма, ДополнительныеПараметры.КлючФормы);

	Иначе

		ПоказатьОповещениеПользователя("Отмена воздействия",, "Регистрация для отправки в CRM не выполнена.", БиблиотекаКартинок.Предупреждение_32);

	КонецЕсли;

КонецПроцедуры
