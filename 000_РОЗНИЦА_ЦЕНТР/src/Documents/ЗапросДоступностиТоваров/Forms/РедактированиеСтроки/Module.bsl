
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;
	
	Номенклатура = Параметры.Номенклатура; 
	
	НоваяСтрока = СтрокаЗапроса.Добавить();
	НоваяСтрока.Количество = Параметры.Количество;
	НоваяСтрока.КоличествоИзменено = Параметры.Количество;
	НоваяСтрока.КоличествоТребование = Параметры.КоличествоТребование;
    НоваяСтрока.КлючСвязи = Параметры.КлючСвязи;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	СтрокаТЧ = СтрокаЗапроса[0]; 

	Если (Не СтрокаТЧ.Количество = СтрокаТЧ.КоличествоИзменено) И ПустаяСтрока(Причина) Тогда 
		
		ТекстОшибки = НСтр("ru='Не указана причина изменения'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"Причина"
			);
		
	ИначеЕсли СтрокаТЧ.Количество = СтрокаТЧ.КоличествоИзменено И Не ПустаяСтрока(Причина)  Тогда 
		
		ТекстОшибки = НСтр("ru='Указана причина изменения, но не изменено количество'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"СтрокаТЧ.КоличествоИзменено"
			);
		
	Иначе 
		
		Результат = Новый Структура;
		Результат.Вставить("Причина"			, Причина);
		Результат.Вставить("СообщениеМагазину"	, СообщениеМагазину);
		Результат.Вставить("ИзменилиКоличество"	, Не СтрокаТЧ.Количество = СтрокаТЧ.КоличествоИзменено);
		Результат.Вставить("Количество"			, СтрокаТЧ.КоличествоИзменено);
		Результат.Вставить("КлючСвязи"			, СтрокаТЧ.КлючСвязи);
		Закрыть(Результат);	
		
	КонецЕсли;
                       
КонецПроцедуры

&НаКлиенте
Процедура СтрокаЗапросаКоличествоИзмененоПриИзменении(Элемент)
	
	СтрокаТЧ = СтрокаЗапроса[0]; 
	
	Элементы.Причина.ТолькоПросмотр = (СтрокаТЧ.Количество = СтрокаТЧ.КоличествоИзменено); 
		
КонецПроцедуры

