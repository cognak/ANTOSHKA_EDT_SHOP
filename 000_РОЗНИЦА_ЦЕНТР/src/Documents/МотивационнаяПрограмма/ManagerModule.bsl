#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства)	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.ДатаНачалаДействия КАК Период,
	|	ТаблицаДокументы.ДатаОкончанияДействия КАК ДатаОкончания,
	|	ТаблицаДокументы.ДляВсехМагазинов,
	|	ТаблицаДокументы.ДляВсехНазначений,
	|	ТаблицаДокументы.ПредметМотивационнойПрограммы
	|ИЗ
	|	Документ.МотивационнаяПрограмма КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.Ссылка = &ДокументСсылка"
	);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();

	ОбщегоНазначенияРТ.ПеренестиСтрокуВыборкиВПараметрыЗапроса(Результат, Реквизиты, Запрос);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ТаблицаПрограмма.Ссылка КАК Регистратор,
	|	ТаблицаМагазины.Магазин КАК Магазин,
	|	ТаблицаНазначения.РаботаВыполняемаяСотрудниками КАК РаботаВыполняемаяСотрудниками,
	|	ТаблицаПрограмма.ПредметПрограммы КАК ПредметПрограммы,
	|	ТаблицаПрограмма.Значение КАК Значение,
	|	ТаблицаПрограмма.СпособПредоставления КАК СпособПредоставления,
	|	ТаблицаПрограмма.КритерийПредоставления КАК КритерийПредоставления,
	|	ТаблицаПрограмма.ЗначениеКритерия КАК ЗначениеКритерия,
	|	ТаблицаПрограмма.Приоритет КАК Приоритет,
	|	&ДатаОкончания КАК ДатаОкончания,
	|	&ПредметМотивационнойПрограммы КАК ПредметМотивационнойПрограммы,
	|	ТаблицаПрограмма.Ссылка КАК Программа,
	|	ТаблицаПрограмма.НомерСтроки КАК КлючПорядка
	|ИЗ
	|	Документ.МотивационнаяПрограмма.Программа КАК ТаблицаПрограмма
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ТаблицаМагазины.Магазин КАК Магазин
	|		ИЗ
	|			Документ.МотивационнаяПрограмма.Магазины КАК ТаблицаМагазины
	|		ГДЕ
	|			ТаблицаМагазины.Ссылка = &ДокументСсылка
	|			И &ДляВсехМагазинов = ЛОЖЬ
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ЗНАЧЕНИЕ(Справочник.Магазины.ПустаяСсылка)
	|		ГДЕ
	|			&ДляВсехМагазинов = ИСТИНА) КАК ТаблицаМагазины
	|		ПО (ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ТаблицаНазначения.РаботаВыполняемаяСотрудниками КАК РаботаВыполняемаяСотрудниками
	|		ИЗ
	|			Документ.МотивационнаяПрограмма.Назначения КАК ТаблицаНазначения
	|		ГДЕ
	|			ТаблицаНазначения.Ссылка = &ДокументСсылка
	|			И &ДляВсехНазначений = ЛОЖЬ
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ЗНАЧЕНИЕ(Справочник.РаботаВыполняемаяСотрудниками.ПустаяСсылка)
	|		ГДЕ
	|			&ДляВсехНазначений = ИСТИНА) КАК ТаблицаНазначения
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаПрограмма.Ссылка = &ДокументСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлючПорядка";

	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаДействиеМотивационныхПрограмм", Запрос.Выполнить().Выгрузить());

КонецПроцедуры

#КонецОбласти

#КонецЕсли
