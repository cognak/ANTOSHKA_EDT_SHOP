#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПараметрыЗагрузкиИзТаблицыExcel(КолонкаКод, КолонкаКоличество, ПерваяСтрокаДанных)	Экспорт

	КолонкаКод         = 2;
	КолонкаКоличество  = 6;
	ПерваяСтрокаДанных = 8;

КонецПроцедуры
///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Ведомость.Дата КАК Период,
	|	Ведомость.Ссылка КАК Ссылка,
	|	Ведомость.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.ВедомостьИнвентаризационная КАК Ведомость
	|ГДЕ
	|	Ведомость.Ссылка = &Ссылка"
	);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Реквизиты = РезультатЗапроса.Выбрать();
	Реквизиты.Следующий(); 
	
	ОбщегоНазначенияРТ.ПеренестиСтрокуВыборкиВПараметрыЗапроса(РезультатЗапроса, Реквизиты, Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	МИНИМУМ(ТабличнаяЧасть.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&ДокументОснование КАК ДокументОснование,
	|	&Ссылка КАК Ведомость,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	СУММА(ТабличнаяЧасть.Количество) КАК Количество
	|ИЗ
	|	Документ.ВедомостьИнвентаризационная.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И НЕ ТабличнаяЧасть.Количество = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Результат = Запрос.ВыполнитьПакет();

	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТоварыПересчитанные", Результат[0].Выгрузить());

КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецЕсли
