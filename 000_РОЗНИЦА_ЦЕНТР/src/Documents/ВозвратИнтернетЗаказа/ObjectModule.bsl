
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЭлектроннаяНакладная") Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ТаблицаСостоянияСтрок.КлючСвязи КАК КлючСвязи,
			|	ВЫБОР
			|		КОГДА ТаблицаСостоянияСтрок.КлючСвязиЗаказа = 0
			|			ТОГДА ТаблицаСостоянияСтрок.КлючСвязи
			|		ИНАЧЕ ТаблицаСостоянияСтрок.КлючСвязиЗаказа
			|	КОНЕЦ КАК КлючСвязиЗаказа,
			|	ТаблицаСостоянияСтрок.ЗаказПокупателя КАК ЗаказПокупателя,
			|	ТаблицаСостоянияСтрок.ЗапросДоступностиТоваров КАК ЗапросДоступностиТоваров,
			|	ТаблицаЭНВозврата.Ссылка КАК ЭлектроннаяНакладная
			|ПОМЕСТИТЬ ДанныеДляВозврата
			|ИЗ
			|	Документ.ЭлектроннаяНакладная КАК ТаблицаЭНВозврата
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектроннаяНакладная КАК ТаблицаЭН
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСтрокЗаказаПокупателя КАК ТаблицаСостоянияСтрок
			|			ПО ТаблицаЭН.ЗаказПокупателя = ТаблицаСостоянияСтрок.ЗаказПокупателя
			|			И ТаблицаЭН.ДокументОснование = ТаблицаСостоянияСтрок.ЗапросДоступностиТоваров
			|		ПО ТаблицаЭНВозврата.ДокументОснование = ТаблицаЭН.Ссылка
			|ГДЕ
			|	ТаблицаЭНВозврата.Ссылка = &Ссылка
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаСостоянияСтрок.КлючСвязи,
			|	ВЫБОР
			|		КОГДА ТаблицаСостоянияСтрок.КлючСвязиЗаказа = 0
			|			ТОГДА ТаблицаСостоянияСтрок.КлючСвязи
			|		ИНАЧЕ ТаблицаСостоянияСтрок.КлючСвязиЗаказа
			|	КОНЕЦ,
			|	ТаблицаСостоянияСтрок.ЗаказПокупателя,
			|	ТаблицаСостоянияСтрок.ЗапросДоступностиТоваров,
			|	ТаблицаЭНВозврата.Ссылка
			|ИЗ
			|	Документ.ЭлектроннаяНакладная КАК ТаблицаЭНВозврата
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектроннаяНакладная КАК ТаблицаЭНПереадресация
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектроннаяНакладная КАК ТаблицаЭН
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСтрокЗаказаПокупателя КАК ТаблицаСостоянияСтрок
			|				ПО ТаблицаЭН.ЗаказПокупателя = ТаблицаСостоянияСтрок.ЗаказПокупателя
			|				И ТаблицаЭН.ДокументОснование = ТаблицаСостоянияСтрок.ЗапросДоступностиТоваров
			|			ПО ТаблицаЭНПереадресация.ДокументОснование = ТаблицаЭН.Ссылка
			|		ПО ТаблицаЭНВозврата.ДокументОснование = ТаблицаЭНПереадресация.Ссылка
			|ГДЕ
			|	ТаблицаЭНВозврата.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ТаблицаТоваров.Ссылка КАК Ссылка,
			|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТоваров.АкционнаяЦена КАК АкционнаяЦена,
			|	ТаблицаТоваров.БонусАкцияНачислен КАК БонусАкцияНачислен,
			|	ТаблицаТоваров.БонусАкцияСписан КАК БонусАкцияСписан,
			|	ТаблицаТоваров.БонусБазаНачислен КАК БонусБазаНачислен,
			|	ТаблицаТоваров.БонусБазаСписан КАК БонусБазаСписан,
			|	ТаблицаТоваров.КлючСвязи КАК КлючСвязи,
			|	ТаблицаТоваров.КлючСвязиБонусныхБаллов КАК КлючСвязиБонусныхБаллов,
			|	ТаблицаТоваров.КлючСвязиЗапросаДоступности КАК КлючСвязиЗапросаДоступности,
			|	ТаблицаТоваров.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
			|	ТаблицаТоваров.КодСтроки КАК КодСтроки,
			|	ТаблицаТоваров.Количество КАК Количество,
			|	ТаблицаТоваров.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ТаблицаТоваров.МагазинПолучатель КАК МагазинПолучатель,
			|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
			|	ТаблицаТоваров.Обеспечение КАК Обеспечение,
			|	ТаблицаТоваров.Отменено КАК Отменено,
			|	ТаблицаТоваров.ПричинаОтмены КАК ПричинаОтмены,
			|	ТаблицаТоваров.ПричинаРучнойСкидки КАК ПричинаРучнойСкидки,
			|	ТаблицаТоваров.Продавец КАК Продавец,
			|	ТаблицаТоваров.ПродажаПодарка КАК ПродажаПодарка,
			|	ТаблицаТоваров.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
			|	ТаблицаТоваров.ПроцентРучнойСкидки КАК ПроцентРучнойСкидки,
			|	ТаблицаТоваров.Резервировать КАК Резервировать,
			|	ТаблицаТоваров.Самовывоз КАК Самовывоз,
			|	ТаблицаТоваров.Склад КАК Склад,
			|	ТаблицаТоваров.Содержание КАК Содержание,
			|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
			|	ТаблицаТоваров.Сумма КАК Сумма,
			|	ТаблицаТоваров.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
			|	ТаблицаТоваров.СуммаБонусныхБалловНачислено КАК СуммаБонусныхБалловНачислено,
			|	ТаблицаТоваров.СуммаБонусныхБалловСписано КАК СуммаБонусныхБалловСписано,
			|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
			|	ТаблицаТоваров.СуммаОкругления КАК СуммаОкругления,
			|	ТаблицаТоваров.СуммаРучнойСкидки КАК СуммаРучнойСкидки,
			|	ТаблицаТоваров.ТипДоставки КАК ТипДоставки,
			|	ТаблицаТоваров.УникальныйИдентификатор КАК УникальныйИдентификатор,
			|	ТаблицаТоваров.Упаковка КАК Упаковка,
			|	ТаблицаТоваров.Характеристика КАК Характеристика,
			|	ТаблицаТоваров.Цена КАК Цена,
			|	ТаблицаТоваров.СуммаДоставки КАК СуммаДоставки,
			|	ДанныеДляВозврата.ЗапросДоступностиТоваров КАК ДокументЗаказа
			|ИЗ
			|	Документ.ЗаказПокупателя.Товары КАК ТаблицаТоваров
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДляВозврата КАК ДанныеДляВозврата
			|		ПО ТаблицаТоваров.Ссылка = ДанныеДляВозврата.ЗаказПокупателя
			|		И ТаблицаТоваров.КлючСвязи = ДанныеДляВозврата.КлючСвязиЗаказа
			|ГДЕ
			|	НЕ ТаблицаТоваров.Отменено
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ТаблицаЗаказа.Контрагент КАК Контрагент,
			|	таблицазаказа.ДисконтнаяКарта КАК ДисконтнаяКарта,
			|	ТаблицаЗаказа.Ссылка КАК ДокументОснование,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыВозвратаЗаказаПокупателя.Новый) КАК Статус,
			|	ЗНАЧЕНИЕ(Перечисление.ТипыВозвратаИнтернетЗаказа.АвтоВозврат) КАК ТипВозврата,
			|	ТаблицаЗаказа.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
			|	ТаблицаЗаказа.Магазин КАК Магазин,
			|	ТаблицаЗаказа.Организация КАК Организация,
			|	ДанныеДляВозврата.ЭлектроннаяНакладная КАК ЭлектроннаяНакладная,
			|	ТаблицаЗаказа.ТипОплаты КАК ТипОплаты,
			|	ВЫБОР
			|		КОГДА ТаблицаЗаказа.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипОплатыЗаказПокупателя.Наличные)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусОплаты.Оплачен)
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусОплаты.НеОплачен)
			|	КОНЕЦ КАК СтатусОплаты,
			|	ТаблицаЗаказа.Ответственный КАК Ответственный,
			|	ТаблицаЗаказа.СуммаДоставки КАК СуммаДоставки
			|ИЗ
			|	Документ.ЗаказПокупателя КАК ТаблицаЗаказа
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДляВозврата КАК ДанныеДляВозврата
			|		ПО ДанныеДляВозврата.ЗаказПокупателя = ТаблицаЗаказа.Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		Выборка = РезультатЗапроса[1].Выгрузить();
		Товары.Загрузить(Выборка);

		Выборка = РезультатЗапроса[2].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		Дата = ТекущаяДатаСеанса();
		ВидВозврата = Перечисления.ВидыВозвратаИнтернетЗаказа.Возврат;

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ТаблицаЗаказа.Контрагент КАК Контрагент,
			|	таблицазаказа.ДисконтнаяКарта КАК ДисконтнаяКарта,
			|	ТаблицаЗаказа.Ссылка КАК ДокументОснование,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыВозвратаЗаказаПокупателя.Новый) КАК Статус,
			|	ЗНАЧЕНИЕ(Перечисление.ТипыВозвратаИнтернетЗаказа.ВозвратПочтой) КАК ТипВозврата,
			|	ТаблицаЗаказа.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
			|	ТаблицаЗаказа.Магазин КАК Магазин,
			|	ТаблицаЗаказа.Организация КАК Организация,
			|	ТаблицаЗаказа.Ответственный КАК Ответственный,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусОплаты.НеОплачен) КАК СтатусОплаты,
			|	ТаблицаЗаказа.ТипОплаты КАК ТипОплаты,
			|	ТаблицаЗаказа.СуммаДоставки КАК СуммаДоставки
			|ИЗ
			|	Документ.ЗаказПокупателя КАК ТаблицаЗаказа
			|ГДЕ
			|	ТаблицаЗаказа.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);

	КонецЕсли;
	
	Для Каждого СтрокаВозврата Из Товары Цикл
		
		СтрокаВозврата.ВозвращенноеКоличествоУпаковок = СтрокаВозврата.КоличествоУпаковок;
		ЗаказыПокупателейКлиентСервер.ПодсчетСтрокиВозврата(ДокументОснование, СтрокаВозврата);
		
	КонецЦикла;
	
	СуммаДокумента = Товары.Итог("Сумма");
	СуммаБонусныхБалловСписано = Товары.Итог("СуммаБонусныхБалловСписано");
	СуммаДоставки = Товары.Итог("СуммаДоставки");
	Ответственный = ПараметрыСеанса.ТекущийПользователь;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ДокументОснование.Пустая() Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыВозвратаЗаказаПокупателя.Чек
			И Не ПочтаОтправлена
			И ТипВозврата = Перечисления.ТипыВозвратаИнтернетЗаказа.ВозвратПочтой Тогда
		
		УчетныеДанныеОтправителя = РаботаСПочтовымиСообщениями.ПолучитьСистемнуюУчетнуюЗапись();
	
		Если Не УчетныеДанныеОтправителя.Пустая() Тогда

			Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
				НомерТТН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлектроннаяНакладная, "Номер");
				НомерЗаказа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Номер");
				Адрес = "im@antoshka.ua; eduard.burdovicin@antoshka.ua";

				ПараметрыПисьма = Новый Структура;
				ПараметрыПисьма.Вставить("Кому", Адрес);
				ПараметрыПисьма.Вставить("Тема", "ТТН вовзрата #"
								 + СокрЛП(НомерТТН));
				ТелоПисьма = "Поступила ТТН вовзрата #"
					+ СокрЛП(НомерТТН)
					+ " на возврат #"
					+ СокрЛП(Номер)
					+ " по заказу #"
					+ СокрЛП(НомерЗаказа)
					+ ".";
				ПараметрыПисьма.Вставить("Тело", ТелоПисьма);

				ЭлектроннаяПочта.ОтправитьПочтовоеСообщение(УчетныеДанныеОтправителя, ПараметрыПисьма);
				ПочтаОтправлена = Истина;

			КонецЕсли;

		КонецЕсли;
	
	КонецЕсли;

	ОтборОшибочных = Новый Структура("ОшибкаВозврата", Истина);
	
	ТЗТовары = Товары.Выгрузить(ОтборОшибочных);

	ЕстьОшибки = Не (ТЗТовары.Количество() = 0);

	ОтборОформленных = Новый Структура("Возвращена", Истина);
	
	ТЗТовары = Товары.Выгрузить(ОтборОформленных);

	Оформлен = (ТЗТовары.Количество() = Товары.Выгрузить().Количество());

	Если (Статус = Перечисления.СтатусыВозвратаЗаказаПокупателя.Отправлен
				И (ТипВозврата = Перечисления.ТипыВозвратаИнтернетЗаказа.ВозвратПочтой
					Или ТипВозврата = Перечисления.ТипыВозвратаИнтернетЗаказа.АвтоВозврат))
			Или (Статус = Перечисления.СтатусыВозвратаЗаказаПокупателя.Чек
				И ТипВозврата = Перечисления.ТипыВозвратаИнтернетЗаказа.ВозвратМагазин) Тогда

		НомерЗаказа = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Номер"));
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ВозвратИнтернетЗаказаТовары.ДокументЗаказа КАК ДокументЗаказа,
			|	ВозвратИнтернетЗаказаТовары.НомерВозвратаNAV КАК НомерВозвратаNAV
			|ПОМЕСТИТЬ ТаблицаСтрок
			|ИЗ
			|	Документ.ВозвратИнтернетЗаказа.Товары КАК ВозвратИнтернетЗаказаТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратИнтернетЗаказа КАК ВозвратИнтернетЗаказа1
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя КАК ЗаказПокупателя
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратИнтернетЗаказа КАК ВозвратИнтернетЗаказа
			|				ПО (ВозвратИнтернетЗаказа.ДокументОснование = ЗаказПокупателя.Ссылка)
			|			ПО (ВозвратИнтернетЗаказа1.ДокументОснование = ЗаказПокупателя.Ссылка)
			|		ПО (ВозвратИнтернетЗаказаТовары.Ссылка = ВозвратИнтернетЗаказа1.Ссылка)
			|ГДЕ
			|	ВозвратИнтернетЗаказа.Ссылка = &Ссылка
			|	И НЕ ВозвратИнтернетЗаказаТовары.НомерВозвратаNAV = """"
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВозвратИнтернетЗаказаТовары.ДокументЗаказа,
			|	""""
			|ИЗ
			|	Документ.ВозвратИнтернетЗаказа.Товары КАК ВозвратИнтернетЗаказаТовары
			|ГДЕ
			|	ВозвратИнтернетЗаказаТовары.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаСтрок.ДокументЗаказа КАК ДокументЗаказа,
			|	МАКСИМУМ(ТаблицаСтрок.НомерВозвратаNAV) КАК НомерВозвратаNAV,
			|	ТаблицаЗапроса.Номер КАК Номер
			|ИЗ
			|	ТаблицаСтрок КАК ТаблицаСтрок
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗапросДоступностиТоваров КАК ТаблицаЗапроса
			|		ПО ТаблицаСтрок.ДокументЗаказа = ТаблицаЗапроса.Ссылка
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаСтрок.ДокументЗаказа,
			|	ТаблицаЗапроса.Номер";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ПустаяСтрока(Выборка.НомерВозвратаNAV) Тогда
				
				Суффикс = "001";
				
			Иначе
					
				Суффикс = Формат(Число(СтрЗаменить(Выборка.НомерВозвратаNAV,"RWZ_" + СокрЛП(Выборка.Номер), "")) + 1, "ЧЦ=3; ЧВН=");

			КонецЕсли;
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ДокументЗаказа", Выборка.ДокументЗаказа);
			СтрокиВозврата = Товары.НайтиСтроки(ПараметрыОтбора);
			Для Каждого СтрокаВозврата Из СтрокиВозврата Цикл
				
				Если ПустаяСтрока(СтрокаВозврата.НомерВозвратаNAV) Тогда
					
					СтрокаВозврата.НомерВозвратаNAV = "RWZ_" + СокрЛП(Выборка.Номер) + Суффикс;
					СтрокаВозврата.ДатаДокумента = ТекущаяДатаСеанса();
					
				КонецЕсли;
				
				
			КонецЦикла;

		КонецЦикла;

	КонецЕсли;
	
	Если ЭтоНовый() Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ВозвратИнтернетЗаказа.Номер КАК НомерВозврата,
			|	ЗаказПокупателя.Номер КАК Номер
			|ИЗ
			|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратИнтернетЗаказа КАК ВозвратИнтернетЗаказа
			|		ПО ВозвратИнтернетЗаказа.ДокументОснование = ЗаказПокупателя.Ссылка
			|ГДЕ
			|	ЗаказПокупателя.Ссылка = &ДокументОснование
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерВозврата УБЫВ";
		
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаНомер = РезультатЗапроса.Выбрать();

		Если ВыборкаНомер.Следующий() Тогда
			Суффикс = Формат(Число(СтрЗаменить(ВыборкаНомер.НомерВозврата,СокрЛП(ВыборкаНомер.Номер) + "-", "") )+ 1, "ЧЦ=3; ЧВН=");
		Иначе
			Суффикс = "001";
			
		КонецЕсли;
		
		НомерЗаказа = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Номер"));

		Номер = СокрЛП(НомерЗаказа) + "-" + Суффикс;
		
		Если ТипВозврата = Перечисления.ТипыВозвратаИнтернетЗаказа.АвтоВозврат Тогда
			
			МагазинВозврата = ОбменMagentoСервер.ПолучитьСкладMagentoПоIDN("ОДЕССА").СкладВыбора;
			
			Для Каждого СтрокаТовара Из Товары Цикл
				
				СтрокаТовара.Возвращена = Истина;
				СтрокаТовара.ОшибкаВозврата = Ложь;
				
			КонецЦикла;
			
			ЕстьОшибки = Ложь;
			Оформлен = Истина; 		

		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	РегистрыСведений.СостояниеЗаказаПокупателя.ЗаписьСостояния(ДокументОснование);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;

	Если Не ТипВозврата = Перечисления.ТипыВозвратаИнтернетЗаказа.ВозвратМагазин Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("МагазинВозврата");
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли
