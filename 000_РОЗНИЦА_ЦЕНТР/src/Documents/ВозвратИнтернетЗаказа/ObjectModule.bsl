
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЭлектроннаяНакладная") Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ТаблицаСостоянияСтрок.КлючСвязи КАК КлючСвязи,
			|	ВЫБОР
			|		КОГДА ТаблицаСостоянияСтрок.КлючСвязиЗаказа = 0
			|			ТОГДА ТаблицаСостоянияСтрок.КлючСвязи
			|		ИНАЧЕ ТаблицаСостоянияСтрок.КлючСвязиЗаказа
			|	КОНЕЦ КАК КлючСвязиЗаказа,
			|	ТаблицаСостоянияСтрок.ЗаказПокупателя КАК ЗаказПокупателя,
			|	ТаблицаСостоянияСтрок.ЗапросДоступностиТоваров КАК ЗапросДоступностиТоваров,
			|	ТаблицаЭНВозврата.Ссылка КАК ЭлектроннаяНакладная
			|ПОМЕСТИТЬ ДанныеДляВозврата
			|ИЗ
			|	Документ.ЭлектроннаяНакладная КАК ТаблицаЭНВозврата
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектроннаяНакладная КАК ТаблицаЭН
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСтрокЗаказаПокупателя КАК ТаблицаСостоянияСтрок
			|			ПО ТаблицаЭН.ЗаказПокупателя = ТаблицаСостоянияСтрок.ЗаказПокупателя
			|			И ТаблицаЭН.ДокументОснование = ТаблицаСостоянияСтрок.ЗапросДоступностиТоваров
			|		ПО ТаблицаЭНВозврата.ДокументОснование = ТаблицаЭН.Ссылка
			|ГДЕ
			|	ТаблицаЭНВозврата.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ТаблицаТоваров.Ссылка,
			|	ТаблицаТоваров.НомерСтроки,
			|	ТаблицаТоваров.АкционнаяЦена,
			|	ТаблицаТоваров.БонусАкцияНачислен,
			|	ТаблицаТоваров.БонусАкцияСписан,
			|	ТаблицаТоваров.БонусБазаНачислен,
			|	ТаблицаТоваров.БонусБазаСписан,
			|	ТаблицаТоваров.КлючСвязи,
			|	ТаблицаТоваров.КлючСвязиБонусныхБаллов,
			|	ТаблицаТоваров.КлючСвязиЗапросаДоступности,
			|	ТаблицаТоваров.КлючСвязиСерийныхНомеров,
			|	ТаблицаТоваров.КодСтроки,
			|	ТаблицаТоваров.Количество,
			|	ТаблицаТоваров.КоличествоУпаковок,
			|	ТаблицаТоваров.МагазинПолучатель,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Обеспечение,
			|	ТаблицаТоваров.Отменено,
			|	ТаблицаТоваров.ПричинаОтмены,
			|	ТаблицаТоваров.ПричинаРучнойСкидки,
			|	ТаблицаТоваров.Продавец,
			|	ТаблицаТоваров.ПродажаПодарка,
			|	ТаблицаТоваров.ПроцентАвтоматическойСкидки,
			|	ТаблицаТоваров.ПроцентРучнойСкидки,
			|	ТаблицаТоваров.Резервировать,
			|	ТаблицаТоваров.Самовывоз,
			|	ТаблицаТоваров.Склад,
			|	ТаблицаТоваров.Содержание,
			|	ТаблицаТоваров.СтавкаНДС,
			|	ТаблицаТоваров.Сумма,
			|	ТаблицаТоваров.СуммаАвтоматическойСкидки,
			|	ТаблицаТоваров.СуммаБонусныхБалловНачислено,
			|	ТаблицаТоваров.СуммаБонусныхБалловСписано,
			|	ТаблицаТоваров.СуммаНДС,
			|	ТаблицаТоваров.СуммаОкругления,
			|	ТаблицаТоваров.СуммаРучнойСкидки,
			|	ТаблицаТоваров.ТипДоставки,
			|	ТаблицаТоваров.УникальныйИдентификатор,
			|	ТаблицаТоваров.Упаковка,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Цена
			|ИЗ
			|	Документ.ЗаказПокупателя.Товары КАК ТаблицаТоваров
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДляВозврата КАК ДанныеДляВозврата
			|		ПО ТаблицаТоваров.Ссылка = ДанныеДляВозврата.ЗаказПокупателя
			|		И ТаблицаТоваров.КлючСвязи = ДанныеДляВозврата.КлючСвязиЗаказа
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ТаблицаЗаказа.Контрагент КАК Контрагент,
			|	таблицазаказа.ДисконтнаяКарта КАК ДисконтнаяКарта,
			|	ТаблицаЗаказа.Ссылка КАК ДокументОснование,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыВозвратаЗаказаПокупателя.Новый) КАК Статус,
			|	ЗНАЧЕНИЕ(Перечисление.ТипыВозвратаИнтернетЗаказа.АвтоВозврат) КАК ТипВозврата,
			|	ТаблицаЗаказа.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
			|	ТаблицаЗаказа.Магазин КАК Магазин,
			|	ТаблицаЗаказа.Организация КАК Организация,
			|	ДанныеДляВозврата.ЭлектроннаяНакладная КАК ЭлектроннаяНакладная,
			|	ТаблицаЗаказа.ТипОплаты КАК ТипОплаты,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусОплаты.НеОплачен) КАК СтатусОплаты,
			|	ТаблицаЗаказа.Ответственный КАК Ответственный
			|ИЗ
			|	Документ.ЗаказПокупателя КАК ТаблицаЗаказа
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДляВозврата КАК ДанныеДляВозврата
			|		ПО ДанныеДляВозврата.ЗаказПокупателя = ТаблицаЗаказа.Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		Выборка = РезультатЗапроса[1].Выгрузить();
		Товары.Загрузить(Выборка);

		Выборка = РезультатЗапроса[2].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		Дата = ТекущаяДатаСеанса();

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ТаблицаЗаказа.Контрагент КАК Контрагент,
			|	таблицазаказа.ДисконтнаяКарта КАК ДисконтнаяКарта,
			|	ТаблицаЗаказа.Ссылка КАК ДокументОснование,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыВозвратаЗаказаПокупателя.Новый) КАК Статус,
			|	ЗНАЧЕНИЕ(Перечисление.ТипыВозвратаИнтернетЗаказа.ВозвратПочтой) КАК ТипВозврата,
			|	ТаблицаЗаказа.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
			|	ТаблицаЗаказа.Магазин КАК Магазин,
			|	ТаблицаЗаказа.Организация КАК Организация,
			|	ТаблицаЗаказа.Ответственный КАК Ответственный,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусОплаты.НеОплачен) КАК СтатусОплаты,
			|	ТаблицаЗаказа.ТипОплаты КАК ТипОплаты
			|ИЗ
			|	Документ.ЗаказПокупателя КАК ТаблицаЗаказа
			|ГДЕ
			|	ТаблицаЗаказа.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);

	КонецЕсли;
	
	Для Каждого СтрокаВозврата Из Товары Цикл
		
		СтрокаВозврата.ВозвращенноеКоличествоУпаковок = СтрокаВозврата.КоличествоУпаковок;
		ЗаказыПокупателейКлиентСервер.ПодсчетСтрокиВозврата(ДокументОснование, СтрокаВозврата);
		
	КонецЦикла;
	
	СуммаДокумента = Товары.Итог("Сумма");

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыВозвратаЗаказаПокупателя.Новый
			И ТипВозврата = Перечисления.ТипыВозвратаИнтернетЗаказа.ВозвратМагазин Тогда
			
		Статус = Перечисления.СтатусыВозвратаЗаказаПокупателя.Чек;
			
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	РегистрыСведений.СостояниеЗаказаПокупателя.ЗаписьСостояния(ДокументОснование);

КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Код процедур и функций

#КонецОбласти

#Область Инициализация

#КонецОбласти

#КонецЕсли
