
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИсключительныйРежим = ТехническаяПоддержкаВызовСервера.ИсключительныйРежим();
	ОбязательноЗаписать = Ложь;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ТоварыВыбраны = Ложь;

		Если Параметры.Основание = Неопределено Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ можна створювати тільки на підставі!");
			Отказ = Истина;
			
		Иначе
			
			ТоварыВыбраны = (ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.ЭлектроннаяНакладная"));

		КонецЕсли;
		
		Элементы.МагазинВозврата.СписокВыбора.Очистить();

		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
			|	Склады.Ссылка,
			|	Склады.IDN
			|ИЗ
			|	Справочник.Склады КАК Склады
			|ГДЕ
			|	Склады.IDN = ""ОДЕССА""";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			Элементы.МагазинВозврата.СписокВыбора.Добавить(Выборка.Ссылка, Выборка.IDN);
			Объект.МагазинВозврата = Элементы.МагазинВозврата.СписокВыбора[0].Значение;
			
		КонецЕсли;

	Иначе
		
		ТоварыВыбраны = Истина;

	КонецЕсли;
	
	КомментарийКВозврату.Параметры.УстановитьЗначениеПараметра("Документ", Объект.Ссылка);
	
	ЗаполнитьСписокМагазиновДляВозврата();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтображениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ОтменаЭН"
			И Не Параметр.Ссылка.Пустая()
			И Объект.ЭлектроннаяНакладная = Параметр.Ссылка Тогда
		
		Объект.ЭлектроннаяНакладная = ПредопределенноеЗначение("Документ.ЭлектроннаяНакладная.ПустаяСсылка");
		ОбязательноЗаписать = Истина;
		Модифицированность = Истина;
		ОтображениеЭлементов();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбязательноЗаписать Тогда

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Потрібно обов''язково записати документ!!!';uk = 'Потрібно обов''язково записати документ!!!'")
			,
			,
			,
			,Отказ);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбязательноЗаписать = Ложь;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница.Имя = "ГруппаТовары" И Не ТоварыВыбраны Тогда
		
		ОткрытиеФормыПодбора();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТипВозвратаПриИзменении(Элемент)
	
	Если Объект.ТипВозврата = ПредопределенноеЗначение("Перечисление.ТипыВозвратаИнтернетЗаказа.ВозвратМагазин")
			И ТипЗнч(Объект.МагазинВозврата) = Тип("СправочникСсылка.Склады") Тогда
			
		Объект.МагазинВозврата = ПредопределенноеЗначение("Справочник.Магазины.ПустаяСсылка");

	КонецЕсли;
	
	ЗаполнитьСписокМагазиновДляВозврата();
	ОтображениеЭлементов();
	
КонецПроцедуры


&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	ОтображениеЭлементов();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыВозвращенноеКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ЗаказыПокупателейКлиентСервер.ПодсчетСтрокиВозврата(Объект.ДокументОснование, ТекущаяСтрока);

	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьИзЗаказа(Команда)

	ОткрытиеФормыПодбора();

 КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлектроннуюНакладную(Команда)

	Если Модифицированность или Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Сначало запишите документ");
	
	Иначе
	
		ОповещениеПослеОтметкиЭлементов = Новый ОписаниеОповещения(
	     "ЗавершениеВводаНомерЭН", ЭтотОбъект
		 );	
	
		ПараметрыДляФормы = Новый Структура("ВозвратныйДокумент", Объект.Ссылка);
		ОткрытьФорму("Документ.ВозвратИнтернетЗаказа.Форма.ФормаСозданияЭН",
				ПараметрыДляФормы,
				ЭтотОбъект,
				,
				,
				,
				ОповещениеПослеОтметкиЭлементов,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварВМагазине(Команда)
	
	Если ПроверитьЗаполнение() Тогда
	
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопрос",
			ЭтотОбъект);
		
		ПоказатьВопрос(Оповещение, "Клієнт приніс товар у магазин? Після позитивної відповіді виправити не можна і дані будуть відправлені до NAV.",
			РежимДиалогаВопрос.ДаНет,
			0,
			КодВозвратаДиалога.Нет,
			"Товар в магазине");
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОтображениеЭлементов()
	
	НовыйСтатус = (Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыВозвратаЗаказаПокупателя.Новый"));
	ВидимостьЭН = Объект.ЭлектроннаяНакладная.Пустая();
	ТипВозвратаМагазин = (Объект.ТипВозврата = ПредопределенноеЗначение("Перечисление.ТипыВозвратаИнтернетЗаказа.ВозвратМагазин"));
	
	Элементы.ГруппаЭлектроннойНакладной.Видимость = Не ТипВозвратаМагазин;
	Элементы.ГруппаТоварВМагазине.Видимость = ТипВозвратаМагазин;
	Элементы.ТоварВМагазине.Доступность = НовыйСтатус;
	//Элементы.ГруппаМагазина.Видимость = ТипВозвратаМагазин;
	
	Элементы.ЭлектроннаяНакладная.Видимость = Не ВидимостьЭН;
	Элементы.СоздатьЭлектроннуюНакладную.Видимость = ВидимостьЭН;
	Если Не ИсключительныйРежим Тогда
		
		Элементы.Статус.ТолькоПросмотр = Истина;
		Элементы.ЭлектроннаяНакладная.ТолькоПросмотр = Истина;
		Элементы.МагазинВозврата.ТолькоПросмотр = Не НовыйСтатус; 
		Элементы.ТипВозврата.ТолькоПросмотр = Не НовыйСтатус; 
		Элементы.Товары.ТолькоПросмотр = Не НовыйСтатус;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ЗаполнитьИзЗаказа()
	
	Результат = Новый Массив;

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаВозврата.КлючСвязиЗапросаДоступности КАК КлючСвязиЗапросаДоступности,
		|	ТаблицаВозврата.КоличествоУпаковок КАК КоличествоУпаковок,
		|	&ЗаказПокупателя КАК ЗаказПокупателя
		|ПОМЕСТИТЬ ТаблицаВозврата
		|ИЗ
		|	&ТаблицаВозврата КАК ТаблицаВозврата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ЕСТЬNULL(ТаблицаВозврата.КоличествоУпаковок, 0) КАК КоличествоУпаковок,
		|	ТаблицаТоваров.КоличествоУпаковок КАК КоличествоВЗаказе,
		|	ТаблицаТоваров.Упаковка КАК Упаковка,
		|	ТаблицаТоваров.КлючСвязиЗапросаДоступности КАК КлючСвязиЗапросаДоступности,
		|	ВозвратИнтернетЗаказаТовары.КоличествоУпаковок КАК КоличествоУпаковокВВозвратах,
		|	ТаблицаСостоянияСтрок.ЗапросДоступностиТоваров КАК ДокументЗаказа
		|ИЗ
		|	Документ.ЗаказПокупателя.Товары КАК ТаблицаТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВозврата КАК ТаблицаВозврата
		|		ПО ТаблицаВозврата.КлючСвязиЗапросаДоступности = ТаблицаТоваров.КлючСвязиЗапросаДоступности
		|		И ТаблицаВозврата.ЗаказПокупателя = ТаблицаТоваров.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратИнтернетЗаказа.Товары КАК ВозвратИнтернетЗаказаТовары
		|		ПО НЕ ВозвратИнтернетЗаказаТовары.Ссылка = &ТекущийВозврат
		|		И ТаблицаТоваров.КлючСвязиЗапросаДоступности = ВозвратИнтернетЗаказаТовары.КлючСвязиЗапросаДоступности
		|		И ВозвратИнтернетЗаказаТовары.Ссылка.ДокументОснование = ТаблицаТоваров.Ссылка
		|		И НЕ ВозвратИнтернетЗаказаТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВозвратаЗаказаПокупателя.Отменён)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСтрокЗаказаПокупателя КАК ТаблицаСостоянияСтрок
		|		ПО ТаблицаТоваров.Ссылка = ТаблицаСостоянияСтрок.ЗаказПокупателя
		|		И ТаблицаТоваров.КлючСвязиЗапросаДоступности = ТаблицаСостоянияСтрок.КлючСвязи
		|ГДЕ
		|	ТаблицаТоваров.Ссылка = &ЗаказПокупателя";
	
	Запрос.УстановитьПараметр("ЗаказПокупателя", Объект.ДокументОснование);
	Запрос.УстановитьПараметр("ТаблицаВозврата", Объект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("ТекущийВозврат", Объект.Ссылка);

	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТЗ Из РезультатЗапроса Цикл
		
		Результат.Добавить(ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТЗ));

	КонецЦикла;

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ОткрытиеФормыПодбора()

	ОповещениеПослеОтметкиЭлементов = Новый ОписаниеОповещения(
	     "ПослеОтметкиЭлементов", ЭтотОбъект
	 );	
 
	ПараметрыДляФормы = Новый Структура("ПараметрыФормы", ЗаполнитьИзЗаказа());
	ОткрытьФорму("Документ.ВозвратИнтернетЗаказа.Форма.ФормаВыбораИзЗаказа",
			ПараметрыДляФормы,
			ЭтотОбъект,
			,
			,
			,
			ОповещениеПослеОтметкиЭлементов,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ПослеОтметкиЭлементов(Элементы, Параметры) Экспорт
 
    Если ТипЗнч(Элементы) = Тип("Массив") Тогда
    	Объект.Товары.Очистить();
        Для Каждого СтрокаВозврата Из Элементы Цикл
        	
        	Если Не СтрокаВозврата.КоличествоУпаковок = 0 Тогда
				
				ТоварыВыбраны = Истина;
				НоваяСтрока = Объект.Товары.Добавить();
        	
        		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВозврата);
        		НоваяСтрока.ВозвращенноеКоличествоУпаковок = НоваяСтрока.КоличествоУпаковок; 
        		
        		ЗаказыПокупателейКлиентСервер.ПодсчетСтрокиВозврата(Объект.ДокументОснование, НоваяСтрока);
        		       		
        	КонецЕсли;

        КонецЦикла;
    КонецЕсли;
    
    Объект.СуммаДокумента = Объект.Товары.Итог("Сумма");
    Объект.СуммаБонусныхБалловСписано = Объект.Товары.Итог("СуммаБонусныхБалловСписано");
 
КонецПроцедуры

&НаКлиенте 
Процедура ЗавершениеВводаНомерЭН(Результат, Параметры) Экспорт 
	
	Если ТипЗнч(Результат) = Тип("ДокументСсылка.ЭлектроннаяНакладная") И Не Результат.Пустая() Тогда
		
		Объект.ЭлектроннаяНакладная = Результат;
		ОбязательноЗаписать = Истина;
		Модифицированность = Истина;
		ОтображениеЭлементов();
	
	КонецЕсли;
	
	ОтображениеЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокМагазиновДляВозврата()
	
	Элементы.МагазинВозврата.СписокВыбора.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Магазины.Ссылка КАК Ссылка,
		|	Склады.IDN КАК IDN
		|ПОМЕСТИТЬ ТаблицаСкладов
		|ИЗ
		|	Справочник.Магазины КАК Магазины
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|		ПО Магазины.СкладПродажи = Склады.Ссылка
		|ГДЕ
		|	Магазины.ВведенВЭксплуатацию
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Склады.Ссылка,
		|	Склады.IDN
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	Склады.IDN = ""ОДЕССА""
		|	И &НуженЦентральныйСклад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаСкладов.Ссылка,
		|	ТаблицаСкладов.IDN КАК IDN
		|ИЗ
		|	ТаблицаСкладов КАК ТаблицаСкладов
		|
		|УПОРЯДОЧИТЬ ПО
		|	IDN";
	
	Запрос.УстановитьПараметр("НуженЦентральныйСклад",
		Не (Объект.ТипВозврата = Перечисления.ТипыВозвратаИнтернетЗаказа.ВозвратМагазин));
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Элементы.МагазинВозврата.СписокВыбора.Добавить(Выборка.Ссылка, Выборка.IDN);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте

Процедура ПослеОтветаНаВопрос(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыВозвратаЗаказаПокупателя.Чек");
		ОбязательноЗаписать = Истина;
		Модифицированность = Истина;
		ОтображениеЭлементов();
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти