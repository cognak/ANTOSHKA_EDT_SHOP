#Область ОбработчикиОсновныхСобытийОбъекта

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ЗаполнениеОбъектовСобытия.ОбщиеДействияПередЗаписью(ЭтотОбъект, Отказ);

	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	Если НЕ ТехническаяПоддержкаВызовСервера.ИсключительныйРежим() Тогда

		ТекущийРегион = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Магазин, "ОбособленноеПодразделениеОрганизации");

		Если НЕ УправлениеПользователями.ДоступноЗначениеПраваДляТекущегоПользователя(
				"РазрешитьУстановкуЛимитовРучныхСкидокПоРегиону", ТекущийРегион) Тогда

			Отказ = Истина;
			Сообщить("Отказано! Нет разрешения на работу с филиалом «" + ТекущийРегион + "».");

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;


КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)


КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	ИнициализироватьДокумент();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)


КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);

	Документы.УстановкаЛимитовРучныхСкидокМагазин.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	ЛимитыРС_Сервер.ОтразитьЛимитыРучныхСкидокРегионы(ДополнительныеСвойства, Движения, Отказ);
	ЛимитыРС_Сервер.ОтразитьЛимитыРучныхСкидокМагазины(ДополнительныеСвойства, Движения, Отказ);
	
	СформироватьСписокРегистровДляКонтроля();

	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	СформироватьСписокРегистровДляКонтроля();

	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);

КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент()

	Дата = ТекущаяДатаСеанса();
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	СписокОбъектов = Новый Массив;
	СписокОбъектов.Добавить(Движения.ЛимитыРучныхСкидокРегионы);
	СписокОбъектов.Добавить(Движения.ЛимитыРучныхСкидокМагазины);

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", СписокОбъектов);

КонецПроцедуры

#КонецОбласти







