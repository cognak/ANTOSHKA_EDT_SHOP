#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура УстановкаПараметровСеанса(ИменаПараметровСеанса)
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыСервер.УстановкаПараметровСеанса(ИменаПараметровСеанса);
	// Конец СтандартныеПодсистемы

//	LNK 31.08.2016 14:09:47 - первое обращение к обработчику
	Если ИменаПараметровСеанса = Неопределено Тогда

		ВерсияПроверялась = Ложь;

		Если КонтекстПриложения.ЭтоКлиентскоеСоединение() И НЕ ПользователиСлужебный.ПользовательНеТребуетАвторизации() Тогда

		//	Работаем только в вызове при начале сеанса.
			ОбменДаннымиСервер.БезопасностьУправленияОбменаДанными(ВерсияПроверялась);

			Если РозничныеПродажиСерверПовтИсп.ИспользоватьВнешнююОбработкуРМК(ОбменДаннымиПовтИсп.ПолучитьДанныеУзла().Магазин) Тогда

				УправлениеДоступом.УстановитьЗащитуОтОпасныхДействий(Ложь);

				ОписаниеЗащитыОтОпасныхДействий = Новый ОписаниеЗащитыОтОпасныхДействий;
				ОписаниеЗащитыОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях = Ложь;
				ПараметрыСеанса.ИмяВнешнегоРМК = ДополнительныеОтчетыИОбработки.ПодключитьВнешнююОбработку(Справочники.ДополнительныеОтчетыИОбработки.РМК, ОписаниеЗащитыОтОпасныхДействий);

			Иначе

				УправлениеДоступом.УстановитьЗащитуОтОпасныхДействий(Истина);

			КонецЕсли;
			//	Бакан 26.06.2024 Доавил установку ДоступныВсеОрганизации в параметры сеанса 
			ПараметрыСеанса.ДоступныВсеОрганизации = ПроверитьДоступностьВсехОрганизаций(ПользователиКлиентСервер.ТекущийПользователь());
		КонецЕсли;

		Если КонтекстПриложения.ЭтоФоновоеЗадание() Тогда

			КонтекстПриложения.РегистрацияВыполненияФоновогоЗадания();

		КонецЕсли;

		Если НЕ ВерсияПроверялась И НЕ ПользователиСлужебный.ПользовательНеТребуетАвторизации() Тогда

			Справочники.СтруктураУзлов.ПроверитьВерсиюУзла();

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьДоступностьВсехОрганизаций(Пользователь)

	УстановитьПривилегированныйРежим(Истина);	//	LNK 27.06.2024 05:19:21

	ДоступныВсеОрганизации = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗначенияДополнительныхПравПользователя.Значение КАК ДоступныВсеОрганизации
		|ИЗ
		|	РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначенияДополнительныхПравПользователя
		|ГДЕ
		|	ЗначенияДополнительныхПравПользователя.Пользователь = &Пользователь
		|	И ЗначенияДополнительныхПравПользователя.Право = &Право";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Право", ПланыВидовХарактеристик.ПраваПользователей.ДоступныВсеОрганизации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда

		ДоступныВсеОрганизации = Выборка.ДоступныВсеОрганизации;

	КонецЕсли;
	
	Возврат	ДоступныВсеОрганизации;
	
КонецФункции


#КонецЕсли


