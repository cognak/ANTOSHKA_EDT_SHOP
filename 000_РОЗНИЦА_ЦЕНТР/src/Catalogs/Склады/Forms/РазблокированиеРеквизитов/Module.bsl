
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокРеквизитов();
	
	УстановитьВсеОтметкиСервер(Истина);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)

	Результат = Новый Массив;
	Если РазрешитьРедактированиеМагазин Тогда
		Результат.Добавить("Магазин");
	КонецЕсли;

	Если РазрешитьРедактированиеОрганизация Тогда
		Результат.Добавить("Организация");
	КонецЕсли;
	
	Если РазрешитьРедактированиеТипСклада Тогда
		Результат.Добавить("ТипСклада");
	КонецЕсли;
		
	Закрыть(Результат);

КонецПроцедуры


&НаКлиенте
Процедура УстановитьВсеОтметки(Команда)
	
	УстановитьВсеОтметкиКлиент(Истина)
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеОтметки(Команда)
	
	УстановитьВсеОтметкиКлиент(Ложь)
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьСписокРеквизитов()
	
	Для каждого ЭлементФормы Из Элементы Цикл
		Если ТипЗнч(ЭлементФормы) = Тип("ПолеФормы") 
			И Найти(ЭлементФормы.Имя, "РазрешитьРедактирование") = 1 Тогда
			СписокРеквизитов.Добавить(ЭлементФормы.Имя);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВсеОтметкиСервер(ЗначениеОтметки)
	
	Для каждого ЭлементСписка из СписокРеквизитов Цикл
		
		ЭтотОбъект[ЭлементСписка.Значение] = ЗначениеОтметки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсеОтметкиКлиент(ЗначениеОтметки)
	
	Для каждого ЭлементСписка из СписокРеквизитов Цикл
		
		ЭтотОбъект[ЭлементСписка.Значение] = ЗначениеОтметки;
		
	КонецЦикла;
	
КонецПроцедуры
