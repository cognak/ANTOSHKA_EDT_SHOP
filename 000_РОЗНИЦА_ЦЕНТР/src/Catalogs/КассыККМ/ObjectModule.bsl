
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка ИЛИ ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(ДополнительныеСвойства, "СлужебныйРежимЗаписи", Ложь) = Истина Тогда

		Возврат;

	КонецЕсли;

	Если НЕ ЭтоГруппа Тогда

		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Магазин, "Организация");

		Если ЗначениеЗаполнено(Организация) И НЕ Организация = Владелец Тогда

			Владелец = Организация;
			Сообщить("Встановлено нового власника для магазину - «" + Владелец + "»");

		КонецЕсли;

		ПроверитьПодключаемоеОборудование(Отказ);

	КонецЕсли;
	
КонецПроцедуры

//	LNK 20.04.2017 10:40:25
Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка ИЛИ ОбщегоНазначенияКлиентСервер.ЗначениеКлючаСтруктуры(ДополнительныеСвойства, "СлужебныйРежимЗаписи", Ложь) = Истина Тогда

		Возврат;

	КонецЕсли;


КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда

		ДанныеЗаполнения.Свойство("Владелец", Владелец);
		ДанныеЗаполнения.Свойство("Магазин", Магазин);

		Если НЕ ЗначениеЗаполнено(Владелец) И ЗначениеЗаполнено(Магазин) Тогда

			Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Магазин, "Организация");

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//	LNK 22.02.2022 10:34:45
Процедура ПроверитьПодключаемоеОборудование(Отказ)

	Если ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор Тогда

		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КассыККМ.Ссылка.Наименование КАК Наименование
		|ИЗ
		|	Справочник.КассыККМ КАК КассыККМ
		|ГДЕ
		|	КассыККМ.Магазин = &Магазин
		|	И КассыККМ.ТипКассы = ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.ФискальныйРегистратор)
		|	И КассыККМ.Владелец = &Владелец
		|	И КассыККМ.РабочееМесто = &РабочееМесто
		|	И НЕ КассыККМ.Ссылка = &Ссылка
		|	И НЕ КассыККМ.РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМеста.ПустаяСсылка)"
		);
		Запрос.УстановитьПараметр("Магазин", Магазин);
		Запрос.УстановитьПараметр("Владелец", Владелец);
		Запрос.УстановитьПараметр("РабочееМесто", РабочееМесто);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		
		Если Выборка.Следующий() Тогда
		
			Отказ = Истина;
			СтрокаСообщения = "Каса з такими ж параметрами вже існує
										 |«" + Выборка.Наименование + "» :
										 |Магазин      - «" + Магазин + "»
										 |Організація  - «" + Владелец + "»
										 |Робоче місце - «" + РабочееМесто + "»"; 
			Сообщить(СтрокаСообщения);
		
		КонецЕсли;

		Если НЕ (Отказ ИЛИ ПодключаемоеОборудование.Пустая()) Тогда

			СписокОборудования = Справочники.ПодключаемоеОборудование.ПолучитьСписокОборудования("ФискальныйРегистратор"
				,
				, РабочееМесто
				, 2
				, ?(ПРРО, 1, 2)
			);
			#Если _ Тогда
			СписокОборудования = Новый Соответствие;
			#КонецЕсли

			Если СписокОборудования.Получить(ПодключаемоеОборудование) = Неопределено Тогда

				Отказ = Истина;
				Сообщить("Відмовлено. Підключене обладнання не відповідає флагу «ПРРО»!");

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры














