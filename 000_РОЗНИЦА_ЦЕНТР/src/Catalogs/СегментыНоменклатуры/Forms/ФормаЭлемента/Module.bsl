///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	// Обработчик подсистемы "Дополнительные отчеты и обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	Элементы.ДатаОчистки.ТолькоПросмотр =
		Объект.СпособФормирования <> Перечисления.СпособыФормированияСегментов.ФормироватьВручную;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СКД = Объект.Ссылка.СхемаКомпоновкиДанных.Получить();
	Иначе
		СКД = Справочники.СегментыНоменклатуры.ПолучитьМакет("ОсновнаяСхема");
	КонецЕсли;
	АдресСКД = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор);

	Элементы.Сформировать.Доступность = НЕ Объект.СпособФормирования =
										Перечисления.СпособыФормированияСегментов.ФормироватьДинамически;

	УстановитьДоступностьЭлементов();

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	Элементы.Сформировать.Доступность = НЕ Объект.СпособФормирования =
										Перечисления.СпособыФормированияСегментов.ФормироватьДинамически;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.СхемаКомпоновкиДанных =
		Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресСКД));
	
КонецПроцедуры

&НаСервере	//	LNK 25.06.2022 06:58:13
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если НастройкиИзменены Тогда

		ФиксироватьСобытиеИзмененияСКД(ТекущийОбъект);

	КонецЕсли;

	НастройкиИзменены = Ложь;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);

	УстановитьДоступностьЭлементов();

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ОткрытьФормуРедактированияМногострочногоТекста(Элемент.ТекстРедактирования , Объект.Описание, Модифицированность, "Описание сегмента"); 
	
КонецПроцедуры

&НаКлиенте
Процедура СпособФормированияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		Если Вопрос(НСтр("ru = 'Текущий состав сегмента будет очищен. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена) = КодВозвратаДиалога.ОК Тогда
			СегментыСервер.Очистить(Объект.Ссылка);
		Иначе	
			СтандартнаяОбработка = Ложь;
		КонецЕсли; 	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособФормированияПриИзменении(Элемент)

	Если Объект.СпособФормирования <>
		ПредопределенноеЗначение("Перечисление.СпособыФормированияСегментов.ФормироватьВручную") Тогда
		Элементы.ДатаОчистки.ТолькоПросмотр = Истина;
		Объект.ДатаОчистки = '00010101';
	КонецЕсли;

	Элементы.Сформировать.Доступность = НЕ Объект.СпособФормирования =
										ПредопределенноеЗначение("Перечисление.СпособыФормированияСегментов.ФормироватьДинамически");
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОткрытьНастройкиСхемыКомпоновкиДанных(Команда)

	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр("ru = 'Настройки сегмента ""%ИмяСегмента%""'");
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = СтрЗаменить(ЗаголовокФормыНастройкиСхемыКомпоновкиДанных, "%ИмяСегмента%", Объект.Наименование);
	
	Результат = ОткрытьФормуМодально("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных",
									 Новый Структура("АдресСхемыКомпоновкиДанных,
													 |ИсточникШаблонов,
													 |Заголовок,
													 |УникальныйИдентификатор,
													 |ИмяШаблонаСКД,
													 |ВозвращатьИмяТекущегоШаблонаСКД",
													 АдресСКД,
													 Объект.Ссылка,
													 ЗаголовокФормыНастройкиСхемыКомпоновкиДанных,
													 УникальныйИдентификатор,
													 Объект.ИмяШаблонаСКД,
													 Истина));
	Если НЕ Результат = Неопределено Тогда

		Объект.ИмяШаблонаСКД = Результат.ИмяТекущегоШаблонаСКД;
		НастройкиИзменены = Истина;

	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста	//	LNK 25.06.2022 06:51:02
Процедура ФиксироватьСобытиеИзмененияСКД(Объект)

	ЖурналСобытий.Регистрация("НОМ.СЕГМЕНТА." + ОбщегоНазначенияКлиентСервер.РеквизитПеречисления(Объект.СпособФормирования,, "undef")
		, УровеньЖурналаРегистрации.Предупреждение
		, Метаданные.Справочники.СегментыНоменклатуры
		, Объект.Ссылка
		,
		, "Интерактивное изменение настроек сегмента (СКД)"
		, СокрЛП(Объект.Код) + " : " + СокрЛП(Объект.Наименование)
		, Ложь
		, Ложь
	);

КонецПроцедуры

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить("СпособФормирования");
	
	Если ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект) Тогда
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтотОбъект, МассивРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

//Процедура формирует сегмент номенклатуры
//
&НаКлиенте
Процедура СформироватьСегмент(Команда)

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Сначала необходимо записать сегмент.'"));
		Возврат;
	КонецЕсли;

	Если Объект.СпособФормирования =
			ПредопределенноеЗначение("Перечисление.СпособыФормированияСегментов.ФормироватьДинамически") Тогда
		ПоказатьПредупреждение(, НСтр("ru='Формирование доступно только для нединамических сегментов.'"));
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Если Вопрос(
				НСтр("ru='Перед формированием необходимо записать сегмент. Записать?'"),
				РежимДиалогаВопрос.ДаНет
			) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		Иначе
			Записать();
		КонецЕсли;
	КонецЕсли;

	СформироватьСегментНаСервере();	//	LNK 21.05.2019 13:31:48

	ПоказатьОповещениеПользователя(
		НСтр("ru='Формирование сегмента номенклатуры'"),,
		НСтр("ru='Сегмент сформирован.'")
	);

КонецПроцедуры

&НаСервере	//	LNK 21.05.2019 13:31:52
Процедура СформироватьСегментНаСервере()

	СегментыСервер.СформироватьНоменклатуруСегмента(Объект.Ссылка, Объект.СпособФормирования);

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере	//	LNK 24.09.2018 14:52:56
Процедура УстановитьДоступностьЭлементов()

	Если Объект.Предопределенный И НЕ ТехническаяПоддержкаВызовСервера.ИсключительныйРежим() Тогда
		
		ТолькоПросмотр = Истина;

	ИначеЕсли НЕ РольДоступна(Метаданные.Роли.АдминистраторСистемы)
		И НЕ Объект.Ссылка.Пустая()
		И НЕ УправлениеПользователями.СегментНоменклатурыДоступен(Объект.Ссылка)	Тогда
		
		ТолькоПросмотр = Истина;

	КонецЕсли;

	Если ТолькоПросмотр Тогда

		Элементы.Сформировать.Доступность = Ложь;
		Элементы.Настройки.Доступность    = Ложь;

	КонецЕсли;

КонецПроцедуры





