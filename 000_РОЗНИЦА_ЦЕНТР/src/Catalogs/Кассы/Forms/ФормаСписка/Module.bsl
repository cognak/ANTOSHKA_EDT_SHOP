
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;

	УстановитьМагазинУзла(Магазин, Параметры);	//	LNK 26.11.2022 06:31:33

	ЗаполнитьОрганизациюВФормеСписка();
	УстановитьОтборДинамическихСписков();
	ОбновитьЭлементыФормы();

	// Обработчик подсистемы "Дополнительные отчеты и обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	УстановитьМагазинУзла(Магазин, Параметры);	//	LNK 26.11.2022 06:31:33
	Настройки.Вставить("Магазин", Магазин);		//	LNK 26.11.2022 06:45:09
	
	Организация = ?(ОбменДаннымиПовтИсп.ЭтоГлавныйУзел(), Настройки.Получить("Организация"), ПараметрыСеанса.ТекущаяОрганизация);
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Владелец", Организация, ЗначениеЗаполнено(Организация));
	УстановитьОтборДинамическихСписков();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	
КонецПроцедуры

//	LNK 26.04.2017 08:26:42
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ПользовательИзменилТекущуюОрганизацию" Тогда

		Если НЕ Организация = Параметр.Организация Тогда

			Организация = Параметр.Организация;
			Магазин     = Параметр.Магазин;

			ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Владелец", Организация, ЗначениеЗаполнено(Организация));
			ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Магазин", Магазин, ЗначениеЗаполнено(Магазин));

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОтборМагазинПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Магазин", Магазин, ЗначениеЗаполнено(Магазин));
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста	//	LNK 26.11.2022 06:31:38
Процедура УстановитьМагазинУзла(Магазин, Параметры)

	Если ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		Если Параметры.Свойство("Отбор")
			И Параметры.Отбор.Свойство("Магазин") Тогда

			Магазин = Параметры.Отбор.Магазин;

		КонецЕсли;

	Иначе	Магазин = ПараметрыСеанса.ТекущийМагазин;

	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция МассивДинамическихСписковДляУстановкиОтбора()

	МассивСписков = Новый Массив;
	МассивСписков.Добавить(Список);
	
	Возврат МассивСписков;

КонецФункции

&НаСервере
Процедура УстановитьОтборДинамическихСписков()
	
	Для Каждого ДинамическийСписок Из МассивДинамическихСписковДляУстановкиОтбора() Цикл
		
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ДинамическийСписок, "Магазин", Магазин , ЗначениеЗаполнено(Магазин), ВидСравненияКомпоновкиДанных.Равно);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОрганизациюВФормеСписка()
	
	Если ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		Если Параметры.Свойство("Отбор")
		   И Параметры.Отбор.Свойство("Владелец") Тогда

			Организация = Параметры.Отбор.Владелец;

		КонецЕсли;

	Иначе	Организация = ПараметрыСеанса.ТекущаяОрганизация;

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыФормы()
	Перем МассивВсехРеквизитов, МассивРеквизитов, ДополнительныеСкрываемыеЭлементы;
	
	Параметры.Свойство("ДополнительныеСкрываемыеЭлементы", ДополнительныеСкрываемыеЭлементы);
	
	Справочники.Кассы.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		Список,
		МассивВсехРеквизитов,
		МассивРеквизитов,
		ДополнительныеСкрываемыеЭлементы);
		
	ДенежныеСредстваСервер.УстановитьВидимостьЭлементовПоМассиву(
		Элементы,
		МассивВсехРеквизитов,
		МассивРеквизитов
	);
	
КонецПроцедуры




