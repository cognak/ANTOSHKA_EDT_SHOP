
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Карта", Карта);
	
	Если НЕ ЗначениеЗаполнено(Карта) Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Карта.ВладелецКарты)
	   И Не ТипЗнч(Карта.ВладелецКарты) = Тип("СправочникСсылка.ФизическиеЛица")Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВладелецКарты = Карта.ВладелецКарты;
	ЗаголовокКарты = "Карта - "+Карта.Наименование;
	
	ЗаполнитьНеАнкетныеТипыДанных();
	
	ЗаполнитьТаблицуВопросов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВопрос();
	
	УправлениеИнтерфейсом();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ЗаписатьВладельцаКарты(ВыбранноеЗначение);
	
	ОбновитьВопрос();
	
	УправлениеИнтерфейсом();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ФлагОтветаПриИзменении(Элемент)
	
	СтрокаТаблицыВопросов = ТаблицаВопросовИОтветов[НомерВопроса - 1];
	СтрокаТаблицыВопросов.Ответ = ФлагОтвета;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветПриИзменении(Элемент)
	
	СтрокаТаблицыВопросов = ТаблицаВопросовИОтветов[НомерВопроса - 1];
	СтрокаТаблицыВопросов.Ответ = Ответ;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЧНОЙ ЧАСТИ "ТаблицаВыборОтвета"

&НаКлиенте
Процедура ТаблицаВыборОтветаПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаВыборОтвета.ТекущиеДанные;
	Если ТекущиеДанные.Пометка Тогда
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Вопрос", ТекущиеДанные.Вопрос);
		
		СтрокиТаблицыВыбора = ТаблицаВыборОтвета.НайтиСтроки(СтруктураПоиска);
		
		Для каждого СтрокаТаблицы Из СтрокиТаблицыВыбора Цикл
			Если Не СтрокаТаблицы.Ответ = ТекущиеДанные.Ответ Тогда
				СтрокаТаблицы.Пометка = Ложь;
			КонецЕсли;
		КонецЦикла;
		СтрокаТаблицыВопросов = ТаблицаВопросовИОтветов[НомерВопроса - 1];
		СтрокаТаблицыВопросов.Ответ = ТекущиеДанные.Ответ;
	Иначе
		СтрокаТаблицыВопросов = ТаблицаВопросовИОтветов[НомерВопроса - 1];
		СтрокаТаблицыВопросов.Ответ = ПредопределенноеЗначение("Справочник.ЗначенияСвойствОбъектов.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыбратьВладельца(Команда)
	
	Если ЗначениеЗаполнено(ВладелецКарты)  Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ВладелецКарты);
		
		ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта", ПараметрыФормы);
		
	Иначе
		
		ОткрытьФормуМодально("Справочник.ФизическиеЛица.ФормаВыбора", , ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарту(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Карта);
	
	ОткрытьФорму("Справочник.ИнформационныеКарты.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийРаздел(Команда)
	
	Если НомерВопроса > 1 Тогда
		НомерВопроса = НомерВопроса - 1;
		
		ОбновитьВопрос();
		
		УправлениеИнтерфейсом();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийРаздел(Команда)
	
	Если НомерВопроса < ТаблицаВопросовИОтветов.Количество() Тогда
		НомерВопроса = НомерВопроса + 1;
		
		ОбновитьВопрос();
		
		УправлениеИнтерфейсом();
	Иначе
		
		ЗаписатьДанныеОпроса();
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаписатьВладельцаКарты(ВыбранноеЗначение)
	
	КартаОбъект = Карта.ПолучитьОбъект();
	ВладелецКарты = ВыбранноеЗначение;
	КартаОбъект.ВладелецКарты = ВладелецКарты;
	КартаОбъект.Записать();
	
	ЗаполнитьТаблицуВопросов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеОпроса()
	
	//Дополнительные реквизиты
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ЭтоДополнительноеСведение", Ложь);
	
	СтрокиТаблицы = ТаблицаВопросовИОтветов.НайтиСтроки(СтруктураПоиска);
	
	ВладелецОбъект = ВладелецКарты.ПолучитьОбъект();
	
	Владелец_ДополнительныеРеквизиты = ВладелецОбъект.ДополнительныеРеквизиты;
	
	Для каждого СтрокаТаблицыВопросов  Из СтрокиТаблицы Цикл
		
		СтрокаДополнительныхРеквизитовВладельца = Владелец_ДополнительныеРеквизиты.Найти(СтрокаТаблицыВопросов.Вопрос , "Свойство");
		
		Если СтрокаДополнительныхРеквизитовВладельца = Неопределено Тогда
			
			СтрокаДополнительныхРеквизитовВладельца = Владелец_ДополнительныеРеквизиты.Добавить();
			СтрокаДополнительныхРеквизитовВладельца.Свойство = СтрокаТаблицыВопросов.Вопрос;
			
		КонецЕсли;
		
		СтрокаДополнительныхРеквизитовВладельца.Значение = СтрокаТаблицыВопросов.Ответ;
		
		Если ОбщегоНазначенияРТ.ИспользоватьНеограниченнуюСтроку(СтрокаТаблицыВопросов.ТипЗначения, СтрокаТаблицыВопросов.МногострочноеПолеВвода) Тогда
			СтрокаДополнительныхРеквизитовВладельца.ТекстоваяСтрока = СтрокаДополнительныхРеквизитовВладельца.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	ВладелецОбъект.Записать();
	
	//Дополнительные сведения
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ЭтоДополнительноеСведение", Истина);
	
	СтрокиТаблицы = ТаблицаВопросовИОтветов.НайтиСтроки(СтруктураПоиска);
	
	РегистрСведений = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	
	РегистрСведений.Отбор.Объект.Установить(ВладелецКарты);
	РегистрСведений.Прочитать();
	
	Владелец_ДополнительныеСведения = РегистрСведений.Выгрузить();
	
	Для каждого СтрокаТаблицыВопросов  Из СтрокиТаблицы Цикл
		
		СтрокаДополнительныхСведений = Владелец_ДополнительныеСведения.Найти(СтрокаТаблицыВопросов.Вопрос , "Свойство");
		
		Если СтрокаДополнительныхСведений = Неопределено Тогда
			
			СтрокаДополнительныхСведений = Владелец_ДополнительныеСведения.Добавить();
			
		КонецЕсли;
		
		СтрокаДополнительныхСведений.Свойство = СтрокаТаблицыВопросов.Вопрос;
		СтрокаДополнительныхСведений.Значение = СтрокаТаблицыВопросов.Ответ;
		СтрокаДополнительныхСведений.Объект   = ВладелецКарты;
		
	КонецЦикла;
	
	РегистрСведений.Загрузить(Владелец_ДополнительныеСведения);
	РегистрСведений.Записать(Истина);
	
	КартаОбъект = Карта.ПолучитьОбъект();
	КартаОбъект.ДатаСледующегоОпроса = '00010101';
	КартаОбъект.ДатаПоследнегоОпроса = ТекущаяДатаСеанса();
	КартаОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНеАнкетныеТипыДанных()
	
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.Магазины"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.Номенклатура"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.НоменклатурныеГруппы"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.Организации"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.Пользователи"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.СегментыНоменклатуры"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.Склады"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.СтраныМира"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.Файлы"));
	НеАнкетныеТипыВопросов.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуВопросов()
	
	НомерВопроса = 0;
	
	ТаблицаВопросовИОтветов.Очистить();
	
	Если НЕ ЗначениеЗаполнено(Карта.ВладелецКарты) Тогда
		Возврат
	КонецЕсли;
	
	ПолучитьТаблицуВопросовИОтветов();
	
	Если ТаблицаВопросовИОтветов.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	НомерСтроки = 0;
	
	КолвоЭлементовКоллекции = ТаблицаВопросовИОтветов.Количество();
	
	Для ОбратныйИндекс = 1 По КолвоЭлементовКоллекции Цикл
		ЭлементКоллекции = ТаблицаВопросовИОтветов[КолвоЭлементовКоллекции - ОбратныйИндекс];
		
		Если ЭлементКоллекции.Вопрос.ТипЗначения.Типы().Количество() = 1 Тогда
			ТипОтвета = ЭлементКоллекции.Вопрос.ТипЗначения.Типы().Получить(0);
			Если Не НеАнкетныеТипыВопросов.НайтиПоЗначению(ТипОтвета) = Неопределено Тогда
				ТаблицаВопросовИОтветов.Удалить(ЭлементКоллекции);
				Продолжить;
			КонецЕсли;
			ЭлементКоллекции.ТипЗначения = ЭлементКоллекции.Вопрос.ТипЗначения;
			Если ТипОтвета = Тип("Булево") Тогда
				ЭлементКоллекции.ИмяСтраницыВопроса = "СтраницаФлага";
			ИначеЕсли ТипОтвета = Тип("СправочникСсылка.ЗначенияСвойствОбъектов") Тогда
				Если НЕ ЕстьОтветНаВопрос(ЭлементКоллекции.Вопрос) Тогда
					ТаблицаВопросовИОтветов.Удалить(ЭлементКоллекции);
					Продолжить;
				КонецЕсли;
				ЭлементКоллекции.ИмяСтраницыВопроса = "СтраницаТаблицаВыбора";
			Иначе
				ЭлементКоллекции.ИмяСтраницыВопроса = "СтраницаПолеВвода";
			КонецЕсли;
		Иначе
			ТаблицаВопросовИОтветов.Удалить(ЭлементКоллекции);
		КонецЕсли;
	КонецЦикла;
	
	НомерСтроки = 0;
	
	Для каждого СтрокаТаблицыВопросов Из ТаблицаВопросовИОтветов Цикл
		
		НомерСтроки = НомерСтроки + 1;
		СтрокаТаблицыВопросов.НомерВопроса = НомерСтроки;
		
	КонецЦикла;
	
	НомерВопроса = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВопрос()
	
	Если НомерВопроса = 0 ИЛИ ТаблицаВопросовИОтветов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицыВопросов = ТаблицаВопросовИОтветов[НомерВопроса - 1];
	ДополнениеКВопросу = " (№"+НомерВопроса + " из "+ТаблицаВопросовИОтветов.Количество()+")";
	ТекстВопроса = "" + СтрокаТаблицыВопросов.Вопрос + ДополнениеКВопросу;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы[СтрокаТаблицыВопросов.ИмяСтраницыВопроса];
	
	ЗаголовокВопроса = ТекстВопроса;
	
	Если СтрокаТаблицыВопросов.ИмяСтраницыВопроса = "СтраницаФлага" Тогда
		Элементы.ФлагОтвета.Заголовок = ТекстВопроса + "       ";
		ФлагОтвета = СтрокаТаблицыВопросов.Ответ;
	ИначеЕсли СтрокаТаблицыВопросов.ИмяСтраницыВопроса = "СтраницаТаблицаВыбора" Тогда
		Элементы.ТаблицаВыборОтвета.ОтборСтрок = Новый ФиксированнаяСтруктура("Вопрос", СтрокаТаблицыВопросов.Вопрос);
	Иначе
		
		Ответ = СтрокаТаблицыВопросов.Ответ;
		Элементы.Ответ.ОграничениеТипа = СтрокаТаблицыВопросов.ТипЗначения;
		Ответ = Элементы.Ответ.ОграничениеТипа.ПривестиЗначение(Ответ);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьТаблицуВопросовИОтветов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство КАК Вопрос,
	|	ВложенныйЗапрос.Значение КАК Ответ,
	|	ЛОЖЬ КАК ЭтоДополнительноеСведение,
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.МногострочноеПолеВвода КАК МногострочноеПолеВвода
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ФизическиеЛицаДополнительныеРеквизиты.Ссылка КАК Ссылка,
	|			ФизическиеЛицаДополнительныеРеквизиты.Свойство КАК Свойство,
	|			ФизическиеЛицаДополнительныеРеквизиты.Значение КАК Значение
	|		ИЗ
	|			Справочник.ФизическиеЛица.ДополнительныеРеквизиты КАК ФизическиеЛицаДополнительныеРеквизиты
	|		ГДЕ
	|			ФизическиеЛицаДополнительныеРеквизиты.Ссылка = &Владелец) КАК ВложенныйЗапрос
	|		ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство = ВложенныйЗапрос.Свойство
	|ГДЕ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_ФизическиеЛица)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Свойство,
	|	ВложенныйЗапрос.Значение,
	|	ИСТИНА,
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Свойство.МногострочноеПолеВвода
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДополнительныеСведения.Объект КАК Объект,
	|			ДополнительныеСведения.Свойство КАК Свойство,
	|			ДополнительныеСведения.Значение КАК Значение
	|		ИЗ
	|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ГДЕ
	|			ДополнительныеСведения.Объект = &Владелец) КАК ВложенныйЗапрос
	|		ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Свойство = ВложенныйЗапрос.Свойство
	|ГДЕ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Ссылка = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_ФизическиеЛица)";
	
	Запрос.УстановитьПараметр("Владелец", Карта.ВладелецКарты);
	
	Результат = Запрос.Выполнить();
	
	ТаблицаВопросовИОтветов.Загрузить(Результат.Выгрузить());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаВопросовИОтветов.Вопрос,
	|	ТаблицаВопросовИОтветов.Ответ
	|ПОМЕСТИТЬ ТаблицаВопросовИОтветов
	|ИЗ
	|	&ТаблицаВопросовИОтветов КАК ТаблицаВопросовИОтветов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Ссылка КАК Ответ,
	|	ЗначенияСвойствОбъектов.Владелец КАК Вопрос
	|ПОМЕСТИТЬ ТаблицаЗначенийСвойств
	|ИЗ
	|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Владелец В
	|			(ВЫБРАТЬ
	|				ТаблицаВопросовИОтветов.Вопрос
	|			ИЗ
	|				ТаблицаВопросовИОтветов КАК ТаблицаВопросовИОтветов)
	|	И (НЕ ЗначенияСвойствОбъектов.ЭтоГруппа)
	|	И (НЕ ЗначенияСвойствОбъектов.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗначенийСвойств.Ответ,
	|	ТаблицаЗначенийСвойств.Вопрос,
	|	ВЫБОР
	|		КОГДА ТаблицаВопросовИОтветов.Вопрос ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	ТаблицаЗначенийСвойств КАК ТаблицаЗначенийСвойств
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВопросовИОтветов КАК ТаблицаВопросовИОтветов
	|		ПО ТаблицаЗначенийСвойств.Ответ = ТаблицаВопросовИОтветов.Ответ
	|			И ТаблицаЗначенийСвойств.Вопрос = ТаблицаВопросовИОтветов.Вопрос";
	
	Запрос.УстановитьПараметр("ТаблицаВопросовИОтветов", ТаблицаВопросовИОтветов.Выгрузить());
	
	Результат = Запрос.Выполнить();
	
	ТаблицаВыборОтвета.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция ЕстьОтветНаВопрос(Вопрос)
	
	Возврат НЕ ТаблицаВыборОтвета.НайтиСтроки(Новый Структура("Вопрос", Вопрос)).Количество() = 0;
	
КонецФункции

&НаКлиенте
Процедура УправлениеИнтерфейсом()
	
	Если ЗначениеЗаполнено(ВладелецКарты) Тогда
		ЗаголовокВладелец = "" + ВладелецКарты;
		Элементы.ВыбратьВладельца.Заголовок = "Открыть владельца";
	Иначе
		ЗаголовокВладелец = "Владелец не задан";
		Элементы.ВыбратьВладельца.Заголовок = "Выбрать владельца";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВладелецКарты) ИЛИ ТаблицаВопросовИОтветов.Количество() = 0  Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПустая;
		Элементы.ГруппаКнопкиПодвал.Доступность = Ложь;
		Элементы.ДекорацияНадписьОНастройкеВопросов.Видимость = ЗначениеЗаполнено(ВладелецКарты) И ТаблицаВопросовИОтветов.Количество() = 0;
	Иначе
		Элементы.ГруппаКнопкиПодвал.Доступность     = Истина;
		Элементы.ПредыдущийРазделПодвал.Доступность = НомерВопроса > 1;
		Элементы.СледующийРазделПодвал.Заголовок    = ?(НомерВопроса = ТаблицаВопросовИОтветов.Количество(), "Записать", "Следующий >>");
	КонецЕсли;
	
КонецПроцедуры
