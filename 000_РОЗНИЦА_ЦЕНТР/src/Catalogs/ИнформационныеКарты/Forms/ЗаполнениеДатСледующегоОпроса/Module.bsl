//Хранит схему компоновки данных
&НаСервере
Перем СКД;

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СКД = Справочники.ИнформационныеКарты.ПолучитьМакет("ОсновнаяСхемаЗаполненияДатСледующегоОпроса");
	АдресСКД = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыбратьКарты(Команда)
	
	ВыбратьКартыСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвертироватьФлажки(Команда)
	
	Для каждого СтрокаТаблицыКарт Из ТаблицаКарт Цикл
	
		СтрокаТаблицыКарт.Пометка = НЕ СтрокаТаблицыКарт.Пометка;
	
	КонецЦикла;
	
КонецПроцедуры

//Процедура передает управление в форму "УпрощеннаяНастройкаСхемыКомпоновкиДанных"
//для задания СКД сегмента
//
&НаКлиенте
Процедура Настройки(Команда)

	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр("ru = 'Настройки выбора карт'");
	
	ОткрытьФормуМодально("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных",
		Новый Структура(
			"АдресСхемыКомпоновкиДанных, 
			|НеРедактироватьСхемуКомпоновкиДанных, 
			|НеНастраиватьУсловноеОформление,
			|НеНастраиватьВыбор,
			|ИсточникШаблонов,
			|Заголовок",
			АдресСКД,
			Истина,
			Истина,
			Истина,
			Неопределено,
			ЗаголовокФормыНастройкиСхемыКомпоновкиДанных));
	
КонецПроцедуры

&НаКлиенте
Процедура ПроставитьДатуСледующегоОпроса(Команда)
	Перем ТекстСообщения;
	
	Если НЕ ЗначениеЗаполнено(ДатаСледующегоОпроса) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не заполнена дата следующего опроса'"),
			ЭтотОбъект,
			"ДатаСледующегоОпроса" ,
		);
		
	КонецЕсли;
	
	Если ТаблицаКарт.Количество() = 0 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Нужно выбрать карты'"),
			ЭтотОбъект,
			"ТаблицаКарт" ,
		);
		
	КонецЕсли;
	
	ПроставитьДатуСледующегоОпросаСервер(ТекстСообщения);
	
	ПоказатьОповещениеПользователя(ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьФлажкиКлиент(Ложь)
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьФлажкиКлиент(Истина)
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ВыбратьКартыСервер()
	
	СКД       = ПолучитьИзВременногоХранилища(АдресСКД);
	Настройки = СКД.НастройкиПоУмолчанию;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СКД,Настройки,,,
		Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений")
	);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ТаблицаКарт.Загрузить(ТаблицаЗначений)
	
КонецПроцедуры


&НаСервере
Процедура ПроставитьДатуСледующегоОпросаСервер(ТекстСообщения)

	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Пометка", Истина);
	
	СтрокиТаблицы = ТаблицаКарт.НайтиСтроки(СтруктураПоиска);
	КоличествоОшибок = 0;
	КоличествоЗаписей = 0;
	Для каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
		
		ОбъектИнформационнаяКарта = СтрокаТаблицы.Карта.ПолучитьОбъект();
		
		Попытка
			ОбъектИнформационнаяКарта.ДатаСледующегоОпроса = ДатаСледующегоОпроса;
			СтрокаТаблицы.ДатаСледующегоОпроса = ДатаСледующегоОпроса;
			ОбъектИнформационнаяКарта.Записать();
			КоличествоЗаписей = КоличествоЗаписей + 1;
		Исключение
			КоличествоОшибок = КоличествоОшибок + 1;
		КонецПопытки;
		
	КонецЦикла;
	
	ТекстЗаписано = "";
	
	Если КоличествоЗаписей > 0 Тогда
		КоличествоПрописьюКратко    = ЧислоПрописью(КоличествоЗаписей, , НСтр("ru = ',,,ж,,,,,0'"));
		КоличествоПрописьюПолностью = ЧислоПрописью(КоличествоЗаписей, , НСтр("ru = 'дисконтная карта, дисконтные карты, дисконтных карт, ж,,,,,0'"));
		НадписьВСообщении = СтрЗаменить(КоличествоПрописьюПолностью, КоличествоПрописьюКратко, " ");
		ТекстЗаписано = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Записано: %1 %2'"),
			КоличествоЗаписей,
			НадписьВСообщении
		);
	КонецЕсли;
	
	ТекстОшибок = "";
	
	Если КоличествоОшибок > 0 Тогда
		
		ТекстОшибок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибок при записи: %1'"),
			КоличествоОшибок
		);
		
		Если КоличествоЗаписей > 0 Тогда
			ТекстОшибок = Символы.ПС + ТекстОшибок;
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстСообщения = ТекстЗаписано + ТекстОшибок;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажкиКлиент(ПометкаФлага)
	
	Для каждого СтрокаТаблицыКарт Из ТаблицаКарт Цикл
	
		СтрокаТаблицыКарт.Пометка = ПометкаФлага;
	
	КонецЦикла;
	
КонецПроцедуры
