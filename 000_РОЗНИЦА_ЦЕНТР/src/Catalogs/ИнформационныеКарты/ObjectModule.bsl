
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидКарты = Перечисления.ВидыИнформационныхКарт.Штриховая Тогда
		
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("КодКарты");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	ИначеЕсли ЗначениеЗаполнено(КодКарты) Тогда
		МаркетинговыеАкцииСервер.ПроверитьИспользованиеКодаКарты(
			ЭтотОбъект,
			Отказ
		);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПриКопировании"
// для объекта
//
Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ЭтоГруппа Тогда

		КодКарты		= "";
		Наименование	= "";
		Резерв			= Ложь;
		Автор			= Неопределено;
		ДатаЗакрытия	= '00010101';
		ДатаОткрытия	= '00010101';
		ДатаСоздания	= ТекущаяДата();
		ДатаПоследнегоОпроса = '00010101';
		ДатаСледующегоОпроса = '00010101';
		Магазин	= Неопределено;

	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПередЗаписью"
//
Процедура ПередЗаписью(Отказ)

	Если НЕ ЭтоГруппа Тогда	//	LNK 13.08.2020 10:34:03

		Если НЕ КодКарты = СокрЛП(КодКарты) Тогда

			КодКарты = СокрЛП(КодКарты);

		КонецЕсли;

	КонецЕсли;

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	Если НЕ ЭтоГруппа Тогда

		ЗначенияПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ПометкаУдаления, Блокирован");

		Если ЗначениеЗаполнено(ВладелецКарты) И Резерв Тогда	//	LNK 16.08.2021 07:22:19

			Резерв = Ложь;	//	"Резерв" может быть только при пустом владельце!

		КонецЕсли;

		Если НЕ (ПометкаУдаления ИЛИ Блокирован) Тогда

			ПроверитьУникальностьКонтактнойИнформации(Отказ);	//	LNK 07.11.2018 07:57:09

		КонецЕсли;

		Если НЕ Отказ Тогда

			Если ВидДисконтнойКарты = Справочники.ВидыДисконтныхКарт.ПредварительныеКартыЛояльности Тогда

				ВладелецКарты = Неопределено;	//	LNK 06.02.2019 07:14:02

			КонецЕсли;

			Если ВидКарты = Перечисления.ВидыИнформационныхКарт.Штриховая Тогда

				КодКарты = "";

			КонецЕсли;

			Если ЭтоНовый() Тогда

				ДатаСоздания = ТекущаяДата();

			ИначеЕсли ДатаСоздания = '00010101' Тогда

				ДатаСоздания = ОбщегоНазначенияКлиентСервер.УникальныйИдентификаторВремя(Ссылка.УникальныйИдентификатор());

			КонецЕсли;

			Если ПометкаУдаления И НЕ ЗначенияПоСсылке.ПометкаУдаления = Истина
			ИЛИ
				 Блокирован И НЕ ЗначенияПоСсылке.Блокирован = Истина
			Тогда

				Блокирован  = Истина;
				ДатаЗакрытия = ТекущаяДата();

			ИначеЕсли НЕ ПометкаУдаления И НЕ Блокирован И НЕ ДатаЗакрытия = '00010101' Тогда

			//	Блокировка карты снята.
				ДатаЗакрытия = '00010101';

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

//	LNK 07.11.2018 07:57:33
Процедура ПроверитьУникальностьКонтактнойИнформации(Отказ)


КонецПроцедуры
	
#КонецОбласти









