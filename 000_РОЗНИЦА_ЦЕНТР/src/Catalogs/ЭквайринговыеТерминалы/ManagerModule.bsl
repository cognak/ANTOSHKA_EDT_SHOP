#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Возвращает организацию, кассу и валюту выбранного эквайрингового терминала.
//
// Параметры:
//  ЭквайринговыйТерминал - СправочникСсылка.ЭквайринговыеТерминалы - Ссылка на эквайринговый терминал
//
// Возвращаемое значение:
//	Структура - Организация, Касса и Валюта эквайрингового терминала
//
Функция РеквизитыЭквайринговогоТерминала(ЭквайринговыйТерминал) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭквайринговыеТерминалы.Организация                            КАК Организация,
	|	ЭквайринговыеТерминалы.Владелец                               КАК ДоговорЭквайринга,
	|	ЭквайринговыеТерминалы.Касса                                  КАК Касса,
	|	ЭквайринговыеТерминалы.Владелец.Эквайрер                      КАК Эквайрер,
	|	ЭквайринговыеТерминалы.Магазин                                КАК Магазин,
	|	ЭквайринговыеТерминалы.ИспользоватьБезПодключенияОборудования КАК ИспользоватьБезПодключенияОборудования
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
	|ГДЕ
	|	ЭквайринговыеТерминалы.Ссылка = &ЭквайринговыйТерминал
	|");
	
	Запрос.УстановитьПараметр("ЭквайринговыйТерминал", ЭквайринговыйТерминал);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Организация = Выборка.Организация;
		Касса = Выборка.Касса;
		ДоговорЭквайринга = Выборка.ДоговорЭквайринга;
		Эквайрер = Выборка.Эквайрер;
		Магазин = Выборка.Магазин;
		ИспользоватьБезПодключенияОборудования = Выборка.ИспользоватьБезПодключенияОборудования;
	Иначе
		Организация = Неопределено;
		Касса = Неопределено;
		ДоговорЭквайринга = Неопределено;
		Эквайрер = Неопределено;
		Магазин = Неопределено;
		ИспользоватьБезПодключенияОборудования = Неопределено;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("Организация, Касса, ДоговорЭквайринга, Эквайрер, Магазин, ИспользоватьБезПодключенияОборудования",
		Организация,
		Касса,
		ДоговорЭквайринга,
		Эквайрер,
		Магазин,
		ИспользоватьБезПодключенияОборудования
	);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

// Возвращает эквайринговый терминал, если найден один эквайринговый терминал по выбранной кассе или организации.
// Возвращает Неопределено, если эквайринговый терминал не найден или эквайринговых терминалов больше одного.
//
// Параметры:
//  Касса - СправочникСсылка.Кассы (СправочникСсылка.КассыККМ) - Выбранная касса
//  Организация - СправочникСсылка.Организации - Выбранная организация
//
// Возвращаемое значение:
//	СправочникСсылка.ЭквайринговыеТерминалы - Найденный эквайринговый терминал
//
Функция ЭквайринговыйТерминалПоУмолчанию(Касса, Организация = Неопределено, Магазин = Неопределено) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ЭквайринговыеТерминалы.Ссылка КАК ЭквайринговыйТерминал
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
	|ГДЕ
	|	(НЕ ЭквайринговыеТерминалы.ПометкаУдаления)
	|	И (ЭквайринговыеТерминалы.Касса = &Касса
	|			ИЛИ &Касса = НЕОПРЕДЕЛЕНО
	|				И ЭквайринговыеТерминалы.Касса ССЫЛКА Справочник.Кассы)
	|	И (ЭквайринговыеТерминалы.Организация = &Организация
	|			ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (ЭквайринговыеТерминалы.Магазин = &Магазин
	|			ИЛИ &Магазин = НЕОПРЕДЕЛЕНО)");
	Если НЕ ЗначениеЗаполнено(Магазин) Тогда
		Магазин = ПараметрыСеанса.ТекущийМагазин;
	КонецЕсли;
	Запрос.УстановитьПараметр("Касса", ?(ЗначениеЗаполнено(Касса), Касса, Неопределено));
	Запрос.УстановитьПараметр("Организация", ?(ЗначениеЗаполнено(Организация), Организация, Неопределено));
	Запрос.УстановитьПараметр("Магазин", ?(ЗначениеЗаполнено(Магазин), Магазин, Неопределено));
	
	Выборка = Запрос.Выполнить().Выбрать();	

	Если Выборка.Количество() = 1
		И Выборка.Следующий() Тогда
		ЭквайринговыйТерминал = Выборка.ЭквайринговыйТерминал;
	Иначе
		ЭквайринговыйТерминал = Справочники.ЭквайринговыеТерминалы.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ЭквайринговыйТерминал;

КонецФункции

#КонецЕсли
