#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	Если НЕ (ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() ИЛИ ОбменДаннымиПовтИсп.ПолучитьДанныеУзла().ВидУзла = Перечисления.ВидыУзлов.ТестовыйУзел) Тогда

		Если НЕ ТехническаяПоддержкаВызовСервера.ВыполняютсяСлужебныеДействия() Тогда

			Сообщить("Отказано! Изменение программ лояльности предусмотрено только в главном узле.");
			Отказ = Истина;

		КонецЕсли;

	КонецЕсли;

	Если НЕ ЭтоГруппа Тогда

		Если НЕ СпособПримененияСкидки = Перечисления.СпособыПримененияСкидокНаценок.НачислитьБонусныеБаллы Тогда

			СовместноеНачислениеСкидокИБонусов = Ложь;
			НачислятьБаллыНаАкционныйТовар     = Ложь;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка И ЗначениеЗаполнено(ОбменДанными.Отправитель) Тогда

		Возврат;

	КонецЕсли;

	Если НЕ ЭтоГруппа Тогда

	//	здесь данные ссылки уже равны записываемому объекту
		РегистрыСведений.КлючевыеАтрибутыПрограммЛояльности.ФиксироватьИзменениеПриЗаписи(Ссылка, Отказ);

	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;

	Если СпособПримененияСкидки = Перечисления.СпособыПримененияСкидокНаценок.ПрименитьВМоментРасчетаСкидокНаценок Тогда

		МассивНепроверяемыхРеквизитов.Добавить("МаксимальныйПроцентОплатыБонусами");
		МассивНепроверяемыхРеквизитов.Добавить("ТипБонусов");

		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти












