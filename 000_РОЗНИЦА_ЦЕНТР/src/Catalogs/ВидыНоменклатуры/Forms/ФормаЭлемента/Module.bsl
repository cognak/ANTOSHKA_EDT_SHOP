////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// подсистема запрета редактирования ключевых реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
		
	УправлениеЭлементамиФормыНаСервере();
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	// ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ДополнительныеОтчетыИОбработки

	Если НЕ ТехническаяПоддержкаПовтИсп.ОтладочныйРежимРаботы() Тогда

		ТолькоПросмотр = Истина;	//	LNK 03.10.2023 15:26:14

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	УправлениеЭлементамиФормыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат Тогда
		ТекущийОбъект.ШаблонЦенника = "";
		ТекущийОбъект.ШаблонЭтикетки = "";
		ТекущийОбъект.НаборСвойствХарактеристик = "";
	КонецЕсли;
	
	Если НЕ ТекущийОбъект.ИспользоватьХарактеристики Тогда
		ТекущийОбъект.ИспользованиеХарактеристик = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// подсистема запрета редактирования ключевых реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
		
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормыНаСервере()
	
	ДоступностьЭлементов = Объект.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.ПодарочныйСертификат;
	Элементы.ШаблонЦенника.Доступность = ДоступностьЭлементов;
	Элементы.ШаблонЭтикетки.Доступность = ДоступностьЭлементов;
	Элементы.ИспользованиеХарактеристик.Доступность = ДоступностьЭлементов И Объект.ИспользоватьХарактеристики;
	
	ТекущаяНастройкаИспользованияСерий = Объект.НастройкаИспользованияСерий;
	
	УстановитьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура УправлениеЭлементамиФормыНаКлиенте()
	
	ДоступностьЭлементов = Объект.ТипНоменклатуры <> ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПодарочныйСертификат");
	Элементы.ШаблонЦенника.Доступность = ДоступностьЭлементов;
	Элементы.ШаблонЭтикетки.Доступность = ДоступностьЭлементов;
	Элементы.ИспользованиеХарактеристик.Доступность = ДоступностьЭлементов И Объект.ИспользоватьХарактеристики;
	Элементы.ИспользоватьХарактеристики.Доступность = ДоступностьЭлементов;
	УстановитьВидимость();
	
КонецПроцедуры

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
			
	Если Не Объект.Ссылка.Пустая() Тогда
		Результат = ОткрытьФормуМодально("Справочник.ВидыНоменклатуры.Форма.РазблокированиеРеквизитов");
		Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
			Если ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект) Тогда
				ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтотОбъект, Результат);
				Если Результат.Найти("СхемаКомпоновкиДанных") <> Неопределено И НЕ ОбщегоНазначенияРТСервер.ЭтоПолноправныйПользователь() Тогда
					Элементы.РедактироватьСхемуКомпоновкиДанных.Доступность = Ложь;
				КонецЕсли;
			КонецЕсли;
			УправлениеЭлементамиФормыНаКлиенте();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	
	Если Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПодарочныйСертификат") Тогда
		Объект.ИспользованиеХарактеристик = ПредопределенноеЗначение("Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать");
		Объект.ИспользоватьХарактеристики = Ложь;
	КонецЕсли;
	
	Если НЕ Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		Объект.ИспользоватьСерии = Ложь;
	КонецЕсли;
	
	УправлениеЭлементамиФормыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеХарактеристикПриИзменении(Элемент)
	
	УправлениеЭлементамиФормыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьХарактеристикиПриИзменении(Элемент)
	
	Элементы.ИспользованиеХарактеристик.Доступность = Объект.ИспользоватьХарактеристики;
	
	Если НЕ Объект.ИспользоватьХарактеристики Тогда
		Объект.ИспользованиеХарактеристик = ПредопределенноеЗначение("Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать");
	Иначе
		Объект.ИспользованиеХарактеристик = ПредопределенноеЗначение("Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры");
	КонецЕсли;
	
	УправлениеЭлементамиФормыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСерииПриИзменении(Элемент)
	
	УправлениеЭлементамиФормыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаИспользованияСерийПриИзменении(Элемент)
	
	Если Объект.НастройкаИспользованияСерий = ПредопределенноеЗначение("Перечисление.НастройкиИспользованияСерийНоменклатуры.ПартияТоваровПоСрокуГодности")
		Или Объект.НастройкаИспользованияСерий = ПредопределенноеЗначение("Перечисление.НастройкиИспользованияСерийНоменклатуры.ПартияТоваровПоНомеруИСрокуГодности") Тогда 
		Элементы.СтраницыТочностьУказанияСерийСрокГарантии.ТекущаяСтраница = Элементы.СтраницаТочностьУказанияСрокаГодности;
	ИначеЕсли Объект.НастройкаИспользованияСерий = ПредопределенноеЗначение("Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара") Тогда
		Элементы.СтраницыТочностьУказанияСерийСрокГарантии.ТекущаяСтраница = Элементы.СтраницаСрокГарантии;
	Иначе
		Элементы.СтраницыТочностьУказанияСерийСрокГарантии.ТекущаяСтраница = Элементы.СтраницаТочностьУказанияСрокаГодностиПустая;
	КонецЕсли;
	
	ТекущаяНастройкаИспользованияСерий = Объект.НастройкаИспользованияСерий;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура УстановитьВидимость()
	
	Если Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
		Элементы.СтраницыИспользованиеСерий.ТекущаяСтраница = Элементы.СтраницаИспользованиеСерий;
	Иначе
		Элементы.СтраницыИспользованиеСерий.ТекущаяСтраница = Элементы.СтраницаИспользованиеСерийПустая;
	КонецЕсли;
	
	Элементы.СтраницаСерии.Видимость = Объект.ИспользоватьСерии И ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	
	Если Объект.НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ПартияТоваровПоСрокуГодности
		Или Объект.НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ПартияТоваровПоНомеруИСрокуГодности Тогда 
		Элементы.СтраницыТочностьУказанияСерийСрокГарантии.ТекущаяСтраница = Элементы.СтраницаТочностьУказанияСрокаГодности;
	ИначеЕсли Объект.НастройкаИспользованияСерий = ПредопределенноеЗначение("Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара") Тогда
		Элементы.СтраницыТочностьУказанияСерийСрокГарантии.ТекущаяСтраница = Элементы.СтраницаСрокГарантии;
	Иначе
		Элементы.СтраницыТочностьУказанияСерийСрокГарантии.ТекущаяСтраница = Элементы.СтраницаТочностьУказанияСрокаГодностиПустая;
	КонецЕсли;
	
КонецПроцедуры
