#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получить статус.
// 
// Параметры:
//  СтруктураСтатуса - Структура - Структура статуса:
//		КодСтатуса - Строка - Код статуса в системе оператора доставки
//		Наименование - Строка - Наименоване статуса в системе оператора доставки
//		ОператорДоставки - ПеречислениеСсылка.ОператорыДоставки 
// 
// Возвращаемое значение:
//  Структура - Структура статуса:
//		* СсылкаКодСтатуса - СправочникСсылка.КодыСтатусовТТН
//		* СтатусПосылки - ПеречислениеСсылка.СтатусыПосылокСлужбыДоставки
//		* НеЗаписывать - Булево
Функция ПолучитьСтатус(СтруктураСтатуса) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СсылкаКодСтатуса", ПустаяСсылка());
	Результат.Вставить("СтатусПосылки", Перечисления.СтатусыПосылокСлужбыДоставки.ПустаяСсылка());
	Результат.Вставить("НеЗаписывать", Ложь);

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КодыСтатусовТТН.Ссылка КАК Ссылка,
		|	КодыСтатусовТТН.СтатусПосылки КАК СтатусПосылки,
		|	КодыСтатусовТТН.НеЗаписывать КАК НеЗаписывать
		|ИЗ
		|	Справочник.КодыСтатусовТТН КАК КодыСтатусовТТН
		|ГДЕ
		|	КодыСтатусовТТН.Код = &КодСтатуса
		|	И КодыСтатусовТТН.ОператорДоставки = &ОператорДоставки";
	
	Запрос.УстановитьПараметр("КодСтатуса", СтруктураСтатуса.КодСтатуса);
	Запрос.УстановитьПараметр("ОператорДоставки", СтруктураСтатуса.ОператорДоставки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Результат.СсылкаКодСтатуса = Выборка.Ссылка;
		Результат.СтатусПосылки = Выборка.СтатусПосылки;
		Результат.НеЗаписывать = Выборка.НеЗаписывать;
		
	Иначе
		
		НовыйЭлемент = СоздатьЭлемент();
		НовыйЭлемент.Код = СтруктураСтатуса.КодСтатуса;
		НовыйЭлемент.Наименование = СтруктураСтатуса.Наименование;
		НовыйЭлемент.НаименованиеСтатуса = СтруктураСтатуса.Наименование;
		НовыйЭлемент.ОператорДоставки = СтруктураСтатуса.ОператорДоставки;
		НовыйЭлемент.СтатусПосылки = Перечисления.СтатусыПосылокСлужбыДоставки.ВДороге;
		НовыйЭлемент.Записать();
		Результат.СсылкаКодСтатуса = НовыйЭлемент.Ссылка;
		Результат.СтатусПосылки = Перечисления.СтатусыПосылокСлужбыДоставки.ВДороге;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытий
// Код процедур и функций
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти

#КонецЕсли