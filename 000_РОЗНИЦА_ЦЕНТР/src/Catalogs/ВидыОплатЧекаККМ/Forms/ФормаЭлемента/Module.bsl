///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;

	УправлениеЭлементамиФормыНаСервере();
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	// ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ДополнительныеОтчетыИОбработки
	
	ИспользоватьОплатуБанковскимиКредитами = ПолучитьФункциональнуюОпцию("ИспользоватьОплатуБанковскимиКредитами");
	
	Если НЕ ИспользоватьОплатуБанковскимиКредитами И НЕ Объект.Предопределенный Тогда
	
		УдаляемыйЭлемент = Элементы.ТипОплаты.СписокВыбора.НайтиПоЗначению(Перечисления.ТипыОплатЧекаККМ.БанковскийКредит);
		
		Если НЕ УдаляемыйЭлемент = Неопределено Тогда
			Элементы.ТипОплаты.СписокВыбора.Удалить(УдаляемыйЭлемент);
		КонецЕсли;
	КонецЕсли;
	
	ИспользоватьОплатуПлатежнымиКартами = ПолучитьФункциональнуюОпцию("ИспользоватьОплатуПлатежнымиКартами");
	
	Если НЕ ИспользоватьОплатуПлатежнымиКартами Тогда
	
		УдаляемыйЭлемент = Элементы.ТипОплаты.СписокВыбора.НайтиПоЗначению(Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта);
		
		Если НЕ УдаляемыйЭлемент = Неопределено Тогда
			Элементы.ТипОплаты.СписокВыбора.Удалить(УдаляемыйЭлемент);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭлементамиФормыНаКлиенте();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТипОплатыПриИзменении(Элемент)
	
	УправлениеЭлементамиФормыНаКлиенте();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УправлениеЭлементамиФормыНаСервере()
	
	Элементы.ТипОплаты.Доступность = НЕ Объект.Предопределенный ИЛИ ТехническаяПоддержкаВызовСервера.ИсключительныйРежим();
		
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭлементамиФормыНаКлиенте()
	
	Если Объект.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.БанковскийКредит") Тогда
		
		Элементы.ГруппаБанковскийКредит.ТекущаяСтраница = Элементы.БанковскийКредит;
		
	Иначе
		
		Элементы.ГруппаБанковскийКредит.ТекущаяСтраница = Элементы.Пустышка;
		
	КонецЕсли;	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
