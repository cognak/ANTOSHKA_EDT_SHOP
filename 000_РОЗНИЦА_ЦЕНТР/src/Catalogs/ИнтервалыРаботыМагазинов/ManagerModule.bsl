#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Магазин работает.
// 
// Параметры:
//  Магазин - СправочникСсылка.Магазины - Магазин для которого определеяю работает ли он
//  ДатаЗапроса - Дата - Дата на которую нужно проверить работу магазина
// 
// Возвращаемое значение:
//  Булево - Магазин работает
Функция МагазинРаботает(Магазин, ДатаЗапроса) Экспорт
	
	Результат = Ложь;

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВремяРаботы.Ссылка.НачалоИнтервала КАК НачалоИнтервала,
		|	ВремяРаботы.Ссылка.ОкончаниеИнтервала КАК ОкончаниеИнтервала
		|ИЗ
		|	Справочник.ИнтервалыРаботыМагазинов.ДниНедели КАК ВремяРаботы
		|ГДЕ
		|	ВремяРаботы.ДеньНедели = &ДеньНедели
		|	И ВремяРаботы.Ссылка.Магазин = &Магазин";
	
	Запрос.УстановитьПараметр("Магазин", Магазин);
	Запрос.УстановитьПараметр("ДеньНедели", Перечисления.ДниНедели.Получить(ДеньНедели(ДатаЗапроса) - 1));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		НачалоДняРаботы = НачалоДня(ДатаЗапроса) + (Выборка.НачалоИнтервала - Дата(1,1,1));
		КонецДняРаботы = НачалоДня(ДатаЗапроса) + (Выборка.ОкончаниеИнтервала - Дата(1,1,1));
		Результат = (ДатаЗапроса >= НачалоДняРаботы) И (ДатаЗапроса <= КонецДняРаботы);
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецЕсли