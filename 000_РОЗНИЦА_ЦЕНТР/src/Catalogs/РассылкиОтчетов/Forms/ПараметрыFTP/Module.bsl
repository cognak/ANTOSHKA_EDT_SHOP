////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	РеквизитСправочника = Метаданные.Справочники.РассылкиОтчетов.Реквизиты.FTPПорт;
	ЗаполнитьЗначенияСвойств(Элементы.Порт, РеквизитСправочника, "Подсказка, МаксимальноеЗначение, МинимальноеЗначение");
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Сервер, Каталог, Порт, Логин, Пароль, ПассивноеСоединение");
	Если ЭтотОбъект.Сервер = "" Тогда
		ЭтотОбъект.Сервер = "сервер";
	КонецЕсли;
	Если ЭтотОбъект.Каталог = "" Тогда
		ЭтотОбъект.Каталог = "/каталог/";
	КонецЕсли;
	ВидимостьДоступность(ЭтотОбъект);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СерверИКаталогПриИзменении(Элемент)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РассылкаОтчетовКлиентСервер.РазобратьFTPАдрес(СерверИКаталог), "Сервер, Каталог");
	ВидимостьДоступность(ЭтотОбъект);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Заполнить(Команда)
	Если Сервер = "" Тогда
		ПолныйАдрес = НСтр("ru = 'ftp://логин:пароль@сервер:порт/каталог/'");
	Иначе
		Если Логин = "" Тогда
			ПолныйАдрес = "ftp://"+ Сервер +":"+ Формат(Порт, "ЧН=21; ЧГ=0") + Каталог;
		Иначе
			ПолныйАдрес = "ftp://"+ Логин +":"+ Пароль +"@"+ Сервер +":"+ Формат(Порт, "ЧН=0; ЧГ=0") + Каталог;
		КонецЕсли;
	КонецЕсли;
	
	Если ВвестиСтроку(ПолныйАдрес, НСтр("ru = 'Введите полный ftp адрес'")) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РассылкаОтчетовКлиентСервер.РазобратьFTPАдрес(ПолныйАдрес));
		ВидимостьДоступность(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	ЗначениеВыбора = Новый Структура("Сервер, Каталог, Порт, Логин, Пароль, ПассивноеСоединение");
	ЗаполнитьЗначенияСвойств(ЗначениеВыбора, ЭтотОбъект);
	ОповеститьОВыборе(ЗначениеВыбора);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьДоступность(ЭтотОбъект, Изменения = "")
	Если Прав(ЭтотОбъект.Каталог, 1) <> "/" Тогда
		ЭтотОбъект.Каталог = ЭтотОбъект.Каталог + "/";
	КонецЕсли;
	ЭтотОбъект.СерверИКаталог = "ftp://"+ ЭтотОбъект.Сервер + ЭтотОбъект.Каталог;
КонецПроцедуры

