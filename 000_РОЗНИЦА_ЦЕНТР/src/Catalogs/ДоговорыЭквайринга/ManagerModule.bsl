#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Возвращает организацию договора эквайринга.
//
// Параметры:
//  ДоговорыЭквайринга - СправочникСсылка.ДоговорыЭквайринга - Ссылка на договор эквайринга
//
// Возвращаемое значение:
//	Структура - Организация договора эквайринга
//
Функция РеквизитыДоговораЭквайринга(ДоговорыЭквайринга) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоговорыЭквайринга.Организация КАК Организация
	|ИЗ
	|	Справочник.ДоговорыЭквайринга КАК ДоговорыЭквайринга
	|ГДЕ
	|	ДоговорыЭквайринга.Ссылка = &ДоговорыЭквайринга
	|");
	
	Запрос.УстановитьПараметр("ДоговорыЭквайринга", ДоговорыЭквайринга);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Организация = Выборка.Организация;
	Иначе
		Организация = Неопределено;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("Организация", Организация);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

// Функция получает реквизиты которые необходимо заблокировать после сохранения.
// Возвращаемое значение:
//	Массив - массив наименований блокируемых реквизитов
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("Организация");
	Возврат Результат;
	
КонецФункции

// Возвращает вид оплаты, если найден один вид оплаты в договоре эквайринга.
// 
// Параметры:
//	ДоговорыЭквайринга - СправочникСсылка.ДоговорыЭквайринга - Ссылка на договор эквайринга
//
// Возвращаемое значение:
//	Структура - Вид оплаты договора эквайринга
//
Функция ВидОплатыПоУмолчанию(ДоговорЭквайрига) Экспорт
	
	СтруктураПараметровПоУмолчанию = Новый Структура();
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ДоговорыЭквайрингаТарифыЗаРасчетноеОбслуживание.ВидОплаты КАК ВидОплаты
	|ИЗ
	|	Справочник.ДоговорыЭквайринга.ТарифыЗаРасчетноеОбслуживание КАК ДоговорыЭквайрингаТарифыЗаРасчетноеОбслуживание
	|ГДЕ
	|	ДоговорыЭквайрингаТарифыЗаРасчетноеОбслуживание.Ссылка = &ДоговорЭквайрига
	|	И НЕ ДоговорыЭквайрингаТарифыЗаРасчетноеОбслуживание.Ссылка.ПометкаУдаления
	|";
	Запрос.УстановитьПараметр("ДоговорЭквайрига", ДоговорЭквайрига);
	ВыборкаПараметрыПоУмолчанию = Запрос.Выполнить().Выбрать();
	Если ВыборкаПараметрыПоУмолчанию.Количество() = 1.00 И ВыборкаПараметрыПоУмолчанию.Следующий() Тогда
		СтруктураПараметровПоУмолчанию.Вставить("ВидОплаты", ВыборкаПараметрыПоУмолчанию.ВидОплаты);
	Иначе
		СтруктураПараметровПоУмолчанию.Вставить("ВидОплаты", Справочники.ВидыОплатЧекаККМ.ПустаяСсылка());
	КонецЕсли;
	
	возврат СтруктураПараметровПоУмолчанию;
	
КонецФункции

#КонецЕсли
