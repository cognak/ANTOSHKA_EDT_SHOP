///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РучноеИзменение = Ложь;
	ВКлассификаторе = Ложь;
	Наименование = "";
	Город		 = "";
	Адрес		 = "";
	Телефоны	 = "";
	
	Если Не Параметры.Свойство("РучноеИзменение") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	РучноеИзменение = Параметры.РучноеИзменение;
	РеквизитВладельца = Параметры.Реквизит;
	
	Если Параметры.ЗначенияПолей.Свойство("КодБанка") Тогда
		КодБанка	= Параметры.ЗначенияПолей.КодБанка;
	КонецЕсли;
	
	
	Если ПустаяСтрока(РучноеИзменение) Тогда
		РучноеИзменение = Ложь;
	КонецЕсли;
	
	Если РучноеИзменение Тогда
		КодБанка	 = Параметры.ЗначенияПолей.КодБанка;
		Наименование = Параметры.ЗначенияПолей.Наименование;
		Город		 = Параметры.ЗначенияПолей.Город;
		Адрес		 = Параметры.ЗначенияПолей.Адрес;
		Телефоны	 = Параметры.ЗначенияПолей.Телефоны;
	Иначе
		Банк = Параметры.Банк;
		Если ТипЗнч(Банк) = Тип("СправочникСсылка.КлассификаторБанков") Тогда
			КодБанка		= Банк.Код;
			Наименование	= Банк.Наименование;
			Город			= Банк.Город;
			Адрес			= Банк.Адрес;
			Телефоны		= Банк.Телефоны;
			ВКлассификаторе = Истина;
		КонецЕсли;
	КонецЕсли;
	ОбработатьФлагРучногоИзменения();
	
	БылиНажатыКнопкиЗакрытия = Ложь;
	
	УстановитьВидимостьКоманд();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПринятьИзмененияЗакрытьФорму(Команда)
	
	БылиНажатыКнопкиЗакрытия = Истина;
	Закрыть();
	ОповеститьОВыборе(РеквизитыБанка());
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	БылиНажатыКнопкиЗакрытия = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	БылиНажатыКнопкиЗакрытия = Истина;
	Закрыть();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ОбработатьФлагРучногоИзменения()
	
	Если РучноеИзменение Тогда
		Элементы.ПоясняющийТекст.Заголовок = НСтр("ru = 'Автоматическое обновление элемента отключено.'");
		ТолькоПросмотр = Ложь;
	Иначе
		Если ВКлассификаторе Тогда
			Элементы.ПоясняющийТекст.Заголовок = НСтр("ru = 'Элемент обновляется автоматически.'");
			ТолькоПросмотр = Истина;
		Иначе
			Элементы.ПоясняющийТекст.Заголовок = НСтр("ru = 'Элемент не найден в классификаторе.Автоматическое обновление элемента отключено.'");
			ТолькоПросмотр = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция РеквизитыБанка()
	
	Результат = Новый Структура;
	Результат.Вставить("Реквизит", РеквизитВладельца);
	
	Если РучноеИзменение Тогда
		Результат.Вставить("РучноеИзменение", РучноеИзменение);
		ЗначенияПолей = Новый Структура;
		ЗначенияПолей.Вставить("КодБанка", КодБанка);
		ЗначенияПолей.Вставить("Наименование", Наименование);
		ЗначенияПолей.Вставить("Город", Город);
		ЗначенияПолей.Вставить("Адрес", Адрес);
		ЗначенияПолей.Вставить("Телефоны", Телефоны);
		ЗначенияПолей.Вставить("РучноеИзменение", РучноеИзменение);
		
		Результат.Вставить("ЗначенияПолей", ЗначенияПолей);
	Иначе
		Если ВКлассификаторе Тогда
			Результат.Вставить("РучноеИзменение", РучноеИзменение);
			Результат.Вставить("Банк", Банк);
		Иначе
			Результат.Вставить("РучноеИзменение", Истина);
			ЗначенияПолей = Новый Структура;
			ЗначенияПолей.Вставить("КодБанка", КодБанка);
			ЗначенияПолей.Вставить("Наименование", Наименование);
			ЗначенияПолей.Вставить("Город", Город);
			ЗначенияПолей.Вставить("Адрес", Адрес);
			ЗначенияПолей.Вставить("Телефоны", Телефоны);
			ЗначенияПолей.Вставить("РучноеИзменение", РучноеИзменение);
			
			Результат.Вставить("ЗначенияПолей", ЗначенияПолей);
		КонецЕсли;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьКоманд()

	Если РучноеИзменение Тогда
		Элементы.ФормаЗакрыть.Видимость = Ложь;
	Иначе
		Элементы.ФормаКомандаОК.Видимость = Ложь;
		Элементы.ФормаКомандаОтмена.Видимость = Ложь;
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры
