#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)

	Перем ЗначениеИзСтруктуры;

	Если Не Параметры.Свойство("Номенклатура", ЗначениеИзСтруктуры) Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;

	ДанныеВыбора = Новый СписокЗначений;

	ОбработкаТабличнойЧастиТоварыСервер.ПолучитьСписокДляВыбораУпаковок(ЗначениеИзСтруктуры, ДанныеВыбора, Истина, Параметры.СтрокаПоиска);

КонецПроцедуры

// Функция возвращает упаковку с наименьшим коэффициентом
//
// Параметры
//	Номенклатура  - номенклатура-владелец упаковки (если используется упаковки, индивидуальные для каждой номенклатуры)
//                 или номенклатура, из которой берется вид номенклатуры (если используеются упаковки, общие для вида номенклатуры)
//
// Возвращаемое значение:
//   СправочникСсылка.УпаковкиНоменклатуры или Неопределенно
//
Функция ПолучитьУпаковкуПоУмолчанию(Номенклатура) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УпаковкиНоменклатуры.Ссылка КАК Упаковка
	|ИЗ
	|	Справочник.УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (УпаковкиНоменклатуры.Владелец = ВЫБОР
	|				КОГДА СпрНоменклатура.НаборУпаковок = ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры)
	|					ТОГДА СпрНоменклатура.Ссылка
	|				КОГДА СпрНоменклатура.НаборУпаковок <> ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ПустаяСсылка)
	|					ТОГДА СпрНоменклатура.НаборУпаковок
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|ГДЕ
	|	СпрНоменклатура.Ссылка = &Номенклатура
	|	И (НЕ УпаковкиНоменклатуры.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УпаковкиНоменклатуры.Коэффициент";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() 	Тогда
		Упаковка = Выборка.Упаковка;
	Иначе
		Упаковка = Справочники.УпаковкиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Упаковка;
	
КонецФункции

Функция СформироватьНаименование(ЕдиницаИзмерения, Коэффициент, ЕдиницаИзмеренияВладельца) Экспорт
	Возврат СокрЛП(СокрЛП(ЕдиницаИзмерения) + " (" + Формат(Коэффициент,"ЧРД=.") + Строка(ЕдиницаИзмеренияВладельца) + ")");
КонецФункции

Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	Результат = Новый Массив;
//	LNK 27.01.2017 12:20:48
//	Результат.Добавить("Коэффициент");
	Результат.Добавить("ЕдиницаИзмерения");
	Возврат Результат;
КонецФункции

#КонецЕсли
