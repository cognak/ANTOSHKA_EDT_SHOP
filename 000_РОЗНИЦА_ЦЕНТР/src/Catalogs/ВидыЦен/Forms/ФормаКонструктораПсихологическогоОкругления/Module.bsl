
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события формы "ПриСозданииНаСервере".
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеОкругления             = 0.01;
	МаксимальноеЗначениеОкругления = 1000;
	
	Если Параметры.Свойство("ТочностьОкругления") Тогда
		ТочностьОкругления = Параметры.ТочностьОкругления;
	КонецЕсли;
	
	Если Параметры.Свойство("ОкруглятьВБольшуюСторону") Тогда
		ОкруглятьВБольшуюСторону = Параметры.ОкруглятьВБольшуюСторону;
	КонецЕсли;
	
	Если Параметры.Свойство("ГраницаДиапазонаЦен") Тогда
		ГраницаДиапазонаЦен = Параметры.ГраницаДиапазонаЦен;
	Иначе 
		ГраницаДиапазонаЦен = -1;
	КонецЕсли;
	
	Пока ЗначениеОкругления <= МаксимальноеЗначениеОкругления
		И (ЗначениеОкругления <= ГраницаДиапазонаЦен / 10 Или ГраницаДиапазонаЦен < 0) Цикл
		
		Элементы.Разряд.СписокВыбора.Добавить(ЗначениеОкругления);
		ЗначениеОкругления = ЗначениеОкругления * 10;
		
	КонецЦикла;
	
	Если Параметры.Свойство("ПсихологическоеОкругление") Тогда
		ПсихологическоеОкругление = Параметры.ПсихологическоеОкругление;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПсихологическоеОкругление) Тогда
		ПсихологическоеОкругление = 0.09;
	КонецЕсли;
	
	Если Параметры.Свойство("ПримерЧисло") Тогда
		ПримерЧисло = Параметры.ПримерЧисло;
	КонецЕсли;
	
	Разрядность = ЦенообразованиеКлиентСервер.РазрядностьПсихологическогоОкругления(ПсихологическоеОкругление);
	Разряд = Pow(10, Разрядность - 1);
	ПоследняяЦифра = ПсихологическоеОкругление / Разряд;
	
	ПримерРезультат = РассчитатьПримерРезультат(ПримерЧисло, ТочностьОкругления, ОкруглятьВБольшуюСторону, ПсихологическоеОкругление);

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

// Процедура - обработчик события "ПриИзменении" поля "ПримерЧисло".
//
&НаКлиенте
Процедура ПримерЧислоПриИзменении(Элемент)
	
	ПримерРезультат = РассчитатьПримерРезультат(ПримерЧисло, ТочностьОкругления, ОкруглятьВБольшуюСторону, ПсихологическоеОкругление);
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "Разряд".
//
&НаКлиенте
Процедура РазрядПриИзменении(Элемент)
	
	ОбновитьОкругление();
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "Цифра".
//
&НаКлиенте
Процедура ЦифраПриИзменении(Элемент)
	
	ОбновитьОкругление();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// Процедура - обработчик команды "ОК".
//
&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(ПсихологическоеОкругление);
	
КонецПроцедуры

// Процедура - обработчик команды "СформироватьПример".
//
&НаКлиенте
Процедура СформироватьПример(Команда)
	
	ПримерРезультат = РассчитатьПримерРезультат(ПримерЧисло, ТочностьОкругления, ОкруглятьВБольшуюСторону, ПсихологическоеОкругление);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Функция РассчитатьПримерРезультат(Знач ПримерЧисло, Знач ТочностьОкругления, Знач ОкруглятьВБольшуюСторону, Знач ПсихологическоеОкругление)
	
	Если ЗначениеЗаполнено(ТочностьОкругления) Тогда
		ПримерЧисло = Ценообразование.ОкруглитьЦену(ПримерЧисло, ТочностьОкругления, ОкруглятьВБольшуюСторону);
	КонецЕсли;
	
	Возврат ЦенообразованиеКлиентСервер.ПрименитьПсихологическоеОкругление(ПримерЧисло, ПсихологическоеОкругление);
	
КонецФункции

// Обновляет значение психологического округления и вычисляет пример
//
&НаКлиенте
Процедура ОбновитьОкругление()
	
	ПсихологическоеОкругление = ПоследняяЦифра * Разряд;
	ПримерРезультат = РассчитатьПримерРезультат(ПримерЧисло, ТочностьОкругления, ОкруглятьВБольшуюСторону, ПсихологическоеОкругление);
	
КонецПроцедуры
