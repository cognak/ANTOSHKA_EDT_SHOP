
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЭтоГруппа И НЕ Сотрудник Тогда

		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("ДатаРождения");
		МассивНепроверяемыхРеквизитов.Добавить("Пол");
		МассивНепроверяемыхРеквизитов.Добавить("Магазин");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);

	ИначеЕсли ЭтоГруппа Тогда

		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("Магазин");
		МассивНепроверяемыхРеквизитов.Добавить("ОбособленноеПодразделение");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);

	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());

	Если ОбменДанными.Загрузка ИЛИ ТехническаяПоддержкаВызовСервера.ИсключительныйРежим() Тогда

		Возврат;

	КонецЕсли;

	Если НЕ ЭтоГруппа Тогда

		ИНН = СокрЛП(ИНН);

	//	LNK 02.04.2018 12:57:22
		Если ПроверитьУникальностьИНН(Отказ) Тогда

			Если Сотрудник И ПустаяСтрока(ИНН) Тогда

				Отказ = Истина;
				Сообщить("Отказано в записи! Для сотрудника должен быть указан ИНН.");

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Перем ВнешниеДанные;

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	Если ДополнительныеСвойства.ЭтоНовый И ДополнительныеСвойства.Свойство("ВнешниеДанные", ВнешниеДанные) Тогда

		ОбменДаннымиЗУП.ФизЛицоФамилияИмяОтчество(Ссылка, ВнешниеДанные);
		ОбменДаннымиЗУП.ФизЛицоПаспорт(Ссылка, ВнешниеДанные);

	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	ИНН = "";
	ДатаРождения  = '00010101';
	НомерПаспорта = "";
	ДатаВыдачиПаспорта = '00010101';
	КемВыданПаспорт = "";

	КонтактнаяИнформация.Очистить();
	ДополнительныеРеквизиты.Очистить();

КонецПроцедуры

//	LNK 02.04.2018 12:55:56
Функция ПроверитьУникальностьИНН(Отказ)

	Если НЕ ПустаяСтрока(ИНН) Тогда

		УстановитьПривилегированныйРежим(Истина);

		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТаблицаСправочник.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ТаблицаСправочник
		|ГДЕ
		|	ТаблицаСправочник.ИНН = &ИНН
		|	И НЕ ТаблицаСправочник.Ссылка = &ТекущаяСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование"
		);
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Запрос.УстановитьПараметр("ТекущаяСсылка", Ссылка);
		
		Результат = Запрос.Выполнить();

		Если НЕ Результат.Пустой() Тогда

			Отказ = Истина;
			Сообщить("Отказано в записи! ИНН «" + ИНН + "» уже использован у следующих физ.лиц:");

			Выборка = Результат.Выбрать();
			
			Пока Выборка.Следующий() Цикл

				Сообщить(" - " + СокрЛП(Выборка.Наименование));

			КонецЦикла;

		КонецЕсли;

		УстановитьПривилегированныйРежим(Ложь);

	КонецЕсли;

	Возврат НЕ Отказ;

КонецФункции // ПроверитьУникальностьИНН()
