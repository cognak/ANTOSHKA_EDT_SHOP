#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Получает реквизиты объекта, которые необходимо блокировать от изменения
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Массив - блокируемые реквизиты объекта
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт

	Результат = Новый Массив;
	Результат.Добавить("ТипПодключаемогоОборудования");
	Результат.Добавить("СвояНумерацияPLUНаОборудовании");
	Результат.Добавить("МаксимальныйКодPLU");
	
	
	Возврат Результат;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КодыТоваров") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "КодыТоваров", "Коды товаров", СформироватьПечатнуюФормуКодыТоваров(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода),,,Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуКодыТоваров(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КодыТоваров";
	
	Макет = УправлениеПечатью.ПолучитьМакет("Справочник.ПравилаОбменаСПодключаемымОборудованиемOffline.ПФ_MXL_КодыТоваров");
	ПервыйДокумент = Истина;
	
	Для Каждого Объект Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ВыгрузкаНаВесы = Объект.ПравилоОбмена.ТипПодключаемогоОборудования = Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		Если ВыгрузкаНаВесы Тогда
			ОбластьМакета.Параметры.ТекстЗаголовка = НСтр("ru = 'Коды товаров PLU для весов с печатью этикеток'");
		Иначе
			ОбластьМакета.Параметры.ТекстЗаголовка = НСтр("ru = 'Коды товаров SKU для кассы ККМ Offline'"); 
		КонецЕсли;
		ОбластьМакета.Параметры.ПравилоОбмена  = Объект.ПравилоОбмена;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьТовар = Макет.ПолучитьОбласть("ШапкаТаблицы|Товар");   
		ОбластьКод   = Макет.ПолучитьОбласть("ШапкаТаблицы|Код");
		Если ВыгрузкаНаВесы Тогда
			ОбластьКод.Параметры.ЗаголовокКода = "PLU"; 
		Иначе
			ОбластьКод.Параметры.ЗаголовокКода = "SKU"; 
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьКод);
		ТабличныйДокумент.Присоединить(ОбластьТовар);
		
		ОбластьКод   = Макет.ПолучитьОбласть("Строка|Код");
		ОбластьТовар = Макет.ПолучитьОбласть("Строка|Товар");
		
		Товары = ПодключаемоеОборудованиеOfflineВызовСервера.ПолучитьТаблицуТоваровДляПравила(Объект.ПравилоОбмена);
		Для Каждого СтрокаТЧ Из Товары Цикл
			
			Если ВыгрузкаНаВесы И СтрокаТЧ.ЕстьОшибки Тогда
				Продолжить;
			КонецЕсли;
			
			ОбластьКод.Параметры.Код = СтрокаТЧ.PLU;
			ТабличныйДокумент.Вывести(ОбластьКод);
			ОбластьТовар.Параметры.Товар = СтрокаТЧ.Наименование;
			ТабличныйДокумент.Присоединить(ОбластьТовар);
			
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Итого");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Вывести подписи.
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		ОбластьМакета.Параметры.Ответственный = Пользователи.ТекущийПользователь();
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Объект.ПравилоОбмена);
	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецЕсли


