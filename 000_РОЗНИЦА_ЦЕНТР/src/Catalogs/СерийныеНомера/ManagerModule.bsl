#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

//	LNK 02.02.2022 05:54:58
Функция ПолучитьРеквизиты(СписокЭлементов)	Экспорт

	Реквизиты = Новый Соответствие;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаСправочник.Ссылка КАК СерийныйНомер,
	|	ТаблицаСправочник.Владелец КАК Номенклатура,
	|	ТаблицаСправочник.КодСерийногоНомера КАК Номер,
	|	ТаблицаСправочник.ДатаСоздания КАК ДатаСоздания
	|ИЗ
	|	Справочник.СерийныеНомера КАК ТаблицаСправочник
	|ГДЕ
	|	ТаблицаСправочник.Ссылка В(&СписокЭлементов)"
	);
	Запрос.УстановитьПараметр("СписокЭлементов", СписокЭлементов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		Реквизиты.Вставить(Выборка.СерийныйНомер
			, Новый Структура(
				"Номенклатура, Номер, ДатаСоздания"
				, Выборка.Номенклатура, Выборка.Номер, Выборка.ДатаСоздания
			)
		);

	КонецЦикла;
	
	Возврат Реквизиты

КонецФункции

Процедура ОчиститьВДокументеНеиспользуемыеСерийныеНомера(ТЧТовары, ТЧСерийныеНомера) Экспорт

	СоотвествиеКлючей = Новый Соответствие;
	Количество        = ТЧСерийныеНомера.Количество();
	Для Итератор = 1 По Количество Цикл

		СтрокаТЧ            = ТЧСерийныеНомера[Количество - Итератор];
		НомерНеИспользуется = СоотвествиеКлючей[СтрокаТЧ.КлючСвязиСерийныхНомеров];
		Если НомерНеИспользуется = Неопределено Тогда

			НомерНеИспользуется = (ТЧТовары.Найти(СтрокаТЧ.КлючСвязиСерийныхНомеров,
				"КлючСвязиСерийныхНомеров") = Неопределено);

			СоотвествиеКлючей.Вставить(СтрокаТЧ.КлючСвязиСерийныхНомеров, НомерНеИспользуется);

		КонецЕсли;

		Если НомерНеИспользуется Тогда
			ТЧСерийныеНомера.Удалить(СтрокаТЧ);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ОчиститьВДокументеНеиспользуемыеСерийныеНомераПоКлючу(КлючСвязи, ТЧСерийныеНомера) Экспорт

	Количество = ТЧСерийныеНомера.Количество();
	Для Итератор = 1 По Количество Цикл

		СтрокаТЧ = ТЧСерийныеНомера[Количество - Итератор];

		Если СтрокаТЧ.КлючСвязиСерийныхНомеров <> КлючСвязи Тогда

			ТЧСерийныеНомера.Удалить(СтрокаТЧ);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция СерийныеНомераНеПринадлежатВладельцу(Номенклатура, МассивСерийныхНомеров) Экспорт

	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Результат
	|ИЗ
	|	Справочник.СерийныеНомера КАК Спр
	|ГДЕ
	|	Спр.Владелец <> &Номенклатура
	|	И Спр.Ссылка В (&МассивСерийныхНомеров)
	|");
	Запрос.УстановитьПараметр("Номенклатура"         , Номенклатура);
	Запрос.УстановитьПараметр("МассивСерийныхНомеров", МассивСерийныхНомеров);

	Возврат Не Запрос.Выполнить().Пустой();

КонецФункции

#КонецЕсли
