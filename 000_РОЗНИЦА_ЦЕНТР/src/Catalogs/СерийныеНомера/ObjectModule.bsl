////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаПроверкиЗаполнения".
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Владелец.ТипСерийногоНомера = Перечисления.ТипыСерийныхНомеровСертификатов.Штриховой Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодСерийногоНомера");
	КонецЕсли;
	
	Если Не Владелец.ТипСерийногоНомера = Перечисления.ТипыСерийныхНомеровСертификатов.Штриховой Тогда
		
		Если НЕ ЭтоНовый() Тогда
			
			ЗначенияРеквизитовВИнформационнойБазе = ОбщегоНазначенияРТ.ПолучитьЗначенияРеквизитовОбъекта(Ссылка, Новый Структура("КодСерийногоНомера"));
			
		КонецЕсли;
		
		Если (ЭтоНовый() ИЛИ ЗначенияРеквизитовВИнформационнойБазе.КодСерийногоНомера <> КодСерийногоНомера)
			И НЕ ПустаяСтрока(КодСерийногоНомера) Тогда
			
			Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			|	СерийныеНомера.Ссылка
			|ИЗ
			|	Справочник.СерийныеНомера КАК СерийныеНомера
			|ГДЕ
			|	СерийныеНомера.КодСерийногоНомера = &КодСерийногоНомера
			|	И СерийныеНомера.Ссылка <> &Ссылка");
			
			Запрос.УстановитьПараметр("КодСерийногоНомера", КодСерийногоНомера);
			Запрос.УстановитьПараметр("Ссылка"            , Ссылка);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				
				ТекстОшибки = НСтр("ru = 'Номер подарочного сертификата %1 уже существует'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, КодСерийногоНомера),
				ЭтотОбъект,
				"КодСерийногоНомера",
				,
				Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

// Процедура - обработчик события "ПередЗаписью".
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	Если ДатаСоздания = '00010101' Тогда

		Если ЭтоНовый() Тогда

			ДатаСоздания = ТекущаяДата();

		Иначе

			ДатаСоздания = ОбщегоНазначенияКлиентСервер.УникальныйИдентификаторВремя(Ссылка.УникальныйИдентификатор());

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры





