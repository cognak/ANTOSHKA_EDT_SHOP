////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура - обработчик события "ПриИзменении" поля "ОтборВладелец".
//
&НаСервере
Процедура ОтборПриИзмененииНаСервере()
	
	УстановитьОтборДинамическихСписков();
	УправлениеЭлементамиФормыНаСервере();
	
КонецПроцедуры 

// Функция возвращает массив динамических списков, для которых требуется установка отбора.
//
&НаСервере
Функция ПолучитьМассивДинамическихСписковНаСервере()

	МассивСписков = Новый Массив;
	МассивСписков.Добавить(Список);
	
	Возврат МассивСписков;

КонецФункции // ПолучитьМассивДинамическихСписковНаСервере()

// Процедура устанавливает отбор динамических списков формы.
//
&НаСервере
Процедура УстановитьОтборДинамическихСписков()
	
	Для Каждого ДинамическийСписок Из ПолучитьМассивДинамическихСписковНаСервере() Цикл
		
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ДинамическийСписок, "Владелец", ОтборВладелец , ЗначениеЗаполнено(ОтборВладелец), ВидСравненияКомпоновкиДанных.Равно);
				
	КонецЦикла;
	
КонецПроцедуры // УстановитьОтборДинамическихСписков()

// Процедура управляет видимостью и доступностью элементов формы на сервере
//
&НаСервере
Процедура УправлениеЭлементамиФормыНаСервере()
	
	Если ОтборВладелец.Пустая() Тогда
		
		ТекстЗаголовка = НСтр("ru = 'Не задан владелец серийных номеров'");
		АвтоЗаголовок = Ложь;
		Заголовок     = ТекстЗаголовка;
		Элементы.Список.ТолькоПросмотр = Истина;
 
	Иначе
		
		Если ОтборВладелец.ИспользоватьСерийныеНомера Тогда

			Заголовок = "";
			АвтоЗаголовок = Истина;	
			Элементы.Список.ТолькоПросмотр = Ложь;
			
		Иначе	
			
			ТекстЗаголовка = НСтр("ru = 'Для элемента: ""%Владелец%"" серийные номера не используются'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Владелец%", Строка(ОтборВладелец));
			АвтоЗаголовок = Ложь;
			Заголовок     = ТекстЗаголовка;
			Элементы.Список.ТолькоПросмотр = Истина;

		КонецЕсли;
			
		ТипСерийногоНомера = ОтборВладелец.ТипСерийногоНомера;
		Элементы.Штрихкод.Видимость = ТипСерийногоНомера = Перечисления.ТипыСерийныхНомеровСертификатов.Смешанный
								ИЛИ   ТипСерийногоНомера = Перечисления.ТипыСерийныхНомеровСертификатов.Штриховой;
		
	КонецЕсли;	
	
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

//Процедура - обработчик события "ПриСозданииНаСервере" формы
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;
	
	// Обработчик подсистемы "Дополнительные отчеты и обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Владелец") Тогда

		ОтборВладелец = Параметры.Отбор.Владелец;
		
	КонецЕсли;
	
	УправлениеЭлементамиФормыНаСервере();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

// Процедура - обработчик события "ПриИзменении" поля "ОтборВладелец"
//
&НаКлиенте
Процедура ОтборВладелецПриИзменении(Элемент)
	
	ОтборПриИзмененииНаСервере();
	
КонецПроцедуры


