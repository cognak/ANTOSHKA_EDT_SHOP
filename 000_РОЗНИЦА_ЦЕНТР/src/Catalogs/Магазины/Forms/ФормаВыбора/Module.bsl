
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Обработчик подсистемы "Дополнительные отчеты и обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);

	Список.Параметры.УстановитьЗначениеПараметра("ИсключительныйРежим", ТехническаяПоддержкаВызовСервера.ИсключительныйРежим());
	Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьТолькоВведенВЭксплуатацию", Истина);
	Список.Параметры.УстановитьЗначениеПараметра("Период", ТекущаяДата())
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьОформлениеЭлементов(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СозданНовыйМагазин" И ТипЗнч(Параметр) = Тип("СправочникСсылка.Магазины") Тогда
		
		Элементы.Список.Обновить();
		Элементы.Список.ТекущаяСтрока = Параметр;
		
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Копирование магазинов запрещено.'"));		
	Иначе  	
		ОткрытьФорму("Справочник.Магазины.Форма.ПомощникНового",,ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте	//	LNK 26.06.2018 15:02:53
Процедура ПоказыватьТолькоВведенВЭксплуатацию(Команда)

	Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьТолькоВведенВЭксплуатацию", НЕ Список.Параметры.Элементы.Найти("ПоказыватьТолькоВведенВЭксплуатацию").Значение);

	Попытка
		
		Для каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл

			Если ЭлементОтбора.Использование И ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВведенВЭксплуатацию") Тогда

				ЭлементОтбора.Использование = Ложь;

			КонецЕсли;

		КонецЦикла;

	Исключение

	КонецПопытки;

	Элементы.Список.Обновить();
	УстановитьОформлениеЭлементов(Истина);

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте	//	LNK 26.06.2018 15:02:48
Процедура УстановитьОформлениеЭлементов(Оповестить)

	Если Список.Параметры.Элементы.Найти("ПоказыватьТолькоВведенВЭксплуатацию").Значение Тогда

			Элементы.ФормаПоказыватьТолькоВведенВЭксплуатацию.Картинка  = БиблиотекаКартинок.ЗнакСтоп;
			ПояснениеОповещения = "Отображаются только введенные в эксплуатацию магазины";

	Иначе	Элементы.ФормаПоказыватьТолькоВведенВЭксплуатацию.Картинка  = БиблиотекаКартинок.ЗнакСтопЧБ;
			ПояснениеОповещения = "Отображаются все магазины";

	КонецЕсли;

	Если Оповестить = Истина Тогда

		ПоказатьОповещениеПользователя("Введен в эксплуатацию",, ПояснениеОповещения, БиблиотекаКартинок.Информация32);

	КонецЕсли;

КонецПроцедуры










