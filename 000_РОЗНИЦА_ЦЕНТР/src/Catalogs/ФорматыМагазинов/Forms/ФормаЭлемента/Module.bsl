////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Обработчик механизма "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	// Обработчик подсистемы "Дополнительные отчеты и обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	
	//
	ПроверитьДоступностьРеквизитов();
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ЦелевоеМестоположениеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	Элементы.ЦелевоеМестоположение.СписокВыбора.Очистить();
	МассивВариантов = ПолучитьВарианты("ФорматыМагазинов", "ЦелевоеМестоположение", "ЭтоГруппа");
	
	Для Каждого Вариант ИЗ МассивВариантов Цикл
		Элементы.ЦелевоеМестоположение.СписокВыбора.Добавить(Вариант);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТехнологияРазмещенияТовараИОбслуживанияПокупателейНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	Элементы.ТехнологияРазмещенияТовараИОбслуживанияПокупателей.СписокВыбора.Очистить();
	МассивВариантов = ПолучитьВарианты("ФорматыМагазинов", "ТехнологияРазмещенияТовараИОбслуживанияПокупателей", "ЭтоГруппа");
	
	Для Каждого Вариант ИЗ МассивВариантов Цикл
		Элементы.ТехнологияРазмещенияТовараИОбслуживанияПокупателей.СписокВыбора.Добавить(Вариант);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлощадьТорговогоЗалаМинимальнаяПриИзменении(Элемент)
	Если Объект.ПлощадьТорговогоЗалаМаксимальная < Объект.ПлощадьТорговогоЗалаМинимальная Тогда
		Если Объект.ПлощадьТорговогоЗалаМаксимальная <> 0 Тогда
			ТекстСообщения = НСтр("ru = 'Минимальная площадь не может быть больше максимальной.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Объект.ПлощадьТорговогоЗалаМаксимальная = Объект.ПлощадьТорговогоЗалаМинимальная;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПлощадьТорговогоЗалаМаксимальнаяПриИзменении(Элемент)
	Если Объект.ПлощадьТорговогоЗалаМаксимальная < Объект.ПлощадьТорговогоЗалаМинимальная Тогда
		ТекстСообщения = НСтр("ru = 'Максимальная площадь не может быть меньше минимальной.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Объект.ПлощадьТорговогоЗалаМинимальная = Объект.ПлощадьТорговогоЗалаМаксимальная;
	КонецЕсли;	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <ЦелевыеГруппыПокупателей>

&НаКлиенте
Процедура ЦелевыеГруппыПокупателейЦелеваяГруппаПокупателейНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	Элементы.ЦелевыеГруппыПокупателейЦелеваяГруппаПокупателей.СписокВыбора.Очистить();
	МассивВариантов = ПолучитьВарианты("ФорматыМагазинов.ЦелевыеГруппыПокупателей", "ЦелеваяГруппаПокупателей", "Ссылка.ЭтоГруппа");
	
	Для Каждого Вариант ИЗ МассивВариантов Цикл
		Элементы.ЦелевыеГруппыПокупателейЦелеваяГруппаПокупателей.СписокВыбора.Добавить(Вариант);
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		Результат = ОткрытьФормуМодально("Справочник.ФорматыМагазинов.Форма.РазблокированиеРеквизитов");
		Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
			
			Если ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект) Тогда
				ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтотОбъект, Результат);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// Функция формирует запрос для получения всех текстов, вводимых ранее в указанное поле
// параметры
//	ИмяТаблицы - строка, содержащее имя таблицы, из которой осуществляется выборка
//	ИмяРеквизита - имя реквизита, значения которого нужно получить
//	СтрокаУсловияГруппы - путь к значению реквизита "ЭтоГруппа" 
//							для исключения попадания в результат запроса групп
// Возвращаемое занчение 
//	МассивВариантов - массив, содержащий значения текстов.
&НаСервере
Функция ПолучитьВарианты(ИмяТаблицы, ИмяРеквизита, СтрокаУсловияГруппы)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Таблица."+ИмяРеквизита+" КАК ЗначениеВарианта
	                      |ИЗ
	                      |	Справочник."+ИмяТаблицы+" КАК Таблица
						  |ГДЕ
						  |	(НЕ Таблица."+СтрокаУсловияГруппы+")
						  |	И Таблица."+ИмяРеквизита+"<>""""
	                      |СГРУППИРОВАТЬ ПО
	                      |	Таблица."+ИмяРеквизита+"
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ЗначениеВарианта
						  |");
	РезультатЗапроса=Запрос.Выполнить();
	МассивВариантов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ЗначениеВарианта");
	
	Возврат МассивВариантов;
КонецФункции

&НаСервере
Процедура ПроверитьДоступностьРеквизитов()
	
	ДоступностьПравилаЦенообразования = ОбщегоНазначенияРТСервер.ПроверитьДоступКРеквизиту(Объект, "ПравилоЦенообразования"  , "Справочник.ПравилаЦенообразования");
	
	Элементы.ПравилоЦенообразования.Видимость   = ДоступностьПравилаЦенообразования;
	
КонецПроцедуры
