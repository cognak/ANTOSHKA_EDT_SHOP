&НаСервереБезКонтекста	//	LNK 06.06.2019 14:19:52
Функция ПолучитьЧекИзЦентральногоУзлаНаСервере(Номер, Дата)

	Подключение = СервисыСервер.Подключение("RetailPack");

	ЧтениеJSON = Новый ЧтениеJSON;	//	пытаемся получить таблицу обработанных ссылок.
	ЧтениеJSON.УстановитьСтроку(Подключение.GetCheck(Номер, Дата));

	ДанныеОтвета = СериализаторXDTO.ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();

	Если НЕ ДанныеОтвета.Ошибка Тогда

		ДокументОбъект = ДанныеОтвета.Документ;
		#Если _ Тогда
		ДокументОбъект = Документы.ЧекККМ.СоздатьДокумент();
		#КонецЕсли

		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);

		ДанныеОтвета.Документ = ДанныеОтвета.Документ.Ссылка;

	КонецЕсли;

	Возврат ДанныеОтвета;

КонецФункции

&НаКлиенте
Процедура ПолучитьЧекИзЦентральногоУзла(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("ПолучитьЧекИзЦентральногоУзлаЗавершение", ЭтотОбъект);

	ПоказатьВопрос(ОписаниеОповещения
		, "Чек ККМ будет получен из Центрального Узла!
		  |Подтвердите своё решение:"
		, РежимДиалогаВопрос.ОКОтмена
		, 60
		, КодВозвратаДиалога.ОК
		, "Web-Сервисы: Получение документа"
		, КодВозвратаДиалога.Отмена
	);


КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЧекИзЦентральногоУзлаЗавершение(КодВозврата, ДополнительныеПараметры)	Экспорт

	Если КодВозврата = КодВозвратаДиалога.ОК Тогда

		ДанныеОтвета = ПолучитьЧекИзЦентральногоУзлаНаСервере(Номер, Дата);

		Если ДанныеОтвета.Ошибка Тогда

			ПоказатьОповещениеПользователя("Ошибка получения",, ДанныеОтвета.ТекстОшибки, БиблиотекаКартинок.Ошибка32);

		Иначе

			ПоказатьОповещениеПользователя("Выполнено",, "Документ получен «" + ДанныеОтвета.Документ + "»", БиблиотекаКартинок.ПлюсЗеленый_128);

		КонецЕсли;

	Иначе

		ПоказатьОповещениеПользователя("Отмена!",, "Действие выполнено не будет", БиблиотекаКартинок.Информация32);

	КонецЕсли;

КонецПроцедуры








