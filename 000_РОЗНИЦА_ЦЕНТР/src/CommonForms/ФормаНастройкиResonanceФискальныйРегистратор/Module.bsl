#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест"
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;

	ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	ЦветОшибки = ЦветаСтиля.ЦветОтрицательногоЧисла;

	Параметры.Свойство("Идентификатор", Идентификатор);
	Заголовок = НСтр("ru='ФР'") + " «" + Строка(Идентификатор) + "»";

	Буффер = Новый Структура("ЭтоIP, Порт, Скорость, Модель, ТаблицаСоответствийНалоговыхГрупп");

	Параметры.Свойство("ЭтоIP"	 , Буффер.ЭтоIP);
	Параметры.Свойство("Порт"	 , Буффер.Порт);
	Параметры.Свойство("Скорость", Буффер.Скорость);
	Параметры.Свойство("Модель"	 , Буффер.Модель);
	Параметры.Свойство("ТаблицаСоответствийНалоговыхГрупп", Буффер.ТаблицаСоответствийНалоговыхГрупп);

	УстановитьСпискиВыбора();

	ЭтоIP	 = ?(Буффер.ЭтоIP    = Неопределено, Ложь, Буффер.ЭтоIP);
	Порт	 = ?(Буффер.Порт     = Неопределено,    1, Буффер.Порт);
	Скорость = ?(Буффер.Скорость = Неопределено,    6, Буффер.Скорость);
	Модель	 = ?(Буффер.Модель   = Неопределено, Элементы.Модель.СписокВыбора[0].Значение, Буффер.Модель);

	ЗаполнитьТаблицуСоответствийИзПараметров(Буффер.ТаблицаСоответствийНалоговыхГрупп);

	Если ТаблицаСоответствий.Количество() < 3 Тогда

		НалоговыеСтавкиИзменены = Истина;
		МенеджерОборудованияСервер.ТаблицаНалоговыхГруппПоУмолчанию(ТаблицаСоответствий);
	
	КонецЕсли; 

	Элементы.ТестУстройства.Видимость    = (ПараметрыСеанса.РабочееМестоКлиента = Идентификатор.РабочееМесто);
	Элементы.УстановитьДрайвер.Видимость = (ПараметрыСеанса.РабочееМестоКлиента = Идентификатор.РабочееМесто);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьИнформациюОДрайвере();
	УстановитьОформлениеЭлементов();

	Если НалоговыеСтавкиИзменены Тогда

		Модифицированность = Истина;
		ПоказатьОповещениеПользователя("Налоговые ставки изменены!"
		,, "Налоговые ставки установлены в состояние «По умолчанию», так как их количество было менее трёх."
		, БиблиотекаКартинок.Информация32);

	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ЭтоIPПриИзменении(Элемент)

	Если ЭтоIP Тогда

			ТипСтрока = Новый ОписаниеТипов("Строка");
			Порт = ТипСтрока.ПривестиЗначение();

	Иначе	Порт     = 1;
			Скорость = 6;	//	115200

	КонецЕсли;

	УстановитьОформлениеЭлементов();

КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)

	Если ЭтоIP Тогда

		Скорость = 0;

	КонецЕсли;

	Параметры.ПараметрыНастройки.Добавить(ЭтоIP	  , "ЭтоIP");
	Параметры.ПараметрыНастройки.Добавить(Порт	  , "Порт");
	Параметры.ПараметрыНастройки.Добавить(Скорость, "Скорость");
	Параметры.ПараметрыНастройки.Добавить(Модель  , "Модель");
	Параметры.ПараметрыНастройки.Добавить(ПолучитьПараметрыИзТаблицыСоответствий(), "ТаблицаСоответствийНалоговыхГрупп");

	ОчиститьСообщения();
	Закрыть(КодВозвратаДиалога.ОК);

КонецПроцедуры

&НаКлиенте
Процедура ТестУстройства(Команда)

	РезультатТеста    = Неопределено;

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	Результат = МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("CheckHealth",
	                                                                      ВходныеПараметры,
	                                                                      ВыходныеПараметры,
	                                                                      Идентификатор,
	                                                                      ПараметрыУстройства());

	ДополнительноеОписание = ?(ТипЗнч(ВыходныеПараметры) = Тип("Массив")
	                           И ВыходныеПараметры.Количество(),
	                           НСтр("ru = 'Дополнительное описание:'") + " " + ВыходныеПараметры[1],
	                           "");
   Если Результат Тогда

		ТекстСообщения = НСтр("ru = 'Тест успешно выполнен.%ПереводСтроки%%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПереводСтроки%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                  "",
		                                                                  Символы.ПС));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                           "",
		                                                                           ДополнительноеОписание));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

	Иначе

		ТекстСообщения = НСтр("ru = 'Тест не пройден.%ПереводСтроки%%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПереводСтроки%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                  "",
		                                                                  Символы.ПС));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                           "",
		                                                                           ДополнительноеОписание));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДрайвер(Команда)


КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте	//	LNK 05.12.2019 11:25:39
Процедура УстановитьОформлениеЭлементов()

	Если ЭтоIP Тогда

		Элементы.ЭтоIP.Заголовок = "IP      .";	//	пробелы и точка - для выравнивания элемента
		Элементы.Порт.Ширина = 20;
		Элементы.Порт.КнопкаВыбора = Ложь;
		Элементы.Порт.РежимВыбораИзСписка = Ложь;
		Элементы.ЭтоIP.Подсказка = "Нажмите для переключения на выбор COM-порта";
		Элементы.Скорость.Видимость = Ложь;

		Элементы.Порт.СписокВыбора.Очистить();

	Иначе

		Элементы.ЭтоIP.Заголовок = " Порт";
		Элементы.Порт.Ширина = 10;
		Элементы.Порт.КнопкаВыбора = Истина;
		Элементы.Порт.РежимВыбораИзСписка = Истина;
		Элементы.ЭтоIP.Подсказка = "Нажмите для переключения на ввод IP адреса";
		Элементы.Скорость.Видимость = Истина;

		УстановитьСпискиВыбора();

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюОДрайвере()

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	Если МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("ПолучитьВерсиюДрайвера",
	                                                               ВходныеПараметры,
	                                                               ВыходныеПараметры,
	                                                               Идентификатор,
	                                                               ПараметрыУстройства()) Тогда
		Драйвер = ВыходныеПараметры[0];
		Версия  = ВыходныеПараметры[1];

	Иначе

		Драйвер = ВыходныеПараметры[2];
		Версия  = НСтр("ru='Не определена'");

	КонецЕсли;

	Элементы.Драйвер.ЦветТекста = ?(Драйвер = НСтр("ru='Не установлен'"), ЦветОшибки, ЦветТекста);
	Элементы.Версия.ЦветТекста  = ?(Версия  = НСтр("ru='Не определена'"), ЦветОшибки, ЦветТекста);

	Элементы.УстановитьДрайвер.Доступность = НЕ (Драйвер = НСтр("ru='Установлен'"));

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСоответствийИзПараметров(ТаблицаСоответствийИзПараметров = Неопределено)
	
	ТаблицаСоответствий.Очистить();
	
	Если ТаблицаСоответствийИзПараметров = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Элемент Из ТаблицаСоответствийИзПараметров Цикл
		
		Строка = ТаблицаСоответствий.Добавить();
		
		Строка.НалоговаяГруппаРРО = Элемент[0];
		Строка.СтавкаНДС          = НДСОбщегоНазначенияКлиентСервер.ПолучитьПоСтрокеСтавкуНДС(Элемент[1]);
		Строка.ПодакцизныйТовар   = Элемент[2];
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыИзТаблицыСоответствий()
	
	Результат = Новый Массив;
	
	Для каждого Строка Из ТаблицаСоответствий Цикл
		
		Элемент = Новый Массив(3);
		
		Элемент[0] = Строка.НалоговаяГруппаРРО;
		Элемент[1] = НДСОбщегоНазначенияКлиентСервер.ПолучитьСтавкуНДССтрокой(Строка.СтавкаНДС);
		Элемент[2] = Строка.ПодакцизныйТовар;
		
		Результат.Добавить(Элемент);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере	//	LNK 09.12.2019 09:52:40
Процедура УстановитьСпискиВыбора()

	Элементы.Порт.СписокВыбора.Очистить();
	Элементы.Скорость.СписокВыбора.Очистить();
	Элементы.Модель.СписокВыбора.Очистить();

	Для Индекс = 1 По 99 Цикл

		Элементы.Порт.СписокВыбора.Добавить(Индекс, "COM" + СокрЛП(Индекс));

	КонецЦикла;

	Элементы.Скорость.СписокВыбора.Добавить(1,   "4800");
	Элементы.Скорость.СписокВыбора.Добавить(2,   "9600");
	Элементы.Скорость.СписокВыбора.Добавить(3,  "19200");
	Элементы.Скорость.СписокВыбора.Добавить(4,  "38400");
	Элементы.Скорость.СписокВыбора.Добавить(5,  "57600");
	Элементы.Скорость.СписокВыбора.Добавить(6, "115200");

	Элементы.Модель.СписокВыбора.Добавить("301МТМ", "Марія 301МТМ");
	Элементы.Модель.СписокВыбора.Добавить("304Т"  , "Марія 304Т");
	Элементы.Модель.СписокВыбора.Добавить("304Т1" , "Марія 304Т1");
	Элементы.Модель.СписокВыбора.Добавить("304Т2" , "Марія 304Т2");
	Элементы.Модель.СписокВыбора.Добавить("304Т3" , "Марія 304Т3");

КонецПроцедуры

&НаКлиенте
Функция ПараметрыУстройства()

	ПараметрыУстройства = Новый Структура();
	ПараметрыУстройства.Вставить("ЭтоIP"	, ЭтоIP);
	ПараметрыУстройства.Вставить("Порт"		, Порт);
	ПараметрыУстройства.Вставить("Скорость"	, Скорость);
	ПараметрыУстройства.Вставить("Модель"	, Модель);
	ПараметрыУстройства.Вставить("ТаблицаСоответствийНалоговыхГрупп", ПолучитьПараметрыИзТаблицыСоответствий());

	Возврат ПараметрыУстройства;

КонецФункции

#КонецОбласти











