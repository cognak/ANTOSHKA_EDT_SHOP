// Текущая форма оставлена для совместимости, используется обработка подсистемы контактной информации
//

////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВидКонтактнойИнформации = Параметры.Вид;
	
	ИмяФормыВидаКонтактнойИнформации = КонтактнаяИнформацияКлиентСерверПовтИсп.ИмяФормыВводаКонтактнойИнформации(ВидКонтактнойИнформации);
	
	// Неоябязатиельное требование преобразование
	Параметры.Свойство("ВозвращатьСписокЗначений", ВозвращатьСписокЗначений);
	
	// Транслируем параметры
	ПараметрыФормыВидаКонтактнойИнформации = Новый Структура;
	ПараметрыФормыВидаКонтактнойИнформации.Вставить("ВидКонтактнойИнформации", ВидКонтактнойИнформации);
	
	ПараметрыФормыВидаКонтактнойИнформации.Вставить("Заголовок",     Параметры.Заголовок);
	ПараметрыФормыВидаКонтактнойИнформации.Вставить("ЗначенияПолей", Параметры.ЗначенияПолей);
	ПараметрыФормыВидаКонтактнойИнформации.Вставить("Представление", Параметры.Представление);
	Если Параметры.Свойство("Комментарий") Тогда
		ПараметрыФормыВидаКонтактнойИнформации.Вставить("Комментарий",   Параметры.Комментарий);
	КонецЕсли;
	
	ПараметрыФормыВидаКонтактнойИнформации.Вставить("ЗакрыватьПриВыборе",            ЗакрыватьПриВыборе);
	ПараметрыФормыВидаКонтактнойИнформации.Вставить("ЗакрыватьПриЗакрытииВладельца", ЗакрыватьПриЗакрытииВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Отказ = Истина;
	
	Результат = ОткрытьФормуМодально(
		ИмяФормыВидаКонтактнойИнформации, ПараметрыФормыВидаКонтактнойИнформации, ВладелецФормы
	);
	
	Если ТипЗнч(Результат)=Тип("Структура") Тогда
		КонтактнаяИнформация = Результат.КонтактнаяИнформация;
		Результат.Удалить("КонтактнаяИнформация");
		
		ЭтоСписок = ТипЗнч(КонтактнаяИнформация)=Тип("СписокЗначений");
		Если ЭтоСписок И ВозвращатьСписокЗначений Тогда
			// Был список, хотят список
			Результат.Вставить("ЗначенияПолей", КонтактнаяИнформация);
		ИначеЕсли ЭтоСписок Тогда
			// Был список, хотят не список
			Результат.Вставить("ЗначенияПолей", ЗначениеПолейВXML(КонтактнаяИнформация, ВидКонтактнойИнформации));
		ИначеЕсли ВозвращатьСписокЗначений Тогда
			// Не список, хотят список
			Результат.Вставить("ЗначенияПолей", ЗначениеПолейВСписок(КонтактнаяИнформация));
		Иначе
			// Не список, хотят не список
			Результат.Вставить("ЗначенияПолей", КонтактнаяИнформация);
		КонецЕсли;
	КонецЕсли;
	
	Закрыть(Результат);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

&НаСервереБезКонтекста
Функция ЗначениеПолейВСписок(КонтактнаяИнформацияXML)
	Результат = КонтактнаяИнформацияСлужебный.КонтактнаяИнформацияВСтаруюСтруктуру(КонтактнаяИнформацияXML);
	Возврат Результат.ЗначенияПолей;
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеПолейВXML(КонтактнаяИнформацияСписком, ВидКонтактнойИнформации)
	Возврат КонтактнаяИнформацияСлужебный.СериализацияКонтактнойИнформации(
		КонтактнаяИнформацияСлужебный.ДесериализацияКонтактнойИнформации(КонтактнаяИнформацияСписком, ВидКонтактнойИнформации)
	);
КонецФункции