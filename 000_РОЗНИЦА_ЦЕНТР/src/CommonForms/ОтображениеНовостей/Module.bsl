////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ОткрытьВсеНовости") Тогда
		ОткрытьВсеНовости();
	ИначеЕсли Параметры.Свойство("ОткрытьНовость") Тогда
		Если Параметры.Свойство("Идентификатор") Тогда 
			ОткрытьНовость(Параметры.Идентификатор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура НажатиеНаНовость(Элемент)
	
	МассивНовостей = ТаблицаВсехНовостей.НайтиСтроки(Новый Структура("ИмяНовости", Элемент.Имя));
	Если МассивНовостей.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Идентификатор = МассивНовостей.Получить(0).Идентификатор;
	ВнешняяСсылка = МассивНовостей.Получить(0).ВнешняяСсылка;
	Если Не ПустаяСтрока(ВнешняяСсылка) Тогда 
		ПерейтиПоНавигационнойСсылке(ВнешняяСсылка);
		Возврат;
	КонецЕсли;
	
    ОткрытьНовость(Идентификатор);
 	
КонецПроцедуры

&НаКлиенте
Процедура ВсеНовостиНажатие(Элемент)
	
	Закрыть();
	ИнформационныйЦентрКлиент.ПоказатьВсеСообщения();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОткрытьВсеНовости()
	
	Заголовок = НСтр("ru = 'Новости'");
	СформироватьСписокВсехНовостей();
	Элементы.ОбщаяГруппа.ТекущаяСтраница = Элементы.ГруппаНовостей;
	
КонецПроцедуры	

&НаСервере
Процедура ОткрытьНовость(Идентификатор)
	
	Элементы.ОбщаяГруппа.ТекущаяСтраница = Элементы.ГруппаНовости;
	УстановитьПривилегированныйРежим(Истина);
	СсылкаНаДанные	= Справочники.ОбщиеДанныеИнформационногоЦентра.НайтиПоРеквизиту("Идентификатор", Идентификатор);
	Если СсылкаНаДанные.Пустая() Тогда 
		Возврат;
	КонецЕсли;	
		
	Заголовок	= СсылкаНаДанные.Наименование;
	
	Вложения	= СсылкаНаДанные.Вложения.Получить();
	ТекстHTML	= СсылкаНаДанные.ТекстHTML;
	
	Если ТипЗнч(Вложения) = Тип("Структура") Тогда 
		СтруктруаВложений = Вложения;
	Иначе	
		СтруктруаВложений = Новый Структура;
	КонецЕсли;	
	
	Содержание.УстановитьHTML(ТекстHTML, СтруктруаВложений);
	
КонецПроцедуры	

&НаСервере
Процедура СформироватьСписокВсехНовостей()
	
	ТаблицаВсехНовостей.Очистить();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВозвращаемаяТаблица = ИнформационныйЦентрСервер.СформироватьСписокВсехНовостей();
	
	Если ВозвращаемаяТаблица.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;	
	
	ТаблицаВсехНовостей.Загрузить(ВозвращаемаяТаблица);
	
	ГруппаНовостей = Элементы.ГруппаВсехНовостей;
	
	Для Итерация = 0 По ТаблицаВсехНовостей.Количество() - 1 Цикл
		
		Критичность		= ТаблицаВсехНовостей.Получить(Итерация).Критичность;
		Наименование	= ТаблицаВсехНовостей.Получить(Итерация).Наименование;
		Картинка		= ?(Критичность > 5, БиблиотекаКартинок.СостояниеОбменаДаннымиОшибка, БиблиотекаКартинок.Информация_старая);
		
		ГруппаНовости						= Элементы.Добавить("ГруппаНовости" + Строка(Итерация), Тип("ГруппаФормы"), ГруппаНовостей);
		ГруппаНовости.Вид 					= ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаНовости.ОтображатьЗаголовок	= Ложь;
		ГруппаНовости.Группировка			= ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		ГруппаНовости.Отображение			= ОтображениеОбычнойГруппы.Нет;
		
		КартинкаНовости						= Элементы.Добавить("КартинкаНовости" + Строка(Итерация), Тип("ДекорацияФормы"), ГруппаНовости);
		КартинкаНовости.Вид					= ВидДекорацииФормы.Картинка;
		КартинкаНовости.Картинка			= Картинка;
		КартинкаНовости.Ширина				= 2;
		КартинкаНовости.Высота				= 1;
		КартинкаНовости.РазмерКартинки		= РазмерКартинки.Растянуть;
		
		ИмяНовости							= Элементы.Добавить("ИмяНовости" + Строка(Итерация), Тип("ДекорацияФормы"), ГруппаНовости);
		ИмяНовости.Вид						= ВидДекорацииФормы.Надпись;
		ИмяНовости.Заголовок				= Наименование;
		ИмяНовости.РастягиватьПоГоризонтали	= Истина;
		ИмяНовости.ВертикальноеПоложение	= ВертикальноеПоложениеЭлемента.Центр;
		ИмяНовости.ВысотаЗаголовка			= 1;
		ИмяНовости.Гиперссылка	            = Истина;
		ИмяНовости.УстановитьДействие("Нажатие", "НажатиеНаНовость");
		
		Если Критичность = 10 Тогда 
			ИмяНовости.Шрифт = Новый Шрифт(, , Истина, , , );
		КонецЕсли;	
		
		ТаблицаВсехНовостей.Получить(Итерация).ИмяНовости = "ИмяНовости" + Строка(Итерация);
	
	КонецЦикла;	
	
КонецПроцедуры	



