
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Характеристика.Видимость = Параметры.ОтображатьИнформациюОСкидкахПоСтроке;
	Элементы.Номенклатура.Видимость   = Параметры.ОтображатьИнформациюОСкидкахПоСтроке;
	
	Элементы.СуммаРучнойСкидки.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьРучныеСкидкиВПродажах");
	
	Валюта = "UAH";
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	
	Если Параметры.ОтображатьИнформациюОСкидкахПоСтроке Тогда
		
		Номенклатура   = Параметры.ТекущиеДанные.Номенклатура;
		Характеристика = Параметры.ТекущиеДанные.Характеристика;
		
		// Общая сумма скидки включает в себя сумму автоматической и ручной скидки.
		СуммаСкидки       = 0;
		СуммаРучнойСкидки = Параметры.ТекущиеДанные.СуммаРучнойСкидки;
		СуммаСкидки       = СуммаСкидки + СуммаРучнойСкидки;
		
		Отбор = Новый Структура("КлючСвязи", Параметры.ТекущиеДанные.КлючСвязи);
		Для Каждого СтрокаТЗСкидкиНаценки Из Параметры.Объект.СкидкиНаценки.НайтиСтроки(Отбор) Цикл
			
			НоваяСтрока = АвтоматическиеСкидки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗСкидкиНаценки);
			Если Параметры.ТекущиеДанные.СуммаБезСкидки <> 0 Тогда
				НоваяСтрока.Процент = 100 * СтрокаТЗСкидкиНаценки.Сумма / Параметры.ТекущиеДанные.СуммаБезСкидки;
			КонецЕсли;
			
			СуммаСкидки = СуммаСкидки + НоваяСтрока.Сумма;
			
		КонецЦикла;
		
	Иначе
		
		СуммаСкидки       = 0;
		СуммаРучнойСкидки = Параметры.Объект.Товары.Итог("СуммаРучнойСкидки");
		СуммаСкидки       = СуммаСкидки + СуммаРучнойСкидки + Параметры.Объект.Товары.Итог("СуммаАвтоматическойСкидки");
		
	КонецЕсли;
	
	Параметры.Свойство("МинимальнаяЦена", МинимальнаяЦена);
	
	// Информация может быть отображена только после непосредственного расчета скидок в документе.
	// После закрытия формы эта информация не сохраняется.
	Если ЗначениеЗаполнено(Параметры.АдресПримененныхСкидокВоВременномХранилище) Тогда
		
		ДанныеРасчетаСкидок = ПолучитьИзВременногоХранилища(Параметры.АдресПримененныхСкидокВоВременномХранилище);
		
		Если Параметры.ОтображатьСегментИсключение Тогда
			ОтобразитьНадписьСегменатИсключения();
		КонецЕсли;
	
		Если Параметры.ОтображатьИнформациюОРасчетеСкидокПоСтроке И НЕ ВходитВСегментИсключение Тогда 
			СформироватьИнформациюОРасчетеСкидокПоСтроке(ДанныеРасчетаСкидок[0].Результаты, Параметры.ТекущиеДанные.КлючСвязи, АвтоматическиеСкидки.Выгрузить());
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоДоУсловийРекурсивно(СтрокаДерева, ЭлементФормы)
	
	КоллекцияЭлементов = СтрокаДерева.ПолучитьЭлементы();
	Для каждого Элемент Из КоллекцияЭлементов Цикл
	
		Если Элемент.Разворачивать Тогда
			ЭлементФормы.Развернуть(Элемент.ПолучитьИдентификатор());
			РазвернутьДеревоДоУсловийРекурсивно(Элемент, ЭлементФормы);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазвернутьДеревоДоУсловийРекурсивно(ИнформацияОРасчетеСкидокПоСтроке, Элементы.ИнформацияОРасчетеСкидокПоСтроке);
	
КонецПроцедуры

&НаСервере
Процедура РазложитьПоКолонкам(СтрокаДерева, НайденнаяСтрока)
	
	Для каждого СтрокаРасшифровки Из НайденнаяСтрока.Расшифровка Цикл
		СтрокаДерева.СуммаАвтоматическойСкидки = СтрокаДерева.СуммаАвтоматическойСкидки + СтрокаРасшифровки.Сумма;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИнформациюОРасчетеСкидокПоСтроке(ВходящееДеревоСкидок, ДеревоСкидок, КлючСвязи, ТаблицаАвтоматическиеСкидки)
	
	Для Каждого СтрокаДерева Из ДеревоСкидок.Строки Цикл
		
		Если СтрокаДерева.ЭтоГруппа Тогда
			
			РассчитатьИнформациюОРасчетеСкидокПоСтроке(ВходящееДеревоСкидок, СтрокаДерева, КлючСвязи, ТаблицаАвтоматическиеСкидки);
			
			СтрокаДерева.ИндексКартинки = СкидкиНаценкиСервер.ПолучитьИндексКартинкиДляГруппы(СтрокаДерева);
			СтрокаДерева.Значение = СтрокаДерева.СкидкаНаценка;
			СтрокаДерева.Разворачивать = Истина;
			
			Для каждого Стр Из СтрокаДерева.Строки Цикл
				Если Стр.Действует Тогда
					СтрокаДерева.Действует        = Истина;
					СтрокаДерева.УсловияВыполнены = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			СтрокаДерева.СуммаАвтоматическойСкидки = СтрокаДерева.Строки.Итог("СуммаАвтоматическойСкидки");
			
		Иначе
			
			СтрокаДерева.ИндексКартинки = СкидкиНаценкиСервер.ПолучитьИндексКартинкиДляСкидки(СтрокаДерева);
			СтрокаДерева.Значение = СтрокаДерева.СкидкаНаценка;
			
			ВсеУсловияВыполнены = Истина;
			Для каждого СтрокаУсловие Из СтрокаДерева.ПараметрыУсловий.ТаблицаУсловий Цикл
				
				НоваяСтрокаУсловие = СтрокаДерева.Строки.Добавить();
				НоваяСтрокаУсловие.Значение       = СтрокаУсловие.УсловиеПредоставления;
				НоваяСтрокаУсловие.ЭтоУсловие     = Истина;
				
				Если СтрокаУсловие.ОбластьОграничения = Перечисления.ВариантыОбластейОграниченияСкидокНаценок.ВСтроке Тогда
					НайденныеСтрокиТаблицыПроверкиУсловий = СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.ТаблицаПроверкиУсловий.Найти(КлючСвязи, "КлючСвязи");
					Если НайденныеСтрокиТаблицыПроверкиУсловий <> Неопределено Тогда
						НазваниеКолонки = СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.СоответствиеУсловийКолонкамТаблицыПроверкиУсловий.Получить(СтрокаУсловие.УсловиеПредоставления);
						Если НазваниеКолонки <> Неопределено Тогда
							НоваяСтрокаУсловие.Действует = НайденныеСтрокиТаблицыПроверкиУсловий[НазваниеКолонки];
						КонецЕсли;
					Иначе
						
					КонецЕсли;
				Иначе
					НоваяСтрокаУсловие.Действует = СтрокаУсловие.Выполнено;
				КонецЕсли;
				
				НоваяСтрокаУсловие.ИндексКартинки = -1;
				
				НоваяСтрокаУсловие.УсловияВыполнены = НоваяСтрокаУсловие.Действует;
				Если НЕ НоваяСтрокаУсловие.Действует Тогда
					ВсеУсловияВыполнены = Ложь;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ВсеУсловияВыполнены Тогда
				Если СтрокаДерева.Управляемая Тогда
					СтрокаДерева.Действует = СтрокаДерева.НазначенаПользователем;
				Иначе
					СтрокаДерева.Действует = Истина;
				КонецЕсли;
				СтрокаДерева.УсловияВыполнены = Истина;
				
				Если СтрокаДерева.Управляемая И НЕ СтрокаДерева.НазначенаПользователем Тогда
				Иначе
					Если ВходящееДеревоСкидок.ТаблицаСкидкиНаценки.НайтиСтроки(Новый Структура("КлючСвязи, СкидкаНаценка", КлючСвязи, СтрокаДерева.СкидкаНаценка)).Количество() = 0 Тогда
						СтрокаДерева.НеПримениласьПоУсловиямСовместногоПрименения = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Подарок Тогда
					Если СтрокаДерева.СкидкаНаценка.УчитыватьПодарокКакПродажу Тогда
						
						СтруктураПоиска = Новый Структура("СкидкаНаценка", СтрокаДерева.СкидкаНаценка);
						
						СтрокиПоиска = ТаблицаАвтоматическиеСкидки.НайтиСтроки(СтруктураПоиска);
						
						СтрокаДерева.СуммаАвтоматическойСкидки = 0;
						
						Для каждого СтрокаПоиска Из СтрокиПоиска Цикл
							
							СтрокаДерева.СуммаАвтоматическойСкидки = СтрокаДерева.СуммаАвтоматическойСкидки + СтрокаПоиска.Сумма;
							
						КонецЦикла;
					КонецЕсли;
					НайденныеСтроки = СтрокаДерева.ТаблицаДанных.НайтиСтроки(Новый Структура("КлючСвязи", КлючСвязи));
					Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
						РазложитьПоКолонкам(СтрокаДерева, НайденнаяСтрока);
					КонецЦикла;
				Иначе
					СтруктураПоиска = Новый Структура("СкидкаНаценка", СтрокаДерева.СкидкаНаценка);
					СтрокиПоиска = ТаблицаАвтоматическиеСкидки.НайтиСтроки(СтруктураПоиска);
					СтрокаДерева.СуммаАвтоматическойСкидки = 0;
					Для каждого СтрокаПоиска Из СтрокиПоиска Цикл
						СтрокаДерева.СуммаАвтоматическойСкидки = СтрокаДерева.СуммаАвтоматическойСкидки + СтрокаПоиска.Сумма;
						Если СтрокаПоиска.ОграниченаМинимальнойЦеной Тогда
							СтрокаДерева.ОграниченаМинимальнойЦеной = Истина;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
			Иначе
				СтрокаДерева.УсловияВыполнены = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // РассчитатьИнформациюОРасчетеСкидокПоДокументу()

&НаСервере
Процедура СформироватьИнформациюОРасчетеСкидокПоСтроке(ВходящееДеревоСкидок, КлючСвязи, ТаблицаАвтоматическиеСкидки)
	
	ДеревоСкидок = ВходящееДеревоСкидок.ДеревоСкидок.Скопировать();
	ДеревоСкидок.Колонки.Добавить("ИндексКартинки",   Новый ОписаниеТипов("Число"));
	ДеревоСкидок.Колонки.Добавить("Действует",        Новый ОписаниеТипов("Булево"));
	ДеревоСкидок.Колонки.Добавить("УсловияВыполнены", Новый ОписаниеТипов("Булево"));
	ДеревоСкидок.Колонки.Добавить("НеПримениласьПоУсловиямСовместногоПрименения", Новый ОписаниеТипов("Булево"));
	ДеревоСкидок.Колонки.Добавить("Разворачивать",  Новый ОписаниеТипов("Булево"));
	ДеревоСкидок.Колонки.Добавить("ЭтоУсловие",     Новый ОписаниеТипов("Булево"));
	ДеревоСкидок.Колонки.Добавить("Значение",       Новый ОписаниеТипов("СправочникСсылка.СкидкиНаценки, СправочникСсылка.УсловияПредоставленияСкидокНаценок"));
	ДеревоСкидок.Колонки.Добавить("СуммаАвтоматическойСкидки", Новый ОписаниеТипов("Число"));
	ДеревоСкидок.Колонки.Добавить("ОграниченаМинимальнойЦеной", Новый ОписаниеТипов("Булево"));
	
	РассчитатьИнформациюОРасчетеСкидокПоСтроке(ВходящееДеревоСкидок, ДеревоСкидок, КлючСвязи, ТаблицаАвтоматическиеСкидки);
	
	ЗначениеВРеквизитФормы(ДеревоСкидок, "ИнформацияОРасчетеСкидокПоСтроке");
	
КонецПроцедуры // СформироватьИнформациюОРасчетеСкидокПоДокументу()

&НаКлиенте
Процедура ИнформацияОРасчетеСкидокПоСтрокеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ИнформацияОРасчетеСкидокПоСтрокеЗначениеОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////
// Служебные

&НаСервере
Процедура ОтобразитьНадписьСегменатИсключения()
	
	ТипНоменклатуры = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Параметры.ТекущиеДанные.Номенклатура, "ТипНоменклатуры");
	Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат Тогда
		Элементы.ТекстОбИСключениях.Видимость = Истина;
		ВходитВСегментИсключение = Истина;
		ТекстЗаголовка = НСтр("ru = 'Номенклатура %1 является подарочным сертификатом.'");
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка,
																						Параметры.ТекущиеДанные.Номенклатура);
		ТекстЗаголовка = ТекстЗаголовка	+ Символы.ПС + НСтр("ru = 'Автоматические и управляемые скидки в данной строке не рассчитываются.'");
		Элементы.ТекстОбИСключениях.Заголовок = ТекстЗаголовка;
		Возврат;
	КонецЕсли;
	
	СегментИсключаемойНоменклатуры = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Параметры.Объект.Магазин, "СегментИсключаемойНоменклатуры");
	Если ЗначениеЗаполнено(СегментИсключаемойНоменклатуры) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НоменклатураСегмента.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|ГДЕ
		|	НоменклатураСегмента.Сегмент = &Сегмент
		|	И НоменклатураСегмента.Номенклатура = &Номенклатура
		|	И НоменклатураСегмента.Характеристика = &Характеристика
		|";
		Запрос.УстановитьПараметр("Сегмент", СегментИсключаемойНоменклатуры);
		Запрос.УстановитьПараметр("Номенклатура", Параметры.ТекущиеДанные.Номенклатура);
		Запрос.УстановитьПараметр("Характеристика", Параметры.ТекущиеДанные.Характеристика);
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			Элементы.ТекстОбИСключениях.Видимость = Истина;
			ВходитВСегментИсключение = Истина;
			Если ЗначениеЗаполнено(Параметры.ТекущиеДанные.Характеристика) Тогда
				ТекстЗаголовка = НСтр("ru = 'Номенклатура %1 с характеристикой %2 входит в состав сегмента-исключения скидок.'");
				ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка,
																							Параметры.ТекущиеДанные.Номенклатура,
																							Параметры.ТекущиеДанные.Характеристика);
			Иначе
				ТекстЗаголовка = НСтр("ru = 'Номенклатура %1 входит в состав сегмента-исключения скидок.'");
				ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка,
																							Параметры.ТекущиеДанные.Номенклатура);
			КонецЕсли;
			ТекстЗаголовка = ТекстЗаголовка	+ Символы.ПС + НСтр("ru = 'Автоматические и управляемые скидки в данной строке не рассчитываются.'");
			Элементы.ТекстОбИСключениях.Заголовок = ТекстЗаголовка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
