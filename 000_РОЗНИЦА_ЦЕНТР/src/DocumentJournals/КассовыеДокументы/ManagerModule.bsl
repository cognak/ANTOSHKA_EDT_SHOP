#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

//	LNK 03.07.2017 17:25:54
Процедура СформироватьЖурналРегистрацииРасходныхИПриходных(ПараметрыОтчета, ТабличноеПоле)	Экспорт

	ТабличныйДокумент = Новый ТабличныйДокумент; // Итоговый табличный документ листа кассовой книги
	ДокументРезультат = Новый ТабличныйДокумент; // Промежуточный табличный документ листа кассовой книги
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	//Вывод шапки
	Макет = ПолучитьМакет("ЖурналРегистрации3А");
	
	// Печать обложки и титульного листа
	СформироватьОбложкуЖурналРегистрации3а(ДокументРезультат, ПараметрыОтчета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ДокументРезультат.Вывести(ОбластьМакета);
	
	//Запрос по ордерам
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала"   , НачалоДня(ПараметрыОтчета.ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ПараметрыОтчета.ДатаОкончания));
	Запрос.УстановитьПараметр("Касса"        , ПараметрыОтчета.Касса);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(РасходныйКассовыйОрдер.Дата, ДЕНЬ) КАК ДеньРасход,
	|	РасходныйКассовыйОрдер.Ссылка КАК ОрдерРасход,
	|	РасходныйКассовыйОрдер.НомерОрдера КАК НомерОрдераРасход,
	|	РасходныйКассовыйОрдер.СуммаДокумента КАК СуммаРасход,
	|	РасходныйКассовыйОрдер.Комментарий КАК КомментарийРасход
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|ГДЕ
	|	РасходныйКассовыйОрдер.ПометкаУдаления = ЛОЖЬ
	|	И РасходныйКассовыйОрдер.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РасходныйКассовыйОрдер.Касса = &Касса
	|	И РасходныйКассовыйОрдер.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДеньРасход,
	|	РасходныйКассовыйОрдер.НомерОрдера";
				 
				 
	РезультатРасход = Запрос.Выполнить().Выбрать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ПриходныйКассовыйОрдер.Дата, ДЕНЬ) КАК ДеньПриход,
	|	ПриходныйКассовыйОрдер.Ссылка КАК ОрдерПриход,
	|	ПриходныйКассовыйОрдер.НомерОрдера КАК НомерОрдераПриход,
	|	ПриходныйКассовыйОрдер.СуммаДокумента КАК СуммаПриход,
	|	ПриходныйКассовыйОрдер.Комментарий КАК КомментарийПриход
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ПриходныйКассовыйОрдер.ПометкаУдаления = ЛОЖЬ
	|	И ПриходныйКассовыйОрдер.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ПриходныйКассовыйОрдер.Касса = &Касса
	|	И ПриходныйКассовыйОрдер.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДеньПриход,
	|	ПриходныйКассовыйОрдер.НомерОрдера";	
				 
	РезультатПриход =Запрос.Выполнить().Выбрать();	
	
	НомерЛиста = 1;
	
	Пока 1=1 Цикл
		
		ЕстьЗапись = Ложь;
		
		Если РезультатПриход.Следующий() Тогда
			
			ЕстьЗапись = Истина;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			ОбластьМакета.Параметры.ДатаПриход  		= РезультатПриход.ДеньПриход;
			ОбластьМакета.Параметры.НомерПриход 		= РезультатПриход.НомерОрдераПриход;
			ОбластьМакета.Параметры.ОрдерПриход			= РезультатПриход.ОрдерПриход;
			ОбластьМакета.Параметры.СуммаПриход 		= РезультатПриход.СуммаПриход;
			ОбластьМакета.Параметры.КомментарийПриход 	= РезультатПриход.КомментарийПриход;
			
		КонецЕсли;	
		
		Если РезультатРасход.Следующий() Тогда
			
			Если Не ЕстьЗапись Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Строка");	
			КонецЕсли;
			
			ЕстьЗапись = Истина;
			
			ОбластьМакета.Параметры.ДатаРасход  		= РезультатРасход.ДеньРасход;
			ОбластьМакета.Параметры.НомерРасход 		= РезультатРасход.НомерОрдераРасход;
			ОбластьМакета.Параметры.ОрдерРасход 		= РезультатРасход.ОрдерРасход;
			ОбластьМакета.Параметры.СуммаРасход 	 	= РезультатРасход.СуммаРасход;
			ОбластьМакета.Параметры.КомментарийРасход 	= РезультатРасход.КомментарийРасход;
			
		КонецЕсли;	
		
		Если ЕстьЗапись Тогда
			Если НЕ ДокументРезультат.ПроверитьВывод(ОбластьМакета) Тогда
				
				// Выведем разрыв страницы
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				
				НомерЛистаДляПерехода = НомерЛиста;
				НомерЛиста = НомерЛиста + 1;
				ИмяОбласти = "Лист_" + Прав("0000000" + Формат(НомерЛистаДляПерехода, "ЧН=; ЧГ="), 7);
				ОбластьЛисты = ДокументРезультат.Область(1,, ДокументРезультат.ВысотаТаблицы,);
				ОбластьЛисты.Имя = ИмяОбласти;
				//ОбластьЛисты.СоздатьФорматСтрок();
				ТабличныйДокумент.Вывести(ДокументРезультат);
			//	ПараметрыОтчета.СписокСформированныхЛистов.Добавить("3" + ИмяОбласти, "Лист " + Формат(НомерЛистаДляПерехода, "ЧН=; ЧГ="));
				
				ДокументРезультат = Новый ТабличныйДокумент;
				ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
				
			КонецЕсли;
			ДокументРезультат.Вывести(ОбластьМакета);
		Иначе
			Прервать;	
		КонецЕсли; 
	
	КонецЦикла; 
	
	ИмяОбласти = "Лист_" + Прав("0000000" + Формат(НомерЛиста, "ЧН=; ЧГ="), 7);
	ОбластьЛисты = ДокументРезультат.Область(1,, ДокументРезультат.ВысотаТаблицы,);
	ОбластьЛисты.Имя = ИмяОбласти;
	ОбластьЛисты.СоздатьФорматСтрок();
	ДокументРезультат.ТолькоПросмотр = Истина;
	ТабличныйДокумент.Вывести(ДокументРезультат);
//	ПараметрыОтчета.СписокСформированныхЛистов.Добавить("3" + ИмяОбласти, "Лист " + Формат(НомерЛиста, "ЧН=; ЧГ="));
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	ТабличноеПоле.Очистить();
	ТабличноеПоле.Вывести(ТабличныйДокумент);
//	ПараметрыОтчета.Вставить("ТабличныйДокумент", ТабличныйДокумент);

КонецПроцедуры

Процедура СформироватьОбложкуЖурналРегистрации3а(ТабличныйДокумент, ПараметрыОтчета)
	
	Если НЕ ПараметрыОтчета.СформироватьОбложку Тогда
		Возврат;
	КонецЕсли;
	
	ИмяОбласти      = "Обложка";
	Обложка         = Новый ТабличныйДокумент;
	МакетОбложка    = ПолучитьМакет("ОбложкаЖурналРегистрации3а");
	ОбластьОбложка  = МакетОбложка.ПолучитьОбласть(ИмяОбласти);

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Кассы.Владелец.НаименованиеПолное КАК НаименованиеПолное,
	|	Кассы.Владелец.КодПоЕДРПОУ КАК КодПоЕДРПОУ
	|ИЗ
	|	Справочник.Кассы КАК Кассы
	|ГДЕ
	|	Кассы.Ссылка = &Касса"
	);
	Запрос.УстановитьПараметр("Касса", ПараметрыОтчета.Касса);
	
	СведенияОбОрганизации = Запрос.Выполнить().Выбрать();
	СведенияОбОрганизации.Следующий();

	ОбластьОбложка.Параметры.Год				 	 = Год(ПараметрыОтчета.ДатаНачала);
	ОбластьОбложка.Параметры.КодЕДРПОУ 				 = СведенияОбОрганизации.КодПоЕДРПОУ;
	ОбластьОбложка.Параметры.НаименованиеОрганизации = СведенияОбОрганизации.НаименованиеПолное;
	
	Обложка.Вывести(ОбластьОбложка);
	ОблОбложка = Обложка.Область(1,, Обложка.ВысотаТаблицы,);
	ОблОбложка.Имя = ИмяОбласти;
	
	ТабличныйДокумент.Вывести(Обложка);
	ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
//	ПараметрыОтчета.СписокСформированныхЛистов.Добавить("1" + ИмяОбласти, "Обложка");
	
КонецПроцедуры

#КонецЕсли
