////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТехнологическиеВидыДоступа = Новый СписокЗначений;
	НеиспользуемыеВидыДоступа  = Новый СписокЗначений;
	
	Для каждого СвойстваВидаДоступа Из УправлениеДоступомСлужебный.СвойстваВидаДоступа() Цикл
		
		Если СвойстваВидаДоступа.ВидДоступаИспользуетсяВсегда Тогда
			ТехнологическиеВидыДоступа.Добавить(СвойстваВидаДоступа.ВидДоступа);
			
		ИначеЕсли НЕ УправлениеДоступомСлужебный.ВидДоступаИспользуется(СвойстваВидаДоступа.ВидДоступа) Тогда
			НеиспользуемыеВидыДоступа.Добавить(СвойстваВидаДоступа.ВидДоступа);
		КонецЕсли;
	КонецЦикла;
	
	Список.Отбор.Элементы[0].ПравоеЗначение = НеиспользуемыеВидыДоступа;
	Список.Отбор.Элементы[1].ПравоеЗначение = ТехнологическиеВидыДоступа;
	Список.Отбор.Элементы[0].Использование = НЕ Параметры.Свойство("ПоказыватьНеиспользуемыеВидыДоступа");
	
	Список.УсловноеОформление.Элементы[0].Отбор.Элементы[0].ПравоеЗначение = НеиспользуемыеВидыДоступа;
	
КонецПроцедуры
