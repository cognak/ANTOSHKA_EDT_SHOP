&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;

	Элементы.ЗаписатьВыделенныеСтроки.Доступность = ТехническаяПоддержкаВызовСервера.ИсключительныйРежим();

КонецПроцедуры

&НаСервере	//	LNK 05.11.2019 10:29:16
Процедура ЗаписатьТекущийНаборЗаписейНаСервере(Регистратор)

	НаборЗаписей = РегистрыСведений.ЦеныНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Записать();

	Для каждого СтрокаНабора Из НаборЗаписей Цикл

		ПланыОбмена.ЗарегистрироватьИзменения(ОбменДаннымиПовтИсп.ПолучитьМассивУзловПланаОбмена("ПоМагазину"), СтрокаНабора.Номенклатура);

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьТекущийНаборЗаписей(Команда)

	ЗаписатьТекущийНаборЗаписейНаСервере(Элементы.Список.ТекущиеДанные.Регистратор);

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущиеДанные = Элемент.ТекущиеДанные;

	Если ВРег(Лев(СокрЛП(ТекущиеДанные.Регистратор), 18)) = "<ОБЪЕКТ НЕ НАЙДЕН>" Тогда

		СтандартнаяОбработка = Ложь;

		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Период", ТекущиеДанные.Период);
		ПараметрыОткрытия.Вставить("Регистратор", ТекущиеДанные.Регистратор);
		ПараметрыОткрытия.Вставить("ВидЦены", ТекущиеДанные.ВидЦены);
		ПараметрыОткрытия.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
		ПараметрыОткрытия.Вставить("Цена", ТекущиеДанные.Цена);
		ПараметрыОткрытия.Вставить("Упаковка", ТекущиеДанные.Упаковка);
		ПараметрыОткрытия.Вставить("Акция", ТекущиеДанные.Акция);
		ПараметрыОткрытия.Вставить("ТипПереоценки", ТекущиеДанные.ТипПереоценки);

		ОткрытьФорму("РегистрСведений.ЦеныНоменклатуры.Форма.ФормаКлючаЗаписи", ПараметрыОткрытия, ЭтотОбъект, ЭтотОбъект,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

	КонецЕсли;

КонецПроцедуры







