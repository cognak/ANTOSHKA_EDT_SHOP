//	LNK 19.02.2019 15:01:27
Процедура РегламентнаяОчистка()	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КачествоОбслуживанияПродаж.Период КАК Период,
	|	КачествоОбслуживанияПродаж.ДокументПродажи КАК ДокументПродажи,
	|	КачествоОбслуживанияПродаж.Магазин КАК Магазин
	|ИЗ
	|	РегистрСведений.КачествоОбслуживанияПродаж КАК КачествоОбслуживанияПродаж
	|ГДЕ
	|	КачествоОбслуживанияПродаж.Период < НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ)
	|	И КачествоОбслуживанияПродаж.ДатаОценки = ДАТАВРЕМЯ(1, 1, 1)
	|	И КачествоОбслуживанияПродаж.Оценка = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	ДокументПродажи"
	);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());

	КачествоВыборка = Запрос.Выполнить().Выбрать();

	НаборЗаписей = РегистрыСведений.КачествоОбслуживанияПродаж.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения"  , Истина);
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;

	Счетчик = 0;

	Пока КачествоВыборка.Следующий() Цикл

		НаборЗаписей.ОбменДанными.Получатели.Очистить();
		ОбменДаннымиСервер.УстановитьПолучателейМагазина(НаборЗаписей.ОбменДанными.Получатели, КачествоВыборка.Магазин);

		НаборЗаписей.Отбор.ДокументПродажи.Установить(КачествоВыборка.ДокументПродажи);
		НаборЗаписей.Записать();

		Счетчик = Счетчик + 1;

	КонецЦикла;

КонецПроцедуры
