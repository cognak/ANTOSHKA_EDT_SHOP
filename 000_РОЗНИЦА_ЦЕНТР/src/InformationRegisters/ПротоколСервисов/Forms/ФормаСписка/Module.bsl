&НаКлиенте	//	LNK 18.12.2021 06:21:51
Процедура КомандаОчиститьРегистр(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьРегистрЗавершение", ЭтотОбъект);

	ПоказатьВопрос(ОписаниеОповещения
		, "Все записи регистра будут удалены!
		  |Процесс удаления может занять значительное время.
		  |Подтвердите своё решение:"
		, РежимДиалогаВопрос.ОКОтмена
		, 30
		, КодВозвратаДиалога.ОК
		, "Очистка регистра"
		, КодВозвратаДиалога.Отмена
	);

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРегистрЗавершение(КодВозврата, ДополнительныеПараметры)	Экспорт

	Если КодВозврата = КодВозвратаДиалога.ОК Тогда

		ВыполнитьОчисткуРегистра();
		Элементы.Список.Обновить();

		ПоказатьОповещениеПользователя("Регистр очищен!"
			,
			, "Все записи регистра сведений удалены"
			, БиблиотекаКартинок.Информация32
		);

	Иначе

		ПоказатьОповещениеПользователя("Отмена"
			,
			, "Воздействие не выполнено"
			, БиблиотекаКартинок.Предупреждение32
		);

	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыполнитьОчисткуРегистра()

	НаборЗаписей = РегистрыСведений.ПротоколСервисов.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();

КонецПроцедуры









