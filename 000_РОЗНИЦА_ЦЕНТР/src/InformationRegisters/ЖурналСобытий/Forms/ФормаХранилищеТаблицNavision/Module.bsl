&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Перем КлючЗаписи;

	Параметры.Свойство("КлючЗаписи", КлючЗаписи);

	Если НЕ ТипЗнч(КлючЗаписи) = Тип("РегистрСведенийКлючЗаписи.ЖурналСобытий") Тогда

		Отказ = Истина;

	КонецЕсли;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЖурналСобытий.Хранилище КАК Хранилище
	|ИЗ
	|	РегистрСведений.ЖурналСобытий КАК ЖурналСобытий
	|ГДЕ
	|	ЖурналСобытий.Период = &Период
	|	И ЖурналСобытий.КодУзлаОбмена = &КодУзлаОбмена
	|	И ЖурналСобытий.Индекс = &Индекс"
	);
	Запрос.УстановитьПараметр("Период", КлючЗаписи.Период);
	Запрос.УстановитьПараметр("КодУзлаОбмена", КлючЗаписи.КодУзлаОбмена);
	Запрос.УстановитьПараметр("Индекс", КлючЗаписи.Индекс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда

		ДанныеХранилища = Выборка.Хранилище.Получить();

		Если ТипЗнч(ДанныеХранилища) = Тип("Структура") И ДанныеХранилища.Свойство("ТаблицыNavision") Тогда

			Заголовок = "ТаблицыNavision";

			ИменаТаблиц = Новый Массив;
			ИменаТаблиц.Добавить("MOVE_HEAD");
			ИменаТаблиц.Добавить("MOVE_LINE");

			Для каждого ИмяТаблицы Из ИменаТаблиц Цикл

				Для каждого Колонка Из ДанныеХранилища.ТаблицыNavision[ИмяТаблицы].Колонки Цикл
					
					Колонка.Заголовок = Колонка.Имя;
					Колонка.Имя = ИмяТаблицы + "_" + Колонка.Имя;

				КонецЦикла;

				СоздатьКолонкиЭлемента(ДанныеХранилища.ТаблицыNavision[ИмяТаблицы], ИмяТаблицы);

			КонецЦикла;

		Иначе

			Отказ = Истина;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СоздатьКолонкиЭлемента(ТаблицаИсточник, ИмяТаблицы)

	СписокРеквизитов = Новый Массив;
//	сначала добавим саму таблицу
	СписокРеквизитов.Добавить(Новый РеквизитФормы(ИмяТаблицы, Новый ОписаниеТипов("ТаблицаЗначений")));

	Для каждого Колонка Из ТаблицаИсточник.Колонки Цикл

		СписокРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, ИмяТаблицы, Колонка.Заголовок));

	КонецЦикла;

	ИзменитьРеквизиты(СписокРеквизитов);

//	Добавим элементы формы
	ТабличноеПоле = Элементы.Добавить(ИмяТаблицы, Тип("ТаблицаФормы"));
	ТабличноеПоле.ПутьКДанным = ИмяТаблицы;

//	запретим менять положение строк и сами строки, отключим командную панель
	ТабличноеПоле.ТолькоПросмотр       = Истина;
	ТабличноеПоле.ИзменятьСоставСтрок  = Ложь;
	ТабличноеПоле.ИзменятьПорядокСтрок = Ложь;
	ТабличноеПоле.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;

	Для Каждого Колонка ИЗ ТаблицаИсточник.Колонки Цикл

		Элемент = Элементы.Добавить("Колонка" + Колонка.Имя, Тип("ПолеФормы"), ТабличноеПоле);
		Элемент.Вид = ВидПоляФормы.ПолеНадписи;
		Элемент.ПутьКДанным = ИмяТаблицы + "." + Колонка.Имя;
		Элемент.Заголовок = Колонка.Заголовок;

	КонецЦикла;

//	заполним таблицу
	ЗначениеВРеквизитФормы(ТаблицаИсточник, ИмяТаблицы);

КонецПроцедуры






