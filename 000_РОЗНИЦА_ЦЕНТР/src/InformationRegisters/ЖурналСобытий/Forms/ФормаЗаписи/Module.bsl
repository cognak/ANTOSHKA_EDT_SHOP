&НаСервере	//	LNK 17.08.2020 12:31:15
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Элементы.ФормаПроверитьКлючСерийногоНомера.Доступность = Ложь;

	Если Запись.Событие = "СЕРТИФИКАТЫ" И РольДоступна(Метаданные.Роли.АдминистраторСистемы) Тогда

		Элементы.ФормаПроверитьКлючСерийногоНомера.Доступность = НЕ ПустаяСтрока(ПолучитьКлючНомера(Запись.Комментарий));


	КонецЕсли;

КонецПроцедуры

&НаКлиенте	//	LNK 17.08.2020 12:34:12
Процедура ПроверитьКлючСерийногоНомера(Команда)

	КлючНомера = ПолучитьКлючНомера(Запись.Комментарий);

	Если НЕ ПустаяСтрока(КлючНомера) Тогда

		ПараметрыФормы = Новый Структура(
			"Заголовок, ТекстСообщения"
			, "Состав серийного номера"
			, ПроверитьДанныеСерийногоНомераНаСервере(КлючНомера)
		);

		ОткрытьФорму("ОбщаяФорма.ФормаПростогоТекстовогоСообщения"
			, ПараметрыФормы
			, ЭтотОбъект
			, ЭтотОбъект
			,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
		);

	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста	//	LNK 17.08.2020 12:34:12
Функция ПроверитьДанныеСерийногоНомераНаСервере(КлючНомера)

	УстановитьПривилегированныйРежим(Истина);

	ПредставлениеНомера = ОбщегоНазначенияКлиентСервер.СтруктураПредставление(
		"Состав ключа «" + КлючНомера + "»",
		ПодарочныеСертификатыСервер.ПолучитьСоставСерийногоНомера(КлючНомера)
	);

	Возврат ПредставлениеНомера;

КонецФункции

&НаКлиентеНаСервереБезКонтекста	//	LNK 17.08.2020 12:45:08
Функция ПолучитьКлючНомера(Комментарий)

	КлючНомера   = "";
	ПозицияФлага = Найти(Комментарий, "[ШК ");

	Если НЕ ПозицияФлага = 0 Тогда

		КлючНомера = СокрЛП(ОбщегоНазначенияКлиентСервер.lx_GetTooken(Сред(Комментарий, ПозицияФлага + 4), "]", 1));

	КонецЕсли;

	Возврат КлючНомера

КонецФункции

&НаКлиенте
Процедура КомандаПолучитьДанныеХранилища(Команда)

	ПараметрыФормы = Новый Структура("КлючЗаписи", Запись.ИсходныйКлючЗаписи);
	ОткрытьФорму("РегистрСведений.ЖурналСобытий.Форма.ФормаХранилищеТаблицNavision", ПараметрыФормы, ЭтотОбъект, ЭтотОбъект,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры













