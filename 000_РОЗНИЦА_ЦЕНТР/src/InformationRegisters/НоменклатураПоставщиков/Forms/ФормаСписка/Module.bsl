
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Поставщик") Тогда
		Поставщик = Параметры.Отбор.Поставщик;
		Элементы.ОтборПоставщик.Видимость 	= Ложь;
		Элементы.Поставщик.Видимость 		= Ложь;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Магазин") Тогда
		Магазин = Параметры.Отбор.Магазин;
		Элементы.ОтборМагазин.Видимость = Ложь;
		Элементы.Магазин.Видимость 		= Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	МагазинНастроек		= Настройки.Получить("Магазин");
	КонтрагентНастроек	= Настройки.Получить("Контрагент");
	Если НЕ ЗначениеЗаполнено(Магазин) Тогда
		Магазин = МагазинНастроек;
	Иначе
		Настройки.Удалить("Магазин");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Поставщик) Тогда
		Контрагент = КонтрагентНастроек;
	Иначе
		Настройки.Удалить("Контрагент");
	КонецЕсли;
	УстановитьОтборДинамическогоСписка("Магазин");
	УстановитьОтборДинамическогоСписка("Поставщик");

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеСтатистикиПоНоменклатуреПоставщиков" Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОтборМагазинПриИзменении(Элемент)
	
	УстановитьОтборДинамическогоСписка("Магазин");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	
	УстановитьОтборДинамическогоСписка("Поставщик");
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОбновлениеНоменклатурыПоставщиков(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	Если ЗначениеЗаполнено(Поставщик) Тогда
		
		ПараметрыФормы.Вставить("Поставщик", Поставщик);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Магазин) Тогда
		
		ПараметрыФормы.Вставить("Магазин", Магазин);
		
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.НоменклатураПоставщиков.Форма.ФормаОбновленияСтатистики", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьОтборДинамическогоСписка(ИмяРеквизита)

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Список,
		ИмяРеквизита,
		ЭтотОбъект[ИмяРеквизита],
		ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизита]));

КонецПроцедуры
