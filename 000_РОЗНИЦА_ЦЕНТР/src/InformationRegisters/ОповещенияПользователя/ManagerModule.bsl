#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//	LNK 03.03.2024 07:26:08
Функция ПолучитьОповещенияПользователя()	Экспорт

	СписокОповещений = Новый Массив;

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаОповещений.Пользователь КАК Пользователь,
	|	ТаблицаОповещений.ДействиеКоманда КАК ДействиеКоманда,
	|	ТаблицаОповещений.Объект КАК Объект,
	|	ИСТИНА КАК Оповестить
	|ИЗ
	|	РегистрСведений.ОповещенияПользователя КАК ТаблицаОповещений
	|ГДЕ
	|	ТаблицаОповещений.Пользователь = &ТекущийПользователь
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТаблицаОповещений.Пользователь,
	|	ТаблицаОповещений.ДействиеКоманда,
	|	ТаблицаОповещений.Объект,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрСведений.ОповещенияПользователя КАК ТаблицаОповещений
	|ГДЕ
	|	РАЗНОСТЬДАТ(ТаблицаОповещений.ДатаИзменения, &ТекущаяДата, МИНУТА) >= 1
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТаблицаОповещений.Пользователь,
	|	ТаблицаОповещений.ДействиеКоманда,
	|	ТаблицаОповещений.Объект,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрСведений.ОповещенияПользователя КАК ТаблицаОповещений
	|ГДЕ
	|	ТаблицаОповещений.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТаблицаОповещений.Пользователь,
	|	ТаблицаОповещений.ДействиеКоманда,
	|	ТаблицаОповещений.Объект,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрСведений.ОповещенияПользователя КАК ТаблицаОповещений
	|ГДЕ
	|	ТаблицаОповещений.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.АдминистраторАвтоматов)"
	);
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());

//	Сообщаем и очищаем по текущему пользователю, а так же просто очищаем все
//	записи, "старшие" 1 минуты, независимо от пользователя.
//	Примечание: 1 минута взята просто "с пальца на ветер". Если будет необходимость, изменяем смело.
//	.. но не забываем отметить об таком изменении в этом комментарии.
//	Так же безусловно чистим все записи, относящиеся к Нуралиеву или пустому пользователю.
	
	РезультатЗапроса = Запрос.Выполнить();

	Если НЕ РезультатЗапроса.Пустой() Тогда

		Выборка = РезультатЗапроса.Выбрать();

		Пока Выборка.Следующий() Цикл

			СписокОповещений.Добавить(
				Новый Структура(
					"Пользователь, ИмяСобытия, Параметр, Оповестить, Выполнено"
					, Выборка.Пользователь
					, Выборка.ДействиеКоманда
					, Новый Структура("Объект", Выборка.Объект)
					, Выборка.Оповестить
					, Ложь
				)
			);

		КонецЦикла;

	КонецЕсли;

	Возврат СписокОповещений;

КонецФункции

//	LNK 03.03.2024 07:35:13
Процедура УдалитьОповещенияПользователя(СписокОповещений)	Экспорт

	УстановитьПривилегированныйРежим(Истина);

	НаборЗаписей = РегистрыСведений.ОповещенияПользователя.СоздатьНаборЗаписей();

	Для каждого СоставОповещения Из СписокОповещений Цикл

		Если СоставОповещения.Выполнено Тогда

			НаборЗаписей.Отбор.Пользователь.Установить(СоставОповещения.Пользователь);
			НаборЗаписей.Отбор.ДействиеКоманда.Установить(СоставОповещения.ИмяСобытия);
			НаборЗаписей.Отбор.Объект.Установить(СоставОповещения.Параметр.Объект);

			НаборЗаписей.Записать();	//	LNK 11.03.2024 17:46:07

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

//	LNK 03.03.2024 07:50:37
Процедура Установить(Объект, ДействиеКоманда)	Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Если НЕ (ПараметрыСеанса.ТекущийПользователь = Справочники.Пользователи.ПустаяСсылка()
		ИЛИ  ПараметрыСеанса.ТекущийПользователь = Справочники.Пользователи.АдминистраторАвтоматов) Тогда

		НаборЗаписей = РегистрыСведений.ОповещенияПользователя.СоздатьНаборЗаписей();

		НаборЗаписей.Отбор.Пользователь.Установить(ПараметрыСеанса.ТекущийПользователь);
		НаборЗаписей.Отбор.ДействиеКоманда.Установить(ДействиеКоманда);
		НаборЗаписей.Отбор.Объект.Установить(Объект);

		ЗаписьНабора = НаборЗаписей.Добавить();

		ЗаписьНабора.Пользователь = НаборЗаписей.Отбор.Пользователь.Значение;
		ЗаписьНабора.ДействиеКоманда = НаборЗаписей.Отбор.ДействиеКоманда.Значение;
		ЗаписьНабора.Объект = НаборЗаписей.Отбор.Объект.Значение;

		НаборЗаписей.Записать();

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти
	
#КонецЕсли













