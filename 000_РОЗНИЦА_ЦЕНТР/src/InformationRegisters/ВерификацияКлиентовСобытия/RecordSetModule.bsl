//	LNK 20.10.2017 15:40:17
Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	Для каждого ЗаписьНабора Из ЭтотОбъект Цикл

		ЗаписьНабора.ДатаИзменения = ТекущаяДата();

	КонецЦикла;

//	\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\/

	Если НЕ Количество() = 0 И Отбор.Нормальный.Значение = Ложь Тогда

		Количество = 1;

	//	Такой статус имеем только при записи отказа клиента!
	//	Ищем предыдущее событие и увеличиваем счетчик таких событий на 1.

		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТаблицаВерификации.Период КАК Период,
		|	ВЫБОР
		|		КОГДА ТаблицаВерификации.Количество = 0
		|			ТОГДА 1
		|		ИНАЧЕ ТаблицаВерификации.Количество
		|	КОНЕЦ КАК Количество
		|ИЗ
		|	РегистрСведений.ВерификацияКлиентовСобытия.СрезПоследних(
		|			&Период,
		|			Контрагент = &Контрагент
		|				И НЕ Нормальный) КАК ТаблицаВерификации"
		);
		Запрос.УстановитьПараметр("Период"    , Отбор.Период.Значение - 1);
		Запрос.УстановитьПараметр("Контрагент", Отбор.Контрагент.Значение);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда

			Количество = Количество + Выборка.Количество;

		КонецЕсли;

		Для каждого ЗаписьНабора Из ЭтотОбъект Цикл

			ЗаписьНабора.Количество = Количество;

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

//	LNK 18.12.2017 10:46:39
Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

//	\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\/

	Если НЕ Количество() = 0 И КонтекстПриложения.ЭтоКлиентскоеСоединение() Тогда

		Для каждого ЗаписьНабора Из ЭтотОбъект Цикл

			Если ЗаписьНабора.Событие = Перечисления.СобытияВерификацииПокупателей.Выполнено Тогда

				ФоновыеЗадания.Выполнить("ВерификацияКлиентовСервер.ОчиститьОтказыОтВерификации", ОбщегоНазначенияКлиентСервер.AAD(ЗаписьНабора.Контрагент, ЗаписьНабора.Период));
			//	ВерификацияКлиентовСервер.ОчиститьОтказыОтВерификации(ЗаписьНабора.Контрагент, ЗаписьНабора.Период);

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры




