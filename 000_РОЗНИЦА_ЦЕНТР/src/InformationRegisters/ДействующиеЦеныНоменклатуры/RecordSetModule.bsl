//	LNK 17.11.2016 15:46:51
Процедура ПередЗаписью(Отказ, Замещение)

//	LNK 13.03.2017 08:00:35
	Если ОбменДанными.Загрузка ИЛИ ДополнительныеСвойства.Свойство("РегистрацияПередачиВNavision") Тогда

		ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);

		Если ОбменДанными.Загрузка Тогда

			Возврат;

		КонецЕсли;

	КонецЕсли;

//	---------------------------------------------------------------------------------------

	ВремяИзменения = ТекущаяДатаСеанса();

	Для каждого ЗаписьНабора Из ЭтотОбъект Цикл

		Если ЗаписьНабора.ВремяИзменения = '00010101' Тогда

			ЗаписьНабора.ВремяИзменения = ВремяИзменения;

		КонецЕсли;

	КонецЦикла;

//	LNK 26.02.2018 12:32:36
//	Нужно выйти на установку предыдущих цен один раз..
	Если НЕ ДополнительныеСвойства.Свойство("ПропуститьУстановкуПредыдущихЦен") И НЕ Количество() = 0 Тогда

		ПроверитьУстановитьПредыдущуюЦенуНоменклатуры(Отказ, Замещение);

	КонецЕсли;

КонецПроцедуры

//	LNK 26.02.2018 12:35:03
Процедура ПроверитьУстановитьПредыдущуюЦенуНоменклатуры(Отказ, Замещение)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаТекущая.ОбъектЦенообразования КАК ОбъектЦенообразования,
	|	ТаблицаТекущая.Номенклатура КАК Номенклатура,
	|	ТаблицаТекущая.Характеристика КАК Характеристика,
	|	ТаблицаТекущая.Цена КАК Цена,
	|	ВЫРАЗИТЬ(ТаблицаТекущая.Упаковка КАК Справочник.УпаковкиНоменклатуры) КАК Упаковка,
	|	ТаблицаТекущая.Акция КАК Акция
	|ПОМЕСТИТЬ ТаблицаНабора
	|ИЗ
	|	&ТаблицаНабора КАК ТаблицаТекущая
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ТаблицаТекущая.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектЦенообразования,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаПредыдущие.Период) КАК Период,
	|	ТаблицаНабора.ОбъектЦенообразования КАК ОбъектЦенообразования,
	|	ТаблицаНабора.Номенклатура КАК Номенклатура,
	|	ТаблицаНабора.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ Фильтр
	|ИЗ
	|	ТаблицаНабора КАК ТаблицаНабора
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействующиеЦеныНоменклатуры КАК ТаблицаПредыдущие
	|		ПО ТаблицаНабора.ОбъектЦенообразования = ТаблицаПредыдущие.ОбъектЦенообразования
	|			И ТаблицаНабора.Номенклатура = ТаблицаПредыдущие.Номенклатура
	|			И ТаблицаНабора.Характеристика = ТаблицаПредыдущие.Характеристика
	|			И (ТаблицаНабора.Цена / ВЫБОР
	|					КОГДА ТаблицаНабора.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|						ТОГДА 1
	|					ИНАЧЕ ТаблицаНабора.Упаковка.Коэффициент
	|				КОНЕЦ <> ТаблицаПредыдущие.Цена / ВЫБОР
	|					КОГДА ТаблицаПредыдущие.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|						ТОГДА 1
	|					ИНАЧЕ ТаблицаПредыдущие.Упаковка.Коэффициент
	|				КОНЕЦ
	|				ИЛИ НЕ ТаблицаНабора.Акция = ТаблицаПредыдущие.Акция)
	|ГДЕ
	|	ТаблицаПредыдущие.Период < НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНабора.ОбъектЦенообразования,
	|	ТаблицаНабора.Номенклатура,
	|	ТаблицаНабора.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНабора.ОбъектЦенообразования КАК Магазин
	|ИЗ
	|	ТаблицаНабора КАК ТаблицаНабора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаНабора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ) КАК Период,
	|	ТаблицаПредыдущие.ОбъектЦенообразования КАК ОбъектЦенообразования,
	|	ТаблицаПредыдущие.Номенклатура КАК Номенклатура,
	|	ТаблицаПредыдущие.Характеристика КАК Характеристика,
	|	ТаблицаПредыдущие.Цена КАК Цена,
	|	ТаблицаПредыдущие.Упаковка КАК Упаковка,
	|	ТаблицаПредыдущие.Акция КАК Акция
	|ИЗ
	|	Фильтр КАК Фильтр
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДействующиеЦеныНоменклатуры КАК ТаблицаПредыдущие
	|		ПО Фильтр.Период = ТаблицаПредыдущие.Период
	|			И Фильтр.ОбъектЦенообразования = ТаблицаПредыдущие.ОбъектЦенообразования
	|			И Фильтр.Номенклатура = ТаблицаПредыдущие.Номенклатура
	|			И Фильтр.Характеристика = ТаблицаПредыдущие.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Фильтр"
	);
	Запрос.УстановитьПараметр("ТекущаяДата"  , ТекущаяДата());
	Запрос.УстановитьПараметр("ТаблицаНабора", Выгрузить());

	Результаты = Запрос.ВыполнитьПакет();

	Если НЕ Результаты[4].Пустой() Тогда

		МагазинВыборка = Результаты[2].Выбрать();
		МагазинВыборка.Следующий();

		НаборЗаписей = РегистрыСведений.ПредыдущиеЦеныНоменклатуры.СоздатьНаборЗаписей();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
		НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения"  , Истина);
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;

		Для каждого Узел Из ОбменДаннымиПовтИсп.ПолучитьСоответствиеУзловМагазинуПоМагазину(Истина).Получить(МагазинВыборка.Магазин) Цикл

			НаборЗаписей.ОбменДанными.Получатели.Добавить(Узел);

		КонецЦикла;

		Выборка = Результаты[4].Выбрать();

		Пока Выборка.Следующий() Цикл

			НаборЗаписей.Очистить();

			НаборЗаписей.Отбор.ОбъектЦенообразования.Установить(Выборка.ОбъектЦенообразования);
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика);

			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, Выборка);
			ЗаписьНабора.ВремяИзменения = ТекущаяДата();

			НаборЗаписей.Записать();

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры












