#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьВерсиюДанных(ОбъектСсылка)	Экспорт

	УстановитьПривилегированныйРежим(Истина);
	#Если _ Тогда
	ОбъектСсылка = Документы.ПеремещениеТоваров.ПустаяСсылка();
	#КонецЕсли

	ВерсияДанных = "";

	Попытка

		Запрос = Новый Запрос(СтрЗаменить(
		"ВЫБРАТЬ
		|	ТаблицаДокумента.ВерсияДанных КАК ВерсияДанных
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &ОбъектСсылка"
		, "Документ.ПеремещениеТоваров КАК", "Документ." + ОбъектСсылка.Метаданные().Имя + " КАК")
		);
		Запрос.УстановитьПараметр("ОбъектСсылка", ОбъектСсылка);

		РезультатЗапроса = Запрос.Выполнить();	//	LNK 28.12.2023 07:09:18

		Если НЕ РезультатЗапроса.Пустой() Тогда
		
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();

			ВерсияДанных = Выборка.ВерсияДанных;

		КонецЕсли;

	Исключение

		ТекстОшибки  = ОписаниеОшибки();
		ВерсияДанных = "";

	КонецПопытки;

	Возврат ВерсияДанных;

КонецФункции

//	LNK 28.12.2023 05:44:43
Процедура УстановитьВерсиюДанных(ДокументОбъект)	Экспорт

//	Вызывается из "ПриЗаписи" следующих документов:
//	ПеремещениеТоваров, ПриходныйОрдерНаТовары, РасходныйОрдерНаТовары 

	Если НЕ ПривилегированныйРежим() Тогда

		УстановитьПривилегированныйРежим(Истина);

	КонецЕсли;

	#Если _ Тогда
	ДокументОбъект = Документы.ПеремещениеТоваров.СоздатьДокумент();
	#КонецЕсли

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаРегистра.Объект КАК Объект,
	|	ТаблицаРегистра.Магазин КАК Магазин,
	|	ТаблицаРегистра.ДатаРегистрации КАК ДатаРегистрации
	|ИЗ
	|	РегистрСведений.ОбъектУчтенВNavision КАК ТаблицаРегистра
	|ГДЕ
	|	ТаблицаРегистра.Объект = &ДокументСсылка
	|	И НЕ ТаблицаРегистра.ВерсияОбъекта = &ВерсияОбъекта
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаРегистрации"
	);
	Запрос.УстановитьПараметр("ДокументСсылка"	, ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ВерсияОбъекта"	, ДокументОбъект.ВерсияДанных);

	РезультатЗапроса = Запрос.Выполнить();

	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		НаборЗаписей = РегистрыСведений.ОбъектУчтенВNavision.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.ДополнительныеСвойства.Вставить("УстановкаВерсииОбъекта", Истина);
		НаборЗаписей.ДополнительныеСвойства.Вставить("НеПроверятьРегистрациюВОбменNavision", Истина);
		НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);

		Выборка = РезультатЗапроса.Выбрать();

		Пока Выборка.Следующий() Цикл

			НаборЗаписей.ОбменДанными.Получатели.Очистить();
			ОбменДаннымиСервер.УстановитьПолучателейМагазина(НаборЗаписей.ОбменДанными.Получатели, Выборка.Магазин);

			НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
			НаборЗаписей.Отбор.Магазин.Установить(Выборка.Магазин);
			НаборЗаписей.Прочитать();

			Для каждого ЗаписьНабора Из НаборЗаписей Цикл

				ЗаписьНабора.ВерсияОбъекта = ДокументОбъект.ВерсияДанных;

			КонецЦикла;

			НаборЗаписей.Записать();

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

//	LNK 27.01.2023 06:31:44
Функция ПолучитьУчтеноNavisionДляОснования(ДокументСсылка)	Экспорт

	Если НЕ ПривилегированныйРежим() Тогда

		УстановитьПривилегированныйРежим(Истина);

	КонецЕсли;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ Фильтр
	|ИЗ
	|	Документ.ПриходныйОрдерНаТовары КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.Ссылка = &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.ДокументОснование
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары КАК ТаблицаДокументы
	|ГДЕ
	|	ТаблицаДокументы.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРегистра.УчтеноNavision КАК УчтеноNavision
	|ИЗ
	|	РегистрСведений.ОбъектУчтенВNavision КАК ТаблицаРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Фильтр КАК Фильтр
	|		ПО ТаблицаРегистра.Объект = Фильтр.ДокументОснование
	|			И (ТаблицаРегистра.УчтеноNavision)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Фильтр"
	);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();

	Возврат НЕ РезультатЗапроса.Пустой();

КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти
	
#КонецЕсли
















