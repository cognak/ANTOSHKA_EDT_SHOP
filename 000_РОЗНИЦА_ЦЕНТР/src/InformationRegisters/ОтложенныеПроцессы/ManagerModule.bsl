//	LNK 17.09.2016 09:02:48
Процедура НазначитьОтложенныйПроцесс(Знач ИмяПроцесса, Параметры, ОписаниеКоманды, ИнтервалВыполнения = 0, ОбновитьЕслиСуществует = Истина)	Экспорт

	УстановитьПривилегированныйРежим(Истина);

	ИмяПроцесса = ВРег(СокрЛП(ИмяПроцесса));
	Период      = ТекущаяДата();

	Если ОбновитьЕслиСуществует Тогда

		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТаблицаРегистра.Период КАК Период
		|ИЗ
		|	РегистрСведений.ОтложенныеПроцессы КАК ТаблицаРегистра
		|ГДЕ
		|	ТаблицаРегистра.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ) И КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ)
		|	И ТаблицаРегистра.ИмяПроцесса = &ИмяПроцесса
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период"
		);
		Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
		Запрос.УстановитьПараметр("ИмяПроцесса", ИмяПроцесса);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда

			Период = Выборка.Период;

		КонецЕсли;

	КонецЕсли;

	МенеджерЗаписи = РегистрыСведений.ОтложенныеПроцессы.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период         = Период;
	МенеджерЗаписи.ИмяПроцесса    = ИмяПроцесса;
	МенеджерЗаписи.ДанныеПроцесса = Новый ХранилищеЗначения(Новый Структура(
		"Параметры, ОписаниеКоманды"
		, Параметры, ОписаниеКоманды));
	МенеджерЗаписи.ИнтервалВыполнения = ИнтервалВыполнения;

	МенеджерЗаписи.Записать(Истина);

	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры


