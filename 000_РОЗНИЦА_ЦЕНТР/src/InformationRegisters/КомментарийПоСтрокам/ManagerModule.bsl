
Процедура ЗаписьКомментария(СтруктураКомментария, ОднаЗапись = Ложь) Экспорт
	
	Если ОднаЗапись Тогда
		
		НаборЗаписей = СоздатьНаборЗаписей();

		НаборЗаписей.Отбор.Документ.Установить(СтруктураКомментария.Документ);
		НаборЗаписей.Отбор.КлючСвязи.Установить(СтруктураКомментария.КлючСвязи);
		
		КомментарийИзменен = Ложь;
		
		Если СтруктураКомментария.УдалитьЗапись Тогда

			НаборЗаписей.Записать();
			
		Иначе

			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() = 0 Тогда
				
				КомментарийИзменен = Истина;
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Документ = СтруктураКомментария.Документ;
				НоваяЗапись.КлючСвязи = СтруктураКомментария.КлючСвязи;
				НоваяЗапись.Дата = СтруктураКомментария.Дата;
			
			Иначе
				
				НоваяЗапись = НаборЗаписей[0];
				КомментарийИзменен
					= Не (СокрЛП(ВРег(НоваяЗапись.Комментарий)) = СокрЛП(ВРег(СтруктураКомментария.Комментарий)));
			КонецЕсли;

			Если КомментарийИзменен Тогда
				
				НоваяЗапись.Автор = СтруктураКомментария.Автор;
				НоваяЗапись.Комментарий = СтруктураКомментария.Комментарий;
				НоваяЗапись.Просмотрен = СтруктураКомментария.Просмотрен;
				Если СтруктураКомментария.УзелСоздания = Неопределено Тогда
					НоваяЗапись.УзелСоздания = ОбменДаннымиПовтИсп.ПолучитьДанныеУзла().ЭлементСтруктуры;
				Иначе
					НоваяЗапись.УзелСоздания = СтруктураКомментария.УзелСоздания;
				КонецЕсли;
				
				НаборЗаписей.Записать();
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
	
		НаборЗаписей = СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Автор.Установить(СтруктураКомментария.Автор);
		НаборЗаписей.Отбор.Документ.Установить(СтруктураКомментария.Документ);
		НаборЗаписей.Отбор.Дата.Установить(СтруктураКомментария.Дата);
		НаборЗаписей.Отбор.КлючСвязи.Установить(СтруктураКомментария.КлючСвязи);
	
		НоваяЗапись = НаборЗаписей.Добавить();
	
		НоваяЗапись.Дата = СтруктураКомментария.Дата;
		НоваяЗапись.Документ = СтруктураКомментария.Документ;
		НоваяЗапись.Автор = СтруктураКомментария.Автор;
		НоваяЗапись.КлючСвязи = СтруктураКомментария.КлючСвязи;
		НоваяЗапись.Комментарий = СтруктураКомментария.Комментарий;
		НоваяЗапись.Просмотрен = СтруктураКомментария.Просмотрен;
		Если СтруктураКомментария.УзелСоздания = Неопределено Тогда
			НоваяЗапись.УзелСоздания = ОбменДаннымиПовтИсп.ПолучитьДанныеУзла().ЭлементСтруктуры;
		Иначе
			НоваяЗапись.УзелСоздания = СтруктураКомментария.УзелСоздания;
		КонецЕсли;
	
		НаборЗаписей.Записать(Истина); 
		
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураКомментария() Экспорт 
	
	СтруктураКомментария = Новый Структура;
	СтруктураКомментария.Вставить("Дата");
	СтруктураКомментария.Вставить("Документ");
	СтруктураКомментария.Вставить("Автор");
	СтруктураКомментария.Вставить("КлючСвязи");
	СтруктураКомментария.Вставить("Комментарий");
	СтруктураКомментария.Вставить("Просмотрен", Ложь);
	СтруктураКомментария.Вставить("УзелСоздания", Неопределено);
	СтруктураКомментария.Вставить("УдалитьЗапись", Ложь);
	
	Возврат СтруктураКомментария;

КонецФункции
