&НаСервере	//	LNK 31.07.2018 14:37:24
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Элементы.СоздатьНовыйЭлемент.Доступность = (Запись.ТипОтправителя = "Справочник.ФизическиеЛица");

	Если ПустаяСтрока(АдресХранилищаВнешнихДанных) Тогда

		АдресХранилищаВнешнихДанных = ПоместитьВоВременноеХранилище(Новый Структура, УникальныйИдентификатор);

	КонецЕсли;
	
	СоответствиеОбъектов.Параметры.УстановитьЗначениеПараметра("УникальныйИдентификаторПриемника",Запись.Идентификатор);

КонецПроцедуры

&НаСервере	//	LNK 31.07.2018 15:21:45
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ДанныеСообщения = ТекущийОбъект.ДанныеСообщения.Получить();

	Если ТипЗнч(ДанныеСообщения) = Тип("Структура") И ДанныеСообщения.Свойство("Данные") Тогда

		ВнешниеДанные = ДанныеСообщения.Данные;
		ВнешниеДанные.Вставить("Идентификатор", Новый УникальныйИдентификатор(ДанныеСообщения.Идентификатор));

		Если ВнешниеДанные.Свойство("Пол") И ТипЗнч("Пол") = Тип("Строка") Тогда

			ВнешниеДанные.Пол = ОбщегоНазначенияКлиентСервер.ЗначениеПеречисления(Перечисления.ПолФизическогоЛица, ВнешниеДанные.Пол);

		КонецЕсли;

	//	Сериализуем в строку JSON так, как в составе имеется ТаблицаЗначений.. на тонком клиенте нет такого типа!
		АдресХранилищаВнешнихДанных = ПоместитьВоВременноеХранилище(ОбщегоНазначенияКлиентСервер.СериализоватьJSON(ВнешниеДанные), УникальныйИдентификатор);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте	//	LNK 31.07.2018 14:37:18
Процедура СоздатьНовыйЭлемент(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьНовыйЭлементЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения
		, "Будет создан новый элемент типа «" + Запись.ТипОтправителя + "»!" + Символы.ПС
		+ "Подтвердите своё решение:"
		, РежимДиалогаВопрос.ОКОтмена, 60
		, КодВозвратаДиалога.ОК
		, "Требуется подтверждение"
		, КодВозвратаДиалога.Отмена
	);

КонецПроцедуры

&НаКлиенте	//	LNK 31.07.2018 14:38:40
Процедура СоздатьНовыйЭлементЗавершение(КодВозврата, ДополнительныеПараметры)	Экспорт

	Если КодВозврата = КодВозвратаДиалога.ОК Тогда

		ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьНовыйЭлементЗакрытиеФормы", ЭтотОбъект);

		ПараметрыФормы = Новый Структура("ЗначенияЗаполненияJSON", ПолучитьИзВременногоХранилища(АдресХранилищаВнешнихДанных));
		ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект, ЭтотОбъект,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

	Иначе

		ПоказатьОповещениеПользователя("Не выполнено!",, "Воздействие прервано пользователем", БиблиотекаКартинок.Ошибка32);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте	//	LNK 01.08.2018 11:10:46
Процедура СоздатьНовыйЭлементЗакрытиеФормы(Значение, ДополнительныеПараметры)	Экспорт

	Перем Выполнено;

	Если ТипЗнч(Значение) = Тип("Структура") Тогда

		Если Значение.Свойство("Выполнено", Выполнено) И Выполнено = Истина Тогда

			УдалитьТекущуюЗапись();

			Модифицированность = Ложь;
			Закрыть();

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаСервере	//	LNK 01.08.2018 12:52:43
Процедура УдалитьТекущуюЗапись()

	ЗаписьОбъект = РеквизитФормыВЗначение("Запись");
	ЗаписьОбъект.Удалить();

КонецПроцедуры // УдалитьТекущуюЗапись()















