//	LNK 29.12.2017 08:10:34
Процедура УдалениеНеактуальныхЗаписейСтатистикиРегистрации()	Экспорт

	Если НЕ ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

	//	Статистика присутствует только в центральном узле.
		Возврат;

	КонецЕсли;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРегистра.Период КАК Период,
	|	ТаблицаРегистра.УзелПланаОбмена КАК УзелПланаОбмена
	|ИЗ
	|	РегистрСведений.СтатистикаРегистрацииИзменений КАК ТаблицаРегистра
	|ГДЕ
	|	(ТаблицаРегистра.Период < &ГраницаПравая
	|			ИЛИ ТаблицаРегистра.УзелПланаОбмена.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период"
	);
	Запрос.УстановитьПараметр("ГраницаПравая", ДобавитьМесяц(НачалоДня(ТекущаяДатаСеанса()), -1));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Счётчик = 0;
	НаборЗаписей = РегистрыСведений.СтатистикаРегистрацииИзменений.СоздатьНаборЗаписей();

	Пока Выборка.Следующий() Цикл

		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Отбор.УзелПланаОбмена.Установить(Выборка.УзелПланаОбмена);

		НаборЗаписей.Записать();
		Счётчик = Счётчик + 1;	//	счётчик отладочно-контрольный.

	КонецЦикла;

КонецПроцедуры
