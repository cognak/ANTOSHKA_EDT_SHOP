#Область ОбработчикиСобытийФормы

&НаСервере	//	LNK 05.05.2022 06:46:13
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

//	Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда

		Возврат;

	КонецЕсли;

	Элементы.ФормаКомандаОчиститьРегистрацию.Доступность = ТехническаяПоддержкаВызовСервера.ОтладочныйРежимРаботы();

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура КомандаОчиститьРегистрациюНаСервере(ВыделенныеСтроки)

	ОбновитьПовторноИспользуемыеЗначения();

	ТаблицаКлючей = ОбщегоНазначенияКлиентСервер.ПолучитьТаблицуОбразРегистра(Метаданные.РегистрыСведений.КомплексОбменаMagento.ПолноеИмя());
	#Если _ Тогда
	ТаблицаКлючей = Новый ТаблицаЗначений;
	#КонецЕсли

	Если ВыделенныеСтроки.Количество() = 0 Тогда

		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КомплексОбменаMagento.Измерение_1 КАК Измерение_1,
		|	КомплексОбменаMagento.Измерение_2 КАК Измерение_2,
		|	КомплексОбменаMagento.Измерение_3 КАК Измерение_3,
		|	КомплексОбменаMagento.ТипРегистрации КАК ТипРегистрации
		|ИЗ
		|	РегистрСведений.КомплексОбменаMagento КАК КомплексОбменаMagento"
		);

		Выборка = Запрос.Выполнить().Выбрать();

		Пока Выборка.Следующий() Цикл

			СтрокаТаблицы = ТаблицаКлючей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);

		КонецЦикла;

	Иначе

		Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл

			СтрокаТаблицы = ТаблицаКлючей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыделеннаяСтрока);

		КонецЦикла;

	КонецЕсли;

	ОбменMagentoСервер.УдалитьРегистрациюПоТаблицеКлючей(ТаблицаКлючей, Истина);

	ТаблицаКлючей.Очистить();

КонецПроцедуры

&НаКлиенте
Процедура КомандаОчиститьРегистрацию(Команда)

	ДополнительныеПараметры = Новый Структура(
		"ВыделенныеСтроки"
		, Элементы.Список.ВыделенныеСтроки
	);

	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьРегистрациюЗавершение", ЭтотОбъект, ДополнительныеПараметры);

	ПоказатьВопрос(ОписаниеОповещения
		, ?(Элементы.Список.ВыделенныеСтроки.Количество() = 0, "Все", "Выделенные") + " записи регистра будут удалены!
		  |Процесс удаления может занять значительное время.
		  |Подтвердите своё решение:
		  |[Да]		- очистить регистр ПОЛНОСТЬЮ
		  |[Нет]	- очистить ТОЛЬКО выделенные строки
		  |[Отмена]	- не выполнять"
		, РежимДиалогаВопрос.ДаНетОтмена
		, 30
		, КодВозвратаДиалога.Нет
		, "Очистка регистра"
		, КодВозвратаДиалога.Отмена
	);

КонецПроцедуры

&НаКлиенте	//	LNK 05.05.2022 06:41:08
Процедура ОчиститьРегистрациюЗавершение(КодВозврата, ДополнительныеПараметры)	Экспорт

	Если КодВозврата = КодВозвратаДиалога.Да ИЛИ КодВозврата = КодВозвратаДиалога.Нет Тогда

		Если КодВозврата = КодВозвратаДиалога.Да Тогда

			ДополнительныеПараметры.ВыделенныеСтроки = Новый Массив;

		КонецЕсли;

		КомандаОчиститьРегистрациюНаСервере(ДополнительныеПараметры.ВыделенныеСтроки);

		ПоказатьОповещениеПользователя("Очистка регистра!"
			,
			, "Указанные записи регистра сведений удалены"
			, БиблиотекаКартинок.Информация32
		);

	Иначе

		ПоказатьОповещениеПользователя("Отмена"
			,
			, "Воздействие не выполнено"
			, БиблиотекаКартинок.Предупреждение32
		);

	КонецЕсли;

	Элементы.Список.Обновить();

КонецПроцедуры

&НаСервере	//	LNK 11.05.2022 07:13:53
Функция КомандаПолучитьКоличествоЗаписейНаСервере()

	КоличествоЗаписей = 0;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ТаблицаРегистра.Измерение_2) КАК Количество
	|ИЗ
	|	РегистрСведений.КомплексОбменаMagento КАК ТаблицаРегистра"
	);

	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда

		КоличествоЗаписей = Выборка.Количество;

	КонецЕсли;

	Возврат КоличествоЗаписей;

КонецФункции

&НаКлиенте	//	LNK 11.05.2022 07:13:49
Процедура КомандаПолучитьКоличествоЗаписей(Команда)

	КоличествоЗаписей = КомандаПолучитьКоличествоЗаписейНаСервере();

	Сообщить("Всего: " + Формат(КоличествоЗаписей, "ЧДЦ=; ЧН=0; ЧГ=") + " записей");

КонецПроцедуры




