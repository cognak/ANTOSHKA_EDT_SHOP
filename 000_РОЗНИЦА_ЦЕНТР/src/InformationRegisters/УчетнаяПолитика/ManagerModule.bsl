#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытийОбъектаМетаданных

Функция ПредставлениеЗаписи(ЗаписьНабора, ОбъектМетаданных)	Экспорт

	Представление = "";

	ИменаКоллекций = Новый Массив;
	ИменаКоллекций.Добавить("СтандартныеРеквизиты");
	ИменаКоллекций.Добавить("Измерения");
	ИменаКоллекций.Добавить("Ресурсы");
	ИменаКоллекций.Добавить("Реквизиты");

	Для каждого ИмяКоллекции Из ИменаКоллекций Цикл

		Для каждого Атрибут Из ОбъектМетаданных[ИмяКоллекции] Цикл

			Если НЕ ТипЗнч(ЗаписьНабора[Атрибут.Имя]) = Тип("ХранилищеЗначения") Тогда

				Представление = Представление + ?(ПустаяСтрока(Представление), "", Символы.ПС)
				+ Атрибут.Имя + ": [" + СокрЛП(ЗаписьНабора[Атрибут.Имя]) + "]";

			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

	Возврат Представление;

КонецФункции

#КонецОбласти

Функция КлючБезопасногоХранилища()	Экспорт

	Возврат "УчетнаяПолитика.ХранилищеОбщее";

КонецФункции

Процедура ПроверитьСтруктуруХранилища(ДанныеХранилища)	Экспорт

	СоставСтруктуры = Новый Массив;
	СоставСтруктуры.Добавить(Новый Структура("Имя, Значение", "ПрефиксыКарт", Справочники.ИнформационныеКарты.СписокПрефиксовПоУмолчанию()));

	Если НЕ ТипЗнч(ДанныеХранилища) = Тип("Структура") Тогда

		ДанныеХранилища = Новый Структура;

	КонецЕсли;

	Для каждого ЭлементСостава Из СоставСтруктуры Цикл

		Если НЕ ДанныеХранилища.Свойство(ЭлементСостава.Имя) Тогда

			ДанныеХранилища.Вставить(ЭлементСостава.Имя, ЭлементСостава.Значение)

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

#КонецЕсли
