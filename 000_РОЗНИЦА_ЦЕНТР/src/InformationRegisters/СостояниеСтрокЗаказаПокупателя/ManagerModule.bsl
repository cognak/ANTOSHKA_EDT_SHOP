Процедура ЗаписьСостояниеСтрокЗаказаПокупателя(СсылкаДокументаПроверки) Экспорт  
	
	Если ТипЗнч(СсылкаДокументаПроверки) = Тип("ДокументСсылка.ЗапросДоступностиТоваров") Тогда 
		
		ЗаписьВРегистр(СсылкаДокументаПроверки);
		
	ИначеЕсли ТипЗнч(СсылкаДокументаПроверки) = Тип("ДокументСсылка.ПриходныйОрдерНаТовары") Тогда  
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТаблицаДоступности.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.ПриходныйОрдерНаТовары КАК ТаблицаОрдер
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК ТаблицаПеремещениеОтОрдера
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК ТаблицаПеремещенияОтПеремещения
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗапросДоступностиТоваров КАК ТаблицаДоступности
			|				ПО ТаблицаПеремещенияОтПеремещения.ДокументОснование = ТаблицаДоступности.Ссылка
			|			ПО ТаблицаПеремещениеОтОрдера.ДокументОснование = ТаблицаПеремещенияОтПеремещения.Ссылка
			|		ПО ТаблицаОрдер.ДокументОснование = ТаблицаПеремещениеОтОрдера.Ссылка
			|ГДЕ
			|	ТаблицаОрдер.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", СсылкаДокументаПроверки);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			ЗаписьВРегистр(Выборка.Ссылка, СсылкаДокументаПроверки.Проведен,, СсылкаДокументаПроверки.Проведен);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(СсылкаДокументаПроверки) = Тип("ДокументСсылка.РасходныйОрдерНаТовары") Тогда  
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТаблицаДоступности.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.РасходныйОрдерНаТовары КАК ТаблицаОрдер
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК ТаблицаПеремещениеОтОрдера
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗапросДоступностиТоваров КАК ТаблицаДоступности
			|			ПО ТаблицаДоступности.Ссылка = ТаблицаПеремещениеОтОрдера.ДокументОснование
			|		ПО ТаблицаОрдер.ДокументОснование = ТаблицаПеремещениеОтОрдера.Ссылка
			|ГДЕ
			|	ТаблицаОрдер.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", СсылкаДокументаПроверки);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			ЗаписьВРегистр(Выборка.Ссылка,, СсылкаДокументаПроверки.Проведен);
			
		КонецЕсли;
		
	КонецЕсли;  

КонецПроцедуры      

Процедура ЗаписьВРегистр(ДокументДляЗаписи, ОтОрдера = Ложь, Отправлен = Неопределено, Получен = Неопределено)
	Для Каждого СтрокаЗапросаДоступности Из ДокументДляЗаписи.Товары Цикл 

		НаборЗаписей = РегистрыСведений.СостояниеСтрокЗаказаПокупателя.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Отбор.ЗаказПокупателя.Установить(ДокументДляЗаписи.ДокументОснование);   
		НаборЗаписей.Отбор.ЗапросДоступностиТоваров.Установить(ДокументДляЗаписи);   
		НаборЗаписей.Отбор.КлючСвязи.Установить(СтрокаЗапросаДоступности.КлючСвязи); 
		
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда 
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.ЗаказПокупателя = ДокументДляЗаписи.ДокументОснование;
			НоваяЗапись.ЗапросДоступностиТоваров = ДокументДляЗаписи;
			НоваяЗапись.КлючСвязи = СтрокаЗапросаДоступности.КлючСвязи; 
		Иначе 
			НоваяЗапись = НаборЗаписей[0];
		КонецЕсли; 
		
		Если ДокументДляЗаписи.СтатусЗапроса = Перечисления.СтатусыЗапросовДоступностиТоваровШапка.Отменён 
				Или ДокументДляЗаписи.СтатусЗапроса = Перечисления.СтатусыЗапросовДоступностиТоваровШапка.Удалён  
				Или СтрокаЗапросаДоступности.СтатусЗапроса = Перечисления.СтатусыЗапросовДоступностиТоваров.Удалён Тогда 
				
			НоваяЗапись.ГотовКПродаже = Ложь;
			НоваяЗапись.Отправлен = Ложь;
			НоваяЗапись.Получен = Ложь;
			
		ИначеЕсли ДокументДляЗаписи.МагазинОтправитель = ДокументДляЗаписи.МагазинПолучатель Тогда
			
			Если ДокументДляЗаписи.СтатусЗапроса = Перечисления.СтатусыЗапросовДоступностиТоваровШапка.ПродажаНаКассе Тогда
				НоваяЗапись.ГотовКПродаже = Истина;
			Иначе 
				НоваяЗапись.ГотовКПродаже = Ложь; 
			КонецЕсли;
			
		ИначеЕсли ОтОрдера Тогда 
			
			НоваяЗапись.ГотовКПродаже = Истина;
			
		КонецЕсли; 
		
		Если Не Отправлен = Неопределено Тогда 
			
			НоваяЗапись.Отправлен = Отправлен;
			
		КонецЕсли;
		
		Если Не Получен = Неопределено Тогда 
			
			НоваяЗапись.Получен = Получен;
			
		КонецЕсли; 
		
		НаборЗаписей.Записать(Истина);
		
	КонецЦикла;
КонецПроцедуры


