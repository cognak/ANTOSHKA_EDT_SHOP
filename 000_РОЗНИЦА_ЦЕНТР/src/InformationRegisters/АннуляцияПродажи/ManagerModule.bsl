//	LNK 27.05.2019 15:29:27
Процедура Регистрация(ДокументПродажи, ПериодРегистрации, СтатусЧекаККМ, Магазин, КассаККМ, КассоваяСмена, Продавец, Номенклатура, Упаковка, Количество, Цена, Сумма, Компьютер)	Экспорт

	Попытка

		НаборЗаписей = РегистрыСведений.АннуляцияПродажи.СоздатьНаборЗаписей();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);

		НаборЗаписей.Отбор.Период.Установить(?(ЗначениеЗаполнено(ПериодРегистрации), ПериодРегистрации, ТекущаяДатаСеанса()));
		НаборЗаписей.Отбор.ДокументПродажи.Установить(ДокументПродажи);
		НаборЗаписей.Отбор.Индекс.Установить(РегистрыСведений.АннуляцияПродажи.ПолучитьИндекс(НаборЗаписей.Отбор.Период.Значение, ДокументПродажи));

		ЗаписьНабора = НаборЗаписей.Добавить();

		ЗаписьНабора.Период          = НаборЗаписей.Отбор.Период.Значение;
		ЗаписьНабора.ДокументПродажи = НаборЗаписей.Отбор.ДокументПродажи.Значение;
		ЗаписьНабора.Индекс          = НаборЗаписей.Отбор.Индекс.Значение;

		ЗаписьНабора.Магазин       = Магазин;
		ЗаписьНабора.КассаККМ      = КассаККМ;
		ЗаписьНабора.КассоваяСмена = КассоваяСмена;
		ЗаписьНабора.СтатусЧекаККМ = СтатусЧекаККМ;
		ЗаписьНабора.Номенклатура  = Номенклатура;
		ЗаписьНабора.Упаковка      = Упаковка;
		ЗаписьНабора.Продавец      = Продавец;
		ЗаписьНабора.Количество    = Количество;
		ЗаписьНабора.Цена          = Цена;
		ЗаписьНабора.Сумма         = Сумма;

		ЗаписьНабора.Компьютер     = Компьютер;

		ЗаписьНабора.Администратор = РольДоступна(Метаданные.Роли.АдминистраторСистемы);

		НаборЗаписей.Записать();

	Исключение

		ТекстОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("РегистрыСведений.АннуляцияПродажи", УровеньЖурналаРегистрации.Предупреждение
		, Метаданные.РегистрыСведений.АннуляцияПродажи,, ТекстОшибки);

	КонецПопытки;

КонецПроцедуры

//	LNK 28.05.2019 13:08:12
Функция ПолучитьИндекс(Период, ДокументПродажи)	Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаРегистра.Индекс) КАК Индекс
	|ИЗ
	|	РегистрСведений.АннуляцияПродажи КАК ТаблицаРегистра
	|ГДЕ
	|	ТаблицаРегистра.Период = &Период
	|	И ТаблицаРегистра.ДокументПродажи = &ДокументПродажи"
	);
	Запрос.УстановитьПараметр("Период"			, Период);
	Запрос.УстановитьПараметр("ДокументПродажи"	, ДокументПродажи);

	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() И НЕ Выборка.Индекс = NULL Тогда

			Индекс = Выборка.Индекс + 1;

	Иначе	Индекс = 0;

	КонецЕсли;

	Возврат Индекс;

КонецФункции // ПолучитьИндекс()
