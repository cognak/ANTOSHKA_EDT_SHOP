#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//	LNK 20.07.2022 06:01:24
Процедура ЗарегистрироватьОформление(ДокументСсылка, Очистка, МагазиныИсключить = Неопределено, ПроверитьНаличие = Ложь)	Экспорт

	Если НЕ ПривилегированныйРежим() Тогда

		УстановитьПривилегированныйРежим(Истина);

	КонецЕсли;

//	Такие наборы записей НЕ никуда передаём! Только для локального использования.
	НаборЗаписей = РегистрыСведений.РеализацияЗаказовРегистрация.СоздатьНаборЗаписей();
//	.. на всякий случай - мало ли что изменится в дальнейшем
	НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
	НаборЗаписей.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;
	НаборЗаписей.ОбменДанными.Загрузка = Истина;

	Для каждого Магазин Из ОбменДаннымиПовтИсп.ПолучитьОсновныеМагазиныУзлов() Цикл

		Если НЕ МагазиныИсключить = Неопределено Тогда

			#Если _ Тогда
			МагазиныИсключить = Новый Соответствие;
			#КонецЕсли
			Если НЕ МагазиныИсключить.Получить(Магазин) = Неопределено Тогда

				Продолжить;

			КонецЕсли;

		КонецЕсли;

		Если ПроверитьНаличие = Истина Тогда

				Записывать = НЕ НаборЗаписейСуществует(ДокументСсылка, Магазин);

		Иначе	Записывать = Истина;

		КонецЕсли;

		Если Записывать = Истина Тогда

			НаборЗаписей.Очистить();
			НаборЗаписей.ОбменДанными.Получатели.Очистить();	//	.. на всякий случай

			НаборЗаписей.Отбор.Объект.Установить(ДокументСсылка);
			НаборЗаписей.Отбор.Магазин.Установить(Магазин);

			Если НЕ Очистка = Истина Тогда

				ЗаписьНабора = НаборЗаписей.Добавить();

				ЗаписьНабора.Объект  = НаборЗаписей.Отбор.Объект.Значение;
				ЗаписьНабора.Магазин = НаборЗаписей.Отбор.Магазин.Значение;

				ЗаписьНабора.Выполнен = Ложь;
				ЗаписьНабора.ДействиеКоманда = "ПРОДАЖА_ПО_ЗАКАЗУ";

				ЗаписьНабора.ДатаИзменения		= ТекущаяДата();
				ЗаписьНабора.ТипОбъектаСтрокой	= ЗаписьНабора.Объект.Метаданные().ПолноеИмя();

			КонецЕсли;

			НаборЗаписей.Записать();

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

//	LNK 25.07.2022 14:47:50
Процедура ПринятьПодтверждениеПринятыхДанных(ДанныеОтвета)	Экспорт

	Если НЕ ДанныеОтвета.ТаблицаПринято.Колонки.Найти("Объект") = Неопределено И НЕ ДанныеОтвета.ТаблицаПринято.Количество() = 0 Тогда

		НаборЗаписей = РегистрыСведений.РеализацияЗаказовРегистрация.СоздатьНаборЗаписей();

		Для каждого СтрокаТаблицы Из ДанныеОтвета.ТаблицаПринято Цикл
		
			НаборЗаписей.Отбор.Объект.Установить(СтрокаТаблицы.Объект);
			НаборЗаписей.Отбор.Магазин.Установить(СтрокаТаблицы.Магазин);

			НаборЗаписей.Прочитать();

			Для каждого ЗаписьНабора Из НаборЗаписей Цикл

				Если НЕ ЗаписьНабора.Выполнен Тогда

					ЗаписьНабора.Выполнен = Истина;

				КонецЕсли;

			КонецЦикла;

			Если НаборЗаписей.Модифицированность() Тогда

				НаборЗаписей.Записать();

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

//	LNK 26.07.2022 11:19:31
Процедура ОчиститьРегистрацию(ДействиеКоманда = Неопределено)	Экспорт

	Если НЕ ПривилегированныйРежим() Тогда

		УстановитьПривилегированныйРежим(Истина);

	КонецЕсли;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРегистра.Объект КАК Объект,
	|	ТаблицаРегистра.Магазин КАК Магазин
	|ИЗ
	|	РегистрСведений.РеализацияЗаказовРегистрация КАК ТаблицаРегистра
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ДействиеКоманда = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ТаблицаРегистра.ДействиеКоманда = &ДействиеКоманда
	|		КОНЕЦ
	|	И ТаблицаРегистра.Выполнен"
	);
	Запрос.УстановитьПараметр("ДействиеКоманда", ДействиеКоманда);

	РезультатЗапроса = Запрос.Выполнить();

	Если НЕ РезультатЗапроса.Пустой() Тогда

		НаборЗаписей = РегистрыСведений.РеализацияЗаказовРегистрация.СоздатьНаборЗаписей();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
		НаборЗаписей.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;
		НаборЗаписей.ОбменДанными.Загрузка = Истина;

		Выборка = РезультатЗапроса.Выбрать();

		Пока Выборка.Следующий() Цикл

			НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
			НаборЗаписей.Отбор.Магазин.Установить(Выборка.Магазин);

			НаборЗаписей.Записать();

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//	LNK 28.07.2022 10:31:17
Функция НаборЗаписейСуществует(Объект, Магазин)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаРегистра.Выполнен КАК Выполнен
	|ИЗ
	|	РегистрСведений.РеализацияЗаказовРегистрация КАК ТаблицаРегистра
	|ГДЕ
	|	ТаблицаРегистра.Объект = &Объект
	|	И ТаблицаРегистра.Магазин = &Магазин
	|	И ТаблицаРегистра.ДействиеКоманда = ""ПРОДАЖА_ПО_ЗАКАЗУ"""
	);
	Запрос.УстановитьПараметр("Объект" , Объект);
	Запрос.УстановитьПараметр("Магазин", Магазин);

	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();

КонецФункции


#КонецОбласти
	
#КонецЕсли
