//	LNK 03.07.2017 15:49:58
#Область ОбработчикиОсновныхСобытийОбъекта

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда

		Возврат;

	КонецЕсли;
	
	Если УправлениеПользователями.ПолучитьБулевоЗначениеПраваПользователя(ПланыВидовХарактеристик.ПраваПользователей.РазрешитьПревышениеСвободногоОстаткаДС, Ложь) = Истина Тогда

		ДополнительныеСвойства.РассчитыватьИзменения = Ложь;
		Возврат;

	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

//	Текущее состояние набора помещается во временную таблицу "ДвиженияДенежныеСредстваНаличныеПередЗаписью",
//	чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.Организация КАК Организация,
	|	Таблица.Касса КАК Касса,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Сумма
	|		ИНАЧЕ -Таблица.Сумма
	|	КОНЕЦ КАК СуммаПередЗаписью
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваНаличныеПередЗаписью
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый"
	);
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый"   , ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;

	Запрос.Выполнить();

КонецПроцедуры

//	LNK 03.07.2017 15:50:10
Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда

		Возврат;

	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

//	Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
//	и помещается во временную таблицу.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.Касса КАК Касса,
	|	СУММА(ТаблицаИзменений.СуммаИзменение) КАК СуммаИзменение
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваНаличныеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Организация КАК Организация,
	|		Таблица.Касса КАК Касса,
	|		Таблица.СуммаПередЗаписью КАК СуммаИзменение
	|	ИЗ
	|		ДвиженияДенежныеСредстваНаличныеПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Организация,
	|		Таблица.Касса,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Сумма
	|			ИНАЧЕ Таблица.Сумма
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваНаличные КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Касса
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.СуммаИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияДенежныеСредстваНаличныеПередЗаписью"
	);
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;

	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
//	Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияДенежныеСредстваНаличныеИзменение", Выборка.Количество > 0);

КонецПроцедуры
	
#КонецОбласти













