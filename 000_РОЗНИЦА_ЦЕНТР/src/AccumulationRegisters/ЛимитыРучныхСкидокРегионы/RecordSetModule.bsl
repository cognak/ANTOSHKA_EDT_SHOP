#Область ОбработчикиСобытийОбъекта

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	ТекущийПользователь = Пользователи.ТекущийПользователь();

	Для каждого ЗаписьНабора Из ЭтотОбъект Цикл

		ЗаписьНабора.ДатаИзменения = ТекущаяДатаСеанса();
		ЗаписьНабора.Пользователь  = ТекущийПользователь;

	КонецЦикла;

//	---------------------------------------------------------------------------------------

	Если НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда

		Возврат;

	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

//	Текущее состояние набора помещается во временную таблицу "ДвиженияЛимитыРучныхСкидокРегионыПередЗаписью",
//	чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаРегистра.ПериодЛимита КАК ПериодЛимита,
	|	ТаблицаРегистра.ПричинаРучнойСкидки КАК ПричинаРучнойСкидки,
	|	ТаблицаРегистра.ОбособленноеПодразделениеОрганизации КАК ОбособленноеПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ТаблицаРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ТаблицаРегистра.Сумма
	|		ИНАЧЕ -ТаблицаРегистра.Сумма
	|	КОНЕЦ КАК СуммаПередЗаписью
	|ПОМЕСТИТЬ ДвиженияЛимитыРучныхСкидокРегионыПередЗаписью
	|ИЗ
	|	РегистрНакопления.ЛимитыРучныхСкидокРегионы КАК ТаблицаРегистра
	|ГДЕ
	|	ТаблицаРегистра.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый"
	);
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый"   , ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;

	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда

		Возврат;

	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

//	Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
//	и помещается во временную таблицу.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаИзменений.ПериодЛимита КАК ПериодЛимита,
	|	ТаблицаИзменений.ПричинаРучнойСкидки КАК ПричинаРучнойСкидки,
	|	ТаблицаИзменений.ОбособленноеПодразделениеОрганизации КАК ОбособленноеПодразделениеОрганизации,
	|	СУММА(ТаблицаИзменений.СуммаИзменение) КАК СуммаИзменение
	|ПОМЕСТИТЬ ДвиженияЛимитыРучныхСкидокРегионыИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ПериодЛимита КАК ПериодЛимита,
	|		Таблица.ПричинаРучнойСкидки КАК ПричинаРучнойСкидки,
	|		Таблица.ОбособленноеПодразделениеОрганизации КАК ОбособленноеПодразделениеОрганизации,
	|		Таблица.СуммаПередЗаписью КАК СуммаИзменение
	|	ИЗ
	|		ДвиженияЛимитыРучныхСкидокРегионыПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ПериодЛимита,
	|		Таблица.ПричинаРучнойСкидки,
	|		Таблица.ОбособленноеПодразделениеОрганизации,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Сумма
	|			ИНАЧЕ Таблица.Сумма
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЛимитыРучныхСкидокРегионы КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ПериодЛимита,
	|	ТаблицаИзменений.ПричинаРучнойСкидки,
	|	ТаблицаИзменений.ОбособленноеПодразделениеОрганизации
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.СуммаИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияЛимитыРучныхСкидокРегионыПередЗаписью"
//	по Сумма к не правильному состоянию регистра приведет уменьшение прихода и увеличение расхода
	);
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;

	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
//	Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияЛимитыРучныхСкидокРегионыИзменение", Выборка.Количество > 0);

КонецПроцедуры
	
#КонецОбласти
