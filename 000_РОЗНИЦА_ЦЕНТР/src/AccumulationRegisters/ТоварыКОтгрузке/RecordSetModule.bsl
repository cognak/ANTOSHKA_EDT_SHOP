
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

//	LNK 09.02.2017 14:56:57
Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда

		Возврат;

	КонецЕсли;

	ТребуетсяКонтроль = Истина;

	Для Каждого Запись Из ЭтотОбъект Цикл

		Если (Запись.ВидДвижения = ВидДвиженияНакопления.Расход И НЕ Запись.Количество = 0) Тогда

			ТребуетсяКонтроль = УправлениеПользователями.ПолучитьБулевоЗначениеПраваПользователя(ПланыВидовХарактеристик.ПраваПользователей.КонтролироватьОстатокПриПроведении, Ложь);
			Прервать;

		КонецЕсли;

	КонецЦикла;

	Если НЕ ТребуетсяКонтроль Тогда

		ДополнительныеСвойства.РассчитыватьИзменения = Ложь;
		Возврат;

	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения   = Истина;

//	Текущее состояние набора помещается во временную таблицу "ДвиженияТоварыКОтгрузкеПередЗаписью",
//	чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ДокументОснование КАК ДокументОснование,
	|	Таблица.Склад КАК Склад,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Количество
	|		ИНАЧЕ -Таблица.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияТоварыКОтгрузкеПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый"
	);
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый"   , ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;

	Запрос.Выполнить();

КонецПроцедуры

//	LNK 09.02.2017 14:57:01
Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда

		Возврат;

	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

//	Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
//	и помещается во временную таблицу.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаИзменений.ДокументОснование КАК ДокументОснование,
	|	ТаблицаИзменений.Склад КАК Склад,
	|	ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика КАК Характеристика,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияТоварыКОтгрузкеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ДокументОснование КАК ДокументОснование,
	|		Таблица.Склад КАК Склад,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияТоварыКОтгрузкеПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ДокументОснование,
	|		Таблица.Склад,
	|		Таблица.Номенклатура,
	|		Таблица.Характеристика,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыКОтгрузке КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ДокументОснование,
	|	ТаблицаИзменений.Склад,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика
	|
	|ИМЕЮЩИЕ
	|	НЕ СУММА(ТаблицаИзменений.КоличествоИзменение) = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияТоварыКОтгрузкеПередЗаписью"
	);
//	по Количество к не правильному состоянию регистра приведет уменьшение прихода и увеличение расхода
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;

	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();

//	Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияТоварыКОтгрузкеИзменение", Выборка.Количество > 0);

КонецПроцедуры
