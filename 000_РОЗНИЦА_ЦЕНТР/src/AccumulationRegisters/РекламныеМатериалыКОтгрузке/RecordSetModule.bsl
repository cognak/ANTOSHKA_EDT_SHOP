
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда

		Возврат;

	КонецЕсли;
	
	ТребуетсяКонтроль = Истина;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если (Запись.ВидДвижения = ВидДвиженияНакопления.Расход И Запись.Количество <> 0) Тогда
			ТребуетсяКонтроль = УправлениеПользователями.ПолучитьБулевоЗначениеПраваПользователя(ПланыВидовХарактеристик.ПраваПользователей.КонтролироватьОстатокПриПроведении, Ложь);
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Не ТребуетсяКонтроль Тогда
		ДополнительныеСвойства.РассчитыватьИзменения = Ложь;
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

//	Текущее состояние набора помещается во временную таблицу "ДвиженияТоварыНаСкладахПередЗаписью",
//	чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА РекламныеМатериалыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РекламныеМатериалыКОтгрузке.Количество
	|		ИНАЧЕ -РекламныеМатериалыКОтгрузке.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью,
	|	РекламныеМатериалыКОтгрузке.Магазин КАК Магазин,
	|	РекламныеМатериалыКОтгрузке.Организация КАК Организация,
	|	РекламныеМатериалыКОтгрузке.РекламныйМатериал КАК РекламныйМатериал,
	|	РекламныеМатериалыКОтгрузке.РекламнаяПлоскость КАК РекламнаяПлоскость,
	|	РекламныеМатериалыКОтгрузке.ДокументРаспоряжение КАК ДокументРаспоряжение
	|ПОМЕСТИТЬ ДвиженияРекламныеМатериалыКОтгрузкеПередЗаписью
	|ИЗ
	|	РегистрНакопления.РекламныеМатериалыКОтгрузке КАК РекламныеМатериалыКОтгрузке
	|ГДЕ
	|	РекламныеМатериалыКОтгрузке.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда

		Возврат;

	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

	

//	Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
//	и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаИзменений.ДокументРаспоряжение КАК ДокументРаспоряжение,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.Магазин КАК Магазин,
	|	ТаблицаИзменений.РекламнаяПлоскость КАК РекламнаяПлоскость,
	|	ТаблицаИзменений.РекламныйМатериал КАК РекламныйМатериал,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияРекламныеМатериалыКОтгрузкеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		Таблица.ДокументРаспоряжение КАК ДокументРаспоряжение,
	|		Таблица.Организация КАК Организация,
	|		Таблица.Магазин КАК Магазин,
	|		Таблица.РекламнаяПлоскость КАК РекламнаяПлоскость,
	|		Таблица.РекламныйМатериал КАК РекламныйМатериал
	|	ИЗ
	|		ДвиженияРекламныеМатериалыКОтгрузкеПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА РекламныеМатериалыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РекламныеМатериалыКОтгрузке.Количество
	|			ИНАЧЕ РекламныеМатериалыКОтгрузке.Количество
	|		КОНЕЦ,
	|		РекламныеМатериалыКОтгрузке.ДокументРаспоряжение,
	|		РекламныеМатериалыКОтгрузке.Организация,
	|		РекламныеМатериалыКОтгрузке.Магазин,
	|		РекламныеМатериалыКОтгрузке.РекламнаяПлоскость,
	|		РекламныеМатериалыКОтгрузке.РекламныйМатериал
	|	ИЗ
	|		РегистрНакопления.РекламныеМатериалыКОтгрузке КАК РекламныеМатериалыКОтгрузке
	|	ГДЕ
	|		РекламныеМатериалыКОтгрузке.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ДокументРаспоряжение,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Магазин,
	|	ТаблицаИзменений.РекламнаяПлоскость,
	|	ТаблицаИзменений.РекламныйМатериал
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияРекламныеМатериалыКОтгрузкеПередЗаписью";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРекламныеМатериалыКОтгрузкеИзменение", Выборка.Количество > 0);

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
