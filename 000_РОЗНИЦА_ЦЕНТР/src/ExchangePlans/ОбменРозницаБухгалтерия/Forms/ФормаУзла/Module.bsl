&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// для предопределенного узла этой информационной базы закладку "НастройкаФильтровРегистрации" не отображаем
	Если ОбменДаннымиПовтИсп.ЭтоПредопределенныйУзелПланаОбмена(Объект.Ссылка) Тогда
		
		Элементы.НастройкаФильтровРегистрации.Видимость = Ложь;
		Элементы.СтраницыФормы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Если Элементы.Найти("РазрешитьРедактированиеРеквизитовОбъекта") <> Неопределено Тогда
			Элементы.РазрешитьРедактированиеРеквизитовОбъекта.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьВидимостьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ОбменДаннымиПовтИсп.ЭтоПредопределенныйУзелПланаОбмена(Объект.Ссылка) Тогда
		ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Оповестить("Запись_УзелПланаОбмена");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередаватьРозничныеЦеныПриИзменении(Элемент)
	
	Если НЕ Объект.ПередаватьРозничныеЦены Тогда
		Объект.ПередаватьЦеныХарактеристик = Ложь;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоОрганизациямПриИзменении(Элемент)
	
	УстановитьВидимостьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьНаСервере()
	
	Элементы.ОтборПоОрганизациям.Видимость	= Объект.ИспользоватьОтборПоОрганизациям;
	Элементы.ПередаватьЦеныХарактеристик.Видимость = Объект.ПередаватьРозничныеЦены И ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
   
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если НЕ Объект.Ссылка.Пустая() 
		И НЕ ЭтоПредопределенныйУзелПланаОбмена() Тогда
		Результат = ОткрытьФормуМодально("ПланОбмена.ОбменРозницаБухгалтерия.Форма.РазблокированиеРеквизитов");
		Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
			Если ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект) Тогда
				ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтотОбъект);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Осуществляет поиск верхней границы диапазона цен
//
// Возвращаемое значение:
// Строка таблицы правил округления
// Если верхней границы нет возвращает -1
//
&НаСервере
Функция ЭтоПредопределенныйУзелПланаОбмена()

	Возврат ОбменДаннымиПовтИсп.ЭтоПредопределенныйУзелПланаОбмена(Объект.Ссылка);

КонецФункции // ЭтоПредопределенныйУзелПланаОбмена()
