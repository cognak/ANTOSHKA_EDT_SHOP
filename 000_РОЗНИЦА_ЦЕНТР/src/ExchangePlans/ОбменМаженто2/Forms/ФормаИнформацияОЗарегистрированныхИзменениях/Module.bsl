
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УзелОбмена = Параметры.УзелОбмена;
	
	Если УзелОбмена = ПланыОбмена.ОбменМаженто2.ЭтотУзел() Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	ОбновитьДеревоИзмененийСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ДеревоИзменений.ПолучитьЭлементы().Количество() = 0 Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru='Изменения не зарегистрированы.';uk='Зміни не зареєстровані.'")
			,,,
			БиблиотекаКартинок.Информация32);
			
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьДеревоИзмененийСервер();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоИзмененийСервер()
	
	СтруктураИзменений = ЗаполнитьСтруктуруИзмененийДляУзла(УзелОбмена);
	
	СтрокиДерева = ДеревоИзменений.ПолучитьЭлементы();
	СтрокиДерева.Очистить();
	
	Если СтруктураИзменений.Контрагенты.Количество() > 0 Тогда
		
		СтрокаДерева = СтрокиДерева.Добавить();
		СтрокаДерева.ВидОбъекта = НСтр("ru='Контрагенты';uk='Контрагенти'");
	
		Для Каждого ЭлементМассива Из СтруктураИзменений.Контрагенты Цикл
			СтрокаОбъекта = СтрокаДерева.ПолучитьЭлементы().Добавить();
			СтрокаОбъекта.Объект = ЭлементМассива;
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектОчистка(Элемент, СтандартнаяОбработка)
	
	Родитель = Элементы.ДеревоИзменений.ТекущиеДанные.ПолучитьРодителя();
	
	Если Родитель = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьРегистрациюСервер(Элементы.ДеревоИзменений.ТекущиеДанные.Объект);
	Родитель.ПолучитьЭлементы().Удалить(Элементы.ДеревоИзменений.ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьРегистрациюСервер(ДанныеСсылка);
	
	ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, ДанныеСсылка);
	
КонецПроцедуры

//// Записывает в структуру ссылки на измененные объекты по узлу плана обмена.
////
//// Параметры:
////	УзелПланаОбмена - ПланОбмена.Ссылка
////	СтруктураВозврата - Структура
////
Функция ЗаполнитьСтруктуруИзмененийДляУзла(УзелПланаОбмена) Экспорт
	
	СтруктураИзменений = Новый Структура;
	СтруктураИзменений.Вставить("Контрагенты", Новый Массив);
	//СтруктураИзменений.Вставить("Услуги", Новый Массив);
	//СтруктураИзменений.Вставить("Ресурсы", Новый Массив);
	//СтруктураИзменений.Вставить("Заказы", Новый Массив);
	//СтруктураИзменений.Вставить("Файлы", Новый Массив);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	КонтрагентыИзменения.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.Контрагенты.Изменения КАК КонтрагентыИзменения
	                      |ГДЕ
	                      |	КонтрагентыИзменения.Узел = &Узел"
						);
	
	Запрос.УстановитьПараметр("Узел", УзелПланаОбмена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураИзменений["Контрагенты"].Добавить(Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат СтруктураИзменений;
	
КонецФункции

Процедура ПолучитьСтруктуруИзменений(Параметры)
	
	Если Параметры.Свойство("УзелОбмена") И Параметры.ВыгружатьТолькоИзменения Тогда
		СтруктураИзменений = ЗаполнитьСтруктуруИзмененийДляУзла(Параметры.УзелОбмена);
	Иначе
		СтруктураИзменений = Новый Структура;
		СтруктураИзменений.Вставить("Товары", Новый Массив);
		СтруктураИзменений.Вставить("Услуги", Новый Массив);
		СтруктураИзменений.Вставить("Заказы", Новый Массив);
		СтруктураИзменений.Вставить("Файлы", Новый Массив);
	КонецЕсли;
	Параметры.Вставить("СтруктураИзменений", СтруктураИзменений);	
	
	МассивИзмененийНоменклатуры = Новый Массив;
	Если Параметры.ВыгружатьТолькоИзменения
		И НЕ Параметры.ВыполнятьПолнуюВыгрузкуПринудительно Тогда
		
		МассивИзмененийНоменклатуры = Параметры.СтруктураИзменений.Товары;
	КонецЕсли;
	Параметры.Вставить("МассивИзмененийНоменклатуры", МассивИзмененийНоменклатуры);
	
КонецПроцедуры

