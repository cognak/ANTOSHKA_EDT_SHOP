#Область ОбработчикиСобытийОбъекта

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	Если НЕ ОбменДаннымиПовтИсп.ЭтоГлавныйУзел() Тогда

		Сообщить("План обмена работает только в главном узле!");
		Отказ = Истина;

	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	Если НЕ Отказ И Активен Тогда

		ПроверитьАктивностьДругихУзлов(Отказ);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Процедура ПроверитьАктивностьДругихУзлов(Отказ)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаУзлы.Ссылка КАК Узел
	|ИЗ
	|	ПланОбмена.ОбменРозницаMagento КАК ТаблицаУзлы
	|ГДЕ
	|	НЕ ТаблицаУзлы.Ссылка = &ТекущийУзел
	|	И ТаблицаУзлы.Активен"
	);
	Запрос.УстановитьПараметр("ТекущийУзел", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		УзелОбъект = Выборка.Узел.ПолучитьОбъект();
		УзелОбъект.Активен = Ложь;
		УзелОбъект.ОбменДанными.Загрузка = Истина;

		УзелОбъект.Записать();

	КонецЦикла;

КонецПроцедуры
