//////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаПодчиненногоУзлаРИБЗавершена = Константы.НастройкаПодчиненногоУзлаРИБЗавершена.Получить();
	ЭтоПодчиненныйУзелРИБ = (ОбменДаннымиПовтИсп.ГлавныйУзел() <> Неопределено);
	
	Если ЭтоПодчиненныйУзелРИБ И НЕ НастройкаПодчиненногоУзлаРИБЗавершена Тогда
		
		Текст = НСтр("ru = 'Настройка в подчиненном узле не предусмотрена'"); 
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			,
			,
			,
			Отказ
		);
		Возврат;
	КонецЕсли;
	
	ОбменДаннымиСервер.ФормаНастройкиУзлаПриСозданииНаСервере(ЭтотОбъект, "ПоРабочемуМесту");
	
	НуженыОрганиченияПоМагазину = Ложь;
	Если ПараметрыСеанса.ИспользуемыеПланыОбмена.Найти("ПоМагазину") <> Неопределено
		И ОбменДаннымиПовтИсп.ГлавныйУзел() <> Неопределено Тогда
		//в узле -магазине РИБ нас интересуют только склады  магазинов, заданных для этого узла
		МассивОграниченийПоМагазинам = ПланыОбмена.ПоМагазину.ЭтотУзел().Магазины.ВыгрузитьКолонку("Магазин");
		НуженыОрганиченияПоМагазину = Истина;
	ИначеЕсли ПараметрыСеанса.ИспользуемыеПланыОбмена.Найти("ПоМагазину") <> Неопределено Тогда
		//остатки по другим складам магазинов РИБ для рабочих мест этого магазина не нужны
		МассивОграниченийПоМагазинам = Новый Массив;
		НуженыОрганиченияПоМагазину = Истина;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Магазины.Ссылка
		|ИЗ
		|	Справочник.Магазины КАК Магазины
		|ГДЕ
		|	(НЕ Магазины.Ссылка В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ПоМагазинуМагазины.Магазин
		|				ИЗ
		|					ПланОбмена.ПоМагазину.Магазины КАК ПоМагазинуМагазины))
		|	И (НЕ Магазины.СкладУправляющейСистемы)");
		ВыборкаМагазинов = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаМагазинов.Следующий() цикл
			
			МассивОграниченийПоМагазинам.Добавить(ВыборкаМагазинов.Ссылка);
			
		КонецЦикла;
	КонецЕсли;
	
	Если НуженыОрганиченияПоМагазину Тогда
		
		Элементы.Магазин.СписокВыбора.ЗагрузитьЗначения(МассивОграниченийПоМагазинам);
		Элементы.Магазин.РежимВыбораИзСписка = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтотОбъект);
	
КонецПроцедуры

//////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ОбменДаннымиКлиент.ФормаНастройкиУзлаКомандаЗакрытьФорму(ЭтотОбъект);
	
КонецПроцедуры

